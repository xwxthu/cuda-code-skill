# 6. Programming Interface


Warning

This document has been replaced by a new [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide). The information in this document should be considered legacy, and this document is no longer being updated as of CUDA 13.0. Please refer to the [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide) for up-to-date information on CUDA.


CUDA C++ provides a simple path for users familiar with the C++ programming language to easily write programs for execution by the device.


It consists of a minimal set of extensions to the C++ language and a runtime library.


The core language extensions have been introduced in [Programming Model](#programming-model). They allow programmers to define a kernel as a C++ function and use some new syntax to specify the grid and block dimension each time the function is called. A complete description of all extensions can be found in [C++ Language Extensions](#c-language-extensions). Any source file that contains some of these extensions must be compiled with `nvcc` as outlined in [Compilation with NVCC](#compilation-with-nvcc).


The runtime is introduced in [CUDA Runtime](#cuda-c-runtime). It provides C and C++ functions that execute on the host to allocate and deallocate device memory, transfer data between host memory and device memory, manage systems with multiple devices, etc. A complete description of the runtime can be found in the CUDA reference manual.


The runtime is built on top of a lower-level C API, the CUDA driver API, which is also accessible by the application. The driver API provides an additional level of control by exposing lower-level concepts such as CUDA contexts - the analogue of host processes for the device - and CUDA modules - the analogue of dynamically loaded libraries for the device. Most applications do not use the driver API as they do not need this additional level of control and when using the runtime, context and module management are implicit, resulting in more concise code. As the runtime is interoperable with the driver API, most applications that need some driver API features can default to use the runtime API and only use the driver API where needed. The driver API is introduced in [Driver API](#driver-api) and fully described in the reference manual.


##  6.1. Compilation with NVCC 

Kernels can be written using the CUDA instruction set architecture, called _PTX_ , which is described in the PTX reference manual. It is however usually more effective to use a high-level programming language such as C++. In both cases, kernels must be compiled into binary code by `nvcc` to execute on the device.

`nvcc` is a compiler driver that simplifies the process of compiling _C++_ or _PTX_ code: It provides simple and familiar command line options and executes them by invoking the collection of tools that implement the different compilation stages. This section gives an overview of `nvcc` workflow and command options. A complete description can be found in the `nvcc` user manual.

###  6.1.1. Compilation Workflow 

####  6.1.1.1. Offline Compilation 

Source files compiled with `nvcc` can include a mix of host code (i.e., code that executes on the host) and device code (i.e., code that executes on the device). `nvcc`’s basic workflow consists in separating device code from host code and then:

  * compiling the device code into an assembly form (_PTX_ code) and/or binary form (_cubin_ object),

  * and modifying the host code by replacing the `<<<...>>>` syntax introduced in [Kernels](#kernels) (and described in more details in [Execution Configuration](#execution-configuration)) by the necessary CUDA runtime function calls to load and launch each compiled kernel from the _PTX_ code and/or _cubin_ object.


The modified host code is output either as C++ code that is left to be compiled using another tool or as object code directly by letting `nvcc` invoke the host compiler during the last compilation stage.

Applications can then:

  * Either link to the compiled host code (this is the most common case),

  * Or ignore the modified host code (if any) and use the CUDA driver API (see [Driver API](#driver-api)) to load and execute the _PTX_ code or _cubin_ object.


####  6.1.1.2. Just-in-Time Compilation 

Any _PTX_ code loaded by an application at runtime is compiled further to binary code by the device driver. This is called _just-in-time compilation_. Just-in-time compilation increases application load time, but allows the application to benefit from any new compiler improvements coming with each new device driver. It is also the only way for applications to run on devices that did not exist at the time the application was compiled, as detailed in [Application Compatibility](#application-compatibility).

When the device driver just-in-time compiles some _PTX_ code for some application, it automatically caches a copy of the generated binary code in order to avoid repeating the compilation in subsequent invocations of the application. The cache - referred to as _compute cache_ \- is automatically invalidated when the device driver is upgraded, so that applications can benefit from the improvements in the new just-in-time compiler built into the device driver.

Environment variables are available to control just-in-time compilation as described in [CUDA Environment Variables](#env-vars)

As an alternative to using `nvcc` to compile CUDA C++ device code, NVRTC can be used to compile CUDA C++ device code to PTX at runtime. NVRTC is a runtime compilation library for CUDA C++; more information can be found in the NVRTC User guide.

###  6.1.2. Binary Compatibility 

Binary code is architecture-specific. A _cubin_ object is generated using the compiler option `-code` that specifies the targeted architecture: For example, compiling with `-code=sm_80` produces binary code for devices of [compute capability](#compute-capability) 8.0. Binary compatibility is guaranteed from one minor revision to the next one, but not from one minor revision to the previous one or across major revisions. In other words, a _cubin_ object generated for compute capability _X.y_ will only execute on devices of compute capability _X.z_ where _z≥y_.

Note

Binary compatibility is supported only for the desktop. It is not supported for Tegra. Also, the binary compatibility between desktop and Tegra is not supported.

###  6.1.3. PTX Compatibility 

Some _PTX_ instructions are only supported on devices of higher compute capabilities. For example, [Warp Shuffle Functions](#warp-shuffle-functions) are only supported on devices of compute capability 5.0 and above. The `-arch` compiler option specifies the compute capability that is assumed when compiling C++ to _PTX_ code. So, code that contains warp shuffle, for example, must be compiled with `-arch=compute_50` (or higher).

_PTX_ code produced for some specific compute capability can always be compiled to binary code of greater or equal compute capability. Note that a binary compiled from an earlier PTX version may not make use of some hardware features. For example, a binary targeting devices of compute capability 7.0 (Volta) compiled from PTX generated for compute capability 6.0 (Pascal) will not make use of Tensor Core instructions, since these were not available on Pascal. As a result, the final binary may perform worse than would be possible if the binary were generated using the latest version of PTX.

_PTX_ code compiled to target [Architecture-Specific Features](#architecture-specific-features) only runs on the exact same physical architecture and nowhere else. Architecture-specific _PTX_ code is not forward and backward compatible. Example code compiled with `sm_90a` or `compute_90a` only runs on devices with compute capability 9.0 and is not backward or forward compatible.

_PTX_ code compiled to target [Family-Specific Features](#family-specific-features) only runs on the exact same physical architecture and other architectures in the same family. Family-specific _PTX_ code is forward compatible with other devices in the same family, and is not backward compatible. Example code compiled with `sm_100f` or `compute_100f` only runs on devices with compute capability 10.0 and 10.3. [Table 25](#family-specific-compatibility) shows the compatibility of family-specific targets with compute capability.

###  6.1.4. Application Compatibility 

To execute code on devices of specific compute capability, an application must load binary or _PTX_ code that is compatible with this compute capability as described in [Binary Compatibility](#binary-compatibility) and [PTX Compatibility](#ptx-compatibility). In particular, to be able to execute code on future architectures with higher compute capability (for which no binary code can be generated yet), an application must load _PTX_ code that will be just-in-time compiled for these devices (see [Just-in-Time Compilation](#just-in-time-compilation)).

Which _PTX_ and binary code gets embedded in a CUDA C++ application is controlled by the `-arch` and `-code` compiler options or the `-gencode` compiler option as detailed in the `nvcc` user manual. For example,
    
    
    nvcc x.cu
            -gencode arch=compute_50,code=sm_50
            -gencode arch=compute_60,code=sm_60
            -gencode arch=compute_70,code=\"compute_70,sm_70\"
    

embeds binary code compatible with compute capability 5.0 and 6.0 (first and second `-gencode` options) and _PTX_ and binary code compatible with compute capability 7.0 (third `-gencode` option).

Host code is generated to automatically select at runtime the most appropriate code to load and execute, which, in the above example, will be:

  * 5.0 binary code for devices with compute capability 5.0 and 5.2,

  * 6.0 binary code for devices with compute capability 6.0 and 6.1,

  * 7.0 binary code for devices with compute capability 7.0 and 7.5,

  * _PTX_ code which is compiled to binary code at runtime for devices with compute capability later than 7.5


`x.cu` can have an optimized code path that uses warp reduction operations, for example, which are only supported in devices of compute capability 8.0 and higher. The `__CUDA_ARCH__` macro can be used to differentiate various code paths based on compute capability. It is only defined for device code. When compiling with `-arch=compute_80` for example, `__CUDA_ARCH__` is equal to `800`.

If `x.cu` is compiled for [Family-Specific Features](#family-specific-features) with `sm_100f` or `compute_100f`, the code can only run on devices in that specific family, which are devices with compute capability 10.0 and 10.3. For family-specific code targets an additional macro `__CUDA_ARCH_FAMILY_SPECIFIC__` is defined. In this example, `__CUDA_ARCH_FAMILY_SPECIFIC__` is equal to `1000`.

If `x.cu` is compiled for [Architecture-Specific Features](#architecture-specific-features) with `sm_100a` or `compute_100a`, the code can only run on devices with compute capability 10.0. For architecture-specific code targets an additional macro `__CUDA_ARCH_SPECIFIC__` is defined. In this example, `__CUDA_ARCH_SPECIFIC__` is equal to `1000`. Because architecture-specific features are a superset of family-specific features, the family-specific macro `__CUDA_ARCH_FAMILY_SPECIFIC__` is also defined and is equal to `1000`.

Applications using the driver API must compile code to separate files and explicitly load and execute the most appropriate file at runtime.

The Volta architecture introduces _Independent Thread Scheduling_ which changes the way threads are scheduled on the GPU. For code relying on specific behavior of [SIMT scheduling](#simt-architecture) in previous architectures, Independent Thread Scheduling may alter the set of participating threads, leading to incorrect results. To aid migration while implementing the corrective actions detailed in [Independent Thread Scheduling](#independent-thread-scheduling-7-x), Volta developers can opt-in to Pascal’s thread scheduling with the compiler option combination `-arch=compute_60 -code=sm_70`.

The `nvcc` user manual lists various shorthands for the `-arch`, `-code`, and `-gencode` compiler options. For example, `-arch=sm_70` is a shorthand for `-arch=compute_70 -code=compute_70,sm_70` (which is the same as `-gencode arch=compute_70,code=\"compute_70,sm_70\"`).

###  6.1.5. C++ Compatibility 

The front end of the compiler processes CUDA source files according to C++ syntax rules. Full C++ is supported for the host code. However, only a subset of C++ is fully supported for the device code as described in [C++ Language Support](#c-cplusplus-language-support).

###  6.1.6. 64-Bit Compatibility 

The 64-bit version of `nvcc` compiles device code in 64-bit mode (i.e., pointers are 64-bit). Device code compiled in 64-bit mode is only supported with host code compiled in 64-bit mode.


##  6.2. CUDA Runtime 

The runtime is implemented in the `cudart` library, which is linked to the application, either statically via `cudart.lib` or `libcudart.a`, or dynamically via `cudart.dll` or `libcudart.so`. Applications that require `cudart.dll` and/or `cudart.so` for dynamic linking typically include them as part of the application installation package. It is only safe to pass the address of CUDA runtime symbols between components that link to the same instance of the CUDA runtime.

All its entry points are prefixed with `cuda`.

As mentioned in [Heterogeneous Programming](#heterogeneous-programming), the CUDA programming model assumes a system composed of a host and a device, each with their own separate memory. [Device Memory](#device-memory) gives an overview of the runtime functions used to manage device memory.

[Shared Memory](#shared-memory) illustrates the use of shared memory, introduced in [Thread Hierarchy](#thread-hierarchy), to maximize performance.

[Page-Locked Host Memory](#page-locked-host-memory) introduces page-locked host memory that is required to overlap kernel execution with data transfers between host and device memory.

[Asynchronous Concurrent Execution](#asynchronous-concurrent-execution) describes the concepts and API used to enable asynchronous concurrent execution at various levels in the system.

[Multi-Device System](#multi-device-system) shows how the programming model extends to a system with multiple devices attached to the same host.

[Error Checking](#error-checking) describes how to properly check the errors generated by the runtime.

[Call Stack](#call-stack) mentions the runtime functions used to manage the CUDA C++ call stack.

[Texture and Surface Memory](#texture-and-surface-memory) presents the texture and surface memory spaces that provide another way to access device memory; they also expose a subset of the GPU texturing hardware.

[Graphics Interoperability](#graphics-interoperability) introduces the various functions the runtime provides to interoperate with the two main graphics APIs, OpenGL and Direct3D.

###  6.2.1. Initialization 

As of CUDA 12.0, the `cudaInitDevice()` and `cudaSetDevice()` calls initialize the runtime and the primary context associated with the specified device. Absent these calls, the runtime will implicitly use device 0 and self-initialize as needed to process other runtime API requests. One needs to keep this in mind when timing runtime function calls and when interpreting the error code from the first call into the runtime. Before 12.0, `cudaSetDevice()` would not initialize the runtime and applications would often use the no-op runtime call `cudaFree(0)` to isolate the runtime initialization from other api activity (both for the sake of timing and error handling).

The runtime creates a CUDA context for each device in the system (see [Context](#context) for more details on CUDA contexts). This context is the _primary context_ for this device and is initialized at the first runtime function which requires an active context on this device. It is shared among all the host threads of the application. As part of this context creation, the device code is just-in-time compiled if necessary (see [Just-in-Time Compilation](#just-in-time-compilation)) and loaded into device memory. This all happens transparently. If needed, for example, for driver API interoperability, the primary context of a device can be accessed from the driver API as described in [Interoperability between Runtime and Driver APIs](#interoperability-between-runtime-and-driver-apis).

When a host thread calls `cudaDeviceReset()`, this destroys the primary context of the device the host thread currently operates on (that is, the current device as defined in [Device Selection](#device-selection)). The next runtime function call made by any host thread that has this device as current will create a new primary context for this device.

Note

The CUDA interfaces use global state that is initialized during host program initiation and destroyed during host program termination. The CUDA runtime and driver cannot detect if this state is invalid, so using any of these interfaces (implicitly or explicitly) during program initiation or termination after main) will result in undefined behavior.

As of CUDA 12.0, `cudaSetDevice()` will now explicitly initialize the runtime after changing the current device for the host thread. Previous versions of CUDA delayed runtime initialization on the new device until the first runtime call was made after `cudaSetDevice()`. This change means that it is now very important to check the return value of `cudaSetDevice()` for initialization errors.

The runtime functions from the error handling and version management sections of the reference manual do not initialize the runtime.

###  6.2.2. Device Memory 

As mentioned in [Heterogeneous Programming](#heterogeneous-programming), the CUDA programming model assumes a system composed of a host and a device, each with their own separate memory. Kernels operate out of device memory, so the runtime provides functions to allocate, deallocate, and copy device memory, as well as transfer data between host memory and device memory.

Device memory can be allocated either as _linear memory_ or as _CUDA arrays_.

CUDA arrays are opaque memory layouts optimized for texture fetching. They are described in [Texture and Surface Memory](#texture-and-surface-memory).

Linear memory is allocated in a single unified address space, which means that separately allocated entities can reference one another via pointers, for example, in a binary tree or linked list. The size of the address space depends on the host system (CPU) and the compute capability of the used GPU:

Table 4 Linear Memory Address Space | x86_64 (AMD64) | POWER (ppc64le) | ARM64  
---|---|---|---  
up to compute capability 5.3 (Maxwell) | 40bit | 40bit | 40bit  
compute capability 6.0 (Pascal) or newer | up to 47bit | up to 49bit | up to 48bit  
  
Note

On devices of compute capability 5.3 (Maxwell) and earlier, the CUDA driver creates an uncommitted 40bit virtual address reservation to ensure that memory allocations (pointers) fall into the supported range. This reservation appears as reserved virtual memory, but does not occupy any physical memory until the program actually allocates memory.

Linear memory is typically allocated using `cudaMalloc()` and freed using `cudaFree()` and data transfer between host memory and device memory are typically done using `cudaMemcpy()`. In the vector addition code sample of [Kernels](#kernels), the vectors need to be copied from host memory to device memory:
    
    
    // Device code
    __global__ void VecAdd(float* A, float* B, float* C, int N)
    {
        int i = blockDim.x * blockIdx.x + threadIdx.x;
        if (i < N)
            C[i] = A[i] + B[i];
    }
    
    // Host code
    int main()
    {
        int N = ...;
        size_t size = N * sizeof(float);
    
        // Allocate input vectors h_A and h_B in host memory
        float* h_A = (float*)malloc(size);
        float* h_B = (float*)malloc(size);
        float* h_C = (float*)malloc(size);
    
        // Initialize input vectors
        ...
    
        // Allocate vectors in device memory
        float* d_A;
        cudaMalloc(&d_A, size);
        float* d_B;
        cudaMalloc(&d_B, size);
        float* d_C;
        cudaMalloc(&d_C, size);
    
        // Copy vectors from host memory to device memory
        cudaMemcpy(d_A, h_A, size, cudaMemcpyHostToDevice);
        cudaMemcpy(d_B, h_B, size, cudaMemcpyHostToDevice);
    
        // Invoke kernel
        int threadsPerBlock = 256;
        int blocksPerGrid =
                (N + threadsPerBlock - 1) / threadsPerBlock;
        VecAdd<<<blocksPerGrid, threadsPerBlock>>>(d_A, d_B, d_C, N);
    
        // Copy result from device memory to host memory
        // h_C contains the result in host memory
        cudaMemcpy(h_C, d_C, size, cudaMemcpyDeviceToHost);
    
        // Free device memory
        cudaFree(d_A);
        cudaFree(d_B);
        cudaFree(d_C);
    
        // Free host memory
        ...
    }
    

Linear memory can also be allocated through `cudaMallocPitch()` and `cudaMalloc3D()`. These functions are recommended for allocations of 2D or 3D arrays as it makes sure that the allocation is appropriately padded to meet the alignment requirements described in [Device Memory Accesses](#device-memory-accesses), therefore ensuring best performance when accessing the row addresses or performing copies between 2D arrays and other regions of device memory (using the `cudaMemcpy2D()` and `cudaMemcpy3D()` functions). The returned pitch (or stride) must be used to access array elements. The following code sample allocates a `width` x `height` 2D array of floating-point values and shows how to loop over the array elements in device code:
    
    
    // Host code
    int width = 64, height = 64;
    float* devPtr;
    size_t pitch;
    cudaMallocPitch(&devPtr, &pitch,
                    width * sizeof(float), height);
    MyKernel<<<100, 512>>>(devPtr, pitch, width, height);
    
    // Device code
    __global__ void MyKernel(float* devPtr,
                             size_t pitch, int width, int height)
    {
        for (int r = 0; r < height; ++r) {
            float* row = (float*)((char*)devPtr + r * pitch);
            for (int c = 0; c < width; ++c) {
                float element = row[c];
            }
        }
    }
    

The following code sample allocates a `width` x `height` x `depth` 3D array of floating-point values and shows how to loop over the array elements in device code:
    
    
    // Host code
    int width = 64, height = 64, depth = 64;
    cudaExtent extent = make_cudaExtent(width * sizeof(float),
                                        height, depth);
    cudaPitchedPtr devPitchedPtr;
    cudaMalloc3D(&devPitchedPtr, extent);
    MyKernel<<<100, 512>>>(devPitchedPtr, width, height, depth);
    
    // Device code
    __global__ void MyKernel(cudaPitchedPtr devPitchedPtr,
                             int width, int height, int depth)
    {
        char* devPtr = devPitchedPtr.ptr;
        size_t pitch = devPitchedPtr.pitch;
        size_t slicePitch = pitch * height;
        for (int z = 0; z < depth; ++z) {
            char* slice = devPtr + z * slicePitch;
            for (int y = 0; y < height; ++y) {
                float* row = (float*)(slice + y * pitch);
                for (int x = 0; x < width; ++x) {
                    float element = row[x];
                }
            }
        }
    }
    

Note

To avoid allocating too much memory and thus impacting system-wide performance, request the allocation parameters from the user based on the problem size. If the allocation fails, you can fallback to other slower memory types (`cudaMallocHost()`, `cudaHostRegister()`, etc.), or return an error telling the user how much memory was needed that was denied. If your application cannot request the allocation parameters for some reason, we recommend using `cudaMallocManaged()` for platforms that support it.

The reference manual lists all the various functions used to copy memory between linear memory allocated with `cudaMalloc()`, linear memory allocated with `cudaMallocPitch()` or `cudaMalloc3D()`, CUDA arrays, and memory allocated for variables declared in global or constant memory space.

The following code sample illustrates various ways of accessing global variables via the runtime API:
    
    
    __constant__ float constData[256];
    float data[256];
    cudaMemcpyToSymbol(constData, data, sizeof(data));
    cudaMemcpyFromSymbol(data, constData, sizeof(data));
    
    __device__ float devData;
    float value = 3.14f;
    cudaMemcpyToSymbol(devData, &value, sizeof(float));
    
    __device__ float* devPointer;
    float* ptr;
    cudaMalloc(&ptr, 256 * sizeof(float));
    cudaMemcpyToSymbol(devPointer, &ptr, sizeof(ptr));
    

`cudaGetSymbolAddress()` is used to retrieve the address pointing to the memory allocated for a variable declared in global memory space. The size of the allocated memory is obtained through `cudaGetSymbolSize()`.

###  6.2.3. Device Memory L2 Access Management 

When a CUDA kernel accesses a data region in the global memory repeatedly, such data accesses can be considered to be _persisting_. On the other hand, if the data is only accessed once, such data accesses can be considered to be _streaming_.

Starting with CUDA 11.0, devices of compute capability 8.0 and above have the capability to influence persistence of data in the L2 cache, potentially providing higher bandwidth and lower latency accesses to global memory.

####  6.2.3.1. L2 Cache Set-Aside for Persisting Accesses 

A portion of the L2 cache can be set aside to be used for persisting data accesses to global memory. Persisting accesses have prioritized use of this set-aside portion of L2 cache, whereas normal or streaming, accesses to global memory can only utilize this portion of L2 when it is unused by persisting accesses.

The L2 cache set-aside size for persisting accesses may be adjusted, within limits:
    
    
    cudaGetDeviceProperties(&prop, device_id);
    size_t size = min(int(prop.l2CacheSize * 0.75), prop.persistingL2CacheMaxSize);
    cudaDeviceSetLimit(cudaLimitPersistingL2CacheSize, size); /* set-aside 3/4 of L2 cache for persisting accesses or the max allowed*/
    

When the GPU is configured in Multi-Instance GPU (MIG) mode, the L2 cache set-aside functionality is disabled.

When using the Multi-Process Service (MPS), the L2 cache set-aside size cannot be changed by `cudaDeviceSetLimit`. Instead, the set-aside size can only be specified at start up of MPS server through the environment variable `CUDA_DEVICE_DEFAULT_PERSISTING_L2_CACHE_PERCENTAGE_LIMIT`.

####  6.2.3.2. L2 Policy for Persisting Accesses 

An access policy window specifies a contiguous region of global memory and a persistence property in the L2 cache for accesses within that region.

The code example below shows how to set an L2 persisting access window using a CUDA Stream.

**CUDA Stream Example**
    
    
    cudaStreamAttrValue stream_attribute;                                         // Stream level attributes data structure
    stream_attribute.accessPolicyWindow.base_ptr  = reinterpret_cast<void*>(ptr); // Global Memory data pointer
    stream_attribute.accessPolicyWindow.num_bytes = num_bytes;                    // Number of bytes for persistence access.
                                                                                  // (Must be less than cudaDeviceProp::accessPolicyMaxWindowSize)
    stream_attribute.accessPolicyWindow.hitRatio  = 0.6;                          // Hint for cache hit ratio
    stream_attribute.accessPolicyWindow.hitProp   = cudaAccessPropertyPersisting; // Type of access property on cache hit
    stream_attribute.accessPolicyWindow.missProp  = cudaAccessPropertyStreaming;  // Type of access property on cache miss.
    
    //Set the attributes to a CUDA stream of type cudaStream_t
    cudaStreamSetAttribute(stream, cudaStreamAttributeAccessPolicyWindow, &stream_attribute);
    

When a kernel subsequently executes in CUDA `stream`, memory accesses within the global memory extent `[ptr..ptr+num_bytes)` are more likely to persist in the L2 cache than accesses to other global memory locations.

L2 persistence can also be set for a CUDA Graph Kernel Node as shown in the example below:

**CUDA GraphKernelNode Example**
    
    
    cudaKernelNodeAttrValue node_attribute;                                     // Kernel level attributes data structure
    node_attribute.accessPolicyWindow.base_ptr  = reinterpret_cast<void*>(ptr); // Global Memory data pointer
    node_attribute.accessPolicyWindow.num_bytes = num_bytes;                    // Number of bytes for persistence access.
                                                                                // (Must be less than cudaDeviceProp::accessPolicyMaxWindowSize)
    node_attribute.accessPolicyWindow.hitRatio  = 0.6;                          // Hint for cache hit ratio
    node_attribute.accessPolicyWindow.hitProp   = cudaAccessPropertyPersisting; // Type of access property on cache hit
    node_attribute.accessPolicyWindow.missProp  = cudaAccessPropertyStreaming;  // Type of access property on cache miss.
    
    //Set the attributes to a CUDA Graph Kernel node of type cudaGraphNode_t
    cudaGraphKernelNodeSetAttribute(node, cudaKernelNodeAttributeAccessPolicyWindow, &node_attribute);
    

The `hitRatio` parameter can be used to specify the fraction of accesses that receive the `hitProp` property. In both of the examples above, 60% of the memory accesses in the global memory region `[ptr..ptr+num_bytes)` have the persisting property and 40% of the memory accesses have the streaming property. Which specific memory accesses are classified as persisting (the `hitProp`) is random with a probability of approximately `hitRatio`; the probability distribution depends upon the hardware architecture and the memory extent.

For example, if the L2 set-aside cache size is 16KB and the `num_bytes` in the `accessPolicyWindow` is 32KB:

  * With a `hitRatio` of 0.5, the hardware will select, at random, 16KB of the 32KB window to be designated as persisting and cached in the set-aside L2 cache area.

  * With a `hitRatio` of 1.0, the hardware will attempt to cache the whole 32KB window in the set-aside L2 cache area. Since the set-aside area is smaller than the window, cache lines will be evicted to keep the most recently used 16KB of the 32KB data in the set-aside portion of the L2 cache.


The `hitRatio` can therefore be used to avoid thrashing of cache lines and overall reduce the amount of data moved into and out of the L2 cache.

A `hitRatio` value below 1.0 can be used to manually control the amount of data different `accessPolicyWindow`s from concurrent CUDA streams can cache in L2. For example, let the L2 set-aside cache size be 16KB; two concurrent kernels in two different CUDA streams, each with a 16KB `accessPolicyWindow`, and both with `hitRatio` value 1.0, might evict each others’ cache lines when competing for the shared L2 resource. However, if both `accessPolicyWindows` have a hitRatio value of 0.5, they will be less likely to evict their own or each others’ persisting cache lines.

####  6.2.3.3. L2 Access Properties 

Three types of access properties are defined for different global memory data accesses:

  1. `cudaAccessPropertyStreaming`: Memory accesses that occur with the streaming property are less likely to persist in the L2 cache because these accesses are preferentially evicted.

  2. `cudaAccessPropertyPersisting`: Memory accesses that occur with the persisting property are more likely to persist in the L2 cache because these accesses are preferentially retained in the set-aside portion of L2 cache.

  3. `cudaAccessPropertyNormal`: This access property forcibly resets previously applied persisting access property to a normal status. Memory accesses with the persisting property from previous CUDA kernels may be retained in L2 cache long after their intended use. This persistence-after-use reduces the amount of L2 cache available to subsequent kernels that do not use the persisting property. Resetting an access property window with the `cudaAccessPropertyNormal` property removes the persisting (preferential retention) status of the prior access, as if the prior access had been without an access property.


####  6.2.3.4. L2 Persistence Example 

The following example shows how to set-aside L2 cache for persistent accesses, use the set-aside L2 cache in CUDA kernels via CUDA Stream and then reset the L2 cache.
    
    
    cudaStream_t stream;
    cudaStreamCreate(&stream);                                                                  // Create CUDA stream
    
    cudaDeviceProp prop;                                                                        // CUDA device properties variable
    cudaGetDeviceProperties( &prop, device_id);                                                 // Query GPU properties
    size_t size = min( int(prop.l2CacheSize * 0.75) , prop.persistingL2CacheMaxSize );
    cudaDeviceSetLimit( cudaLimitPersistingL2CacheSize, size);                                  // set-aside 3/4 of L2 cache for persisting accesses or the max allowed
    
    size_t window_size = min(prop.accessPolicyMaxWindowSize, num_bytes);                        // Select minimum of user defined num_bytes and max window size.
    
    cudaStreamAttrValue stream_attribute;                                                       // Stream level attributes data structure
    stream_attribute.accessPolicyWindow.base_ptr  = reinterpret_cast<void*>(data1);               // Global Memory data pointer
    stream_attribute.accessPolicyWindow.num_bytes = window_size;                                // Number of bytes for persistence access
    stream_attribute.accessPolicyWindow.hitRatio  = 0.6;                                        // Hint for cache hit ratio
    stream_attribute.accessPolicyWindow.hitProp   = cudaAccessPropertyPersisting;               // Persistence Property
    stream_attribute.accessPolicyWindow.missProp  = cudaAccessPropertyStreaming;                // Type of access property on cache miss
    
    cudaStreamSetAttribute(stream, cudaStreamAttributeAccessPolicyWindow, &stream_attribute);   // Set the attributes to a CUDA Stream
    
    for(int i = 0; i < 10; i++) {
        cuda_kernelA<<<grid_size,block_size,0,stream>>>(data1);                                 // This data1 is used by a kernel multiple times
    }                                                                                           // [data1 + num_bytes) benefits from L2 persistence
    cuda_kernelB<<<grid_size,block_size,0,stream>>>(data1);                                     // A different kernel in the same stream can also benefit
                                                                                                // from the persistence of data1
    
    stream_attribute.accessPolicyWindow.num_bytes = 0;                                          // Setting the window size to 0 disable it
    cudaStreamSetAttribute(stream, cudaStreamAttributeAccessPolicyWindow, &stream_attribute);   // Overwrite the access policy attribute to a CUDA Stream
    cudaCtxResetPersistingL2Cache();                                                            // Remove any persistent lines in L2
    
    cuda_kernelC<<<grid_size,block_size,0,stream>>>(data2);                                     // data2 can now benefit from full L2 in normal mode
    

####  6.2.3.5. Reset L2 Access to Normal 

A persisting L2 cache line from a previous CUDA kernel may persist in L2 long after it has been used. Hence, a reset to normal for L2 cache is important for streaming or normal memory accesses to utilize the L2 cache with normal priority. There are three ways a persisting access can be reset to normal status.

  1. Reset a previous persisting memory region with the access property, `cudaAccessPropertyNormal`.

  2. Reset all persisting L2 cache lines to normal by calling `cudaCtxResetPersistingL2Cache()`.

  3. **Eventually** untouched lines are automatically reset to normal. Reliance on automatic reset is strongly discouraged because of the undetermined length of time required for automatic reset to occur.


####  6.2.3.6. Manage Utilization of L2 set-aside cache 

Multiple CUDA kernels executing concurrently in different CUDA streams may have a different access policy window assigned to their streams. However, the L2 set-aside cache portion is shared among all these concurrent CUDA kernels. As a result, the net utilization of this set-aside cache portion is the sum of all the concurrent kernels’ individual use. The benefits of designating memory accesses as persisting diminish as the volume of persisting accesses exceeds the set-aside L2 cache capacity.

To manage utilization of the set-aside L2 cache portion, an application must consider the following:

  * Size of L2 set-aside cache.

  * CUDA kernels that may concurrently execute.

  * The access policy window for all the CUDA kernels that may concurrently execute.

  * When and how L2 reset is required to allow normal or streaming accesses to utilize the previously set-aside L2 cache with equal priority.


####  6.2.3.7. Query L2 cache Properties 

Properties related to L2 cache are a part of `cudaDeviceProp` struct and can be queried using CUDA runtime API `cudaGetDeviceProperties`

CUDA Device Properties include:

  * `l2CacheSize`: The amount of available L2 cache on the GPU.

  * `persistingL2CacheMaxSize`: The maximum amount of L2 cache that can be set-aside for persisting memory accesses.

  * `accessPolicyMaxWindowSize`: The maximum size of the access policy window.


####  6.2.3.8. Control L2 Cache Set-Aside Size for Persisting Memory Access 

The L2 set-aside cache size for persisting memory accesses is queried using CUDA runtime API `cudaDeviceGetLimit` and set using CUDA runtime API `cudaDeviceSetLimit` as a `cudaLimit`. The maximum value for setting this limit is `cudaDeviceProp::persistingL2CacheMaxSize`.
    
    
    enum cudaLimit {
        /* other fields not shown */
        cudaLimitPersistingL2CacheSize
    };
    

###  6.2.4. Shared Memory 

As detailed in [Variable Memory Space Specifiers](#variable-memory-space-specifiers) shared memory is allocated using the `__shared__` memory space specifier.

Shared memory is expected to be much faster than global memory as mentioned in [Thread Hierarchy](#thread-hierarchy) and detailed in [Shared Memory](#shared-memory). It can be used as scratchpad memory (or software managed cache) to minimize global memory accesses from a CUDA block as illustrated by the following matrix multiplication example.

The following code sample is a straightforward implementation of matrix multiplication that does not take advantage of shared memory. Each thread reads one row of _A_ and one column of _B_ and computes the corresponding element of _C_ as illustrated in [Figure 8](#shared-memory-matrix-multiplication-no-shared-memory). _A_ is therefore read _B.width_ times from global memory and _B_ is read _A.height_ times.
    
    
    // Matrices are stored in row-major order:
    // M(row, col) = *(M.elements + row * M.width + col)
    typedef struct {
        int width;
        int height;
        float* elements;
    } Matrix;
    
    // Thread block size
    #define BLOCK_SIZE 16
    
    // Forward declaration of the matrix multiplication kernel
    __global__ void MatMulKernel(const Matrix, const Matrix, Matrix);
    
    // Matrix multiplication - Host code
    // Matrix dimensions are assumed to be multiples of BLOCK_SIZE
    void MatMul(const Matrix A, const Matrix B, Matrix C)
    {
        // Load A and B to device memory
        Matrix d_A;
        d_A.width = A.width; d_A.height = A.height;
        size_t size = A.width * A.height * sizeof(float);
        cudaMalloc(&d_A.elements, size);
        cudaMemcpy(d_A.elements, A.elements, size,
                   cudaMemcpyHostToDevice);
        Matrix d_B;
        d_B.width = B.width; d_B.height = B.height;
        size = B.width * B.height * sizeof(float);
        cudaMalloc(&d_B.elements, size);
        cudaMemcpy(d_B.elements, B.elements, size,
                   cudaMemcpyHostToDevice);
    
        // Allocate C in device memory
        Matrix d_C;
        d_C.width = C.width; d_C.height = C.height;
        size = C.width * C.height * sizeof(float);
        cudaMalloc(&d_C.elements, size);
    
        // Invoke kernel
        dim3 dimBlock(BLOCK_SIZE, BLOCK_SIZE);
        dim3 dimGrid(B.width / dimBlock.x, A.height / dimBlock.y);
        MatMulKernel<<<dimGrid, dimBlock>>>(d_A, d_B, d_C);
    
        // Read C from device memory
        cudaMemcpy(C.elements, d_C.elements, size,
                   cudaMemcpyDeviceToHost);
    
        // Free device memory
        cudaFree(d_A.elements);
        cudaFree(d_B.elements);
        cudaFree(d_C.elements);
    }
    
    // Matrix multiplication kernel called by MatMul()
    __global__ void MatMulKernel(Matrix A, Matrix B, Matrix C)
    {
        // Each thread computes one element of C
        // by accumulating results into Cvalue
        float Cvalue = 0;
        int row = blockIdx.y * blockDim.y + threadIdx.y;
        int col = blockIdx.x * blockDim.x + threadIdx.x;
        for (int e = 0; e < A.width; ++e)
            Cvalue += A.elements[row * A.width + e]
                    * B.elements[e * B.width + col];
        C.elements[row * C.width + col] = Cvalue;
    }
    

![_images/matrix-multiplication-without-shared-memory.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/matrix-multiplication-without-shared-memory.png)

Figure 8 Matrix Multiplication without Shared Memory

The following code sample is an implementation of matrix multiplication that does take advantage of shared memory. In this implementation, each thread block is responsible for computing one square sub-matrix _Csub_ of _C_ and each thread within the block is responsible for computing one element of _Csub_. As illustrated in [Figure 9](#shared-memory-matrix-multiplication-shared-memory), _Csub_ is equal to the product of two rectangular matrices: the sub-matrix of _A_ of dimension (_A.width, block_size_) that has the same row indices as _Csub_ , and the sub-matrix of _B_ of dimension (_block_size, A.width_ )that has the same column indices as _Csub_. In order to fit into the device’s resources, these two rectangular matrices are divided into as many square matrices of dimension _block_size_ as necessary and _Csub_ is computed as the sum of the products of these square matrices. Each of these products is performed by first loading the two corresponding square matrices from global memory to shared memory with one thread loading one element of each matrix, and then by having each thread compute one element of the product. Each thread accumulates the result of each of these products into a register and once done writes the result to global memory.

By blocking the computation this way, we take advantage of fast shared memory and save a lot of global memory bandwidth since _A_ is only read (_B.width / block_size_) times from global memory and _B_ is read (_A.height / block_size_) times.

The _Matrix_ type from the previous code sample is augmented with a _stride_ field, so that sub-matrices can be efficiently represented with the same type. [__device__](#device-function-specifier) functions are used to get and set elements and build any sub-matrix from a matrix.
    
    
    // Matrices are stored in row-major order:
    // M(row, col) = *(M.elements + row * M.stride + col)
    typedef struct {
        int width;
        int height;
        int stride;
        float* elements;
    } Matrix;
    // Get a matrix element
    __device__ float GetElement(const Matrix A, int row, int col)
    {
        return A.elements[row * A.stride + col];
    }
    // Set a matrix element
    __device__ void SetElement(Matrix A, int row, int col,
                               float value)
    {
        A.elements[row * A.stride + col] = value;
    }
    // Get the BLOCK_SIZExBLOCK_SIZE sub-matrix Asub of A that is
    // located col sub-matrices to the right and row sub-matrices down
    // from the upper-left corner of A
     __device__ Matrix GetSubMatrix(Matrix A, int row, int col)
    {
        Matrix Asub;
        Asub.width    = BLOCK_SIZE;
        Asub.height   = BLOCK_SIZE;
        Asub.stride   = A.stride;
        Asub.elements = &A.elements[A.stride * BLOCK_SIZE * row
                                             + BLOCK_SIZE * col];
        return Asub;
    }
    // Thread block size
    #define BLOCK_SIZE 16
    // Forward declaration of the matrix multiplication kernel
    __global__ void MatMulKernel(const Matrix, const Matrix, Matrix);
    // Matrix multiplication - Host code
    // Matrix dimensions are assumed to be multiples of BLOCK_SIZE
    void MatMul(const Matrix A, const Matrix B, Matrix C)
    {
        // Load A and B to device memory
        Matrix d_A;
        d_A.width = d_A.stride = A.width; d_A.height = A.height;
        size_t size = A.width * A.height * sizeof(float);
        cudaMalloc(&d_A.elements, size);
        cudaMemcpy(d_A.elements, A.elements, size,
                   cudaMemcpyHostToDevice);
        Matrix d_B;
        d_B.width = d_B.stride = B.width; d_B.height = B.height;
        size = B.width * B.height * sizeof(float);
        cudaMalloc(&d_B.elements, size);
        cudaMemcpy(d_B.elements, B.elements, size,
        cudaMemcpyHostToDevice);
        // Allocate C in device memory
        Matrix d_C;
        d_C.width = d_C.stride = C.width; d_C.height = C.height;
        size = C.width * C.height * sizeof(float);
        cudaMalloc(&d_C.elements, size);
        // Invoke kernel
        dim3 dimBlock(BLOCK_SIZE, BLOCK_SIZE);
        dim3 dimGrid(B.width / dimBlock.x, A.height / dimBlock.y);
        MatMulKernel<<<dimGrid, dimBlock>>>(d_A, d_B, d_C);
        // Read C from device memory
        cudaMemcpy(C.elements, d_C.elements, size,
                   cudaMemcpyDeviceToHost);
        // Free device memory
        cudaFree(d_A.elements);
        cudaFree(d_B.elements);
        cudaFree(d_C.elements);
    }
    // Matrix multiplication kernel called by MatMul()
     __global__ void MatMulKernel(Matrix A, Matrix B, Matrix C)
    {
        // Block row and column
        int blockRow = blockIdx.y;
        int blockCol = blockIdx.x;
        // Each thread block computes one sub-matrix Csub of C
        Matrix Csub = GetSubMatrix(C, blockRow, blockCol);
        // Each thread computes one element of Csub
        // by accumulating results into Cvalue
        float Cvalue = 0;
        // Thread row and column within Csub
        int row = threadIdx.y;
        int col = threadIdx.x;
        // Loop over all the sub-matrices of A and B that are
        // required to compute Csub
        // Multiply each pair of sub-matrices together
        // and accumulate the results
        for (int m = 0; m < (A.width / BLOCK_SIZE); ++m) {
            // Get sub-matrix Asub of A
            Matrix Asub = GetSubMatrix(A, blockRow, m);
            // Get sub-matrix Bsub of B
            Matrix Bsub = GetSubMatrix(B, m, blockCol);
            // Shared memory used to store Asub and Bsub respectively
            __shared__ float As[BLOCK_SIZE][BLOCK_SIZE];
            __shared__ float Bs[BLOCK_SIZE][BLOCK_SIZE];
            // Load Asub and Bsub from device memory to shared memory
            // Each thread loads one element of each sub-matrix
            As[row][col] = GetElement(Asub, row, col);
            Bs[row][col] = GetElement(Bsub, row, col);
            // Synchronize to make sure the sub-matrices are loaded
            // before starting the computation
            __syncthreads();
            // Multiply Asub and Bsub together
            for (int e = 0; e < BLOCK_SIZE; ++e)
                Cvalue += As[row][e] * Bs[e][col];
            // Synchronize to make sure that the preceding
            // computation is done before loading two new
            // sub-matrices of A and B in the next iteration
            __syncthreads();
        }
        // Write Csub to device memory
        // Each thread writes one element
        SetElement(Csub, row, col, Cvalue);
    }
    

![_images/matrix-multiplication-with-shared-memory.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/matrix-multiplication-with-shared-memory.png)

Figure 9 Matrix Multiplication with Shared Memory

###  6.2.5. Distributed Shared Memory 

Thread block clusters introduced in compute capability 9.0 provide the ability for threads in a thread block cluster to access shared memory of all the participating thread blocks in a cluster. This partitioned shared memory is called _Distributed Shared Memory_ , and the corresponding address space is called Distributed shared memory address space. Threads that belong to a thread block cluster, can read, write or perform atomics in the distributed address space, regardless whether the address belongs to the local thread block or a remote thread block. Whether a kernel uses distributed shared memory or not, the shared memory size specifications, static or dynamic is still per thread block. The size of distributed shared memory is just the number of thread blocks per cluster multiplied by the size of shared memory per thread block.

Accessing data in distributed shared memory requires all the thread blocks to exist. A user can guarantee that all thread blocks have started executing using `cluster.sync()` from [Cluster Group](#cluster-group-cg) API. The user also needs to ensure that all distributed shared memory operations happen before the exit of a thread block, e.g., if a remote thread block is trying to read a given thread block’s shared memory, user needs to ensure that the shared memory read by remote thread block is completed before it can exit.

CUDA provides a mechanism to access to distributed shared memory, and applications can benefit from leveraging its capabilities. Lets look at a simple histogram computation and how to optimize it on the GPU using thread block cluster. A standard way of computing histograms is do the computation in the shared memory of each thread block and then perform global memory atomics. A limitation of this approach is the shared memory capacity. Once the histogram bins no longer fit in the shared memory, a user needs to directly compute histograms and hence the atomics in the global memory. With distributed shared memory, CUDA provides an intermediate step, where a depending on the histogram bins size, histogram can be computed in shared memory, distributed shared memory or global memory directly.

The CUDA kernel example below shows how to compute histograms in shared memory or distributed shared memory, depending on the number of histogram bins.
    
    
    #include <cooperative_groups.h>
    
    // Distributed Shared memory histogram kernel
    __global__ void clusterHist_kernel(int *bins, const int nbins, const int bins_per_block, const int *__restrict__ input,
                                       size_t array_size)
    {
      extern __shared__ int smem[];
      namespace cg = cooperative_groups;
      int tid = cg::this_grid().thread_rank();
    
      // Cluster initialization, size and calculating local bin offsets.
      cg::cluster_group cluster = cg::this_cluster();
      unsigned int clusterBlockRank = cluster.block_rank();
      int cluster_size = cluster.dim_blocks().x;
    
      for (int i = threadIdx.x; i < bins_per_block; i += blockDim.x)
      {
        smem[i] = 0; //Initialize shared memory histogram to zeros
      }
    
      // cluster synchronization ensures that shared memory is initialized to zero in
      // all thread blocks in the cluster. It also ensures that all thread blocks
      // have started executing and they exist concurrently.
      cluster.sync();
    
      for (int i = tid; i < array_size; i += blockDim.x * gridDim.x)
      {
        int ldata = input[i];
    
        //Find the right histogram bin.
        int binid = ldata;
        if (ldata < 0)
          binid = 0;
        else if (ldata >= nbins)
          binid = nbins - 1;
    
        //Find destination block rank and offset for computing
        //distributed shared memory histogram
        int dst_block_rank = (int)(binid / bins_per_block);
        int dst_offset = binid % bins_per_block;
    
        //Pointer to target block shared memory
        int *dst_smem = cluster.map_shared_rank(smem, dst_block_rank);
    
        //Perform atomic update of the histogram bin
        atomicAdd(dst_smem + dst_offset, 1);
      }
    
      // cluster synchronization is required to ensure all distributed shared
      // memory operations are completed and no thread block exits while
      // other thread blocks are still accessing distributed shared memory
      cluster.sync();
    
      // Perform global memory histogram, using the local distributed memory histogram
      int *lbins = bins + cluster.block_rank() * bins_per_block;
      for (int i = threadIdx.x; i < bins_per_block; i += blockDim.x)
      {
        atomicAdd(&lbins[i], smem[i]);
      }
    }
    

The above kernel can be launched at runtime with a cluster size depending on the amount of distributed shared memory required. If histogram is small enough to fit in shared memory of just one block, user can launch kernel with cluster size 1. The code snippet below shows how to launch a cluster kernel dynamically based depending on shared memory requirements.
    
    
    // Launch via extensible launch
    {
      cudaLaunchConfig_t config = {0};
      config.gridDim = array_size / threads_per_block;
      config.blockDim = threads_per_block;
    
      // cluster_size depends on the histogram size.
      // ( cluster_size == 1 ) implies no distributed shared memory, just thread block local shared memory
      int cluster_size = 2; // size 2 is an example here
      int nbins_per_block = nbins / cluster_size;
    
      //dynamic shared memory size is per block.
      //Distributed shared memory size =  cluster_size * nbins_per_block * sizeof(int)
      config.dynamicSmemBytes = nbins_per_block * sizeof(int);
    
      CUDA_CHECK(::cudaFuncSetAttribute((void *)clusterHist_kernel, cudaFuncAttributeMaxDynamicSharedMemorySize, config.dynamicSmemBytes));
    
      cudaLaunchAttribute attribute[1];
      attribute[0].id = cudaLaunchAttributeClusterDimension;
      attribute[0].val.clusterDim.x = cluster_size;
      attribute[0].val.clusterDim.y = 1;
      attribute[0].val.clusterDim.z = 1;
    
      config.numAttrs = 1;
      config.attrs = attribute;
    
      cudaLaunchKernelEx(&config, clusterHist_kernel, bins, nbins, nbins_per_block, input, array_size);
    }
    

###  6.2.6. Page-Locked Host Memory 

The runtime provides functions to allow the use of _page-locked_ (also known as _pinned_) host memory (as opposed to regular pageable host memory allocated by `malloc()`):

  * `cudaHostAlloc()` and `cudaFreeHost()` allocate and free page-locked host memory;

  * `cudaHostRegister()` page-locks a range of memory allocated by `malloc()` (see reference manual for limitations).


Using page-locked host memory has several benefits:

  * Copies between page-locked host memory and device memory can be performed concurrently with kernel execution for some devices as mentioned in [Asynchronous Concurrent Execution](#asynchronous-concurrent-execution).

  * On some devices, page-locked host memory can be mapped into the address space of the device, eliminating the need to copy it to or from device memory as detailed in [Mapped Memory](#mapped-memory).

  * On systems with a front-side bus, bandwidth between host memory and device memory is higher if host memory is allocated as page-locked and even higher if in addition it is allocated as write-combining as described in [Write-Combining Memory](#write-combining-memory).


Note

Page-locked host memory is not cached on non I/O coherent Tegra devices. Also, `cudaHostRegister()` is not supported on non I/O coherent Tegra devices.

The simple zero-copy CUDA sample comes with a detailed document on the page-locked memory APIs.

####  6.2.6.1. Portable Memory 

A block of page-locked memory can be used in conjunction with any device in the system (see [Multi-Device System](#multi-device-system) for more details on multi-device systems), but by default, the benefits of using page-locked memory described above are only available in conjunction with the device that was current when the block was allocated (and with all devices sharing the same unified address space, if any, as described in [Unified Virtual Address Space](#unified-virtual-address-space)). To make these advantages available to all devices, the block needs to be allocated by passing the flag `cudaHostAllocPortable` to `cudaHostAlloc()` or page-locked by passing the flag `cudaHostRegisterPortable` to `cudaHostRegister()`.

####  6.2.6.2. Write-Combining Memory 

By default page-locked host memory is allocated as cacheable. It can optionally be allocated as _write-combining_ instead by passing flag `cudaHostAllocWriteCombined` to `cudaHostAlloc()`. Write-combining memory frees up the host’s L1 and L2 cache resources, making more cache available to the rest of the application. In addition, write-combining memory is not snooped during transfers across the PCI Express bus, which can improve transfer performance by up to 40%.

Reading from write-combining memory from the host is prohibitively slow, so write-combining memory should in general be used for memory that the host only writes to.

Using CPU atomic instructions on WC memory should be avoided because not all CPU implementations guarantee that functionality.

####  6.2.6.3. Mapped Memory 

A block of page-locked host memory can also be mapped into the address space of the device by passing flag `cudaHostAllocMapped` to `cudaHostAlloc()` or by passing flag `cudaHostRegisterMapped` to `cudaHostRegister()`. Such a block has therefore in general two addresses: one in host memory that is returned by `cudaHostAlloc()` or `malloc()`, and one in device memory that can be retrieved using `cudaHostGetDevicePointer()` and then used to access the block from within a kernel. The only exception is for pointers allocated with `cudaHostAlloc()` and when a unified address space is used for the host and the device as mentioned in [Unified Virtual Address Space](#unified-virtual-address-space).

Accessing host memory directly from within a kernel does not provide the same bandwidth as device memory, but does have some advantages:

  * There is no need to allocate a block in device memory and copy data between this block and the block in host memory; data transfers are implicitly performed as needed by the kernel;

  * There is no need to use streams (see [Concurrent Data Transfers](#concurrent-data-transfers)) to overlap data transfers with kernel execution; the kernel-originated data transfers automatically overlap with kernel execution.


Since mapped page-locked memory is shared between host and device however, the application must synchronize memory accesses using streams or events (see [Asynchronous Concurrent Execution](#asynchronous-concurrent-execution)) to avoid any potential read-after-write, write-after-read, or write-after-write hazards.

To be able to retrieve the device pointer to any mapped page-locked memory, page-locked memory mapping must be enabled by calling `cudaSetDeviceFlags()` with the `cudaDeviceMapHost` flag before any other CUDA call is performed. Otherwise, `cudaHostGetDevicePointer()` will return an error.

`cudaHostGetDevicePointer()` also returns an error if the device does not support mapped page-locked host memory. Applications may query this capability by checking the `canMapHostMemory` device property (see [Device Enumeration](#device-enumeration)), which is equal to 1 for devices that support mapped page-locked host memory.

Note that atomic functions (see [Atomic Functions](#atomic-functions)) operating on mapped page-locked memory are not atomic from the point of view of the host or other devices.

Also note that CUDA runtime requires that 1-byte, 2-byte, 4-byte, 8-byte, and 16-byte naturally aligned loads and stores to host memory initiated from the device are preserved as single accesses from the point of view of the host and other devices. On some platforms, atomics to memory may be broken by the hardware into separate load and store operations. These component load and store operations have the same requirements on preservation of naturally aligned accesses. The CUDA runtime does not support a PCI Express bus topology where a PCI Express bridge splits 8-byte naturally aligned operations and NVIDIA is not aware of any topology that splits 16-byte naturally aligned operations.

###  6.2.7. Memory Synchronization Domains 

####  6.2.7.1. Memory Fence Interference 

Some CUDA applications may see degraded performance due to memory fence/flush operations waiting on more transactions than those necessitated by the CUDA memory consistency model.
    
    
    __managed__ int x = 0;
    __device__  cuda::atomic<int, cuda::thread_scope_device> a(0);
    __managed__ cuda::atomic<int, cuda::thread_scope_system> b(0);
    

|  |   
---|---|---  
Thread 1 (SM)
    
    
    x = 1;
    a = 1;
    

|  Thread 2 (SM)
    
    
    while (a != 1) ;
    assert(x == 1);
    b = 1;
    

|  Thread 3 (CPU)
    
    
    while (b != 1) ;
    assert(x == 1);
      
  
Consider the example above. The CUDA memory consistency model guarantees that the asserted condition will be true, so the write to `x` from thread 1 must be visible to thread 3, before the write to `b` from thread 2.

The memory ordering provided by the release and acquire of `a` is only sufficient to make `x` visible to thread 2, not thread 3, as it is a device-scope operation. The system-scope ordering provided by release and acquire of `b`, therefore, needs to ensure not only writes issued from thread 2 itself are visible to thread 3, but also writes from other threads that are visible to thread 2. This is known as cumulativity. As the GPU cannot know at the time of execution which writes have been guaranteed at the source level to be visible and which are visible only by chance timing, it must cast a conservatively wide net for in-flight memory operations.

This sometimes leads to interference: because the GPU is waiting on memory operations it is not required to at the source level, the fence/flush may take longer than necessary.

Note that fences may occur explicitly as intrinsics or atomics in code, like in the example, or implicitly to implement _synchronizes-with_ relationships at task boundaries.

A common example is when a kernel is performing computation in local GPU memory, and a parallel kernel (e.g. from NCCL) is performing communications with a peer. Upon completion, the local kernel will implicitly flush its writes to satisfy any _synchronizes-with_ relationships to downstream work. This may unnecessarily wait, fully or partially, on slower nvlink or PCIe writes from the communication kernel.

####  6.2.7.2. Isolating Traffic with Domains 

Beginning with Hopper architecture GPUs and CUDA 12.0, the memory synchronization domains feature provides a way to alleviate such interference. In exchange for explicit assistance from code, the GPU can reduce the net cast by a fence operation. Each kernel launch is given a domain ID. Writes and fences are tagged with the ID, and a fence will only order writes matching the fence’s domain. In the concurrent compute vs communication example, the communication kernels can be placed in a different domain.

When using domains, code must abide by the rule that **ordering or synchronization between distinct domains on the same GPU requires system-scope fencing**. Within a domain, device-scope fencing remains sufficient. This is necessary for cumulativity as one kernel’s writes will not be encompassed by a fence issued from a kernel in another domain. In essence, cumulativity is satisfied by ensuring that cross-domain traffic is flushed to the system scope ahead of time.

Note that this modifies the definition of `thread_scope_device`. However, because kernels will default to domain 0 as described below, backward compatibility is maintained.

####  6.2.7.3. Using Domains in CUDA 

Domains are accessible via the new launch attributes `cudaLaunchAttributeMemSyncDomain` and `cudaLaunchAttributeMemSyncDomainMap`. The former selects between logical domains `cudaLaunchMemSyncDomainDefault` and `cudaLaunchMemSyncDomainRemote`, and the latter provides a mapping from logical to physical domains. The remote domain is intended for kernels performing remote memory access in order to isolate their memory traffic from local kernels. Note, however, the selection of a particular domain does not affect what memory access a kernel may legally perform.

The domain count can be queried via device attribute `cudaDevAttrMemSyncDomainCount`. Hopper has 4 domains. To facilitate portable code, domains functionality can be used on all devices and CUDA will report a count of 1 prior to Hopper.

Having logical domains eases application composition. An individual kernel launch at a low level in the stack, such as from NCCL, can select a semantic logical domain without concern for the surrounding application architecture. Higher levels can steer logical domains using the mapping. The default value for the logical domain if it is not set is the default domain, and the default mapping is to map the default domain to 0 and the remote domain to 1 (on GPUs with more than 1 domain). Specific libraries may tag launches with the remote domain in CUDA 12.0 and later; for example, NCCL 2.16 will do so. Together, this provides a beneficial use pattern for common applications out of the box, with no code changes needed in other components, frameworks, or at application level. An alternative use pattern, for example in an application using nvshmem or with no clear separation of kernel types, could be to partition parallel streams. Stream A may map both logical domains to physical domain 0, stream B to 1, and so on.
    
    
    // Example of launching a kernel with the remote logical domain
    cudaLaunchAttribute domainAttr;
    domainAttr.id = cudaLaunchAttrMemSyncDomain;
    domainAttr.val = cudaLaunchMemSyncDomainRemote;
    cudaLaunchConfig_t config;
    // Fill out other config fields
    config.attrs = &domainAttr;
    config.numAttrs = 1;
    cudaLaunchKernelEx(&config, myKernel, kernelArg1, kernelArg2...);
    
    
    
    // Example of setting a mapping for a stream
    // (This mapping is the default for streams starting on Hopper if not
    // explicitly set, and provided for illustration)
    cudaLaunchAttributeValue mapAttr;
    mapAttr.memSyncDomainMap.default_ = 0;
    mapAttr.memSyncDomainMap.remote = 1;
    cudaStreamSetAttribute(stream, cudaLaunchAttributeMemSyncDomainMap, &mapAttr);
    
    
    
    // Example of mapping different streams to different physical domains, ignoring
    // logical domain settings
    cudaLaunchAttributeValue mapAttr;
    mapAttr.memSyncDomainMap.default_ = 0;
    mapAttr.memSyncDomainMap.remote = 0;
    cudaStreamSetAttribute(streamA, cudaLaunchAttributeMemSyncDomainMap, &mapAttr);
    mapAttr.memSyncDomainMap.default_ = 1;
    mapAttr.memSyncDomainMap.remote = 1;
    cudaStreamSetAttribute(streamB, cudaLaunchAttributeMemSyncDomainMap, &mapAttr);
    

As with other launch attributes, these are exposed uniformly on CUDA streams, individual launches using `cudaLaunchKernelEx`, and kernel nodes in CUDA graphs. A typical use would set the mapping at stream level and the logical domain at launch level (or bracketing a section of stream use) as described above.

Both attributes are copied to graph nodes during stream capture. Graphs take both attributes from the node itself, essentially an indirect way of specifying a physical domain. Domain-related attributes set on the stream a graph is launched into are not used in execution of the graph.

###  6.2.8. Asynchronous Concurrent Execution 

CUDA exposes the following operations as independent tasks that can operate concurrently with one another:

  * Computation on the host;

  * Computation on the device;

  * Memory transfers from the host to the device;

  * Memory transfers from the device to the host;

  * Memory transfers within the memory of a given device;

  * Memory transfers among devices.


The level of concurrency achieved between these operations will depend on the feature set and compute capability of the device as described below.

####  6.2.8.1. Concurrent Execution between Host and Device 

Concurrent host execution is facilitated through asynchronous library functions that return control to the host thread before the device completes the requested task. Using asynchronous calls, many device operations can be queued up together to be executed by the CUDA driver when appropriate device resources are available. This relieves the host thread of much of the responsibility to manage the device, leaving it free for other tasks. The following device operations are asynchronous with respect to the host:

  * Kernel launches;

  * Memory copies within a single device’s memory;

  * Memory copies from host to device of a memory block of 64 KB or less;

  * Memory copies performed by functions that are suffixed with `Async`;

  * Memory set function calls.


Programmers can globally disable asynchronicity of kernel launches for all CUDA applications running on a system by setting the `CUDA_LAUNCH_BLOCKING` environment variable to 1. This feature is provided for debugging purposes only and should not be used as a way to make production software run reliably.

Kernel launches are synchronous if hardware counters are collected via a profiler (Nsight Compute) unless concurrent kernel profiling is enabled. `Async` memory copies might also be synchronous if they involve host memory that is not page-locked.

####  6.2.8.2. Concurrent Kernel Execution 

Some devices of compute capability 2.x and higher can execute multiple kernels concurrently. Applications may query this capability by checking the `concurrentKernels` device property (see [Device Enumeration](#device-enumeration)), which is equal to 1 for devices that support it.

The maximum number of kernel launches that a device can execute concurrently depends on its compute capability and is listed in [Table 27](#features-and-technical-specifications-technical-specifications-per-compute-capability).

A kernel from one CUDA context cannot execute concurrently with a kernel from another CUDA context. The GPU may time slice to provide forward progress to each context. If a user wants to run kernels from multiple process simultaneously on the SM, one must enable MPS.

Kernels that use many textures or a large amount of local memory are less likely to execute concurrently with other kernels.

####  6.2.8.3. Overlap of Data Transfer and Kernel Execution 

Some devices can perform an asynchronous memory copy to or from the GPU concurrently with kernel execution. Applications may query this capability by checking the `asyncEngineCount` device property (see [Device Enumeration](#device-enumeration)), which is greater than zero for devices that support it. If host memory is involved in the copy, it must be page-locked.

It is also possible to perform an intra-device copy simultaneously with kernel execution (on devices that support the `concurrentKernels` device property) and/or with copies to or from the device (for devices that support the `asyncEngineCount` property). Intra-device copies are initiated using the standard memory copy functions with destination and source addresses residing on the same device.

####  6.2.8.4. Concurrent Data Transfers 

Some devices of compute capability 2.x and higher can overlap copies to and from the device. Applications may query this capability by checking the `asyncEngineCount` device property (see [Device Enumeration](#device-enumeration)), which is equal to 2 for devices that support it. In order to be overlapped, any host memory involved in the transfers must be page-locked.

####  6.2.8.5. Streams 

Applications manage the concurrent operations described above through _streams_. A stream is a sequence of commands (possibly issued by different host threads) that execute in order. Different streams, on the other hand, may execute their commands out of order with respect to one another or concurrently; this behavior is not guaranteed and should therefore not be relied upon for correctness (for example, inter-kernel communication is undefined). The commands issued on a stream may execute when all the dependencies of the command are met. The dependencies could be previously launched commands on same stream or dependencies from other streams. The successful completion of synchronize call guarantees that all the commands launched are completed.

#####  6.2.8.5.1. Creation and Destruction of Streams 

A stream is defined by creating a stream object and specifying it as the stream parameter to a sequence of kernel launches and host `<->` device memory copies. The following code sample creates two streams and allocates an array `hostPtr` of `float` in page-locked memory.
    
    
    cudaStream_t stream[2];
    for (int i = 0; i < 2; ++i)
        cudaStreamCreate(&stream[i]);
    float* hostPtr;
    cudaMallocHost(&hostPtr, 2 * size);
    

Each of these streams is defined by the following code sample as a sequence of one memory copy from host to device, one kernel launch, and one memory copy from device to host:
    
    
    for (int i = 0; i < 2; ++i) {
        cudaMemcpyAsync(inputDevPtr + i * size, hostPtr + i * size,
                        size, cudaMemcpyHostToDevice, stream[i]);
        MyKernel <<<100, 512, 0, stream[i]>>>
              (outputDevPtr + i * size, inputDevPtr + i * size, size);
        cudaMemcpyAsync(hostPtr + i * size, outputDevPtr + i * size,
                        size, cudaMemcpyDeviceToHost, stream[i]);
    }
    

Each stream copies its portion of input array `hostPtr` to array `inputDevPtr` in device memory, processes `inputDevPtr` on the device by calling `MyKernel()`, and copies the result `outputDevPtr` back to the same portion of `hostPtr`. [Overlapping Behavior](#overlapping-behavior) describes how the streams overlap in this example depending on the capability of the device. Note that `hostPtr` must point to page-locked host memory for any overlap to occur.

Streams are released by calling `cudaStreamDestroy()`.
    
    
    for (int i = 0; i < 2; ++i)
        cudaStreamDestroy(stream[i]);
    

In case the device is still doing work in the stream when `cudaStreamDestroy()` is called, the function will return immediately and the resources associated with the stream will be released automatically once the device has completed all work in the stream.

#####  6.2.8.5.2. Default Stream 

Kernel launches and host `<->` device memory copies that do not specify any stream parameter, or equivalently that set the stream parameter to zero, are issued to the default stream. They are therefore executed in order.

For code that is compiled using the `--default-stream per-thread` compilation flag (or that defines the `CUDA_API_PER_THREAD_DEFAULT_STREAM` macro before including CUDA headers (`cuda.h` and `cuda_runtime.h`)), the default stream is a regular stream and each host thread has its own default stream.

Note

`#define CUDA_API_PER_THREAD_DEFAULT_STREAM 1` cannot be used to enable this behavior when the code is compiled by `nvcc` as `nvcc` implicitly includes `cuda_runtime.h` at the top of the translation unit. In this case the `--default-stream per-thread` compilation flag needs to be used or the `CUDA_API_PER_THREAD_DEFAULT_STREAM` macro needs to be defined with the `-DCUDA_API_PER_THREAD_DEFAULT_STREAM=1` compiler flag.

For code that is compiled using the `--default-stream legacy` compilation flag, the default stream is a special stream called the _NULL stream_ and each device has a single NULL stream used for all host threads. The NULL stream is special as it causes implicit synchronization as described in [Implicit Synchronization](#implicit-synchronization).

For code that is compiled without specifying a `--default-stream` compilation flag, `--default-stream legacy` is assumed as the default.

#####  6.2.8.5.3. Explicit Synchronization 

There are various ways to explicitly synchronize streams with each other.

`cudaDeviceSynchronize()` waits until all preceding commands in all streams of all host threads have completed.

`cudaStreamSynchronize()`takes a stream as a parameter and waits until all preceding commands in the given stream have completed. It can be used to synchronize the host with a specific stream, allowing other streams to continue executing on the device.

`cudaStreamWaitEvent()`takes a stream and an event as parameters (see [Events](#events) for a description of events)and makes all the commands added to the given stream after the call to `cudaStreamWaitEvent()`delay their execution until the given event has completed.

`cudaStreamQuery()`provides applications with a way to know if all preceding commands in a stream have completed.

#####  6.2.8.5.4. Implicit Synchronization 

Two operations from different streams cannot run concurrently if any CUDA operation on the NULL stream is submitted in-between them, unless the streams are non-blocking streams (created with the `cudaStreamNonBlocking` flag).

Applications should follow these guidelines to improve their potential for concurrent kernel execution:

  * All independent operations should be issued before dependent operations,

  * Synchronization of any kind should be delayed as long as possible.


#####  6.2.8.5.5. Overlapping Behavior 

The amount of execution overlap between two streams depends on the order in which the commands are issued to each stream and whether or not the device supports overlap of data transfer and kernel execution (see [Overlap of Data Transfer and Kernel Execution](#overlap-of-data-transfer-and-kernel-execution)), concurrent kernel execution (see [Concurrent Kernel Execution](#concurrent-kernel-execution)), and/or concurrent data transfers (see [Concurrent Data Transfers](#concurrent-data-transfers)).

For example, on devices that do not support concurrent data transfers, the two streams of the code sample of [Creation and Destruction of Streams](#creation-and-destruction-streams) do not overlap at all because the memory copy from host to device is issued to stream[1] after the memory copy from device to host is issued to stream[0], so it can only start once the memory copy from device to host issued to stream[0] has completed. If the code is rewritten the following way (and assuming the device supports overlap of data transfer and kernel execution)
    
    
    for (int i = 0; i < 2; ++i)
        cudaMemcpyAsync(inputDevPtr + i * size, hostPtr + i * size,
                        size, cudaMemcpyHostToDevice, stream[i]);
    for (int i = 0; i < 2; ++i)
        MyKernel<<<100, 512, 0, stream[i]>>>
              (outputDevPtr + i * size, inputDevPtr + i * size, size);
    for (int i = 0; i < 2; ++i)
        cudaMemcpyAsync(hostPtr + i * size, outputDevPtr + i * size,
                        size, cudaMemcpyDeviceToHost, stream[i]);
    

then the memory copy from host to device issued to stream[1] overlaps with the kernel launch issued to stream[0].

On devices that do support concurrent data transfers, the two streams of the code sample of [Creation and Destruction of Streams](#creation-and-destruction-streams) do overlap: The memory copy from host to device issued to stream[1] overlaps with the memory copy from device to host issued to stream[0] and even with the kernel launch issued to stream[0] (assuming the device supports overlap of data transfer and kernel execution).

#####  6.2.8.5.6. Host Functions (Callbacks) 

The runtime provides a way to insert a CPU function call at any point into a stream via `cudaLaunchHostFunc()`. The provided function is executed on the host once all commands issued to the stream before the callback have completed.

The following code sample adds the host function `MyCallback` to each of two streams after issuing a host-to-device memory copy, a kernel launch and a device-to-host memory copy into each stream. The function will begin execution on the host after each of the device-to-host memory copies completes.
    
    
    void CUDART_CB MyCallback(void *data){
        printf("Inside callback %d\n", (size_t)data);
    }
    ...
    for (size_t i = 0; i < 2; ++i) {
        cudaMemcpyAsync(devPtrIn[i], hostPtr[i], size, cudaMemcpyHostToDevice, stream[i]);
        MyKernel<<<100, 512, 0, stream[i]>>>(devPtrOut[i], devPtrIn[i], size);
        cudaMemcpyAsync(hostPtr[i], devPtrOut[i], size, cudaMemcpyDeviceToHost, stream[i]);
        cudaLaunchHostFunc(stream[i], MyCallback, (void*)i);
    }
    

The commands that are issued in a stream after a host function do not start executing before the function has completed.

A host function enqueued into a stream must not make CUDA API calls (directly or indirectly), as it might end up waiting on itself if it makes such a call leading to a deadlock.

#####  6.2.8.5.7. Stream Priorities 

The relative priorities of streams can be specified at creation using `cudaStreamCreateWithPriority()`. The range of allowable priorities, ordered as [ greatest priority, least priority ] can be obtained using the `cudaDeviceGetStreamPriorityRange()` function. At runtime, the GPU scheduler utilizes stream priorities to determine task execution order, but these priorities serve as hints rather than guarantees. When selecting work to launch, pending tasks in higher-priority streams take precedence over those in lower-priority streams. Higher-priority tasks do not preempt already running lower-priority tasks. The GPU does not reassess work queues during task execution, and increasing a stream’s priority will not interrupt ongoing work. Stream priorities influence task execution without enforcing strict ordering, so users can leverage stream priorities to influence task execution without relying on strict ordering guarantees.

The following code sample obtains the allowable range of priorities for the current device, and creates streams with the highest and lowest available priorities.
    
    
    // get the range of stream priorities for this device
    int leastPriority, greatestPriority;
    cudaDeviceGetStreamPriorityRange(&leastPriority, &greatestPriority);
    // create streams with highest and lowest available priorities
    cudaStream_t st_high, st_low;
    cudaStreamCreateWithPriority(&st_high, cudaStreamNonBlocking, greatestPriority));
    cudaStreamCreateWithPriority(&st_low, cudaStreamNonBlocking, leastPriority);
    

####  6.2.8.6. Programmatic Dependent Launch and Synchronization 

Warning

This document has been replaced by a new [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide). The information in this document should be considered legacy, and this document is no longer being updated as of CUDA 13.0. Please refer to the [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide) for up-to-date information on CUDA.

The _Programmatic Dependent Launch_ mechanism allows for a dependent _secondary_ kernel to launch before the _primary_ kernel it depends on in the same CUDA stream has finished executing. Available starting with devices of compute capability 9.0, this technique can provide performance benefits when the _secondary_ kernel can complete significant work that does not depend on the results of the _primary_ kernel.

#####  6.2.8.6.1. Background 

A CUDA application utilizes the GPU by launching and executing multiple kernels on it. A typical GPU activity timeline is shown in [Figure 10](#gpu-activity).

[![GPU activity timeline](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/gpu-activity.png)](_images/gpu-activity.png)

Figure 10 GPU activity timeline

Here, `secondary_kernel` is launched after `primary_kernel` finishes its execution. Serialized execution is usually necessary because `secondary_kernel` depends on result data produced by `primary_kernel`. If `secondary_kernel` has no dependency on `primary_kernel`, both of them can be launched concurrently by using [Streams](#streams). Even if `secondary_kernel` is dependent on `primary_kernel`, there is some potential for concurrent execution. For example, almost all the kernels have some sort of _preamble_ section during which tasks such as zeroing buffers or loading constant values are performed.

[![Preamble section of ``secondary_kernel``](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/secondary-kernel-preamble.png)](_images/secondary-kernel-preamble.png)

Figure 11 Preamble section of `secondary_kernel`

[Figure 11](#secondary-kernel-preamble) demonstrates the portion of `secondary_kernel` that could be executed concurrently without impacting the application. Note that concurrent launch also allows us to hide the launch latency of `secondary_kernel` behind the execution of `primary_kernel`.

[![Concurrent execution of ``primary_kernel`` and ``secondary_kernel``](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/preamble-overlap.png)](_images/preamble-overlap.png)

Figure 12 Concurrent execution of `primary_kernel` and `secondary_kernel`

The concurrent launch and execution of `secondary_kernel` shown in [Figure 12](#preamble-overlap) is achievable using _Programmatic Dependent Launch_.

_Programmatic Dependent Launch_ introduces changes to the CUDA kernel launch APIs as explained in following section. These APIs require at least compute capability 9.0 to provide overlapping execution.

#####  6.2.8.6.2. API Description 

In Programmatic Dependent Launch, a primary and a secondary kernel are launched in the same CUDA stream. The primary kernel should execute `cudaTriggerProgrammaticLaunchCompletion` with all thread blocks when it’s ready for the secondary kernel to launch. The secondary kernel must be launched using the extensible launch API as shown.
    
    
    __global__ void primary_kernel() {
       // Initial work that should finish before starting secondary kernel
    
       // Trigger the secondary kernel
       cudaTriggerProgrammaticLaunchCompletion();
    
       // Work that can coincide with the secondary kernel
    }
    
    __global__ void secondary_kernel()
    {
       // Independent work
    
       // Will block until all primary kernels the secondary kernel is dependent on have completed and flushed results to global memory
       cudaGridDependencySynchronize();
    
       // Dependent work
    }
    
    cudaLaunchAttribute attribute[1];
    attribute[0].id = cudaLaunchAttributeProgrammaticStreamSerialization;
    attribute[0].val.programmaticStreamSerializationAllowed = 1;
    configSecondary.attrs = attribute;
    configSecondary.numAttrs = 1;
    
    primary_kernel<<<grid_dim, block_dim, 0, stream>>>();
    cudaLaunchKernelEx(&configSecondary, secondary_kernel);
    

When the secondary kernel is launched using the `cudaLaunchAttributeProgrammaticStreamSerialization` attribute, the CUDA driver is safe to launch the secondary kernel early and not wait on the completion and memory flush of the primary before launching the secondary.

The CUDA driver can launch the secondary kernel when all primary thread blocks have launched and executed `cudaTriggerProgrammaticLaunchCompletion`. If the primary kernel doesn’t execute the trigger, it implicitly occurs after all thread blocks in the primary kernel exit.

In either case, the secondary thread blocks might launch before data written by the primary kernel is visible. As such, when the secondary kernel is configured with _Programmatic Dependent Launch_ , it must always use `cudaGridDependencySynchronize` or other means to verify that the result data from the primary is available.

Please note that these methods provide the opportunity for the primary and secondary kernels to execute concurrently, however this behavior is opportunistic and not guaranteed to lead to concurrent kernel execution. Reliance on concurrent execution in this manner is unsafe and can lead to deadlock.

#####  6.2.8.6.3. Use in CUDA Graphs 

Programmatic Dependent Launch can be used in [CUDA Graphs](#cuda-graphs) via [stream capture](#creating-a-graph-using-stream-capture) or directly via [edge data](#edge-data). To program this feature in a CUDA Graph with edge data, use a `cudaGraphDependencyType` value of `cudaGraphDependencyTypeProgrammatic` on an edge connecting two kernel nodes. This edge type makes the upstream kernel visible to a `cudaGridDependencySynchronize()` in the downstream kernel. This type must be used with an outgoing port of either `cudaGraphKernelNodePortLaunchCompletion` or `cudaGraphKernelNodePortProgrammatic`.

The resulting graph equivalents for stream capture are as follows:

Stream code (abbreviated) | Resulting graph edge  
---|---  
      
    
    cudaLaunchAttribute attribute;
    attribute.id = cudaLaunchAttributeProgrammaticStreamSerialization;
    attribute.val.programmaticStreamSerializationAllowed = 1;
    

| 
    
    
    cudaGraphEdgeData edgeData;
    edgeData.type = cudaGraphDependencyTypeProgrammatic;
    edgeData.from_port = cudaGraphKernelNodePortProgrammatic;
      
      
    
    cudaLaunchAttribute attribute;
    attribute.id = cudaLaunchAttributeProgrammaticEvent;
    attribute.val.programmaticEvent.triggerAtBlockStart = 0;
    

| 
    
    
    cudaGraphEdgeData edgeData;
    edgeData.type = cudaGraphDependencyTypeProgrammatic;
    edgeData.from_port = cudaGraphKernelNodePortProgrammatic;
      
      
    
    cudaLaunchAttribute attribute;
    attribute.id = cudaLaunchAttributeProgrammaticEvent;
    attribute.val.programmaticEvent.triggerAtBlockStart = 1;
    

| 
    
    
    cudaGraphEdgeData edgeData;
    edgeData.type = cudaGraphDependencyTypeProgrammatic;
    edgeData.from_port = cudaGraphKernelNodePortLaunchCompletion;
      
  
####  6.2.8.7. CUDA Graphs 

CUDA Graphs present a new model for work submission in CUDA. A graph is a series of operations, such as kernel launches, connected by dependencies, which is defined separately from its execution. This allows a graph to be defined once and then launched repeatedly. Separating out the definition of a graph from its execution enables a number of optimizations: first, CPU launch costs are reduced compared to streams, because much of the setup is done in advance; second, presenting the whole workflow to CUDA enables optimizations which might not be possible with the piecewise work submission mechanism of streams.

To see the optimizations possible with graphs, consider what happens in a stream: when you place a kernel into a stream, the host driver performs a sequence of operations in preparation for the execution of the kernel on the GPU. These operations, necessary for setting up and launching the kernel, are an overhead cost which must be paid for each kernel that is issued. For a GPU kernel with a short execution time, this overhead cost can be a significant fraction of the overall end-to-end execution time.

Work submission using graphs is separated into three distinct stages: definition, instantiation, and execution.

  * During the definition phase, a program creates a description of the operations in the graph along with the dependencies between them.

  * Instantiation takes a snapshot of the graph template, validates it, and performs much of the setup and initialization of work with the aim of minimizing what needs to be done at launch. The resulting instance is known as an _executable graph._

  * An executable graph may be launched into a stream, similar to any other CUDA work. It may be launched any number of times without repeating the instantiation.


#####  6.2.8.7.1. Graph Structure 

An operation forms a node in a graph. The dependencies between the operations are the edges. These dependencies constrain the execution sequence of the operations.

An operation may be scheduled at any time once the nodes on which it depends are complete. Scheduling is left up to the CUDA system.

######  6.2.8.7.1.1. Node Types 

A graph node can be one of:

  * kernel

  * CPU function call

  * memory copy

  * memset

  * empty node

  * waiting on an [event](#events)

  * recording an [event](#events)

  * signalling an [external semaphore](#external-resource-interoperability)

  * waiting on an [external semaphore](#external-resource-interoperability)

  * [conditional node](#conditional-graph-nodes)

  * [Graph Memory Nodes](#graph-memory-nodes)

  * child graph: To execute a separate nested graph, as shown in the following figure.


[![Child Graph Example](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/child-graph.png)](_images/child-graph.png)

Figure 13 Child Graph Example

######  6.2.8.7.1.2. Edge Data 

CUDA 12.3 introduced edge data on CUDA Graphs. Edge data modifies a dependency specified by an edge and consists of three parts: an outgoing port, an incoming port, and a type. An outgoing port specifies when an associated edge is triggered. An incoming port specifies what portion of a node is dependent on an associated edge. A type modifies the relation between the endpoints.

Port values are specific to node type and direction, and edge types may be restricted to specific node types. In all cases, zero-initialized edge data represents default behavior. Outgoing port 0 waits on an entire task, incoming port 0 blocks an entire task, and edge type 0 is associated with a full dependency with memory synchronizing behavior.

Edge data is optionally specified in various graph APIs via a parallel array to the associated nodes. If it is omitted as an input parameter, zero-initialized data is used. If it is omitted as an output (query) parameter, the API accepts this if the edge data being ignored is all zero-initialized, and returns `cudaErrorLossyQuery` if the call would discard information.

Edge data is also available in some stream capture APIs: `cudaStreamBeginCaptureToGraph()`, `cudaStreamGetCaptureInfo()`, and `cudaStreamUpdateCaptureDependencies()`. In these cases, there is not yet a downstream node. The data is associated with a dangling edge (half edge) which will either be connected to a future captured node or discarded at termination of stream capture. Note that some edge types do not wait on full completion of the upstream node. These edges are ignored when considering if a stream capture has been fully rejoined to the origin stream, and cannot be discarded at the end of capture. See [Creating a Graph Using Stream Capture](#creating-a-graph-using-stream-capture).

Currently, no node types define additional incoming ports, and only kernel nodes define additional outgoing ports. There is one non-default dependency type, `cudaGraphDependencyTypeProgrammatic`, which enables [Programmatic Dependent Launch](#programmatic-dependent-launch-and-synchronization) between two kernel nodes.

#####  6.2.8.7.2. Creating a Graph Using Graph APIs 

Graphs can be created via two mechanisms: explicit API and stream capture. The following is an example of creating and executing the below graph.

[![Creating a Graph Using Graph APIs Example](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/create-a-graph.png)](_images/create-a-graph.png)

Figure 14 Creating a Graph Using Graph APIs Example
    
    
    // Create the graph - it starts out empty
    cudaGraphCreate(&graph, 0);
    
    // For the purpose of this example, we'll create
    // the nodes separately from the dependencies to
    // demonstrate that it can be done in two stages.
    // Note that dependencies can also be specified
    // at node creation.
    cudaGraphAddKernelNode(&a, graph, NULL, 0, &nodeParams);
    cudaGraphAddKernelNode(&b, graph, NULL, 0, &nodeParams);
    cudaGraphAddKernelNode(&c, graph, NULL, 0, &nodeParams);
    cudaGraphAddKernelNode(&d, graph, NULL, 0, &nodeParams);
    
    // Now set up dependencies on each node
    cudaGraphAddDependencies(graph, &a, &b, NULL, 1);     // A->B
    cudaGraphAddDependencies(graph, &a, &c, NULL, 1);     // A->C
    cudaGraphAddDependencies(graph, &b, &d, NULL, 1);     // B->D
    cudaGraphAddDependencies(graph, &c, &d, NULL, 1);     // C->D
    

#####  6.2.8.7.3. Creating a Graph Using Stream Capture 

Stream capture provides a mechanism to create a graph from existing stream-based APIs. A section of code which launches work into streams, including existing code, can be bracketed with calls to `cudaStreamBeginCapture()` and `cudaStreamEndCapture()`. See below.
    
    
    cudaGraph_t graph;
    
    cudaStreamBeginCapture(stream);
    
    kernel_A<<< ..., stream >>>(...);
    kernel_B<<< ..., stream >>>(...);
    libraryCall(stream);
    kernel_C<<< ..., stream >>>(...);
    
    cudaStreamEndCapture(stream, &graph);
    

A call to `cudaStreamBeginCapture()` places a stream in capture mode. When a stream is being captured, work launched into the stream is not enqueued for execution. It is instead appended to an internal graph that is progressively being built up. This graph is then returned by calling `cudaStreamEndCapture()`, which also ends capture mode for the stream. A graph which is actively being constructed by stream capture is referred to as a _capture graph._

Stream capture can be used on any CUDA stream except `cudaStreamLegacy` (the “NULL stream”). Note that it _can_ be used on `cudaStreamPerThread`. If a program is using the legacy stream, it may be possible to redefine stream 0 to be the per-thread stream with no functional change. See [Default Stream](#default-stream).

Whether a stream is being captured can be queried with `cudaStreamIsCapturing()`.

Work can be captured to an existing graph using `cudaStreamBeginCaptureToGraph()`. Instead of capturing to an internal graph, work is captured to a graph provided by the user.

######  6.2.8.7.3.1. Cross-stream Dependencies and Events 

Stream capture can handle cross-stream dependencies expressed with `cudaEventRecord()` and `cudaStreamWaitEvent()`, provided the event being waited upon was recorded into the same capture graph.

When an event is recorded in a stream that is in capture mode, it results in a _captured event._ A captured event represents a set of nodes in a capture graph.

When a captured event is waited on by a stream, it places the stream in capture mode if it is not already, and the next item in the stream will have additional dependencies on the nodes in the captured event. The two streams are then being captured to the same capture graph.

When cross-stream dependencies are present in stream capture, `cudaStreamEndCapture()` must still be called in the same stream where `cudaStreamBeginCapture()` was called; this is the _origin stream_. Any other streams which are being captured to the same capture graph, due to event-based dependencies, must also be joined back to the origin stream. This is illustrated below. All streams being captured to the same capture graph are taken out of capture mode upon `cudaStreamEndCapture()`. Failure to rejoin to the origin stream will result in failure of the overall capture operation.
    
    
    // stream1 is the origin stream
    cudaStreamBeginCapture(stream1);
    
    kernel_A<<< ..., stream1 >>>(...);
    
    // Fork into stream2
    cudaEventRecord(event1, stream1);
    cudaStreamWaitEvent(stream2, event1);
    
    kernel_B<<< ..., stream1 >>>(...);
    kernel_C<<< ..., stream2 >>>(...);
    
    // Join stream2 back to origin stream (stream1)
    cudaEventRecord(event2, stream2);
    cudaStreamWaitEvent(stream1, event2);
    
    kernel_D<<< ..., stream1 >>>(...);
    
    // End capture in the origin stream
    cudaStreamEndCapture(stream1, &graph);
    
    // stream1 and stream2 no longer in capture mode
    

Graph returned by the above code is shown in [Figure 14](#creating-a-graph-using-api-fig-creating-using-graph-apis).

Note

When a stream is taken out of capture mode, the next non-captured item in the stream (if any) will still have a dependency on the most recent prior non-captured item, despite intermediate items having been removed.

######  6.2.8.7.3.2. Prohibited and Unhandled Operations 

It is invalid to synchronize or query the execution status of a stream which is being captured or a captured event, because they do not represent items scheduled for execution. It is also invalid to query the execution status of or synchronize a broader handle which encompasses an active stream capture, such as a device or context handle when any associated stream is in capture mode.

When any stream in the same context is being captured, and it was not created with `cudaStreamNonBlocking`, any attempted use of the legacy stream is invalid. This is because the legacy stream handle at all times encompasses these other streams; enqueueing to the legacy stream would create a dependency on the streams being captured, and querying it or synchronizing it would query or synchronize the streams being captured.

It is therefore also invalid to call synchronous APIs in this case. Synchronous APIs, such as `cudaMemcpy()`, enqueue work to the legacy stream and synchronize it before returning.

Note

As a general rule, when a dependency relation would connect something that is captured with something that was not captured and instead enqueued for execution, CUDA prefers to return an error rather than ignore the dependency. An exception is made for placing a stream into or out of capture mode; this severs a dependency relation between items added to the stream immediately before and after the mode transition.

It is invalid to merge two separate capture graphs by waiting on a captured event from a stream which is being captured and is associated with a different capture graph than the event. It is invalid to wait on a non-captured event from a stream which is being captured without specifying the cudaEventWaitExternal flag.

A small number of APIs that enqueue asynchronous operations into streams are not currently supported in graphs and will return an error if called with a stream which is being captured, such as `cudaStreamAttachMemAsync()`.

######  6.2.8.7.3.3. Invalidation 

When an invalid operation is attempted during stream capture, any associated capture graphs are _invalidated_. When a capture graph is invalidated, further use of any streams which are being captured or captured events associated with the graph is invalid and will return an error, until stream capture is ended with `cudaStreamEndCapture()`. This call will take the associated streams out of capture mode, but will also return an error value and a NULL graph.

#####  6.2.8.7.4. CUDA User Objects 

CUDA User Objects can be used to help manage the lifetime of resources used by asynchronous work in CUDA. In particular, this feature is useful for [CUDA Graphs](#cuda-graphs) and [stream capture](#creating-a-graph-using-stream-capture).

Various resource management schemes are not compatible with CUDA graphs. Consider for example an event-based pool or a synchronous-create, asynchronous-destroy scheme.
    
    
    // Library API with pool allocation
    void libraryWork(cudaStream_t stream) {
        auto &resource = pool.claimTemporaryResource();
        resource.waitOnReadyEventInStream(stream);
        launchWork(stream, resource);
        resource.recordReadyEvent(stream);
    }
    
    
    
    // Library API with asynchronous resource deletion
    void libraryWork(cudaStream_t stream) {
        Resource *resource = new Resource(...);
        launchWork(stream, resource);
        cudaLaunchHostFunc(
            stream,
            [](void *resource) {
                delete static_cast<Resource *>(resource);
            },
            resource,
            0);
        // Error handling considerations not shown
    }
    

These schemes are difficult with CUDA graphs because of the non-fixed pointer or handle for the resource which requires indirection or graph update, and the synchronous CPU code needed each time the work is submitted. They also do not work with stream capture if these considerations are hidden from the caller of the library, and because of use of disallowed APIs during capture. Various solutions exist such as exposing the resource to the caller. CUDA user objects present another approach.

A CUDA user object associates a user-specified destructor callback with an internal refcount, similar to C++ `shared_ptr`. References may be owned by user code on the CPU and by CUDA graphs. Note that for user-owned references, unlike C++ smart pointers, there is no object representing the reference; users must track user-owned references manually. A typical use case would be to immediately move the sole user-owned reference to a CUDA graph after the user object is created.

When a reference is associated to a CUDA graph, CUDA will manage the graph operations automatically. A cloned `cudaGraph_t` retains a copy of every reference owned by the source `cudaGraph_t`, with the same multiplicity. An instantiated `cudaGraphExec_t` retains a copy of every reference in the source `cudaGraph_t`. When a `cudaGraphExec_t` is destroyed without being synchronized, the references are retained until the execution is completed.

Here is an example use.
    
    
    cudaGraph_t graph;  // Preexisting graph
    
    Object *object = new Object;  // C++ object with possibly nontrivial destructor
    cudaUserObject_t cuObject;
    cudaUserObjectCreate(
        &cuObject,
        object,  // Here we use a CUDA-provided template wrapper for this API,
                 // which supplies a callback to delete the C++ object pointer
        1,  // Initial refcount
        cudaUserObjectNoDestructorSync  // Acknowledge that the callback cannot be
                                        // waited on via CUDA
    );
    cudaGraphRetainUserObject(
        graph,
        cuObject,
        1,  // Number of references
        cudaGraphUserObjectMove  // Transfer a reference owned by the caller (do
                                 // not modify the total reference count)
    );
    // No more references owned by this thread; no need to call release API
    cudaGraphExec_t graphExec;
    cudaGraphInstantiate(&graphExec, graph, nullptr, nullptr, 0);  // Will retain a
                                                                   // new reference
    cudaGraphDestroy(graph);  // graphExec still owns a reference
    cudaGraphLaunch(graphExec, 0);  // Async launch has access to the user objects
    cudaGraphExecDestroy(graphExec);  // Launch is not synchronized; the release
                                      // will be deferred if needed
    cudaStreamSynchronize(0);  // After the launch is synchronized, the remaining
                               // reference is released and the destructor will
                               // execute. Note this happens asynchronously.
    // If the destructor callback had signaled a synchronization object, it would
    // be safe to wait on it at this point.
    

References owned by graphs in child graph nodes are associated to the child graphs, not the parents. If a child graph is updated or deleted, the references change accordingly. If an executable graph or child graph is updated with `cudaGraphExecUpdate` or `cudaGraphExecChildGraphNodeSetParams`, the references in the new source graph are cloned and replace the references in the target graph. In either case, if previous launches are not synchronized, any references which would be released are held until the launches have finished executing.

There is not currently a mechanism to wait on user object destructors via a CUDA API. Users may signal a synchronization object manually from the destructor code. In addition, it is not legal to call CUDA APIs from the destructor, similar to the restriction on `cudaLaunchHostFunc`. This is to avoid blocking a CUDA internal shared thread and preventing forward progress. It is legal to signal another thread to perform an API call, if the dependency is one way and the thread doing the call cannot block forward progress of CUDA work.

User objects are created with `cudaUserObjectCreate`, which is a good starting point to browse related APIs.

#####  6.2.8.7.5. Updating Instantiated Graphs 

Work submission using graphs is separated into three distinct stages: definition, instantiation, and execution. In situations where the workflow is not changing, the overhead of definition and instantiation can be amortized over many executions, and graphs provide a clear advantage over streams.

A graph is a snapshot of a workflow, including kernels, parameters, and dependencies, in order to replay it as rapidly and efficiently as possible. In situations where the workflow changes the graph becomes out of date and must be modified. Major changes to graph structure such as topology or types of nodes will require re-instantiation of the source graph because various topology-related optimization techniques must be re-applied.

The cost of repeated instantiation can reduce the overall performance benefit from graph execution, but it is common for only node parameters, such as kernel parameters and `cudaMemcpy` addresses, to change while graph topology remains the same. For this case, CUDA provides a lightweight mechanism known as “Graph Update,” which allows certain node parameters to be modified in-place without having to rebuild the entire graph. This is much more efficient than re-instantiation.

Updates will take effect the next time the graph is launched, so they will not impact previous graph launches, even if they are running at the time of the update. A graph may be updated and relaunched repeatedly, so multiple updates/launches can be queued on a stream.

CUDA provides two mechanisms for updating instantiated graph parameters, whole graph update and individual node update. Whole graph update allows the user to supply a topologically identical `cudaGraph_t` object whose nodes contain updated parameters. Individual node update allows the user to explicitly update the parameters of individual nodes. Using an updated `cudaGraph_t` is more convenient when a large number of nodes are being updated, or when the graph topology is unknown to the caller (i.e., The graph resulted from stream capture of a library call). Using individual node update is preferred when the number of changes is small and the user has the handles to the nodes requiring updates. Individual node update skips the topology checks and comparisons for unchanged nodes, so it can be more efficient in many cases.

CUDA also provides a mechanism for enabling and disabling individual nodes without affecting their current parameters.

The following sections explain each approach in more detail.

######  6.2.8.7.5.1. Graph Update Limitations 

Kernel nodes:

  * The owning context of the function cannot change.

  * A node whose function originally did not use CUDA dynamic parallelism cannot be updated to a function which uses CUDA dynamic parallelism.


`cudaMemset` and `cudaMemcpy` nodes:

  * The CUDA device(s) to which the operand(s) was allocated/mapped cannot change.

  * The source/destination memory must be allocated from the same context as the original source/destination memory.

  * Only 1D `cudaMemset`/`cudaMemcpy` nodes can be changed.


Additional memcpy node restrictions:

  * Changing either the source or destination memory type (i.e., `cudaPitchedPtr`, `cudaArray_t`, etc.), or the type of transfer (i.e., `cudaMemcpyKind`) is not supported.


External semaphore wait nodes and record nodes:

  * Changing the number of semaphores is not supported.


Conditional nodes:

  * The order of handle creation and assignment must match between the graphs.

  * Changing node parameters is not supported (i.e. number of graphs in the conditional, node context, etc).

  * Changing parameters of nodes within the conditional body graph is subject to the rules above.


Memory nodes:

  * It is not possible to update a `cudaGraphExec_t` with a `cudaGraph_t` if the `cudaGraph_t` is currently instantiated as a different `cudaGraphExec_t`.


There are no restrictions on updates to host nodes, event record nodes, or event wait nodes.

######  6.2.8.7.5.2. Whole Graph Update 

`cudaGraphExecUpdate()` allows an instantiated graph (the “original graph”) to be updated with the parameters from a topologically identical graph (the “updating” graph). The topology of the updating graph must be identical to the original graph used to instantiate the `cudaGraphExec_t`. In addition, the order in which the dependencies are specified must match. Finally, CUDA needs to consistently order the sink nodes (nodes with no dependencies). CUDA relies on the order of specific api calls to achieve consistent sink node ordering.

More explicitly, following the following rules will cause `cudaGraphExecUpdate()` to pair the nodes in the original graph and the updating graph deterministically:

  1. For any capturing stream, the API calls operating on that stream must be made in the same order, including event wait and other api calls not directly corresponding to node creation.

  2. The API calls which directly manipulate a given graph node’s incoming edges (including captured stream APIs, node add APIs, and edge addition / removal APIs) must be made in the same order. Moreover, when dependencies are specified in arrays to these APIs, the order in which the dependencies are specified inside those arrays must match.

  3. Sink nodes must be consistently ordered. Sink nodes are nodes without dependent nodes / outgoing edges in the final graph at the time of the `cudaGraphExecUpdate()` invocation. The following operations affect sink node ordering (if present) and must (as a combined set) be made in the same order:

     * Node add APIs resulting in a sink node.

     * Edge removal resulting in a node becoming a sink node.

     * `cudaStreamUpdateCaptureDependencies()`, if it removes a sink node from a capturing stream’s dependency set.

     * `cudaStreamEndCapture()`.


The following example shows how the API could be used to update an instantiated graph:
    
    
    cudaGraphExec_t graphExec = NULL;
    
    for (int i = 0; i < 10; i++) {
        cudaGraph_t graph;
        cudaGraphExecUpdateResult updateResult;
        cudaGraphNode_t errorNode;
    
        // In this example we use stream capture to create the graph.
        // You can also use the Graph API to produce a graph.
        cudaStreamBeginCapture(stream, cudaStreamCaptureModeGlobal);
    
        // Call a user-defined, stream based workload, for example
        do_cuda_work(stream);
    
        cudaStreamEndCapture(stream, &graph);
    
        // If we've already instantiated the graph, try to update it directly
        // and avoid the instantiation overhead
        if (graphExec != NULL) {
            // If the graph fails to update, errorNode will be set to the
            // node causing the failure and updateResult will be set to a
            // reason code.
            cudaGraphExecUpdate(graphExec, graph, &errorNode, &updateResult);
        }
    
        // Instantiate during the first iteration or whenever the update
        // fails for any reason
        if (graphExec == NULL || updateResult != cudaGraphExecUpdateSuccess) {
    
            // If a previous update failed, destroy the cudaGraphExec_t
            // before re-instantiating it
            if (graphExec != NULL) {
                cudaGraphExecDestroy(graphExec);
            }
            // Instantiate graphExec from graph. The error node and
            // error message parameters are unused here.
            cudaGraphInstantiate(&graphExec, graph, NULL, NULL, 0);
        }
    
        cudaGraphDestroy(graph);
        cudaGraphLaunch(graphExec, stream);
        cudaStreamSynchronize(stream);
    }
    

A typical workflow is to create the initial `cudaGraph_t` using either the stream capture or graph API. The `cudaGraph_t` is then instantiated and launched as normal. After the initial launch, a new `cudaGraph_t` is created using the same method as the initial graph and `cudaGraphExecUpdate()` is called. If the graph update is successful, indicated by the `updateResult` parameter in the above example, the updated `cudaGraphExec_t` is launched. If the update fails for any reason, the `cudaGraphExecDestroy()` and `cudaGraphInstantiate()` are called to destroy the original `cudaGraphExec_t` and instantiate a new one.

It is also possible to update the `cudaGraph_t` nodes directly (i.e., Using `cudaGraphKernelNodeSetParams()`) and subsequently update the `cudaGraphExec_t`, however it is more efficient to use the explicit node update APIs covered in the next section.

Conditional handle flags and default values are updated as part of the graph update.

Please see the [Graph API](https://docs.nvidia.com/cuda/cuda-runtime-api/group__CUDART__GRAPH.html#group__CUDART__GRAPH) for more information on usage and current limitations.

######  6.2.8.7.5.3. Individual Node Update 

Instantiated graph node parameters can be updated directly. This eliminates the overhead of instantiation as well as the overhead of creating a new `cudaGraph_t`. If the number of nodes requiring update is small relative to the total number of nodes in the graph, it is better to update the nodes individually. The following methods are available for updating `cudaGraphExec_t` nodes:

  * `cudaGraphExecKernelNodeSetParams()`

  * `cudaGraphExecMemcpyNodeSetParams()`

  * `cudaGraphExecMemsetNodeSetParams()`

  * `cudaGraphExecHostNodeSetParams()`

  * `cudaGraphExecChildGraphNodeSetParams()`

  * `cudaGraphExecEventRecordNodeSetEvent()`

  * `cudaGraphExecEventWaitNodeSetEvent()`

  * `cudaGraphExecExternalSemaphoresSignalNodeSetParams()`

  * `cudaGraphExecExternalSemaphoresWaitNodeSetParams()`


Please see the [Graph API](https://docs.nvidia.com/cuda/cuda-runtime-api/group__CUDART__GRAPH.html#group__CUDART__GRAPH) for more information on usage and current limitations.

######  6.2.8.7.5.4. Individual Node Enable 

Kernel, memset and memcpy nodes in an instantiated graph can be enabled or disabled using the `cudaGraphNodeSetEnabled()` API. This allows the creation of a graph which contains a superset of the desired functionality which can be customized for each launch. The enable state of a node can be queried using the `cudaGraphNodeGetEnabled()` API.

A disabled node is functionally equivalent to empty node until it is reenabled. Node parameters are not affected by enabling/disabling a node. Enable state is unaffected by individual node update or whole graph update with `cudaGraphExecUpdate()`. Parameter updates while the node is disabled will take effect when the node is reenabled.

The following methods are available for enabling/disabling `cudaGraphExec_t` nodes, as well as querying their status:

  * `cudaGraphNodeSetEnabled()`

  * `cudaGraphNodeGetEnabled()`


Refer to the [Graph API](https://docs.nvidia.com/cuda/cuda-runtime-api/group__CUDART__GRAPH.html#group__CUDART__GRAPH) for more information on usage and current limitations.

#####  6.2.8.7.6. Using Graph APIs 

`cudaGraph_t` objects are not thread-safe. It is the responsibility of the user to ensure that multiple threads do not concurrently access the same `cudaGraph_t`.

A `cudaGraphExec_t` cannot run concurrently with itself. A launch of a `cudaGraphExec_t` will be ordered after previous launches of the same executable graph.

Graph execution is done in streams for ordering with other asynchronous work. However, the stream is for ordering only; it does not constrain the internal parallelism of the graph, nor does it affect where graph nodes execute.

See [Graph API.](https://docs.nvidia.com/cuda/cuda-runtime-api/group__CUDART__GRAPH.html#group__CUDART__GRAPH)

#####  6.2.8.7.7. Device Graph Launch 

There are many workflows which need to make data-dependent decisions during runtime and execute different operations depending on those decisions. Rather than offloading this decision-making process to the host, which may require a round-trip from the device, users may prefer to perform it on the device. To that end, CUDA provides a mechanism to launch graphs from the device.

Device graph launch provides a convenient way to perform dynamic control flow from the device, be it something as simple as a loop or as complex as a device-side work scheduler. This functionality is only available on systems which support [unified addressing](#unified-virtual-address-space).

Graphs which can be launched from the device will henceforth be referred to as device graphs, and graphs which cannot be launched from the device will be referred to as host graphs.

Device graphs can be launched from both the host and device, whereas host graphs can only be launched from the host. Unlike host launches, launching a device graph from the device while a previous launch of the graph is running will result in an error, returning `cudaErrorInvalidValue`; therefore, a device graph cannot be launched twice from the device at the same time. Launching a device graph from the host and device simultaneously will result in undefined behavior.

######  6.2.8.7.7.1. Device Graph Creation 

In order for a graph to be launched from the device, it must be instantiated explicitly for device launch. This is achieved by passing the `cudaGraphInstantiateFlagDeviceLaunch` flag to the `cudaGraphInstantiate()` call. As is the case for host graphs, device graph structure is fixed at time of instantiation and cannot be updated without re-instantiation, and instantiation can only be performed on the host. In order for a graph to be able to be instantiated for device launch, it must adhere to various requirements.

####### 6.2.8.7.7.1.1. Device Graph Requirements

General requirements:

  * The graph’s nodes must all reside on a single device.

  * The graph can only contain kernel nodes, memcpy nodes, memset nodes, and child graph nodes.


Kernel nodes:

  * Use of CUDA Dynamic Parallelism by kernels in the graph is not permitted.

  * Cooperative launches are permitted so long as MPS is not in use.


Memcpy nodes:

  * Only copies involving device memory and/or pinned device-mapped host memory are permitted.

  * Copies involving CUDA arrays are not permitted.

  * Both operands must be accessible from the current device at time of instantiation. Note that the copy operation will be performed from the device on which the graph resides, even if it is targeting memory on another device.


####### 6.2.8.7.7.1.2. Device Graph Upload

In order to launch a graph on the device, it must first be uploaded to the device to populate the necessary device resources. This can be achieved in one of two ways.

Firstly, the graph can be uploaded explicitly, either via `cudaGraphUpload()` or by requesting an upload as part of instantiation via `cudaGraphInstantiateWithParams()`.

Alternatively, the graph can first be launched from the host, which will perform this upload step implicitly as part of the launch.

Examples of all three methods can be seen below:
    
    
    // Explicit upload after instantiation
    cudaGraphInstantiate(&deviceGraphExec1, deviceGraph1, cudaGraphInstantiateFlagDeviceLaunch);
    cudaGraphUpload(deviceGraphExec1, stream);
    
    // Explicit upload as part of instantiation
    cudaGraphInstantiateParams instantiateParams = {0};
    instantiateParams.flags = cudaGraphInstantiateFlagDeviceLaunch | cudaGraphInstantiateFlagUpload;
    instantiateParams.uploadStream = stream;
    cudaGraphInstantiateWithParams(&deviceGraphExec2, deviceGraph2, &instantiateParams);
    
    // Implicit upload via host launch
    cudaGraphInstantiate(&deviceGraphExec3, deviceGraph3, cudaGraphInstantiateFlagDeviceLaunch);
    cudaGraphLaunch(deviceGraphExec3, stream);
    

####### 6.2.8.7.7.1.3. Device Graph Update

Device graphs can only be updated from the host, and must be re-uploaded to the device upon executable graph update in order for the changes to take effect. This can be achieved using the same methods outlined in the previous section. Unlike host graphs, launching a device graph from the device while an update is being applied will result in undefined behavior.

######  6.2.8.7.7.2. Device Launch 

Device graphs can be launched from both the host and the device via `cudaGraphLaunch()`, which has the same signature on the device as on the host. Device graphs are launched via the same handle on the host and the device. Device graphs must be launched from another graph when launched from the device.

Device-side graph launch is per-thread and multiple launches may occur from different threads at the same time, so the user will need to select a single thread from which to launch a given graph.

####### 6.2.8.7.7.2.1. Device Launch Modes

Unlike host launch, device graphs cannot be launched into regular CUDA streams, and can only be launched into distinct named streams, which each denote a specific launch mode:

Table 5 Device-only Graph Launch Streams Stream | Launch Mode  
---|---  
`cudaStreamGraphFireAndForget` | Fire and forget launch  
`cudaStreamGraphTailLaunch` | Tail launch  
`cudaStreamGraphFireAndForgetAsSibling` | Sibling launch  
  
######## 6.2.8.7.7.2.1.1. Fire and Forget Launch

As the name suggests, a fire and forget launch is submitted to the GPU immediately, and it runs independently of the launching graph. In a fire-and-forget scenario, the launching graph is the parent, and the launched graph is the child.

[![_images/fire-and-forget-simple.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/fire-and-forget-simple.png)](_images/fire-and-forget-simple.png)

Figure 15 Fire and forget launch

The above diagram can be generated by the sample code below:
    
    
    __global__ void launchFireAndForgetGraph(cudaGraphExec_t graph) {
        cudaGraphLaunch(graph, cudaStreamGraphFireAndForget);
    }
    
    void graphSetup() {
        cudaGraphExec_t gExec1, gExec2;
        cudaGraph_t g1, g2;
    
        // Create, instantiate, and upload the device graph.
        create_graph(&g2);
        cudaGraphInstantiate(&gExec2, g2, cudaGraphInstantiateFlagDeviceLaunch);
        cudaGraphUpload(gExec2, stream);
    
        // Create and instantiate the launching graph.
        cudaStreamBeginCapture(stream, cudaStreamCaptureModeGlobal);
        launchFireAndForgetGraph<<<1, 1, 0, stream>>>(gExec2);
        cudaStreamEndCapture(stream, &g1);
        cudaGraphInstantiate(&gExec1, g1);
    
        // Launch the host graph, which will in turn launch the device graph.
        cudaGraphLaunch(gExec1, stream);
    }
    

A graph can have up to 120 total fire-and-forget graphs during the course of its execution. This total resets between launches of the same parent graph.

######## 6.2.8.7.7.2.1.2. Graph Execution Environments

In order to fully understand the device-side synchronization model, it is first necessary to understand the concept of an execution environment.

When a graph is launched from the device, it is launched into its own execution environment. The execution environment of a given graph encapsulates all work in the graph as well as all generated fire and forget work. The graph can be considered complete when it has completed execution and when all generated child work is complete.

The below diagram shows the environment encapsulation that would be generated by the fire-and-forget sample code in the previous section.

[![_images/fire-and-forget-environments.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/fire-and-forget-environments.png)](_images/fire-and-forget-environments.png)

Figure 16 Fire and forget launch, with execution environments

These environments are also hierarchical, so a graph environment can include multiple levels of child-environments from fire and forget launches.

[![_images/fire-and-forget-nested-environments.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/fire-and-forget-nested-environments.png)](_images/fire-and-forget-nested-environments.png)

Figure 17 Nested fire and forget environments

When a graph is launched from the host, there exists a stream environment that parents the execution environment of the launched graph. The stream environment encapsulates all work generated as part of the overall launch. The stream launch is complete (i.e. downstream dependent work may now run) when the overall stream environment is marked as complete.

[![_images/device-graph-stream-environment.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/device-graph-stream-environment.png)](_images/device-graph-stream-environment.png)

Figure 18 The stream environment, visualized

######## 6.2.8.7.7.2.1.3. Tail Launch

Unlike on the host, it is not possible to synchronize with device graphs from the GPU via traditional methods such as `cudaDeviceSynchronize()` or `cudaStreamSynchronize()`. Rather, in order to enable serial work dependencies, a different launch mode - tail launch - is offered, to provide similar functionality.

A tail launch executes when a graph’s environment is considered complete - ie, when the graph and all its children are complete. When a graph completes, the environment of the next graph in the tail launch list will replace the completed environment as a child of the parent environment. Like fire-and-forget launches, a graph can have multiple graphs enqueued for tail launch.

[![_images/tail-launch-simple.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/tail-launch-simple.png)](_images/tail-launch-simple.png)

Figure 19 A simple tail launch

The above execution flow can be generated by the code below:
    
    
    __global__ void launchTailGraph(cudaGraphExec_t graph) {
        cudaGraphLaunch(graph, cudaStreamGraphTailLaunch);
    }
    
    void graphSetup() {
        cudaGraphExec_t gExec1, gExec2;
        cudaGraph_t g1, g2;
    
        // Create, instantiate, and upload the device graph.
        create_graph(&g2);
        cudaGraphInstantiate(&gExec2, g2, cudaGraphInstantiateFlagDeviceLaunch);
        cudaGraphUpload(gExec2, stream);
    
        // Create and instantiate the launching graph.
        cudaStreamBeginCapture(stream, cudaStreamCaptureModeGlobal);
        launchTailGraph<<<1, 1, 0, stream>>>(gExec2);
        cudaStreamEndCapture(stream, &g1);
        cudaGraphInstantiate(&gExec1, g1);
    
        // Launch the host graph, which will in turn launch the device graph.
        cudaGraphLaunch(gExec1, stream);
    }
    

Tail launches enqueued by a given graph will execute one at a time, in order of when they were enqueued. So the first enqueued graph will run first, and then the second, and so on.

![_images/tail-launch-ordering-simple.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/tail-launch-ordering-simple.png)

Figure 20 Tail launch ordering

Tail launches enqueued by a tail graph will execute before tail launches enqueued by previous graphs in the tail launch list. These new tail launches will execute in the order they are enqueued.

![_images/tail-launch-ordering-complex.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/tail-launch-ordering-complex.png)

Figure 21 Tail launch ordering when enqueued from multiple graphs

A graph can have up to 255 pending tail launches.

######### 6.2.8.7.7.2.1.3.1. Tail Self-launch

It is possible for a device graph to enqueue itself for a tail launch, although a given graph can only have one self-launch enqueued at a time. In order to query the currently running device graph so that it can be relaunched, a new device-side function is added:
    
    
    cudaGraphExec_t cudaGetCurrentGraphExec();
    

This function returns the handle of the currently running graph if it is a device graph. If the currently executing kernel is not a node within a device graph, this function will return NULL.

Below is sample code showing usage of this function for a relaunch loop:
    
    
    __device__ int relaunchCount = 0;
    
    __global__ void relaunchSelf() {
        int relaunchMax = 100;
    
        if (threadIdx.x == 0) {
            if (relaunchCount < relaunchMax) {
                cudaGraphLaunch(cudaGetCurrentGraphExec(), cudaStreamGraphTailLaunch);
            }
    
            relaunchCount++;
        }
    }
    

######## 6.2.8.7.7.2.1.4. Sibling Launch

Sibling launch is a variation of fire-and-forget launch in which the graph is launched not as a child of the launching graph’s execution environment, but rather as a child of the launching graph’s parent environment. Sibling launch is equivalent to a fire-and-forget launch from the launching graph’s parent environment.

![_images/sibling-launch-simple.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/sibling-launch-simple.png)

Figure 22 A simple sibling launch

The above diagram can be generated by the sample code below:
    
    
    __global__ void launchSiblingGraph(cudaGraphExec_t graph) {
        cudaGraphLaunch(graph, cudaStreamGraphFireAndForgetAsSibling);
    }
    
    void graphSetup() {
        cudaGraphExec_t gExec1, gExec2;
        cudaGraph_t g1, g2;
    
        // Create, instantiate, and upload the device graph.
        create_graph(&g2);
        cudaGraphInstantiate(&gExec2, g2, cudaGraphInstantiateFlagDeviceLaunch);
        cudaGraphUpload(gExec2, stream);
    
        // Create and instantiate the launching graph.
        cudaStreamBeginCapture(stream, cudaStreamCaptureModeGlobal);
        launchSiblingGraph<<<1, 1, 0, stream>>>(gExec2);
        cudaStreamEndCapture(stream, &g1);
        cudaGraphInstantiate(&gExec1, g1);
    
        // Launch the host graph, which will in turn launch the device graph.
        cudaGraphLaunch(gExec1, stream);
    }
    

Since sibling launches are not launched into the launching graph’s execution environment, they will not gate tail launches enqueued by the launching graph.

#####  6.2.8.7.8. Conditional Graph Nodes 

Conditional nodes allow conditional execution and looping of a graph contained within the conditional node. This allows dynamic and iterative workflows to be represented completely within a graph and frees up the host CPU to perform other work in parallel.

Evaluation of the condition value is performed on the device when the dependencies of the conditional node have been met. Conditional nodes can be one of the following types:

  * Conditional [IF nodes](#conditional-if-nodes) execute their body graph once if the condition value is non-zero when the node is executed. An optional second body graph can be provided and this will be executed once if the condition value is zero when the node is executed.

  * Conditional [WHILE nodes](#conditional-while-nodes) execute their body graph if the condition value is non-zero when the node is executed and will continue to execute their body graph until the condition value is zero.

  * Conditional [SWITCH nodes](#conditional-switch-nodes) execute the nth body graph once if the condition value is equal to n. If the condition value does not correspond to a body graph, no body graph is launched.


A condition value is accessed by a [conditional handle](#conditional-handles) , which must be created before the node. The condition value can be set by device code using `cudaGraphSetConditional()`. A default value, applied on each graph launch, can also be specified when the handle is created.

When the conditional node is created, an empty graph is created and the handle is returned to the user so that the graph can be populated. This conditional body graph can be populated using either the [graph APIs](#creating-a-graph-using-graph-apis) or [cudaStreamBeginCaptureToGraph()](#creating-a-graph-using-stream-capture).

Conditional nodes can be nested.

######  6.2.8.7.8.1. Conditional Handles 

A condition value is represented by `cudaGraphConditionalHandle` and is created by `cudaGraphConditionalHandleCreate()`.

The handle must be associated with a single conditional node. Handles cannot be destroyed.

If `cudaGraphCondAssignDefault` is specified when the handle is created, the condition value will be initialized to the specified default at the beginning of each graph execution. If this flag is not provided, the condition value is undefined at the start of each graph execution and code should not assume that the condition value persists across executions.

The default value and flags associated with a handle will be updated during [whole graph update](#whole-graph-update).

######  6.2.8.7.8.2. Conditional Node Body Graph Requirements 

General requirements:

  * The graph’s nodes must all reside on a single device.

  * The graph can only contain kernel nodes, empty nodes, memcpy nodes, memset nodes, child graph nodes, and conditional nodes.


Kernel nodes:

  * Use of CUDA Dynamic Parallelism or Device Graph Launch by kernels in the graph is not permitted.

  * Cooperative launches are permitted so long as MPS is not in use.


Memcpy/Memset nodes:

  * Only copies/memsets involving device memory and/or pinned device-mapped host memory are permitted.

  * Copies/memsets involving CUDA arrays are not permitted.

  * Both operands must be accessible from the current device at time of instantiation. Note that the copy operation will be performed from the device on which the graph resides, even if it is targeting memory on another device.


######  6.2.8.7.8.3. Conditional IF Nodes 

The body graph of an IF node will be executed once if the condition is non-zero when the node is executed. The following diagram depicts a 3 node graph where the middle node, B, is a conditional node:

![_images/conditional-if-node.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/conditional-if-node.png)

Figure 23 Conditional IF Node

The following code illustrates the creation of a graph containing an IF conditional node. The default value of the condition is set using an upstream kernel. The body of the conditional is populated using the [graph API](#creating-a-graph-using-graph-apis).
    
    
    __global__ void setHandle(cudaGraphConditionalHandle handle)
    {
        ...
        cudaGraphSetConditional(handle, value);
        ...
    }
    
    void graphSetup() {
        cudaGraph_t graph;
        cudaGraphExec_t graphExec;
        cudaGraphNode_t node;
        void *kernelArgs[1];
        int value = 1;
    
        cudaGraphCreate(&graph, 0);
    
        cudaGraphConditionalHandle handle;
        cudaGraphConditionalHandleCreate(&handle, graph);
    
        // Use a kernel upstream of the conditional to set the handle value
        cudaGraphNodeParams params = { cudaGraphNodeTypeKernel };
        params.kernel.func = (void *)setHandle;
        params.kernel.gridDim.x = params.kernel.gridDim.y = params.kernel.gridDim.z = 1;
        params.kernel.blockDim.x = params.kernel.blockDim.y = params.kernel.blockDim.z = 1;
        params.kernel.kernelParams = kernelArgs;
        kernelArgs[0] = &handle;
        cudaGraphAddNode(&node, graph, NULL, NULL, 0, &params);
    
        cudaGraphNodeParams cParams = { cudaGraphNodeTypeConditional };
        cParams.conditional.handle = handle;
        cParams.conditional.type   = cudaGraphCondTypeIf;
        cParams.conditional.size   = 1;
        cudaGraphAddNode(&node, graph, &node, NULL, 1, &cParams);
    
        cudaGraph_t bodyGraph = cParams.conditional.phGraph_out[0];
    
        // Populate the body of the conditional node
        ...
        cudaGraphAddNode(&node, bodyGraph, NULL, NULL, 0, &params);
    
        cudaGraphInstantiate(&graphExec, graph, NULL, NULL, 0);
        cudaGraphLaunch(graphExec, 0);
        cudaDeviceSynchronize();
    
        cudaGraphExecDestroy(graphExec);
        cudaGraphDestroy(graph);
    }
    

Starting in CUDA 12.8, IF nodes can also have an optional second body graph which is executed once when the node is executed if the condition value is zero.
    
    
    void graphSetup() {
        cudaGraph_t graph;
        cudaGraphExec_t graphExec;
        cudaGraphNode_t node;
        void *kernelArgs[1];
        int value = 1;
    
        cudaGraphCreate(&graph, 0);
    
        cudaGraphConditionalHandle handle;
        cudaGraphConditionalHandleCreate(&handle, graph);
    
        // Use a kernel upstream of the conditional to set the handle value
        cudaGraphNodeParams params = { cudaGraphNodeTypeKernel };
        params.kernel.func = (void *)setHandle;
        params.kernel.gridDim.x = params.kernel.gridDim.y = params.kernel.gridDim.z = 1;
        params.kernel.blockDim.x = params.kernel.blockDim.y = params.kernel.blockDim.z = 1;
        params.kernel.kernelParams = kernelArgs;
        kernelArgs[0] = &handle;
        cudaGraphAddNode(&node, graph, NULL, NULL, 0, &params);
    
        cudaGraphNodeParams cParams = { cudaGraphNodeTypeConditional };
        cParams.conditional.handle = handle;
        cParams.conditional.type   = cudaGraphCondTypeIf;
        cParams.conditional.size   = 2; // Note that size is now set to '2'
        cudaGraphAddNode(&node, graph, &node, NULL, 1, &cParams);
    
        cudaGraph_t ifBodyGraph = cParams.conditional.phGraph_out[0];
        cudaGraph_t elseBodyGraph = cParams.conditional.phGraph_out[1];
    
        // Populate the body graphs of the conditional node
        ...
        cudaGraphAddNode(&node, ifBodyGraph, NULL, NULL, 0, &params);
        ...
        cudaGraphAddNode(&node, elseBodyGraph, NULL, NULL, 0, &params);
    
        cudaGraphInstantiate(&graphExec, graph, NULL, NULL, 0);
        cudaGraphLaunch(graphExec, 0);
        cudaDeviceSynchronize();
    
        cudaGraphExecDestroy(graphExec);
        cudaGraphDestroy(graph);
    }
    

######  6.2.8.7.8.4. Conditional WHILE Nodes 

The body graph of a WHILE node will be executed as long as the condition is non-zero. The condition will be evaluated when the node is executed and after completion of the body graph. The following diagram depicts a 3 node graph where the middle node, B, is a conditional node:

![_images/conditional-while-node.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/conditional-while-node.png)

Figure 24 Conditional WHILE Node

The following code illustrates the creation of a graph containing a WHILE conditional node. The handle is created using _cudaGraphCondAssignDefault_ to avoid the need for an upstream kernel. The body of the conditional is populated using the [graph API](#creating-a-graph-using-graph-apis).
    
    
    __global__ void loopKernel(cudaGraphConditionalHandle handle)
    {
        static int count = 10;
        cudaGraphSetConditional(handle, --count ? 1 : 0);
    }
    
    void graphSetup() {
        cudaGraph_t graph;
        cudaGraphExec_t graphExec;
        cudaGraphNode_t node;
        void *kernelArgs[1];
    
        cuGraphCreate(&graph, 0);
    
        cudaGraphConditionalHandle handle;
        cudaGraphConditionalHandleCreate(&handle, graph, 1, cudaGraphCondAssignDefault);
    
        cudaGraphNodeParams cParams = { cudaGraphNodeTypeConditional };
        cParams.conditional.handle = handle;
        cParams.conditional.type   = cudaGraphCondTypeWhile;
        cParams.conditional.size   = 1;
        cudaGraphAddNode(&node, graph, NULL, NULL, 0, &cParams);
    
        cudaGraph_t bodyGraph = cParams.conditional.phGraph_out[0];
    
        cudaGraphNodeParams params = { cudaGraphNodeTypeKernel };
        params.kernel.func = (void *)loopKernel;
        params.kernel.gridDim.x = params.kernel.gridDim.y = params.kernel.gridDim.z = 1;
        params.kernel.blockDim.x = params.kernel.blockDim.y = params.kernel.blockDim.z = 1;
        params.kernel.kernelParams = kernelArgs;
        kernelArgs[0] = &handle;
        cudaGraphAddNode(&node, bodyGraph, NULL, NULL, 0, &params);
    
        cudaGraphInstantiate(&graphExec, graph, NULL, NULL, 0);
        cudaGraphLaunch(graphExec, 0);
        cudaDeviceSynchronize();
    
        cudaGraphExecDestroy(graphExec);
        cudaGraphDestroy(graph);
    }
    

######  6.2.8.7.8.5. Conditional SWITCH Nodes 

SWITCH nodes, added in CUDA 12.8, execute 1 of n different graphs within the conditional node. The nth graph will be executed when the SWITCH node is evaluated if the condition value is n. If the condition value is greater than or equal to n, no graph will be executed. The following diagram depicts a 3 node graph where the middle node, B, is a conditional node:

![_images/conditional-switch-node.png](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/conditional-switch-node.png)

Figure 25 Conditional SWITCH Node

The following code illustrates the creation of a graph containing a SWITCH conditional node. The value of the condition is set using an upstream kernel. The bodies of the conditional are populated using the [graph API](#creating-a-graph-using-graph-apis).
    
    
    __global__ void setHandle(cudaGraphConditionalHandle handle)
    {
        ...
        cudaGraphSetConditional(handle, value);
        ...
    }
    
    void graphSetup() {
        cudaGraph_t graph;
        cudaGraphExec_t graphExec;
        cudaGraphNode_t node;
        void *kernelArgs[1];
        int value = 1;
    
        cudaGraphCreate(&graph, 0);
    
        cudaGraphConditionalHandle handle;
        cudaGraphConditionalHandleCreate(&handle, graph);
    
        // Use a kernel upstream of the conditional to set the handle value
        cudaGraphNodeParams params = { cudaGraphNodeTypeKernel };
        params.kernel.func = (void *)setHandle;
        params.kernel.gridDim.x = params.kernel.gridDim.y = params.kernel.gridDim.z = 1;
        params.kernel.blockDim.x = params.kernel.blockDim.y = params.kernel.blockDim.z = 1;
        params.kernel.kernelParams = kernelArgs;
        kernelArgs[0] = &handle;
        cudaGraphAddNode(&node, graph, NULL, NULL, 0, &params);
    
        cudaGraphNodeParams cParams = { cudaGraphNodeTypeConditional };
        cParams.conditional.handle = handle;
        cParams.conditional.type   = cudaGraphCondTypeSwitch;
        cParams.conditional.size   = 5;
        cudaGraphAddNode(&node, graph, &node, NULL, 1, &cParams);
    
        cudaGraph_t *bodyGraphs = cParams.conditional.phGraph_out;
    
        // Populate the first body of the conditional node
        ...
        cudaGraphAddNode(&node, bodyGraphs[0], NULL, NULL, 0, &params);
        ...
        // Populate the last body of the conditional node
        cudaGraphAddNode(&node, bodyGraphs[4], NULL, NULL, 0, &params);
    
        cudaGraphInstantiate(&graphExec, graph, NULL, NULL, 0);
        cudaGraphLaunch(graphExec, 0);
        cudaDeviceSynchronize();
    
        cudaGraphExecDestroy(graphExec);
        cudaGraphDestroy(graph);
    }
    

####  6.2.8.8. Events 

The runtime also provides a way to closely monitor the device’s progress, as well as perform accurate timing, by letting the application asynchronously record _events_ at any point in the program, and query when these events are completed. An event has completed when all tasks - or optionally, all commands in a given stream - preceding the event have completed. Events in stream zero are completed after all preceding tasks and commands in all streams are completed.

#####  6.2.8.8.1. Creation and Destruction of Events 

The following code sample creates two events:
    
    
    cudaEvent_t start, stop;
    cudaEventCreate(&start);
    cudaEventCreate(&stop);
    

They are destroyed this way:
    
    
    cudaEventDestroy(start);
    cudaEventDestroy(stop);
    

#####  6.2.8.8.2. Elapsed Time 

The events created in [Creation and Destruction of Events](#creation-and-destruction-events) can be used to time the code sample of [Creation and Destruction of Streams](#creation-and-destruction-streams) the following way:
    
    
    cudaEventRecord(start, 0);
    for (int i = 0; i < 2; ++i) {
        cudaMemcpyAsync(inputDev + i * size, inputHost + i * size,
                        size, cudaMemcpyHostToDevice, stream[i]);
        MyKernel<<<100, 512, 0, stream[i]>>>
                   (outputDev + i * size, inputDev + i * size, size);
        cudaMemcpyAsync(outputHost + i * size, outputDev + i * size,
                        size, cudaMemcpyDeviceToHost, stream[i]);
    }
    cudaEventRecord(stop, 0);
    cudaEventSynchronize(stop);
    float elapsedTime;
    cudaEventElapsedTime(&elapsedTime, start, stop);
    

####  6.2.8.9. Synchronous Calls 

When a synchronous function is called, control is not returned to the host thread before the device has completed the requested task. Whether the host thread will then yield, block, or spin can be specified by calling `cudaSetDeviceFlags()`with some specific flags (see reference manual for details) before any other CUDA call is performed by the host thread.

###  6.2.9. Multi-Device System 

####  6.2.9.1. Device Enumeration 

A host system can have multiple devices. The following code sample shows how to enumerate these devices, query their properties, and determine the number of CUDA-enabled devices.
    
    
    int deviceCount;
    cudaGetDeviceCount(&deviceCount);
    int device;
    for (device = 0; device < deviceCount; ++device) {
        cudaDeviceProp deviceProp;
        cudaGetDeviceProperties(&deviceProp, device);
        printf("Device %d has compute capability %d.%d.\n",
               device, deviceProp.major, deviceProp.minor);
    }
    

####  6.2.9.2. Device Selection 

A host thread can set the device it operates on at any time by calling `cudaSetDevice()`. Device memory allocations and kernel launches are made on the currently set device; streams and events are created in association with the currently set device. If no call to `cudaSetDevice()` is made, the current device is device 0.

The following code sample illustrates how setting the current device affects memory allocation and kernel execution.
    
    
    size_t size = 1024 * sizeof(float);
    cudaSetDevice(0);            // Set device 0 as current
    float* p0;
    cudaMalloc(&p0, size);       // Allocate memory on device 0
    MyKernel<<<1000, 128>>>(p0); // Launch kernel on device 0
    cudaSetDevice(1);            // Set device 1 as current
    float* p1;
    cudaMalloc(&p1, size);       // Allocate memory on device 1
    MyKernel<<<1000, 128>>>(p1); // Launch kernel on device 1
    

####  6.2.9.3. Stream and Event Behavior 

A kernel launch will fail if it is issued to a stream that is not associated to the current device as illustrated in the following code sample.
    
    
    cudaSetDevice(0);               // Set device 0 as current
    cudaStream_t s0;
    cudaStreamCreate(&s0);          // Create stream s0 on device 0
    MyKernel<<<100, 64, 0, s0>>>(); // Launch kernel on device 0 in s0
    cudaSetDevice(1);               // Set device 1 as current
    cudaStream_t s1;
    cudaStreamCreate(&s1);          // Create stream s1 on device 1
    MyKernel<<<100, 64, 0, s1>>>(); // Launch kernel on device 1 in s1
    
    // This kernel launch will fail:
    MyKernel<<<100, 64, 0, s0>>>(); // Launch kernel on device 1 in s0
    

A memory copy will succeed even if it is issued to a stream that is not associated to the current device.

`cudaEventRecord()` will fail if the input event and input stream are associated to different devices.

`cudaEventElapsedTime()` will fail if the two input events are associated to different devices.

`cudaEventSynchronize()` and `cudaEventQuery()` will succeed even if the input event is associated to a device that is different from the current device.

`cudaStreamWaitEvent()` will succeed even if the input stream and input event are associated to different devices. `cudaStreamWaitEvent()` can therefore be used to synchronize multiple devices with each other.

Each device has its own default stream (see [Default Stream](#default-stream)), so commands issued to the default stream of a device may execute out of order or concurrently with respect to commands issued to the default stream of any other device.

####  6.2.9.4. Peer-to-Peer Memory Access 

Depending on the system properties, specifically the PCIe and/or NVLINK topology, devices are able to address each other’s memory (i.e., a kernel executing on one device can dereference a pointer to the memory of the other device). This peer-to-peer memory access feature is supported between two devices if `cudaDeviceCanAccessPeer()` returns true for these two devices.

Peer-to-peer memory access is only supported in 64-bit applications and must be enabled between two devices by calling `cudaDeviceEnablePeerAccess()` as illustrated in the following code sample. On non-NVSwitch enabled systems, each device can support a system-wide maximum of eight peer connections.

A unified address space is used for both devices (see [Unified Virtual Address Space](#unified-virtual-address-space)), so the same pointer can be used to address memory from both devices as shown in the code sample below.
    
    
    cudaSetDevice(0);                   // Set device 0 as current
    float* p0;
    size_t size = 1024 * sizeof(float);
    cudaMalloc(&p0, size);              // Allocate memory on device 0
    MyKernel<<<1000, 128>>>(p0);        // Launch kernel on device 0
    cudaSetDevice(1);                   // Set device 1 as current
    cudaDeviceEnablePeerAccess(0, 0);   // Enable peer-to-peer access
                                        // with device 0
    
    // Launch kernel on device 1
    // This kernel launch can access memory on device 0 at address p0
    MyKernel<<<1000, 128>>>(p0);
    

#####  6.2.9.4.1. IOMMU on Linux 

On Linux only, CUDA and the display driver does not support IOMMU-enabled bare-metal PCIe peer to peer memory copy. However, CUDA and the display driver does support IOMMU via VM pass through. As a consequence, users on Linux, when running on a native bare metal system, should disable the IOMMU. The IOMMU should be enabled and the VFIO driver be used as a PCIe pass through for virtual machines.

On Windows the above limitation does not exist.

See also [Allocating DMA Buffers on 64-bit Platforms](https://download.nvidia.com/XFree86/Linux-x86_64/396.51/README/dma_issues.html).

####  6.2.9.5. Peer-to-Peer Memory Copy 

Memory copies can be performed between the memories of two different devices.

When a unified address space is used for both devices (see [Unified Virtual Address Space](#unified-virtual-address-space)), this is done using the regular memory copy functions mentioned in [Device Memory](#device-memory).

Otherwise, this is done using `cudaMemcpyPeer()`, `cudaMemcpyPeerAsync()`, `cudaMemcpy3DPeer()`, or `cudaMemcpy3DPeerAsync()` as illustrated in the following code sample.
    
    
    cudaSetDevice(0);                   // Set device 0 as current
    float* p0;
    size_t size = 1024 * sizeof(float);
    cudaMalloc(&p0, size);              // Allocate memory on device 0
    cudaSetDevice(1);                   // Set device 1 as current
    float* p1;
    cudaMalloc(&p1, size);              // Allocate memory on device 1
    cudaSetDevice(0);                   // Set device 0 as current
    MyKernel<<<1000, 128>>>(p0);        // Launch kernel on device 0
    cudaSetDevice(1);                   // Set device 1 as current
    cudaMemcpyPeer(p1, 1, p0, 0, size); // Copy p0 to p1
    MyKernel<<<1000, 128>>>(p1);        // Launch kernel on device 1
    

A copy (in the implicit _NULL_ stream) between the memories of two different devices:

  * does not start until all commands previously issued to either device have completed and

  * runs to completion before any commands (see [Asynchronous Concurrent Execution](#asynchronous-concurrent-execution)) issued after the copy to either device can start.


Consistent with the normal behavior of streams, an asynchronous copy between the memories of two devices may overlap with copies or kernels in another stream.

Note that if peer-to-peer access is enabled between two devices via `cudaDeviceEnablePeerAccess()` as described in [Peer-to-Peer Memory Access](#peer-to-peer-memory-access), peer-to-peer memory copy between these two devices no longer needs to be staged through the host and is therefore faster.

###  6.2.10. Unified Virtual Address Space 

When the application is run as a 64-bit process, a single address space is used for the host and all the devices of compute capability 2.0 and higher. All host memory allocations made via CUDA API calls and all device memory allocations on supported devices are within this virtual address range. As a consequence:

  * The location of any memory on the host allocated through CUDA, or on any of the devices which use the unified address space, can be determined from the value of the pointer using `cudaPointerGetAttributes()`.

  * When copying to or from the memory of any device which uses the unified address space, the `cudaMemcpyKind` parameter of `cudaMemcpy*()` can be set to `cudaMemcpyDefault` to determine locations from the pointers. This also works for host pointers not allocated through CUDA, as long as the current device uses unified addressing.

  * Allocations via `cudaHostAlloc()` are automatically portable (see [Portable Memory](#portable-memory)) across all the devices for which the unified address space is used, and pointers returned by `cudaHostAlloc()` can be used directly from within kernels running on these devices (i.e., there is no need to obtain a device pointer via `cudaHostGetDevicePointer()` as described in [Mapped Memory](#mapped-memory).


Applications may query if the unified address space is used for a particular device by checking that the `unifiedAddressing` device property (see [Device Enumeration](#device-enumeration)) is equal to 1.

###  6.2.11. Interprocess Communication 

Any device memory pointer or event handle created by a host thread can be directly referenced by any other thread within the same process. It is not valid outside this process however, and therefore cannot be directly referenced by threads belonging to a different process.

To share device memory pointers and events across processes, an application must use the Inter Process Communication API, which is described in detail in the reference manual. The IPC API is only supported for 64-bit processes on Linux and for devices of compute capability 2.0 and higher. Note that the IPC API is not supported for `cudaMallocManaged` allocations.

Using this API, an application can get the IPC handle for a given device memory pointer using `cudaIpcGetMemHandle()`, pass it to another process using standard IPC mechanisms (for example, interprocess shared memory or files), and use `cudaIpcOpenMemHandle()` to retrieve a device pointer from the IPC handle that is a valid pointer within this other process. Event handles can be shared using similar entry points.

Note that allocations made by `cudaMalloc()` may be sub-allocated from a larger block of memory for performance reasons. In such case, CUDA IPC APIs will share the entire underlying memory block which may cause other sub-allocations to be shared, which can potentially lead to information disclosure between processes. To prevent this behavior, it is recommended to only share allocations with a 2MiB aligned size.

An example of using the IPC API is where a single primary process generates a batch of input data, making the data available to multiple secondary processes without requiring regeneration or copying.

Applications using CUDA IPC to communicate with each other should be compiled, linked, and run with the same CUDA driver and runtime.

Note

Since CUDA 11.5, only events-sharing IPC APIs are supported on L4T and embedded Linux Tegra devices with compute capability 7.x and higher. The memory-sharing IPC APIs are still not supported on Tegra platforms.

###  6.2.12. Error Checking 

All runtime functions return an error code, but for an asynchronous function (see [Asynchronous Concurrent Execution](#asynchronous-concurrent-execution)), this error code cannot possibly report any of the asynchronous errors that could occur on the device since the function returns before the device has completed the task; the error code only reports errors that occur on the host prior to executing the task, typically related to parameter validation; if an asynchronous error occurs, it will be reported by some subsequent unrelated runtime function call.

The only way to check for asynchronous errors just after some asynchronous function call is therefore to synchronize just after the call by calling `cudaDeviceSynchronize()` (or by using any other synchronization mechanisms described in [Asynchronous Concurrent Execution](#asynchronous-concurrent-execution)) and checking the error code returned by `cudaDeviceSynchronize()`.

The runtime maintains an error variable for each host thread that is initialized to `cudaSuccess` and is overwritten by the error code every time an error occurs (be it a parameter validation error or an asynchronous error). `cudaPeekAtLastError()` returns this variable. `cudaGetLastError()` returns this variable and resets it to `cudaSuccess`.

Kernel launches do not return any error code, so `cudaPeekAtLastError()` or `cudaGetLastError()` must be called just after the kernel launch to retrieve any pre-launch errors. To ensure that any error returned by `cudaPeekAtLastError()` or `cudaGetLastError()` does not originate from calls prior to the kernel launch, one has to make sure that the runtime error variable is set to `cudaSuccess` just before the kernel launch, for example, by calling `cudaGetLastError()` just before the kernel launch. Kernel launches are asynchronous, so to check for asynchronous errors, the application must synchronize in-between the kernel launch and the call to `cudaPeekAtLastError()` or `cudaGetLastError()`.

Note that `cudaErrorNotReady` that may be returned by `cudaStreamQuery()` and `cudaEventQuery()` is not considered an error and is therefore not reported by `cudaPeekAtLastError()` or `cudaGetLastError()`.

###  6.2.13. Call Stack 

On devices of compute capability 2.x and higher, the size of the call stack can be queried using`cudaDeviceGetLimit()` and set using `cudaDeviceSetLimit()`.

When the call stack overflows, the kernel call fails with a stack overflow error if the application is run via a CUDA debugger (CUDA-GDB, Nsight) or an unspecified launch error, otherwise. When the compiler cannot determine the stack size, it issues a warning saying Stack size cannot be statically determined. This is usually the case with recursive functions. Once this warning is issued, user will need to set stack size manually if default stack size is not sufficient.

###  6.2.14. Texture and Surface Memory 

CUDA supports a subset of the texturing hardware that the GPU uses for graphics to access texture and surface memory. Reading data from texture or surface memory instead of global memory can have several performance benefits as described in [Device Memory Accesses](#device-memory-accesses).

####  6.2.14.1. Texture Memory 

Texture memory is read from kernels using the device functions described in [Texture Functions](#texture-functions). The process of reading a texture calling one of these functions is called a _texture fetch_. Each texture fetch specifies a parameter called a _texture object_ for the texture object API.

The texture object specifies:

  * The _texture_ , which is the piece of texture memory that is fetched. Texture objects are created at runtime and the texture is specified when creating the texture object as described in [Texture Object API](#texture-object-api).

  * Its _dimensionality_ that specifies whether the texture is addressed as a one dimensional array using one texture coordinate, a two-dimensional array using two texture coordinates, or a three-dimensional array using three texture coordinates. Elements of the array are called _texels_ , short for _texture elements_. The _texture width_ , _height_ , and _depth_ refer to the size of the array in each dimension. [Table 27](#features-and-technical-specifications-technical-specifications-per-compute-capability) lists the maximum texture width, height, and depth depending on the compute capability of the device.

  * The type of a texel, which is restricted to the basic integer and single-precision floating-point types and any of the 1-, 2-, and 4-component vector types defined in [Built-in Vector Types](#built-in-vector-types) that are derived from the basic integer and single-precision floating-point types.

  * The _read mode_ , which is equal to `cudaReadModeNormalizedFloat` or `cudaReadModeElementType`. If it is `cudaReadModeNormalizedFloat` and the type of the texel is a 16-bit or 8-bit integer type, the value returned by the texture fetch is actually returned as floating-point type and the full range of the integer type is mapped to [0.0, 1.0] for unsigned integer type and [-1.0, 1.0] for signed integer type; for example, an unsigned 8-bit texture element with the value 0xff reads as 1. If it is `cudaReadModeElementType`, no conversion is performed.

  * Whether texture coordinates are normalized or not. By default, textures are referenced (by the functions of [Texture Functions](#texture-functions)) using floating-point coordinates in the range [0, N-1] where N is the size of the texture in the dimension corresponding to the coordinate. For example, a texture that is 64x32 in size will be referenced with coordinates in the range [0, 63] and [0, 31] for the x and y dimensions, respectively. Normalized texture coordinates cause the coordinates to be specified in the range [0.0, 1.0-1/N] instead of [0, N-1], so the same 64x32 texture would be addressed by normalized coordinates in the range [0, 1-1/N] in both the x and y dimensions. Normalized texture coordinates are a natural fit to some applications’ requirements, if it is preferable for the texture coordinates to be independent of the texture size.

  * The _addressing mode_. It is valid to call the device functions of Section B.8 with coordinates that are out of range. The addressing mode defines what happens in that case. The default addressing mode is to clamp the coordinates to the valid range: [0, N) for non-normalized coordinates and [0.0, 1.0) for normalized coordinates. If the border mode is specified instead, texture fetches with out-of-range texture coordinates return zero. For normalized coordinates, the wrap mode and the mirror mode are also available. When using the wrap mode, each coordinate x is converted to _frac(x)=x - floor(x)_ where _floor(x)_ is the largest integer not greater than _x_. When using the mirror mode, each coordinate _x_ is converted to _frac(x)_ if _floor(x)_ is even and _1-frac(x)_ if _floor(x)_ is odd. The addressing mode is specified as an array of size three whose first, second, and third elements specify the addressing mode for the first, second, and third texture coordinates, respectively; the addressing mode are `cudaAddressModeBorder`, `cudaAddressModeClamp`, `cudaAddressModeWrap`, and `cudaAddressModeMirror`; `cudaAddressModeWrap` and `cudaAddressModeMirror` are only supported for normalized texture coordinates

  * The _filtering_ mode which specifies how the value returned when fetching the texture is computed based on the input texture coordinates. Linear texture filtering may be done only for textures that are configured to return floating-point data. It performs low-precision interpolation between neighboring texels. When enabled, the texels surrounding a texture fetch location are read and the return value of the texture fetch is interpolated based on where the texture coordinates fell between the texels. Simple linear interpolation is performed for one-dimensional textures, bilinear interpolation for two-dimensional textures, and trilinear interpolation for three-dimensional textures. [Texture Fetching](#texture-fetching) gives more details on texture fetching. The filtering mode is equal to `cudaFilterModePoint` or `cudaFilterModeLinear`. If it is `cudaFilterModePoint`, the returned value is the texel whose texture coordinates are the closest to the input texture coordinates. If it is `cudaFilterModeLinear`, the returned value is the linear interpolation of the two (for a one-dimensional texture), four (for a two dimensional texture), or eight (for a three dimensional texture) texels whose texture coordinates are the closest to the input texture coordinates. `cudaFilterModeLinear` is only valid for returned values of floating-point type.


[Texture Object API](#texture-object-api) introduces the texture object API.

[16-Bit Floating-Point Textures](#sixteen-bit-floating-point-textures) explains how to deal with 16-bit floating-point textures.

Textures can also be layered as described in [Layered Textures](#layered-textures).

[Cubemap Textures](#cubemap-textures) and [Cubemap Layered Textures](#cubemap-layered-textures) describe a special type of texture, the cubemap texture.

[Texture Gather](#texture-gather) describes a special texture fetch, texture gather.

#####  6.2.14.1.1. Texture Object API 

A texture object is created using `cudaCreateTextureObject()` from a resource description of type `struct cudaResourceDesc`, which specifies the texture, and from a texture description defined as such:
    
    
    struct cudaTextureDesc
    {
        enum cudaTextureAddressMode addressMode[3];
        enum cudaTextureFilterMode  filterMode;
        enum cudaTextureReadMode    readMode;
        int                         sRGB;
        int                         normalizedCoords;
        unsigned int                maxAnisotropy;
        enum cudaTextureFilterMode  mipmapFilterMode;
        float                       mipmapLevelBias;
        float                       minMipmapLevelClamp;
        float                       maxMipmapLevelClamp;
    };
    

  * `addressMode` specifies the addressing mode;

  * `filterMode` specifies the filter mode;

  * `readMode` specifies the read mode;

  * `normalizedCoords` specifies whether texture coordinates are normalized or not;

  * See reference manual for `sRGB`, `maxAnisotropy`, `mipmapFilterMode`, `mipmapLevelBias`, `minMipmapLevelClamp`, and `maxMipmapLevelClamp`.


The following code sample applies some simple transformation kernel to a texture.
    
    
    // Simple transformation kernel
    __global__ void transformKernel(float* output,
                                    cudaTextureObject_t texObj,
                                    int width, int height,
                                    float theta)
    {
        // Calculate normalized texture coordinates
        unsigned int x = blockIdx.x * blockDim.x + threadIdx.x;
        unsigned int y = blockIdx.y * blockDim.y + threadIdx.y;
    
        float u = x / (float)width;
        float v = y / (float)height;
    
        // Transform coordinates
        u -= 0.5f;
        v -= 0.5f;
        float tu = u * cosf(theta) - v * sinf(theta) + 0.5f;
        float tv = v * cosf(theta) + u * sinf(theta) + 0.5f;
    
        // Read from texture and write to global memory
        output[y * width + x] = tex2D<float>(texObj, tu, tv);
    }
    
    
    
    // Host code
    int main()
    {
        const int height = 1024;
        const int width = 1024;
        float angle = 0.5;
    
        // Allocate and set some host data
        float *h_data = (float *)std::malloc(sizeof(float) * width * height);
        for (int i = 0; i < height * width; ++i)
            h_data[i] = i;
    
        // Allocate CUDA array in device memory
        cudaChannelFormatDesc channelDesc =
            cudaCreateChannelDesc(32, 0, 0, 0, cudaChannelFormatKindFloat);
        cudaArray_t cuArray;
        cudaMallocArray(&cuArray, &channelDesc, width, height);
    
        // Set pitch of the source (the width in memory in bytes of the 2D array pointed
        // to by src, including padding), we dont have any padding
        const size_t spitch = width * sizeof(float);
        // Copy data located at address h_data in host memory to device memory
        cudaMemcpy2DToArray(cuArray, 0, 0, h_data, spitch, width * sizeof(float),
                            height, cudaMemcpyHostToDevice);
    
        // Specify texture
        struct cudaResourceDesc resDesc;
        memset(&resDesc, 0, sizeof(resDesc));
        resDesc.resType = cudaResourceTypeArray;
        resDesc.res.array.array = cuArray;
    
        // Specify texture object parameters
        struct cudaTextureDesc texDesc;
        memset(&texDesc, 0, sizeof(texDesc));
        texDesc.addressMode[0] = cudaAddressModeWrap;
        texDesc.addressMode[1] = cudaAddressModeWrap;
        texDesc.filterMode = cudaFilterModeLinear;
        texDesc.readMode = cudaReadModeElementType;
        texDesc.normalizedCoords = 1;
    
        // Create texture object
        cudaTextureObject_t texObj = 0;
        cudaCreateTextureObject(&texObj, &resDesc, &texDesc, NULL);
    
        // Allocate result of transformation in device memory
        float *output;
        cudaMalloc(&output, width * height * sizeof(float));
    
        // Invoke kernel
        dim3 threadsperBlock(16, 16);
        dim3 numBlocks((width + threadsperBlock.x - 1) / threadsperBlock.x,
                        (height + threadsperBlock.y - 1) / threadsperBlock.y);
        transformKernel<<<numBlocks, threadsperBlock>>>(output, texObj, width, height,
                                                        angle);
        // Copy data from device back to host
        cudaMemcpy(h_data, output, width * height * sizeof(float),
                    cudaMemcpyDeviceToHost);
    
        // Destroy texture object
        cudaDestroyTextureObject(texObj);
    
        // Free device memory
        cudaFreeArray(cuArray);
        cudaFree(output);
    
        // Free host memory
        free(h_data);
    
        return 0;
    }
    

#####  6.2.14.1.2. 16-Bit Floating-Point Textures 

The 16-bit floating-point or _half_ format supported by CUDA arrays is the same as the IEEE 754-2008 binary2 format.

CUDA C++ does not support a matching data type, but provides intrinsic functions to convert to and from the 32-bit floating-point format via the `unsigned short` type: `__float2half_rn(float)` and `__half2float(unsigned short)`. These functions are only supported in device code. Equivalent functions for the host code can be found in the OpenEXR library, for example.

16-bit floating-point components are promoted to 32 bit float during texture fetching before any filtering is performed.

A channel description for the 16-bit floating-point format can be created by calling one of the `cudaCreateChannelDescHalf*()` functions.

#####  6.2.14.1.3. Layered Textures 

A one-dimensional or two-dimensional layered texture (also known as _texture array_ in Direct3D and _array texture_ in OpenGL) is a texture made up of a sequence of layers, all of which are regular textures of same dimensionality, size, and data type.

A one-dimensional layered texture is addressed using an integer index and a floating-point texture coordinate; the index denotes a layer within the sequence and the coordinate addresses a texel within that layer. A two-dimensional layered texture is addressed using an integer index and two floating-point texture coordinates; the index denotes a layer within the sequence and the coordinates address a texel within that layer.

A layered texture can only be a CUDA array by calling `cudaMalloc3DArray()` with the `cudaArrayLayered` flag (and a height of zero for one-dimensional layered texture).

Layered textures are fetched using the device functions described in [tex1DLayered()](#tex1dlayered-object) and [tex2DLayered()](#tex2dlayered-object). Texture filtering (see [Texture Fetching](#texture-fetching)) is done only within a layer, not across layers.

Layered textures are only supported on devices of compute capability 2.0 and higher.

#####  6.2.14.1.4. Cubemap Textures 

A _cubemap_ texture is a special type of two-dimensional layered texture that has six layers representing the faces of a cube:

  * The width of a layer is equal to its height.

  * The cubemap is addressed using three texture coordinates _x_ , _y_ , and _z_ that are interpreted as a direction vector emanating from the center of the cube and pointing to one face of the cube and a texel within the layer corresponding to that face. More specifically, the face is selected by the coordinate with largest magnitude _m_ and the corresponding layer is addressed using coordinates _(s/m+1)/2_ and _(t/m+1)/2_ where _s_ and _t_ are defined in [Table 6](#cubemap-textures-cubemap-fetch).


Table 6 Cubemap Fetch | face | m | s | t  
---|---|---|---|---  
`|x| > |y|` and `|x| > |z|` | x ≥ 0 | 0 | x | -z | -y  
x < 0 | 1 | -x | z | -y  
`|y| > |x|` and `|y| > |z|` | y ≥ 0 | 2 | y | x | z  
y < 0 | 3 | -y | x | -z  
`|z| > |x|` and `|z| > |y|` | z ≥ 0 | 4 | z | x | -y  
z < 0 | 5 | -z | -x | -y  
  
A cubemap texture can only be a CUDA array by calling `cudaMalloc3DArray()` with the `cudaArrayCubemap` flag.

Cubemap textures are fetched using the device function described in [texCubemap()](#texcubemap-object).

Cubemap textures are only supported on devices of compute capability 2.0 and higher.

#####  6.2.14.1.5. Cubemap Layered Textures 

A _cubemap layered_ texture is a layered texture whose layers are cubemaps of same dimension.

A cubemap layered texture is addressed using an integer index and three floating-point texture coordinates; the index denotes a cubemap within the sequence and the coordinates address a texel within that cubemap.

A cubemap layered texture can only be a CUDA array by calling `cudaMalloc3DArray()` with the `cudaArrayLayered` and `cudaArrayCubemap` flags.

Cubemap layered textures are fetched using the device function described in [texCubemapLayered()](#texcubemaplayered-object). Texture filtering (see [Texture Fetching](#texture-fetching)) is done only within a layer, not across layers.

Cubemap layered textures are only supported on devices of compute capability 2.0 and higher.

#####  6.2.14.1.6. Texture Gather 

Texture gather is a special texture fetch that is available for two-dimensional textures only. It is performed by the `tex2Dgather()` function, which has the same parameters as `tex2D()`, plus an additional `comp` parameter equal to 0, 1, 2, or 3 (see [tex2Dgather()](#tex2dgather-object)). It returns four 32-bit numbers that correspond to the value of the component `comp` of each of the four texels that would have been used for bilinear filtering during a regular texture fetch. For example, if these texels are of values (253, 20, 31, 255), (250, 25, 29, 254), (249, 16, 37, 253), (251, 22, 30, 250), and `comp` is 2, `tex2Dgather()` returns (31, 29, 37, 30).

Note that texture coordinates are computed with only 8 bits of fractional precision. `tex2Dgather()` may therefore return unexpected results for cases where `tex2D()` would use 1.0 for one of its weights (α or β, see [Linear Filtering](#linear-filtering)). For example, with an _x_ texture coordinate of 2.49805: _xB=x-0.5=1.99805_ , however the fractional part of _xB_ is stored in an 8-bit fixed-point format. Since 0.99805 is closer to 256.f/256.f than it is to 255.f/256.f, _xB_ has the value 2. A `tex2Dgather()` in this case would therefore return indices 2 and 3 in _x_ , instead of indices 1 and 2.

Texture gather is only supported for CUDA arrays created with the `cudaArrayTextureGather` flag and of width and height less than the maximum specified in [Table 27](#features-and-technical-specifications-technical-specifications-per-compute-capability) for texture gather, which is smaller than for regular texture fetch.

Texture gather is only supported on devices of compute capability 2.0 and higher.

####  6.2.14.2. Surface Memory 

For devices of compute capability 2.0 and higher, a CUDA array (described in [Cubemap Surfaces](#cubemap-surfaces)), created with the `cudaArraySurfaceLoadStore` flag, can be read and written via a _surface object_ using the functions described in [Surface Functions](#surface-functions).

[Table 27](#features-and-technical-specifications-technical-specifications-per-compute-capability) lists the maximum surface width, height, and depth depending on the compute capability of the device.

#####  6.2.14.2.1. Surface Object API 

A surface object is created using `cudaCreateSurfaceObject()` from a resource description of type `struct cudaResourceDesc`. Unlike texture memory, surface memory uses byte addressing. This means that the x-coordinate used to access a texture element via texture functions needs to be multiplied by the byte size of the element to access the same element via a surface function. For example, the element at texture coordinate x of a one-dimensional floating-point CUDA array bound to a texture object `texObj` and a surface object `surfObj` is read using `tex1d(texObj, x)` via `texObj`, but `surf1Dread(surfObj, 4*x)` via `surfObj`. Similarly, the element at texture coordinate x and y of a two-dimensional floating-point CUDA array bound to a texture object `texObj` and a surface object `surfObj` is accessed using `tex2d(texObj, x, y)` via `texObj`, but `surf2Dread(surfObj, 4*x, y)` via `surObj` (the byte offset of the y-coordinate is internally calculated from the underlying line pitch of the CUDA array).

The following code sample applies some simple transformation kernel to a surface.
    
    
    // Simple copy kernel
    __global__ void copyKernel(cudaSurfaceObject_t inputSurfObj,
                               cudaSurfaceObject_t outputSurfObj,
                               int width, int height)
    {
        // Calculate surface coordinates
        unsigned int x = blockIdx.x * blockDim.x + threadIdx.x;
        unsigned int y = blockIdx.y * blockDim.y + threadIdx.y;
        if (x < width && y < height) {
            uchar4 data;
            // Read from input surface
            surf2Dread(&data,  inputSurfObj, x * 4, y);
            // Write to output surface
            surf2Dwrite(data, outputSurfObj, x * 4, y);
        }
    }
    
    // Host code
    int main()
    {
        const int height = 1024;
        const int width = 1024;
    
        // Allocate and set some host data
        unsigned char *h_data =
            (unsigned char *)std::malloc(sizeof(unsigned char) * width * height * 4);
        for (int i = 0; i < height * width * 4; ++i)
            h_data[i] = i;
    
        // Allocate CUDA arrays in device memory
        cudaChannelFormatDesc channelDesc =
            cudaCreateChannelDesc(8, 8, 8, 8, cudaChannelFormatKindUnsigned);
        cudaArray_t cuInputArray;
        cudaMallocArray(&cuInputArray, &channelDesc, width, height,
                        cudaArraySurfaceLoadStore);
        cudaArray_t cuOutputArray;
        cudaMallocArray(&cuOutputArray, &channelDesc, width, height,
                        cudaArraySurfaceLoadStore);
    
        // Set pitch of the source (the width in memory in bytes of the 2D array
        // pointed to by src, including padding), we dont have any padding
        const size_t spitch = 4 * width * sizeof(unsigned char);
        // Copy data located at address h_data in host memory to device memory
        cudaMemcpy2DToArray(cuInputArray, 0, 0, h_data, spitch,
                            4 * width * sizeof(unsigned char), height,
                            cudaMemcpyHostToDevice);
    
        // Specify surface
        struct cudaResourceDesc resDesc;
        memset(&resDesc, 0, sizeof(resDesc));
        resDesc.resType = cudaResourceTypeArray;
    
        // Create the surface objects
        resDesc.res.array.array = cuInputArray;
        cudaSurfaceObject_t inputSurfObj = 0;
        cudaCreateSurfaceObject(&inputSurfObj, &resDesc);
        resDesc.res.array.array = cuOutputArray;
        cudaSurfaceObject_t outputSurfObj = 0;
        cudaCreateSurfaceObject(&outputSurfObj, &resDesc);
    
        // Invoke kernel
        dim3 threadsperBlock(16, 16);
        dim3 numBlocks((width + threadsperBlock.x - 1) / threadsperBlock.x,
                        (height + threadsperBlock.y - 1) / threadsperBlock.y);
        copyKernel<<<numBlocks, threadsperBlock>>>(inputSurfObj, outputSurfObj, width,
                                                    height);
    
        // Copy data from device back to host
        cudaMemcpy2DFromArray(h_data, spitch, cuOutputArray, 0, 0,
                                4 * width * sizeof(unsigned char), height,
                                cudaMemcpyDeviceToHost);
    
        // Destroy surface objects
        cudaDestroySurfaceObject(inputSurfObj);
        cudaDestroySurfaceObject(outputSurfObj);
    
        // Free device memory
        cudaFreeArray(cuInputArray);
        cudaFreeArray(cuOutputArray);
    
        // Free host memory
        free(h_data);
    
      return 0;
    }
    

#####  6.2.14.2.2. Cubemap Surfaces 

Cubemap surfaces are accessed using`surfCubemapread()` and `surfCubemapwrite()` ([surfCubemapread()](#surfcubemapread-object) and [surfCubemapwrite()](#surfcubemapwrite-object)) as a two-dimensional layered surface, i.e., using an integer index denoting a face and two floating-point texture coordinates addressing a texel within the layer corresponding to this face. Faces are ordered as indicated in [Table 6](#cubemap-textures-cubemap-fetch).

#####  6.2.14.2.3. Cubemap Layered Surfaces 

Cubemap layered surfaces are accessed using `surfCubemapLayeredread()` and `surfCubemapLayeredwrite()` ([surfCubemapLayeredread()](#surfcubemaplayeredread-object) and [surfCubemapLayeredwrite()](#surfcubemaplayeredwrite-object)) as a two-dimensional layered surface, i.e., using an integer index denoting a face of one of the cubemaps and two floating-point texture coordinates addressing a texel within the layer corresponding to this face. Faces are ordered as indicated in [Table 6](#cubemap-textures-cubemap-fetch), so index ((2 * 6) + 3), for example, accesses the fourth face of the third cubemap.

####  6.2.14.3. CUDA Arrays 

CUDA arrays are opaque memory layouts optimized for texture fetching. They are one dimensional, two dimensional, or three-dimensional and composed of elements, each of which has 1, 2 or 4 components that may be signed or unsigned 8-, 16-, or 32-bit integers, 16-bit floats, or 32-bit floats. CUDA arrays are only accessible by kernels through texture fetching as described in [Texture Memory](#texture-memory) or surface reading and writing as described in [Surface Memory](#surface-memory).

####  6.2.14.4. Read/Write Coherency 

The texture and surface memory is cached (see [Device Memory Accesses](#device-memory-accesses)) and within the same kernel call, the cache is not kept coherent with respect to global memory writes and surface memory writes, so any texture fetch or surface read to an address that has been written to via a global write or a surface write in the same kernel call returns undefined data. In other words, a thread can safely read some texture or surface memory location only if this memory location has been updated by a previous kernel call or memory copy, but not if it has been previously updated by the same thread or another thread from the same kernel call.

###  6.2.15. Graphics Interoperability 

Some resources from OpenGL and Direct3D may be mapped into the address space of CUDA, either to enable CUDA to read data written by OpenGL or Direct3D, or to enable CUDA to write data for consumption by OpenGL or Direct3D.

A resource must be registered to CUDA before it can be mapped using the functions mentioned in [OpenGL Interoperability](#opengl-interoperability) and [Direct3D Interoperability](#direct3d-interoperability). These functions return a pointer to a CUDA graphics resource of type `struct cudaGraphicsResource`. Registering a resource is potentially high-overhead and therefore typically called only once per resource. A CUDA graphics resource is unregistered using `cudaGraphicsUnregisterResource()`. Each CUDA context which intends to use the resource is required to register it separately.

Once a resource is registered to CUDA, it can be mapped and unmapped as many times as necessary using `cudaGraphicsMapResources()` and `cudaGraphicsUnmapResources()`. `cudaGraphicsResourceSetMapFlags()` can be called to specify usage hints (write-only, read-only) that the CUDA driver can use to optimize resource management.

A mapped resource can be read from or written to by kernels using the device memory address returned by `cudaGraphicsResourceGetMappedPointer()` for buffers and`cudaGraphicsSubResourceGetMappedArray()` for CUDA arrays.

Accessing a resource through OpenGL, Direct3D, or another CUDA context while it is mapped produces undefined results. [OpenGL Interoperability](#opengl-interoperability) and [Direct3D Interoperability](#direct3d-interoperability) give specifics for each graphics API and some code samples. [SLI Interoperability](#sli-interoperability) gives specifics for when the system is in SLI mode.

####  6.2.15.1. OpenGL Interoperability 

The OpenGL resources that may be mapped into the address space of CUDA are OpenGL buffer, texture, and renderbuffer objects.

A buffer object is registered using `cudaGraphicsGLRegisterBuffer()`. In CUDA, it appears as a device pointer and can therefore be read and written by kernels or via `cudaMemcpy()` calls.

A texture or renderbuffer object is registered using `cudaGraphicsGLRegisterImage()`. In CUDA, it appears as a CUDA array. Kernels can read from the array by binding it to a texture or surface reference. They can also write to it via the surface write functions if the resource has been registered with the `cudaGraphicsRegisterFlagsSurfaceLoadStore` flag. The array can also be read and written via `cudaMemcpy2D()` calls. `cudaGraphicsGLRegisterImage()` supports all texture formats with 1, 2, or 4 components and an internal type of float (for example, `GL_RGBA_FLOAT32`), normalized integer (for example, `GL_RGBA8, GL_INTENSITY16`), and unnormalized integer (for example, `GL_RGBA8UI`) (please note that since unnormalized integer formats require OpenGL 3.0, they can only be written by shaders, not the fixed function pipeline).

The OpenGL context whose resources are being shared has to be current to the host thread making any OpenGL interoperability API calls.

Please note: When an OpenGL texture is made bindless (say for example by requesting an image or texture handle using the `glGetTextureHandle`*/`glGetImageHandle`* APIs) it cannot be registered with CUDA. The application needs to register the texture for interop before requesting an image or texture handle.

The following code sample uses a kernel to dynamically modify a 2D `width` x `height` grid of vertices stored in a vertex buffer object:
    
    
    GLuint positionsVBO;
    struct cudaGraphicsResource* positionsVBO_CUDA;
    
    int main()
    {
        // Initialize OpenGL and GLUT for device 0
        // and make the OpenGL context current
        ...
        glutDisplayFunc(display);
    
        // Explicitly set device 0
        cudaSetDevice(0);
    
        // Create buffer object and register it with CUDA
        glGenBuffers(1, &positionsVBO);
        glBindBuffer(GL_ARRAY_BUFFER, positionsVBO);
        unsigned int size = width * height * 4 * sizeof(float);
        glBufferData(GL_ARRAY_BUFFER, size, 0, GL_DYNAMIC_DRAW);
        glBindBuffer(GL_ARRAY_BUFFER, 0);
        cudaGraphicsGLRegisterBuffer(&positionsVBO_CUDA,
                                     positionsVBO,
                                     cudaGraphicsMapFlagsWriteDiscard);
    
        // Launch rendering loop
        glutMainLoop();
    
        ...
    }
    
    void display()
    {
        // Map buffer object for writing from CUDA
        float4* positions;
        cudaGraphicsMapResources(1, &positionsVBO_CUDA, 0);
        size_t num_bytes;
        cudaGraphicsResourceGetMappedPointer((void**)&positions,
                                             &num_bytes,
                                             positionsVBO_CUDA));
    
        // Execute kernel
        dim3 dimBlock(16, 16, 1);
        dim3 dimGrid(width / dimBlock.x, height / dimBlock.y, 1);
        createVertices<<<dimGrid, dimBlock>>>(positions, time,
                                              width, height);
    
        // Unmap buffer object
        cudaGraphicsUnmapResources(1, &positionsVBO_CUDA, 0);
    
        // Render from buffer object
        glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);
        glBindBuffer(GL_ARRAY_BUFFER, positionsVBO);
        glVertexPointer(4, GL_FLOAT, 0, 0);
        glEnableClientState(GL_VERTEX_ARRAY);
        glDrawArrays(GL_POINTS, 0, width * height);
        glDisableClientState(GL_VERTEX_ARRAY);
    
        // Swap buffers
        glutSwapBuffers();
        glutPostRedisplay();
    }
    
    
    
    void deleteVBO()
    {
        cudaGraphicsUnregisterResource(positionsVBO_CUDA);
        glDeleteBuffers(1, &positionsVBO);
    }
    
    __global__ void createVertices(float4* positions, float time,
                                   unsigned int width, unsigned int height)
    {
        unsigned int x = blockIdx.x * blockDim.x + threadIdx.x;
        unsigned int y = blockIdx.y * blockDim.y + threadIdx.y;
    
        // Calculate uv coordinates
        float u = x / (float)width;
        float v = y / (float)height;
        u = u * 2.0f - 1.0f;
        v = v * 2.0f - 1.0f;
    
        // calculate simple sine wave pattern
        float freq = 4.0f;
        float w = sinf(u * freq + time)
                * cosf(v * freq + time) * 0.5f;
    
        // Write positions
        positions[y * width + x] = make_float4(u, w, v, 1.0f);
    }
    

On Windows and for Quadro GPUs, `cudaWGLGetDevice()` can be used to retrieve the CUDA device associated to the handle returned by `wglEnumGpusNV()`. Quadro GPUs offer higher performance OpenGL interoperability than GeForce and Tesla GPUs in a multi-GPU configuration where OpenGL rendering is performed on the Quadro GPU and CUDA computations are performed on other GPUs in the system.

####  6.2.15.2. Direct3D Interoperability 

Direct3D interoperability is supported for Direct3D 9Ex, Direct3D 10, and Direct3D 11.

A CUDA context may interoperate only with Direct3D devices that fulfill the following criteria: Direct3D 9Ex devices must be created with `DeviceType` set to `D3DDEVTYPE_HAL` and `BehaviorFlags` with the `D3DCREATE_HARDWARE_VERTEXPROCESSING` flag; Direct3D 10 and Direct3D 11 devices must be created with `DriverType` set to `D3D_DRIVER_TYPE_HARDWARE`.

The Direct3D resources that may be mapped into the address space of CUDA are Direct3D buffers, textures, and surfaces. These resources are registered using `cudaGraphicsD3D9RegisterResource()`, `cudaGraphicsD3D10RegisterResource()`, and `cudaGraphicsD3D11RegisterResource()`.

The following code sample uses a kernel to dynamically modify a 2D `width` x `height` grid of vertices stored in a vertex buffer object.

#####  6.2.15.2.1. Direct3D 9 Version 
    
    
    IDirect3D9* D3D;
    IDirect3DDevice9* device;
    struct CUSTOMVERTEX {
        FLOAT x, y, z;
        DWORD color;
    };
    IDirect3DVertexBuffer9* positionsVB;
    struct cudaGraphicsResource* positionsVB_CUDA;
    
    int main()
    {
        int dev;
        // Initialize Direct3D
        D3D = Direct3DCreate9Ex(D3D_SDK_VERSION);
    
        // Get a CUDA-enabled adapter
        unsigned int adapter = 0;
        for (; adapter < g_pD3D->GetAdapterCount(); adapter++) {
            D3DADAPTER_IDENTIFIER9 adapterId;
            g_pD3D->GetAdapterIdentifier(adapter, 0, &adapterId);
            if (cudaD3D9GetDevice(&dev, adapterId.DeviceName)
                == cudaSuccess)
                break;
        }
    
         // Create device
        ...
        D3D->CreateDeviceEx(adapter, D3DDEVTYPE_HAL, hWnd,
                            D3DCREATE_HARDWARE_VERTEXPROCESSING,
                            &params, NULL, &device);
    
        // Use the same device
        cudaSetDevice(dev);
    
        // Create vertex buffer and register it with CUDA
        unsigned int size = width * height * sizeof(CUSTOMVERTEX);
        device->CreateVertexBuffer(size, 0, D3DFVF_CUSTOMVERTEX,
                                   D3DPOOL_DEFAULT, &positionsVB, 0);
        cudaGraphicsD3D9RegisterResource(&positionsVB_CUDA,
                                         positionsVB,
                                         cudaGraphicsRegisterFlagsNone);
        cudaGraphicsResourceSetMapFlags(positionsVB_CUDA,
                                        cudaGraphicsMapFlagsWriteDiscard);
    
        // Launch rendering loop
        while (...) {
            ...
            Render();
            ...
        }
        ...
    }
    
    
    
    void Render()
    {
        // Map vertex buffer for writing from CUDA
        float4* positions;
        cudaGraphicsMapResources(1, &positionsVB_CUDA, 0);
        size_t num_bytes;
        cudaGraphicsResourceGetMappedPointer((void**)&positions,
                                             &num_bytes,
                                             positionsVB_CUDA));
    
        // Execute kernel
        dim3 dimBlock(16, 16, 1);
        dim3 dimGrid(width / dimBlock.x, height / dimBlock.y, 1);
        createVertices<<<dimGrid, dimBlock>>>(positions, time,
                                              width, height);
    
        // Unmap vertex buffer
        cudaGraphicsUnmapResources(1, &positionsVB_CUDA, 0);
    
        // Draw and present
        ...
    }
    
    void releaseVB()
    {
        cudaGraphicsUnregisterResource(positionsVB_CUDA);
        positionsVB->Release();
    }
    
    __global__ void createVertices(float4* positions, float time,
                                   unsigned int width, unsigned int height)
    {
        unsigned int x = blockIdx.x * blockDim.x + threadIdx.x;
        unsigned int y = blockIdx.y * blockDim.y + threadIdx.y;
    
        // Calculate uv coordinates
        float u = x / (float)width;
        float v = y / (float)height;
        u = u * 2.0f - 1.0f;
        v = v * 2.0f - 1.0f;
    
        // Calculate simple sine wave pattern
        float freq = 4.0f;
        float w = sinf(u * freq + time)
                * cosf(v * freq + time) * 0.5f;
    
        // Write positions
        positions[y * width + x] =
                    make_float4(u, w, v, __int_as_float(0xff00ff00));
    }
    

#####  6.2.15.2.2. Direct3D 10 Version 
    
    
    ID3D10Device* device;
    struct CUSTOMVERTEX {
        FLOAT x, y, z;
        DWORD color;
    };
    ID3D10Buffer* positionsVB;
    struct cudaGraphicsResource* positionsVB_CUDA;
    
    int main()
    {
        int dev;
        // Get a CUDA-enabled adapter
        IDXGIFactory* factory;
        CreateDXGIFactory(__uuidof(IDXGIFactory), (void**)&factory);
        IDXGIAdapter* adapter = 0;
        for (unsigned int i = 0; !adapter; ++i) {
            if (FAILED(factory->EnumAdapters(i, &adapter))
                break;
            if (cudaD3D10GetDevice(&dev, adapter) == cudaSuccess)
                break;
            adapter->Release();
        }
        factory->Release();
    
        // Create swap chain and device
        ...
        D3D10CreateDeviceAndSwapChain(adapter,
                                      D3D10_DRIVER_TYPE_HARDWARE, 0,
                                      D3D10_CREATE_DEVICE_DEBUG,
                                      D3D10_SDK_VERSION,
                                      &swapChainDesc, &swapChain,
                                      &device);
        adapter->Release();
    
        // Use the same device
        cudaSetDevice(dev);
    
        // Create vertex buffer and register it with CUDA
        unsigned int size = width * height * sizeof(CUSTOMVERTEX);
        D3D10_BUFFER_DESC bufferDesc;
        bufferDesc.Usage          = D3D10_USAGE_DEFAULT;
        bufferDesc.ByteWidth      = size;
        bufferDesc.BindFlags      = D3D10_BIND_VERTEX_BUFFER;
        bufferDesc.CPUAccessFlags = 0;
        bufferDesc.MiscFlags      = 0;
        device->CreateBuffer(&bufferDesc, 0, &positionsVB);
        cudaGraphicsD3D10RegisterResource(&positionsVB_CUDA,
                                          positionsVB,
                                          cudaGraphicsRegisterFlagsNone);
                                          cudaGraphicsResourceSetMapFlags(positionsVB_CUDA,
                                          cudaGraphicsMapFlagsWriteDiscard);
    
        // Launch rendering loop
        while (...) {
            ...
            Render();
            ...
        }
        ...
    }
    
    
    
    void Render()
    {
        // Map vertex buffer for writing from CUDA
        float4* positions;
        cudaGraphicsMapResources(1, &positionsVB_CUDA, 0);
        size_t num_bytes;
        cudaGraphicsResourceGetMappedPointer((void**)&positions,
                                             &num_bytes,
                                             positionsVB_CUDA));
    
        // Execute kernel
        dim3 dimBlock(16, 16, 1);
        dim3 dimGrid(width / dimBlock.x, height / dimBlock.y, 1);
        createVertices<<<dimGrid, dimBlock>>>(positions, time,
                                              width, height);
    
        // Unmap vertex buffer
        cudaGraphicsUnmapResources(1, &positionsVB_CUDA, 0);
    
        // Draw and present
        ...
    }
    
    void releaseVB()
    {
        cudaGraphicsUnregisterResource(positionsVB_CUDA);
        positionsVB->Release();
    }
    
    __global__ void createVertices(float4* positions, float time,
                                   unsigned int width, unsigned int height)
    {
        unsigned int x = blockIdx.x * blockDim.x + threadIdx.x;
        unsigned int y = blockIdx.y * blockDim.y + threadIdx.y;
    
        // Calculate uv coordinates
        float u = x / (float)width;
        float v = y / (float)height;
        u = u * 2.0f - 1.0f;
        v = v * 2.0f - 1.0f;
    
        // Calculate simple sine wave pattern
        float freq = 4.0f;
        float w = sinf(u * freq + time)
                * cosf(v * freq + time) * 0.5f;
    
        // Write positions
        positions[y * width + x] =
                    make_float4(u, w, v, __int_as_float(0xff00ff00));
    }
    

#####  6.2.15.2.3. Direct3D 11 Version 
    
    
    ID3D11Device* device;
    struct CUSTOMVERTEX {
        FLOAT x, y, z;
        DWORD color;
    };
    ID3D11Buffer* positionsVB;
    struct cudaGraphicsResource* positionsVB_CUDA;
    
    int main()
    {
        int dev;
        // Get a CUDA-enabled adapter
        IDXGIFactory* factory;
        CreateDXGIFactory(__uuidof(IDXGIFactory), (void**)&factory);
        IDXGIAdapter* adapter = 0;
        for (unsigned int i = 0; !adapter; ++i) {
            if (FAILED(factory->EnumAdapters(i, &adapter))
                break;
            if (cudaD3D11GetDevice(&dev, adapter) == cudaSuccess)
                break;
            adapter->Release();
        }
        factory->Release();
    
        // Create swap chain and device
        ...
        sFnPtr_D3D11CreateDeviceAndSwapChain(adapter,
                                             D3D11_DRIVER_TYPE_HARDWARE,
                                             0,
                                             D3D11_CREATE_DEVICE_DEBUG,
                                             featureLevels, 3,
                                             D3D11_SDK_VERSION,
                                             &swapChainDesc, &swapChain,
                                             &device,
                                             &featureLevel,
                                             &deviceContext);
        adapter->Release();
    
        // Use the same device
        cudaSetDevice(dev);
    
        // Create vertex buffer and register it with CUDA
        unsigned int size = width * height * sizeof(CUSTOMVERTEX);
        D3D11_BUFFER_DESC bufferDesc;
        bufferDesc.Usage          = D3D11_USAGE_DEFAULT;
        bufferDesc.ByteWidth      = size;
        bufferDesc.BindFlags      = D3D11_BIND_VERTEX_BUFFER;
        bufferDesc.CPUAccessFlags = 0;
        bufferDesc.MiscFlags      = 0;
        device->CreateBuffer(&bufferDesc, 0, &positionsVB);
        cudaGraphicsD3D11RegisterResource(&positionsVB_CUDA,
                                          positionsVB,
                                          cudaGraphicsRegisterFlagsNone);
        cudaGraphicsResourceSetMapFlags(positionsVB_CUDA,
                                        cudaGraphicsMapFlagsWriteDiscard);
    
        // Launch rendering loop
        while (...) {
            ...
            Render();
            ...
        }
        ...
    }
    
    
    
    void Render()
    {
        // Map vertex buffer for writing from CUDA
        float4* positions;
        cudaGraphicsMapResources(1, &positionsVB_CUDA, 0);
        size_t num_bytes;
        cudaGraphicsResourceGetMappedPointer((void**)&positions,
                                             &num_bytes,
                                             positionsVB_CUDA));
    
        // Execute kernel
        dim3 dimBlock(16, 16, 1);
        dim3 dimGrid(width / dimBlock.x, height / dimBlock.y, 1);
        createVertices<<<dimGrid, dimBlock>>>(positions, time,
                                              width, height);
    
        // Unmap vertex buffer
        cudaGraphicsUnmapResources(1, &positionsVB_CUDA, 0);
    
        // Draw and present
        ...
    }
    
    void releaseVB()
    {
        cudaGraphicsUnregisterResource(positionsVB_CUDA);
        positionsVB->Release();
    }
    
        __global__ void createVertices(float4* positions, float time,
                              unsigned int width, unsigned int height)
    {
        unsigned int x = blockIdx.x * blockDim.x + threadIdx.x;
        unsigned int y = blockIdx.y * blockDim.y + threadIdx.y;
    
    // Calculate uv coordinates
        float u = x / (float)width;
        float v = y / (float)height;
        u = u * 2.0f - 1.0f;
        v = v * 2.0f - 1.0f;
    
        // Calculate simple sine wave pattern
        float freq = 4.0f;
        float w = sinf(u * freq + time)
                * cosf(v * freq + time) * 0.5f;
    
        // Write positions
        positions[y * width + x] =
                    make_float4(u, w, v, __int_as_float(0xff00ff00));
    }
    

####  6.2.15.3. SLI Interoperability 

In a system with multiple GPUs, all CUDA-enabled GPUs are accessible via the CUDA driver and runtime as separate devices. There are however special considerations as described below when the system is in SLI mode.

First, an allocation in one CUDA device on one GPU will consume memory on other GPUs that are part of the SLI configuration of the Direct3D or OpenGL device. Because of this, allocations may fail earlier than otherwise expected.

Second, applications should create multiple CUDA contexts, one for each GPU in the SLI configuration. While this is not a strict requirement, it avoids unnecessary data transfers between devices. The application can use the `cudaD3D[9|10|11]GetDevices()` for Direct3D and `cudaGLGetDevices()` for OpenGL set of calls to identify the CUDA device handle(s) for the device(s) that are performing the rendering in the current and next frame. Given this information the application will typically choose the appropriate device and map Direct3D or OpenGL resources to the CUDA device returned by `cudaD3D[9|10|11]GetDevices()` or `cudaGLGetDevices()` when the `deviceList` parameter is set to `cudaD3D[9|10|11]DeviceListCurrentFrame` or `cudaGLDeviceListCurrentFrame`.

Please note that resource returned from `cudaGraphicsD9D[9|10|11]RegisterResource` and `cudaGraphicsGLRegister[Buffer|Image]` must be only used on device the registration happened. Therefore on SLI configurations when data for different frames is computed on different CUDA devices it is necessary to register the resources for each separately.

See [Direct3D Interoperability](#direct3d-interoperability) and [OpenGL Interoperability](#opengl-interoperability) for details on how the CUDA runtime interoperate with Direct3D and OpenGL, respectively.

###  6.2.16. External Resource Interoperability 

External resource interoperability allows CUDA to import certain resources that are explicitly exported by other APIs. These objects are typically exported by other APIs using handles native to the Operating System, like file descriptors on Linux or NT handles on Windows. They could also be exported using other unified interfaces such as the NVIDIA Software Communication Interface. There are two types of resources that can be imported: memory objects and synchronization objects.

Memory objects can be imported into CUDA using `cudaImportExternalMemory()`. An imported memory object can be accessed from within kernels using device pointers mapped onto the memory object via `cudaExternalMemoryGetMappedBuffer()`or CUDA mipmapped arrays mapped via `cudaExternalMemoryGetMappedMipmappedArray()`. Depending on the type of memory object, it may be possible for more than one mapping to be setup on a single memory object. The mappings must match the mappings setup in the exporting API. Any mismatched mappings result in undefined behavior. Imported memory objects must be freed using `cudaDestroyExternalMemory()`. Freeing a memory object does not free any mappings to that object. Therefore, any device pointers mapped onto that object must be explicitly freed using `cudaFree()` and any CUDA mipmapped arrays mapped onto that object must be explicitly freed using `cudaFreeMipmappedArray()`. It is illegal to access mappings to an object after it has been destroyed.

Synchronization objects can be imported into CUDA using `cudaImportExternalSemaphore()`. An imported synchronization object can then be signaled using `cudaSignalExternalSemaphoresAsync()` and waited on using `cudaWaitExternalSemaphoresAsync()`. It is illegal to issue a wait before the corresponding signal has been issued. Also, depending on the type of the imported synchronization object, there may be additional constraints imposed on how they can be signaled and waited on, as described in subsequent sections. Imported semaphore objects must be freed using `cudaDestroyExternalSemaphore()`. All outstanding signals and waits must have completed before the semaphore object is destroyed.

####  6.2.16.1. Vulkan Interoperability 

#####  6.2.16.1.1. Matching device UUIDs 

When importing memory and synchronization objects exported by Vulkan, they must be imported and mapped on the same device as they were created on. The CUDA device that corresponds to the Vulkan physical device on which the objects were created can be determined by comparing the UUID of a CUDA device with that of the Vulkan physical device, as shown in the following code sample. Note that the Vulkan physical device should not be part of a device group that contains more than one Vulkan physical device. The device group as returned by `vkEnumeratePhysicalDeviceGroups` that contains the given Vulkan physical device must have a physical device count of 1.
    
    
    int getCudaDeviceForVulkanPhysicalDevice(VkPhysicalDevice vkPhysicalDevice) {
        VkPhysicalDeviceIDProperties vkPhysicalDeviceIDProperties = {};
        vkPhysicalDeviceIDProperties.sType = VK_STRUCTURE_TYPE_PHYSICAL_DEVICE_ID_PROPERTIES;
        vkPhysicalDeviceIDProperties.pNext = NULL;
    
        VkPhysicalDeviceProperties2 vkPhysicalDeviceProperties2 = {};
        vkPhysicalDeviceProperties2.sType = VK_STRUCTURE_TYPE_PHYSICAL_DEVICE_PROPERTIES_2;
        vkPhysicalDeviceProperties2.pNext = &vkPhysicalDeviceIDProperties;
    
        vkGetPhysicalDeviceProperties2(vkPhysicalDevice, &vkPhysicalDeviceProperties2);
    
        int cudaDeviceCount;
        cudaGetDeviceCount(&cudaDeviceCount);
    
        for (int cudaDevice = 0; cudaDevice < cudaDeviceCount; cudaDevice++) {
            cudaDeviceProp deviceProp;
            cudaGetDeviceProperties(&deviceProp, cudaDevice);
            if (!memcmp(&deviceProp.uuid, vkPhysicalDeviceIDProperties.deviceUUID, VK_UUID_SIZE)) {
                return cudaDevice;
            }
        }
        return cudaInvalidDeviceId;
    }
    

#####  6.2.16.1.2. Importing Memory Objects 

On Linux and Windows 10, both dedicated and non-dedicated memory objects exported by Vulkan can be imported into CUDA. On Windows 7, only dedicated memory objects can be imported. When importing a Vulkan dedicated memory object, the flag `cudaExternalMemoryDedicated` must be set.

A Vulkan memory object exported using `VK_EXTERNAL_MEMORY_HANDLE_TYPE_OPAQUE_FD_BIT` can be imported into CUDA using the file descriptor associated with that object as shown below. Note that CUDA assumes ownership of the file descriptor once it is imported. Using the file descriptor after a successful import results in undefined behavior.
    
    
    cudaExternalMemory_t importVulkanMemoryObjectFromFileDescriptor(int fd, unsigned long long size, bool isDedicated) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeOpaqueFd;
        desc.handle.fd = fd;
        desc.size = size;
        if (isDedicated) {
            desc.flags |= cudaExternalMemoryDedicated;
        }
    
        cudaImportExternalMemory(&extMem, &desc);
    
        // Input parameter 'fd' should not be used beyond this point as CUDA has assumed ownership of it
    
        return extMem;
    }
    

A Vulkan memory object exported using `VK_EXTERNAL_MEMORY_HANDLE_TYPE_OPAQUE_WIN32_BIT` can be imported into CUDA using the NT handle associated with that object as shown below. Note that CUDA does not assume ownership of the NT handle and it is the application’s responsibility to close the handle when it is not required anymore. The NT handle holds a reference to the resource, so it must be explicitly freed before the underlying memory can be freed.
    
    
    cudaExternalMemory_t importVulkanMemoryObjectFromNTHandle(HANDLE handle, unsigned long long size, bool isDedicated) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeOpaqueWin32;
        desc.handle.win32.handle = handle;
        desc.size = size;
        if (isDedicated) {
            desc.flags |= cudaExternalMemoryDedicated;
        }
    
        cudaImportExternalMemory(&extMem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extMem;
    }
    

A Vulkan memory object exported using `VK_EXTERNAL_MEMORY_HANDLE_TYPE_OPAQUE_WIN32_BIT` can also be imported using a named handle if one exists as shown below.
    
    
    cudaExternalMemory_t importVulkanMemoryObjectFromNamedNTHandle(LPCWSTR name, unsigned long long size, bool isDedicated) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeOpaqueWin32;
        desc.handle.win32.name = (void *)name;
        desc.size = size;
        if (isDedicated) {
            desc.flags |= cudaExternalMemoryDedicated;
        }
    
        cudaImportExternalMemory(&extMem, &desc);
    
        return extMem;
    }
    

A Vulkan memory object exported using VK_EXTERNAL_MEMORY_HANDLE_TYPE_OPAQUE_WIN32_KMT_BIT can be imported into CUDA using the globally shared D3DKMT handle associated with that object as shown below. Since a globally shared D3DKMT handle does not hold a reference to the underlying memory it is automatically destroyed when all other references to the resource are destroyed.
    
    
    cudaExternalMemory_t importVulkanMemoryObjectFromKMTHandle(HANDLE handle, unsigned long long size, bool isDedicated) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeOpaqueWin32Kmt;
        desc.handle.win32.handle = (void *)handle;
        desc.size = size;
        if (isDedicated) {
            desc.flags |= cudaExternalMemoryDedicated;
        }
    
        cudaImportExternalMemory(&extMem, &desc);
    
        return extMem;
    }
    

#####  6.2.16.1.3. Mapping Buffers onto Imported Memory Objects 

A device pointer can be mapped onto an imported memory object as shown below. The offset and size of the mapping must match that specified when creating the mapping using the corresponding Vulkan API. All mapped device pointers must be freed using `cudaFree()`.
    
    
    void * mapBufferOntoExternalMemory(cudaExternalMemory_t extMem, unsigned long long offset, unsigned long long size) {
    
        void *ptr = NULL;
    
        cudaExternalMemoryBufferDesc desc = {};
    
    
    
        memset(&desc, 0, sizeof(desc));
    
    
    
        desc.offset = offset;
    
        desc.size = size;
    
    
    
        cudaExternalMemoryGetMappedBuffer(&ptr, extMem, &desc);
    
    
    
        // Note: ‘ptr’ must eventually be freed using cudaFree()
    
        return ptr;
    
    }
    

#####  6.2.16.1.4. Mapping Mipmapped Arrays onto Imported Memory Objects 

A CUDA mipmapped array can be mapped onto an imported memory object as shown below. The offset, dimensions, format and number of mip levels must match that specified when creating the mapping using the corresponding Vulkan API. Additionally, if the mipmapped array is bound as a color target in Vulkan, the flag`cudaArrayColorAttachment` must be set. All mapped mipmapped arrays must be freed using `cudaFreeMipmappedArray()`. The following code sample shows how to convert Vulkan parameters into the corresponding CUDA parameters when mapping mipmapped arrays onto imported memory objects.
    
    
    cudaMipmappedArray_t mapMipmappedArrayOntoExternalMemory(cudaExternalMemory_t extMem, unsigned long long offset, cudaChannelFormatDesc *formatDesc, cudaExtent *extent, unsigned int flags, unsigned int numLevels) {
        cudaMipmappedArray_t mipmap = NULL;
        cudaExternalMemoryMipmappedArrayDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.offset = offset;
        desc.formatDesc = *formatDesc;
        desc.extent = *extent;
        desc.flags = flags;
        desc.numLevels = numLevels;
    
        // Note: 'mipmap' must eventually be freed using cudaFreeMipmappedArray()
        cudaExternalMemoryGetMappedMipmappedArray(&mipmap, extMem, &desc);
    
        return mipmap;
    }
    
    cudaChannelFormatDesc getCudaChannelFormatDescForVulkanFormat(VkFormat format)
    {
        cudaChannelFormatDesc d;
    
        memset(&d, 0, sizeof(d));
    
        switch (format) {
        case VK_FORMAT_R8_UINT:             d.x = 8;  d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R8_SINT:             d.x = 8;  d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R8G8_UINT:           d.x = 8;  d.y = 8;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R8G8_SINT:           d.x = 8;  d.y = 8;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R8G8B8A8_UINT:       d.x = 8;  d.y = 8;  d.z = 8;  d.w = 8;  d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R8G8B8A8_SINT:       d.x = 8;  d.y = 8;  d.z = 8;  d.w = 8;  d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R16_UINT:            d.x = 16; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R16_SINT:            d.x = 16; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R16G16_UINT:         d.x = 16; d.y = 16; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R16G16_SINT:         d.x = 16; d.y = 16; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R16G16B16A16_UINT:   d.x = 16; d.y = 16; d.z = 16; d.w = 16; d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R16G16B16A16_SINT:   d.x = 16; d.y = 16; d.z = 16; d.w = 16; d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R32_UINT:            d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R32_SINT:            d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R32_SFLOAT:          d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindFloat;    break;
        case VK_FORMAT_R32G32_UINT:         d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R32G32_SINT:         d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R32G32_SFLOAT:       d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindFloat;    break;
        case VK_FORMAT_R32G32B32A32_UINT:   d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindUnsigned; break;
        case VK_FORMAT_R32G32B32A32_SINT:   d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindSigned;   break;
        case VK_FORMAT_R32G32B32A32_SFLOAT: d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindFloat;    break;
        default: assert(0);
        }
    
    
    
        return d;
    }
    
    cudaExtent getCudaExtentForVulkanExtent(VkExtent3D vkExt, uint32_t arrayLayers, VkImageViewType vkImageViewType) {
        cudaExtent e = { 0, 0, 0 };
    
        switch (vkImageViewType) {
        case VK_IMAGE_VIEW_TYPE_1D:         e.width = vkExt.width; e.height = 0;            e.depth = 0;           break;
        case VK_IMAGE_VIEW_TYPE_2D:         e.width = vkExt.width; e.height = vkExt.height; e.depth = 0;           break;
        case VK_IMAGE_VIEW_TYPE_3D:         e.width = vkExt.width; e.height = vkExt.height; e.depth = vkExt.depth; break;
        case VK_IMAGE_VIEW_TYPE_CUBE:       e.width = vkExt.width; e.height = vkExt.height; e.depth = arrayLayers; break;
        case VK_IMAGE_VIEW_TYPE_1D_ARRAY:   e.width = vkExt.width; e.height = 0;            e.depth = arrayLayers; break;
        case VK_IMAGE_VIEW_TYPE_2D_ARRAY:   e.width = vkExt.width; e.height = vkExt.height; e.depth = arrayLayers; break;
        case VK_IMAGE_VIEW_TYPE_CUBE_ARRAY: e.width = vkExt.width; e.height = vkExt.height; e.depth = arrayLayers; break;
        default: assert(0);
        }
    
        return e;
    }
    
    unsigned int getCudaMipmappedArrayFlagsForVulkanImage(VkImageViewType vkImageViewType, VkImageUsageFlags vkImageUsageFlags, bool allowSurfaceLoadStore) {
        unsigned int flags = 0;
    
        switch (vkImageViewType) {
        case VK_IMAGE_VIEW_TYPE_CUBE:       flags |= cudaArrayCubemap;                    break;
        case VK_IMAGE_VIEW_TYPE_CUBE_ARRAY: flags |= cudaArrayCubemap | cudaArrayLayered; break;
        case VK_IMAGE_VIEW_TYPE_1D_ARRAY:   flags |= cudaArrayLayered;                    break;
        case VK_IMAGE_VIEW_TYPE_2D_ARRAY:   flags |= cudaArrayLayered;                    break;
        default: break;
        }
    
        if (vkImageUsageFlags & VK_IMAGE_USAGE_COLOR_ATTACHMENT_BIT) {
            flags |= cudaArrayColorAttachment;
        }
    
        if (allowSurfaceLoadStore) {
            flags |= cudaArraySurfaceLoadStore;
        }
        return flags;
    }
    

#####  6.2.16.1.5. Importing Synchronization Objects 

A Vulkan semaphore object exported using `VK_EXTERNAL_SEMAPHORE_HANDLE_TYPE_OPAQUE_FD_BIT`can be imported into CUDA using the file descriptor associated with that object as shown below. Note that CUDA assumes ownership of the file descriptor once it is imported. Using the file descriptor after a successful import results in undefined behavior.
    
    
    cudaExternalSemaphore_t importVulkanSemaphoreObjectFromFileDescriptor(int fd) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeOpaqueFd;
        desc.handle.fd = fd;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        // Input parameter 'fd' should not be used beyond this point as CUDA has assumed ownership of it
    
        return extSem;
    }
    

A Vulkan semaphore object exported using `VK_EXTERNAL_SEMAPHORE_HANDLE_TYPE_OPAQUE_WIN32_BIT` can be imported into CUDA using the NT handle associated with that object as shown below. Note that CUDA does not assume ownership of the NT handle and it is the application’s responsibility to close the handle when it is not required anymore. The NT handle holds a reference to the resource, so it must be explicitly freed before the underlying semaphore can be freed.
    
    
    cudaExternalSemaphore_t importVulkanSemaphoreObjectFromNTHandle(HANDLE handle) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeOpaqueWin32;
        desc.handle.win32.handle = handle;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extSem;
    }
    

A Vulkan semaphore object exported using `VK_EXTERNAL_SEMAPHORE_HANDLE_TYPE_OPAQUE_WIN32_BIT` can also be imported using a named handle if one exists as shown below.
    
    
    cudaExternalSemaphore_t importVulkanSemaphoreObjectFromNamedNTHandle(LPCWSTR name) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeOpaqueWin32;
        desc.handle.win32.name = (void *)name;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        return extSem;
    }
    

A Vulkan semaphore object exported using `VK_EXTERNAL_SEMAPHORE_HANDLE_TYPE_OPAQUE_WIN32_KMT_BIT` can be imported into CUDA using the globally shared D3DKMT handle associated with that object as shown below. Since a globally shared D3DKMT handle does not hold a reference to the underlying semaphore it is automatically destroyed when all other references to the resource are destroyed.
    
    
    cudaExternalSemaphore_t importVulkanSemaphoreObjectFromKMTHandle(HANDLE handle) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeOpaqueWin32Kmt;
        desc.handle.win32.handle = (void *)handle;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        return extSem;
    }
    

#####  6.2.16.1.6. Signaling/Waiting on Imported Synchronization Objects 

An imported Vulkan semaphore object can be signaled as shown below. Signaling such a semaphore object sets it to the signaled state. The corresponding wait that waits on this signal must be issued in Vulkan. Additionally, the wait that waits on this signal must be issued after this signal has been issued.
    
    
    void signalExternalSemaphore(cudaExternalSemaphore_t extSem, cudaStream_t stream) {
        cudaExternalSemaphoreSignalParams params = {};
    
        memset(&params, 0, sizeof(params));
    
        cudaSignalExternalSemaphoresAsync(&extSem, &params, 1, stream);
    }
    

An imported Vulkan semaphore object can be waited on as shown below. Waiting on such a semaphore object waits until it reaches the signaled state and then resets it back to the unsignaled state. The corresponding signal that this wait is waiting on must be issued in Vulkan. Additionally, the signal must be issued before this wait can be issued.
    
    
    void waitExternalSemaphore(cudaExternalSemaphore_t extSem, cudaStream_t stream) {
        cudaExternalSemaphoreWaitParams params = {};
    
        memset(&params, 0, sizeof(params));
    
        cudaWaitExternalSemaphoresAsync(&extSem, &params, 1, stream);
    }
    

####  6.2.16.2. OpenGL Interoperability 

Traditional OpenGL-CUDA interop as outlined in [OpenGL Interoperability](#opengl-interoperability) works by CUDA directly consuming handles created in OpenGL. However, since OpenGL can also consume memory and synchronization objects created in Vulkan, there exists an alternative approach to doing OpenGL-CUDA interop. Essentially, memory and synchronization objects exported by Vulkan could be imported into both, OpenGL and CUDA, and then used to coordinate memory accesses between OpenGL and CUDA. Please refer to the following OpenGL extensions for further details on how to import memory and synchronization objects exported by Vulkan:

  * GL_EXT_memory_object

  * GL_EXT_memory_object_fd

  * GL_EXT_memory_object_win32

  * GL_EXT_semaphore

  * GL_EXT_semaphore_fd

  * GL_EXT_semaphore_win32


####  6.2.16.3. Direct3D 12 Interoperability 

#####  6.2.16.3.1. Matching Device LUIDs 

When importing memory and synchronization objects exported by Direct3D 12, they must be imported and mapped on the same device as they were created on. The CUDA device that corresponds to the Direct3D 12 device on which the objects were created can be determined by comparing the LUID of a CUDA device with that of the Direct3D 12 device, as shown in the following code sample. Note that the Direct3D 12 device must not be created on a linked node adapter. I.e. the node count as returned by `ID3D12Device::GetNodeCount` must be 1.
    
    
    int getCudaDeviceForD3D12Device(ID3D12Device *d3d12Device) {
        LUID d3d12Luid = d3d12Device->GetAdapterLuid();
    
        int cudaDeviceCount;
        cudaGetDeviceCount(&cudaDeviceCount);
    
        for (int cudaDevice = 0; cudaDevice < cudaDeviceCount; cudaDevice++) {
            cudaDeviceProp deviceProp;
            cudaGetDeviceProperties(&deviceProp, cudaDevice);
            char *cudaLuid = deviceProp.luid;
    
            if (!memcmp(&d3d12Luid.LowPart, cudaLuid, sizeof(d3d12Luid.LowPart)) &&
                !memcmp(&d3d12Luid.HighPart, cudaLuid + sizeof(d3d12Luid.LowPart), sizeof(d3d12Luid.HighPart))) {
                return cudaDevice;
            }
        }
        return cudaInvalidDeviceId;
    }
    

#####  6.2.16.3.2. Importing Memory Objects 

A shareable Direct3D 12 heap memory object, created by setting the flag `D3D12_HEAP_FLAG_SHARED` in the call to `ID3D12Device::CreateHeap`, can be imported into CUDA using the NT handle associated with that object as shown below. Note that it is the application’s responsibility to close the NT handle when it is not required anymore. The NT handle holds a reference to the resource, so it must be explicitly freed before the underlying memory can be freed.
    
    
    cudaExternalMemory_t importD3D12HeapFromNTHandle(HANDLE handle, unsigned long long size) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeD3D12Heap;
        desc.handle.win32.handle = (void *)handle;
        desc.size = size;
    
        cudaImportExternalMemory(&extMem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extMem;
    }
    

A shareable Direct3D 12 heap memory object can also be imported using a named handle if one exists as shown below.
    
    
    cudaExternalMemory_t importD3D12HeapFromNamedNTHandle(LPCWSTR name, unsigned long long size) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeD3D12Heap;
        desc.handle.win32.name = (void *)name;
        desc.size = size;
    
        cudaImportExternalMemory(&extMem, &desc);
    
        return extMem;
    }
    

A shareable Direct3D 12 committed resource, created by setting the flag `D3D12_HEAP_FLAG_SHARED` in the call to `D3D12Device::CreateCommittedResource`, can be imported into CUDA using the NT handle associated with that object as shown below. When importing a Direct3D 12 committed resource, the flag `cudaExternalMemoryDedicated` must be set. Note that it is the application’s responsibility to close the NT handle when it is not required anymore. The NT handle holds a reference to the resource, so it must be explicitly freed before the underlying memory can be freed.
    
    
    cudaExternalMemory_t importD3D12CommittedResourceFromNTHandle(HANDLE handle, unsigned long long size) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeD3D12Resource;
        desc.handle.win32.handle = (void *)handle;
        desc.size = size;
        desc.flags |= cudaExternalMemoryDedicated;
    
        cudaImportExternalMemory(&extMem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extMem;
    }
    

A shareable Direct3D 12 committed resource can also be imported using a named handle if one exists as shown below.
    
    
    cudaExternalMemory_t importD3D12CommittedResourceFromNamedNTHandle(LPCWSTR name, unsigned long long size) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeD3D12Resource;
        desc.handle.win32.name = (void *)name;
        desc.size = size;
        desc.flags |= cudaExternalMemoryDedicated;
    
        cudaImportExternalMemory(&extMem, &desc);
    
        return extMem;
    }
    

#####  6.2.16.3.3. Mapping Buffers onto Imported Memory Objects 

A device pointer can be mapped onto an imported memory object as shown below. The offset and size of the mapping must match that specified when creating the mapping using the corresponding Direct3D 12 API. All mapped device pointers must be freed using `cudaFree()`.
    
    
    void * mapBufferOntoExternalMemory(cudaExternalMemory_t extMem, unsigned long long offset, unsigned long long size) {
        void *ptr = NULL;
        cudaExternalMemoryBufferDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.offset = offset;
        desc.size = size;
    
        cudaExternalMemoryGetMappedBuffer(&ptr, extMem, &desc);
    
        // Note: 'ptr' must eventually be freed using cudaFree()
        return ptr;
    }
    

#####  6.2.16.3.4. Mapping Mipmapped Arrays onto Imported Memory Objects 

A CUDA mipmapped array can be mapped onto an imported memory object as shown below. The offset, dimensions, format and number of mip levels must match that specified when creating the mapping using the corresponding Direct3D 12 API. Additionally, if the mipmapped array can be bound as a render target in Direct3D 12, the flag `cudaArrayColorAttachment` must be set. All mapped mipmapped arrays must be freed using `cudaFreeMipmappedArray()`. The following code sample shows how to convert Vulkan parameters into the corresponding CUDA parameters when mapping mipmapped arrays onto imported memory objects.
    
    
    cudaMipmappedArray_t mapMipmappedArrayOntoExternalMemory(cudaExternalMemory_t extMem, unsigned long long offset, cudaChannelFormatDesc *formatDesc, cudaExtent *extent, unsigned int flags, unsigned int numLevels) {
        cudaMipmappedArray_t mipmap = NULL;
        cudaExternalMemoryMipmappedArrayDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.offset = offset;
        desc.formatDesc = *formatDesc;
        desc.extent = *extent;
        desc.flags = flags;
        desc.numLevels = numLevels;
    
        // Note: 'mipmap' must eventually be freed using cudaFreeMipmappedArray()
        cudaExternalMemoryGetMappedMipmappedArray(&mipmap, extMem, &desc);
    
        return mipmap;
    }
    
    cudaChannelFormatDesc getCudaChannelFormatDescForDxgiFormat(DXGI_FORMAT dxgiFormat)
    {
        cudaChannelFormatDesc d;
    
        memset(&d, 0, sizeof(d));
    
        switch (dxgiFormat) {
        case DXGI_FORMAT_R8_UINT:            d.x = 8;  d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R8_SINT:            d.x = 8;  d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R8G8_UINT:          d.x = 8;  d.y = 8;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R8G8_SINT:          d.x = 8;  d.y = 8;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R8G8B8A8_UINT:      d.x = 8;  d.y = 8;  d.z = 8;  d.w = 8;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R8G8B8A8_SINT:      d.x = 8;  d.y = 8;  d.z = 8;  d.w = 8;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R16_UINT:           d.x = 16; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R16_SINT:           d.x = 16; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R16G16_UINT:        d.x = 16; d.y = 16; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R16G16_SINT:        d.x = 16; d.y = 16; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R16G16B16A16_UINT:  d.x = 16; d.y = 16; d.z = 16; d.w = 16; d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R16G16B16A16_SINT:  d.x = 16; d.y = 16; d.z = 16; d.w = 16; d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R32_UINT:           d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R32_SINT:           d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R32_FLOAT:          d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindFloat;    break;
        case DXGI_FORMAT_R32G32_UINT:        d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R32G32_SINT:        d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R32G32_FLOAT:       d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindFloat;    break;
        case DXGI_FORMAT_R32G32B32A32_UINT:  d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R32G32B32A32_SINT:  d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R32G32B32A32_FLOAT: d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindFloat;    break;
        default: assert(0);
    
    
    
        }
    
        return d;
    }
    
    cudaExtent getCudaExtentForD3D12Extent(UINT64 width, UINT height, UINT16 depthOrArraySize, D3D12_SRV_DIMENSION d3d12SRVDimension) {
        cudaExtent e = { 0, 0, 0 };
    
        switch (d3d12SRVDimension) {
        case D3D12_SRV_DIMENSION_TEXTURE1D:        e.width = width; e.height = 0;      e.depth = 0;                break;
        case D3D12_SRV_DIMENSION_TEXTURE2D:        e.width = width; e.height = height; e.depth = 0;                break;
        case D3D12_SRV_DIMENSION_TEXTURE3D:        e.width = width; e.height = height; e.depth = depthOrArraySize; break;
        case D3D12_SRV_DIMENSION_TEXTURECUBE:      e.width = width; e.height = height; e.depth = depthOrArraySize; break;
        case D3D12_SRV_DIMENSION_TEXTURE1DARRAY:   e.width = width; e.height = 0;      e.depth = depthOrArraySize; break;
        case D3D12_SRV_DIMENSION_TEXTURE2DARRAY:   e.width = width; e.height = height; e.depth = depthOrArraySize; break;
        case D3D12_SRV_DIMENSION_TEXTURECUBEARRAY: e.width = width; e.height = height; e.depth = depthOrArraySize; break;
        default: assert(0);
        }
    
        return e;
    }
    
    unsigned int getCudaMipmappedArrayFlagsForD3D12Resource(D3D12_SRV_DIMENSION d3d12SRVDimension, D3D12_RESOURCE_FLAGS d3d12ResourceFlags, bool allowSurfaceLoadStore) {
        unsigned int flags = 0;
    
        switch (d3d12SRVDimension) {
        case D3D12_SRV_DIMENSION_TEXTURECUBE:      flags |= cudaArrayCubemap;                    break;
        case D3D12_SRV_DIMENSION_TEXTURECUBEARRAY: flags |= cudaArrayCubemap | cudaArrayLayered; break;
        case D3D12_SRV_DIMENSION_TEXTURE1DARRAY:   flags |= cudaArrayLayered;                    break;
        case D3D12_SRV_DIMENSION_TEXTURE2DARRAY:   flags |= cudaArrayLayered;                    break;
        default: break;
        }
    
        if (d3d12ResourceFlags & D3D12_RESOURCE_FLAG_ALLOW_RENDER_TARGET) {
            flags |= cudaArrayColorAttachment;
        }
        if (allowSurfaceLoadStore) {
            flags |= cudaArraySurfaceLoadStore;
        }
    
        return flags;
    }
    

#####  6.2.16.3.5. Importing Synchronization Objects 

A shareable Direct3D 12 fence object, created by setting the flag `D3D12_FENCE_FLAG_SHARED` in the call to `ID3D12Device::CreateFence`, can be imported into CUDA using the NT handle associated with that object as shown below. Note that it is the application’s responsibility to close the handle when it is not required anymore. The NT handle holds a reference to the resource, so it must be explicitly freed before the underlying semaphore can be freed.
    
    
    cudaExternalSemaphore_t importD3D12FenceFromNTHandle(HANDLE handle) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeD3D12Fence;
        desc.handle.win32.handle = handle;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extSem;
    }
    

A shareable Direct3D 12 fence object can also be imported using a named handle if one exists as shown below.
    
    
    cudaExternalSemaphore_t importD3D12FenceFromNamedNTHandle(LPCWSTR name) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeD3D12Fence;
        desc.handle.win32.name = (void *)name;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        return extSem;
    }
    

#####  6.2.16.3.6. Signaling/Waiting on Imported Synchronization Objects 

An imported Direct3D 12 fence object can be signaled as shown below. Signaling such a fence object sets its value to the one specified. The corresponding wait that waits on this signal must be issued in Direct3D 12. Additionally, the wait that waits on this signal must be issued after this signal has been issued.
    
    
    void signalExternalSemaphore(cudaExternalSemaphore_t extSem, unsigned long long value, cudaStream_t stream) {
        cudaExternalSemaphoreSignalParams params = {};
    
        memset(&params, 0, sizeof(params));
    
        params.params.fence.value = value;
    
        cudaSignalExternalSemaphoresAsync(&extSem, &params, 1, stream);
    }
    

An imported Direct3D 12 fence object can be waited on as shown below. Waiting on such a fence object waits until its value becomes greater than or equal to the specified value. The corresponding signal that this wait is waiting on must be issued in Direct3D 12. Additionally, the signal must be issued before this wait can be issued.
    
    
    void waitExternalSemaphore(cudaExternalSemaphore_t extSem, unsigned long long value, cudaStream_t stream) {
        cudaExternalSemaphoreWaitParams params = {};
    
        memset(&params, 0, sizeof(params));
    
        params.params.fence.value = value;
    
        cudaWaitExternalSemaphoresAsync(&extSem, &params, 1, stream);
    }
    

####  6.2.16.4. Direct3D 11 Interoperability 

#####  6.2.16.4.1. Matching Device LUIDs 

When importing memory and synchronization objects exported by Direct3D 11, they must be imported and mapped on the same device as they were created on. The CUDA device that corresponds to the Direct3D 11 device on which the objects were created can be determined by comparing the LUID of a CUDA device with that of the Direct3D 11 device, as shown in the following code sample.
    
    
    int getCudaDeviceForD3D11Device(ID3D11Device *d3d11Device) {
        IDXGIDevice *dxgiDevice;
        d3d11Device->QueryInterface(__uuidof(IDXGIDevice), (void **)&dxgiDevice);
    
        IDXGIAdapter *dxgiAdapter;
        dxgiDevice->GetAdapter(&dxgiAdapter);
    
        DXGI_ADAPTER_DESC dxgiAdapterDesc;
        dxgiAdapter->GetDesc(&dxgiAdapterDesc);
    
        LUID d3d11Luid = dxgiAdapterDesc.AdapterLuid;
    
        int cudaDeviceCount;
        cudaGetDeviceCount(&cudaDeviceCount);
    
        for (int cudaDevice = 0; cudaDevice < cudaDeviceCount; cudaDevice++) {
            cudaDeviceProp deviceProp;
            cudaGetDeviceProperties(&deviceProp, cudaDevice);
            char *cudaLuid = deviceProp.luid;
    
            if (!memcmp(&d3d11Luid.LowPart, cudaLuid, sizeof(d3d11Luid.LowPart)) &&
                !memcmp(&d3d11Luid.HighPart, cudaLuid + sizeof(d3d11Luid.LowPart), sizeof(d3d11Luid.HighPart))) {
                return cudaDevice;
            }
        }
        return cudaInvalidDeviceId;
    }
    

#####  6.2.16.4.2. Importing Memory Objects 

A shareable Direct3D 11 texture resource, viz, `ID3D11Texture1D`, `ID3D11Texture2D` or `ID3D11Texture3D`, can be created by setting either the `D3D11_RESOURCE_MISC_SHARED` or `D3D11_RESOURCE_MISC_SHARED_KEYEDMUTEX` (on Windows 7) or `D3D11_RESOURCE_MISC_SHARED_NTHANDLE` (on Windows 10) when calling `ID3D11Device:CreateTexture1D`, `ID3D11Device:CreateTexture2D` or `ID3D11Device:CreateTexture3D` respectively. A shareable Direct3D 11 buffer resource, `ID3D11Buffer`, can be created by specifying either of the above flags when calling `ID3D11Device::CreateBuffer`. A shareable resource created by specifying the `D3D11_RESOURCE_MISC_SHARED_NTHANDLE` can be imported into CUDA using the NT handle associated with that object as shown below. Note that it is the application’s responsibility to close the NT handle when it is not required anymore. The NT handle holds a reference to the resource, so it must be explicitly freed before the underlying memory can be freed. When importing a Direct3D 11 resource, the flag `cudaExternalMemoryDedicated` must be set.
    
    
    cudaExternalMemory_t importD3D11ResourceFromNTHandle(HANDLE handle, unsigned long long size) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeD3D11Resource;
        desc.handle.win32.handle = (void *)handle;
        desc.size = size;
        desc.flags |= cudaExternalMemoryDedicated;
    
        cudaImportExternalMemory(&extMem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extMem;
    }
    

A shareable Direct3D 11 resource can also be imported using a named handle if one exists as shown below.
    
    
    cudaExternalMemory_t importD3D11ResourceFromNamedNTHandle(LPCWSTR name, unsigned long long size) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeD3D11Resource;
        desc.handle.win32.name = (void *)name;
        desc.size = size;
        desc.flags |= cudaExternalMemoryDedicated;
    
        cudaImportExternalMemory(&extMem, &desc);
    
        return extMem;
    }
    

A shareable Direct3D 11 resource, created by specifying the `D3D11_RESOURCE_MISC_SHARED` or `D3D11_RESOURCE_MISC_SHARED_KEYEDMUTEX`, can be imported into CUDA using the globally shared `D3DKMT` handle associated with that object as shown below. Since a globally shared `D3DKMT` handle does not hold a reference to the underlying memory it is automatically destroyed when all other references to the resource are destroyed.
    
    
    cudaExternalMemory_t importD3D11ResourceFromKMTHandle(HANDLE handle, unsigned long long size) {
        cudaExternalMemory_t extMem = NULL;
        cudaExternalMemoryHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalMemoryHandleTypeD3D11ResourceKmt;
        desc.handle.win32.handle = (void *)handle;
        desc.size = size;
        desc.flags |= cudaExternalMemoryDedicated;
    
        cudaImportExternalMemory(&extMem, &desc);
    
        return extMem;
    }
    

#####  6.2.16.4.3. Mapping Buffers onto Imported Memory Objects 

A device pointer can be mapped onto an imported memory object as shown below. The offset and size of the mapping must match that specified when creating the mapping using the corresponding Direct3D 11 API. All mapped device pointers must be freed using `cudaFree()`.
    
    
    void * mapBufferOntoExternalMemory(cudaExternalMemory_t extMem, unsigned long long offset, unsigned long long size) {
        void *ptr = NULL;
        cudaExternalMemoryBufferDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.offset = offset;
        desc.size = size;
    
        cudaExternalMemoryGetMappedBuffer(&ptr, extMem, &desc);
    
        // Note: ‘ptr’ must eventually be freed using cudaFree()
        return ptr;
    }
    

#####  6.2.16.4.4. Mapping Mipmapped Arrays onto Imported Memory Objects 

A CUDA mipmapped array can be mapped onto an imported memory object as shown below. The offset, dimensions, format and number of mip levels must match that specified when creating the mapping using the corresponding Direct3D 11 API. Additionally, if the mipmapped array can be bound as a render target in Direct3D 12, the flag `cudaArrayColorAttachment` must be set. All mapped mipmapped arrays must be freed using `cudaFreeMipmappedArray()`. The following code sample shows how to convert Direct3D 11 parameters into the corresponding CUDA parameters when mapping mipmapped arrays onto imported memory objects.
    
    
    cudaMipmappedArray_t mapMipmappedArrayOntoExternalMemory(cudaExternalMemory_t extMem, unsigned long long offset, cudaChannelFormatDesc *formatDesc, cudaExtent *extent, unsigned int flags, unsigned int numLevels) {
        cudaMipmappedArray_t mipmap = NULL;
        cudaExternalMemoryMipmappedArrayDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.offset = offset;
        desc.formatDesc = *formatDesc;
        desc.extent = *extent;
        desc.flags = flags;
        desc.numLevels = numLevels;
    
        // Note: 'mipmap' must eventually be freed using cudaFreeMipmappedArray()
        cudaExternalMemoryGetMappedMipmappedArray(&mipmap, extMem, &desc);
    
        return mipmap;
    }
    
    cudaChannelFormatDesc getCudaChannelFormatDescForDxgiFormat(DXGI_FORMAT dxgiFormat)
    {
        cudaChannelFormatDesc d;
        memset(&d, 0, sizeof(d));
        switch (dxgiFormat) {
        case DXGI_FORMAT_R8_UINT:            d.x = 8;  d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R8_SINT:            d.x = 8;  d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R8G8_UINT:          d.x = 8;  d.y = 8;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R8G8_SINT:          d.x = 8;  d.y = 8;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R8G8B8A8_UINT:      d.x = 8;  d.y = 8;  d.z = 8;  d.w = 8;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R8G8B8A8_SINT:      d.x = 8;  d.y = 8;  d.z = 8;  d.w = 8;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R16_UINT:           d.x = 16; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R16_SINT:           d.x = 16; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R16G16_UINT:        d.x = 16; d.y = 16; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R16G16_SINT:        d.x = 16; d.y = 16; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R16G16B16A16_UINT:  d.x = 16; d.y = 16; d.z = 16; d.w = 16; d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R16G16B16A16_SINT:  d.x = 16; d.y = 16; d.z = 16; d.w = 16; d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R32_UINT:           d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R32_SINT:           d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R32_FLOAT:          d.x = 32; d.y = 0;  d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindFloat;    break;
        case DXGI_FORMAT_R32G32_UINT:        d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R32G32_SINT:        d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R32G32_FLOAT:       d.x = 32; d.y = 32; d.z = 0;  d.w = 0;  d.f = cudaChannelFormatKindFloat;    break;
        case DXGI_FORMAT_R32G32B32A32_UINT:  d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindUnsigned; break;
        case DXGI_FORMAT_R32G32B32A32_SINT:  d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindSigned;   break;
        case DXGI_FORMAT_R32G32B32A32_FLOAT: d.x = 32; d.y = 32; d.z = 32; d.w = 32; d.f = cudaChannelFormatKindFloat;    break;
        default: assert(0);
        }
    
    
    
        return d;
    }
    
    cudaExtent getCudaExtentForD3D11Extent(UINT64 width, UINT height, UINT16 depthOrArraySize, D3D12_SRV_DIMENSION d3d11SRVDimension) {
        cudaExtent e = { 0, 0, 0 };
    
        switch (d3d11SRVDimension) {
        case D3D11_SRV_DIMENSION_TEXTURE1D:        e.width = width; e.height = 0;      e.depth = 0;                break;
        case D3D11_SRV_DIMENSION_TEXTURE2D:        e.width = width; e.height = height; e.depth = 0;                break;
        case D3D11_SRV_DIMENSION_TEXTURE3D:        e.width = width; e.height = height; e.depth = depthOrArraySize; break;
        case D3D11_SRV_DIMENSION_TEXTURECUBE:      e.width = width; e.height = height; e.depth = depthOrArraySize; break;
        case D3D11_SRV_DIMENSION_TEXTURE1DARRAY:   e.width = width; e.height = 0;      e.depth = depthOrArraySize; break;
        case D3D11_SRV_DIMENSION_TEXTURE2DARRAY:   e.width = width; e.height = height; e.depth = depthOrArraySize; break;
        case D3D11_SRV_DIMENSION_TEXTURECUBEARRAY: e.width = width; e.height = height; e.depth = depthOrArraySize; break;
        default: assert(0);
        }
        return e;
    }
    
    unsigned int getCudaMipmappedArrayFlagsForD3D12Resource(D3D11_SRV_DIMENSION d3d11SRVDimension, D3D11_BIND_FLAG d3d11BindFlags, bool allowSurfaceLoadStore) {
        unsigned int flags = 0;
    
        switch (d3d11SRVDimension) {
        case D3D11_SRV_DIMENSION_TEXTURECUBE:      flags |= cudaArrayCubemap;                    break;
        case D3D11_SRV_DIMENSION_TEXTURECUBEARRAY: flags |= cudaArrayCubemap | cudaArrayLayered; break;
        case D3D11_SRV_DIMENSION_TEXTURE1DARRAY:   flags |= cudaArrayLayered;                    break;
        case D3D11_SRV_DIMENSION_TEXTURE2DARRAY:   flags |= cudaArrayLayered;                    break;
        default: break;
        }
    
        if (d3d11BindFlags & D3D11_BIND_RENDER_TARGET) {
            flags |= cudaArrayColorAttachment;
        }
    
        if (allowSurfaceLoadStore) {
            flags |= cudaArraySurfaceLoadStore;
        }
    
        return flags;
    }
    

#####  6.2.16.4.5. Importing Synchronization Objects 

A shareable Direct3D 11 fence object, created by setting the flag `D3D11_FENCE_FLAG_SHARED` in the call to `ID3D11Device5::CreateFence`, can be imported into CUDA using the NT handle associated with that object as shown below. Note that it is the application’s responsibility to close the handle when it is not required anymore. The NT handle holds a reference to the resource, so it must be explicitly freed before the underlying semaphore can be freed.
    
    
    cudaExternalSemaphore_t importD3D11FenceFromNTHandle(HANDLE handle) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeD3D11Fence;
        desc.handle.win32.handle = handle;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extSem;
    }
    

A shareable Direct3D 11 fence object can also be imported using a named handle if one exists as shown below.
    
    
    cudaExternalSemaphore_t importD3D11FenceFromNamedNTHandle(LPCWSTR name) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeD3D11Fence;
        desc.handle.win32.name = (void *)name;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        return extSem;
    }
    

A shareable Direct3D 11 keyed mutex object associated with a shareable Direct3D 11 resource, viz, `IDXGIKeyedMutex`, created by setting the flag `D3D11_RESOURCE_MISC_SHARED_KEYEDMUTEX`, can be imported into CUDA using the NT handle associated with that object as shown below. Note that it is the application’s responsibility to close the handle when it is not required anymore. The NT handle holds a reference to the resource, so it must be explicitly freed before the underlying semaphore can be freed.
    
    
    cudaExternalSemaphore_t importD3D11KeyedMutexFromNTHandle(HANDLE handle) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeKeyedMutex;
        desc.handle.win32.handle = handle;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extSem;
    }
    

A shareable Direct3D 11 keyed mutex object can also be imported using a named handle if one exists as shown below.
    
    
    cudaExternalSemaphore_t importD3D11KeyedMutexFromNamedNTHandle(LPCWSTR name) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeKeyedMutex;
        desc.handle.win32.name = (void *)name;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        return extSem;
    }
    

A shareable Direct3D 11 keyed mutex object can be imported into CUDA using the globally shared D3DKMT handle associated with that object as shown below. Since a globally shared D3DKMT handle does not hold a reference to the underlying memory it is automatically destroyed when all other references to the resource are destroyed.
    
    
    cudaExternalSemaphore_t importD3D11FenceFromKMTHandle(HANDLE handle) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeKeyedMutexKmt;
        desc.handle.win32.handle = handle;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        // Input parameter 'handle' should be closed if it's not needed anymore
        CloseHandle(handle);
    
        return extSem;
    }
    

#####  6.2.16.4.6. Signaling/Waiting on Imported Synchronization Objects 

An imported Direct3D 11 fence object can be signaled as shown below. Signaling such a fence object sets its value to the one specified. The corresponding wait that waits on this signal must be issued in Direct3D 11. Additionally, the wait that waits on this signal must be issued after this signal has been issued.
    
    
    void signalExternalSemaphore(cudaExternalSemaphore_t extSem, unsigned long long value, cudaStream_t stream) {
        cudaExternalSemaphoreSignalParams params = {};
    
        memset(&params, 0, sizeof(params));
    
        params.params.fence.value = value;
    
        cudaSignalExternalSemaphoresAsync(&extSem, &params, 1, stream);
    }
    

An imported Direct3D 11 fence object can be waited on as shown below. Waiting on such a fence object waits until its value becomes greater than or equal to the specified value. The corresponding signal that this wait is waiting on must be issued in Direct3D 11. Additionally, the signal must be issued before this wait can be issued.
    
    
    void waitExternalSemaphore(cudaExternalSemaphore_t extSem, unsigned long long value, cudaStream_t stream) {
        cudaExternalSemaphoreWaitParams params = {};
    
        memset(&params, 0, sizeof(params));
    
        params.params.fence.value = value;
    
        cudaWaitExternalSemaphoresAsync(&extSem, &params, 1, stream);
    }
    

An imported Direct3D 11 keyed mutex object can be signaled as shown below. Signaling such a keyed mutex object by specifying a key value releases the keyed mutex for that value. The corresponding wait that waits on this signal must be issued in Direct3D 11 with the same key value. Additionally, the Direct3D 11 wait must be issued after this signal has been issued.
    
    
    void signalExternalSemaphore(cudaExternalSemaphore_t extSem, unsigned long long key, cudaStream_t stream) {
        cudaExternalSemaphoreSignalParams params = {};
    
        memset(&params, 0, sizeof(params));
    
        params.params.keyedmutex.key = key;
    
        cudaSignalExternalSemaphoresAsync(&extSem, &params, 1, stream);
    }
    

An imported Direct3D 11 keyed mutex object can be waited on as shown below. A timeout value in milliseconds is needed when waiting on such a keyed mutex. The wait operation waits until the keyed mutex value is equal to the specified key value or until the timeout has elapsed. The timeout interval can also be an infinite value. In case an infinite value is specified the timeout never elapses. The windows INFINITE macro must be used to specify an infinite timeout. The corresponding signal that this wait is waiting on must be issued in Direct3D 11. Additionally, the Direct3D 11 signal must be issued before this wait can be issued.
    
    
    void waitExternalSemaphore(cudaExternalSemaphore_t extSem, unsigned long long key, unsigned int timeoutMs, cudaStream_t stream) {
        cudaExternalSemaphoreWaitParams params = {};
    
        memset(&params, 0, sizeof(params));
    
        params.params.keyedmutex.key = key;
        params.params.keyedmutex.timeoutMs = timeoutMs;
    
        cudaWaitExternalSemaphoresAsync(&extSem, &params, 1, stream);
    }
    

####  6.2.16.5. NVIDIA Software Communication Interface Interoperability (NVSCI) 

NvSciBuf and NvSciSync are interfaces developed for serving the following purposes:

  * NvSciBuf: Allows applications to allocate and exchange buffers in memory

  * NvSciSync: Allows applications to manage synchronization objects at operation boundaries


More details on these interfaces are available at: <https://docs.nvidia.com/drive>.

#####  6.2.16.5.1. Importing Memory Objects 

For allocating an NvSciBuf object compatible with a given CUDA device, the corresponding GPU id must be set with `NvSciBufGeneralAttrKey_GpuId` in the NvSciBuf attribute list as shown below. Optionally, applications can specify the following attributes -

  * `NvSciBufGeneralAttrKey_NeedCpuAccess`: Specifies if CPU access is required for the buffer

  * `NvSciBufRawBufferAttrKey_Align`: Specifies the alignment requirement of `NvSciBufType_RawBuffer`

  * `NvSciBufGeneralAttrKey_RequiredPerm`: Different access permissions can be configured for different UMDs per NvSciBuf memory object instance. For example, to provide the GPU with read-only access permissions to the buffer, create a duplicate NvSciBuf object using `NvSciBufObjDupWithReducePerm()` with `NvSciBufAccessPerm_Readonly` as the input parameter. Then import this newly created duplicate object with reduced permission into CUDA as shown

  * `NvSciBufGeneralAttrKey_EnableGpuCache`: To control GPU L2 cacheability

  * `NvSciBufGeneralAttrKey_EnableGpuCompression`: To specify GPU compression


Note

For more details on these attributes and their valid input options, refer to NvSciBuf Documentation.

The following code snippet illustrates their sample usage.
    
    
    NvSciBufObj createNvSciBufObject() {
       // Raw Buffer Attributes for CUDA
        NvSciBufType bufType = NvSciBufType_RawBuffer;
        uint64_t rawsize = SIZE;
        uint64_t align = 0;
        bool cpuaccess_flag = true;
        NvSciBufAttrValAccessPerm perm = NvSciBufAccessPerm_ReadWrite;
    
        NvSciRmGpuId gpuid[] ={};
        CUuuid uuid;
        cuDeviceGetUuid(&uuid, dev));
    
        memcpy(&gpuid[0].bytes, &uuid.bytes, sizeof(uuid.bytes));
        // Disable cache on dev
        NvSciBufAttrValGpuCache gpuCache[] = {{gpuid[0], false}};
        NvSciBufAttrValGpuCompression gpuCompression[] = {{gpuid[0], NvSciBufCompressionType_GenericCompressible}};
        // Fill in values
        NvSciBufAttrKeyValuePair rawbuffattrs[] = {
             { NvSciBufGeneralAttrKey_Types, &bufType, sizeof(bufType) },
             { NvSciBufRawBufferAttrKey_Size, &rawsize, sizeof(rawsize) },
             { NvSciBufRawBufferAttrKey_Align, &align, sizeof(align) },
             { NvSciBufGeneralAttrKey_NeedCpuAccess, &cpuaccess_flag, sizeof(cpuaccess_flag) },
             { NvSciBufGeneralAttrKey_RequiredPerm, &perm, sizeof(perm) },
             { NvSciBufGeneralAttrKey_GpuId, &gpuid, sizeof(gpuid) },
             { NvSciBufGeneralAttrKey_EnableGpuCache &gpuCache, sizeof(gpuCache) },
             { NvSciBufGeneralAttrKey_EnableGpuCompression &gpuCompression, sizeof(gpuCompression) }
        };
    
        // Create list by setting attributes
        err = NvSciBufAttrListSetAttrs(attrListBuffer, rawbuffattrs,
                sizeof(rawbuffattrs)/sizeof(NvSciBufAttrKeyValuePair));
    
        NvSciBufAttrListCreate(NvSciBufModule, &attrListBuffer);
    
        // Reconcile And Allocate
        NvSciBufAttrListReconcile(&attrListBuffer, 1, &attrListReconciledBuffer,
                           &attrListConflictBuffer)
        NvSciBufObjAlloc(attrListReconciledBuffer, &bufferObjRaw);
        return bufferObjRaw;
    }
    
    
    
    NvSciBufObj bufferObjRo; // Readonly NvSciBuf memory obj
    // Create a duplicate handle to the same memory buffer with reduced permissions
    NvSciBufObjDupWithReducePerm(bufferObjRaw, NvSciBufAccessPerm_Readonly, &bufferObjRo);
    return bufferObjRo;
    

The allocated NvSciBuf memory object can be imported in CUDA using the NvSciBufObj handle as shown below. Application should query the allocated NvSciBufObj for attributes required for filling CUDA External Memory Descriptor. Note that the attribute list and NvSciBuf objects should be maintained by the application. If the NvSciBuf object imported into CUDA is also mapped by other drivers, then based on `NvSciBufGeneralAttrKey_GpuSwNeedCacheCoherency` output attribute value the application must use NvSciSync objects (refer to [Importing Synchronization Objects](#importing-synchronization-objects-nvsci)) as appropriate barriers to maintain coherence between CUDA and the other drivers.

Note

For more details on how to allocate and maintain NvSciBuf objects refer to NvSciBuf API Documentation.
    
    
    cudaExternalMemory_t importNvSciBufObject (NvSciBufObj bufferObjRaw) {
    
        /*************** Query NvSciBuf Object **************/
        NvSciBufAttrKeyValuePair bufattrs[] = {
                    { NvSciBufRawBufferAttrKey_Size, NULL, 0 },
                    { NvSciBufGeneralAttrKey_GpuSwNeedCacheCoherency, NULL, 0 },
                    { NvSciBufGeneralAttrKey_EnableGpuCompression, NULL, 0 }
        };
        NvSciBufAttrListGetAttrs(retList, bufattrs,
            sizeof(bufattrs)/sizeof(NvSciBufAttrKeyValuePair)));
                    ret_size = *(static_cast<const uint64_t*>(bufattrs[0].value));
    
        // Note cache and compression are per GPU attributes, so read values for specific gpu by comparing UUID
        // Read cacheability granted by NvSciBuf
        int numGpus = bufattrs[1].len / sizeof(NvSciBufAttrValGpuCache);
        NvSciBufAttrValGpuCache[] cacheVal = (NvSciBufAttrValGpuCache *)bufattrs[1].value;
        bool ret_cacheVal;
        for (int i = 0; i < numGpus; i++) {
            if (memcmp(gpuid[0].bytes, cacheVal[i].gpuId.bytes, sizeof(CUuuid)) == 0) {
                ret_cacheVal = cacheVal[i].cacheability);
            }
        }
    
        // Read compression granted by NvSciBuf
        numGpus = bufattrs[2].len / sizeof(NvSciBufAttrValGpuCompression);
        NvSciBufAttrValGpuCompression[] compVal = (NvSciBufAttrValGpuCompression *)bufattrs[2].value;
        NvSciBufCompressionType ret_compVal;
        for (int i = 0; i < numGpus; i++) {
            if (memcmp(gpuid[0].bytes, compVal[i].gpuId.bytes, sizeof(CUuuid)) == 0) {
                ret_compVal = compVal[i].compressionType);
            }
        }
    
        /*************** NvSciBuf Registration With CUDA **************/
    
        // Fill up CUDA_EXTERNAL_MEMORY_HANDLE_DESC
        cudaExternalMemoryHandleDesc memHandleDesc;
        memset(&memHandleDesc, 0, sizeof(memHandleDesc));
        memHandleDesc.type = cudaExternalMemoryHandleTypeNvSciBuf;
        memHandleDesc.handle.nvSciBufObject = bufferObjRaw;
        // Set the NvSciBuf object with required access permissions in this step
        memHandleDesc.handle.nvSciBufObject = bufferObjRo;
        memHandleDesc.size = ret_size;
        cudaImportExternalMemory(&extMemBuffer, &memHandleDesc);
        return extMemBuffer;
     }
    

#####  6.2.16.5.2. Mapping Buffers onto Imported Memory Objects 

A device pointer can be mapped onto an imported memory object as shown below. The offset and size of the mapping can be filled as per the attributes of the allocated `NvSciBufObj`. All mapped device pointers must be freed using `cudaFree()`.
    
    
    void * mapBufferOntoExternalMemory(cudaExternalMemory_t extMem, unsigned long long offset, unsigned long long size) {
        void *ptr = NULL;
        cudaExternalMemoryBufferDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.offset = offset;
        desc.size = size;
    
        cudaExternalMemoryGetMappedBuffer(&ptr, extMem, &desc);
    
        // Note: 'ptr' must eventually be freed using cudaFree()
        return ptr;
    }
    

#####  6.2.16.5.3. Mapping Mipmapped Arrays onto Imported Memory Objects 

A CUDA mipmapped array can be mapped onto an imported memory object as shown below. The offset, dimensions and format can be filled as per the attributes of the allocated `NvSciBufObj`. All mapped mipmapped arrays must be freed using `cudaFreeMipmappedArray()`. The following code sample shows how to convert NvSciBuf attributes into the corresponding CUDA parameters when mapping mipmapped arrays onto imported memory objects.

Note

The number of mip levels must be 1.
    
    
    cudaMipmappedArray_t mapMipmappedArrayOntoExternalMemory(cudaExternalMemory_t extMem, unsigned long long offset, cudaChannelFormatDesc *formatDesc, cudaExtent *extent, unsigned int flags, unsigned int numLevels) {
        cudaMipmappedArray_t mipmap = NULL;
        cudaExternalMemoryMipmappedArrayDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.offset = offset;
        desc.formatDesc = *formatDesc;
        desc.extent = *extent;
        desc.flags = flags;
        desc.numLevels = numLevels;
    
        // Note: 'mipmap' must eventually be freed using cudaFreeMipmappedArray()
        cudaExternalMemoryGetMappedMipmappedArray(&mipmap, extMem, &desc);
    
        return mipmap;
    }
    

#####  6.2.16.5.4. Importing Synchronization Objects 

NvSciSync attributes that are compatible with a given CUDA device can be generated using `cudaDeviceGetNvSciSyncAttributes()`. The returned attribute list can be used to create a `NvSciSyncObj` that is guaranteed compatibility with a given CUDA device.
    
    
    NvSciSyncObj createNvSciSyncObject() {
        NvSciSyncObj nvSciSyncObj
        int cudaDev0 = 0;
        int cudaDev1 = 1;
        NvSciSyncAttrList signalerAttrList = NULL;
        NvSciSyncAttrList waiterAttrList = NULL;
        NvSciSyncAttrList reconciledList = NULL;
        NvSciSyncAttrList newConflictList = NULL;
    
        NvSciSyncAttrListCreate(module, &signalerAttrList);
        NvSciSyncAttrListCreate(module, &waiterAttrList);
        NvSciSyncAttrList unreconciledList[2] = {NULL, NULL};
        unreconciledList[0] = signalerAttrList;
        unreconciledList[1] = waiterAttrList;
    
        cudaDeviceGetNvSciSyncAttributes(signalerAttrList, cudaDev0, CUDA_NVSCISYNC_ATTR_SIGNAL);
        cudaDeviceGetNvSciSyncAttributes(waiterAttrList, cudaDev1, CUDA_NVSCISYNC_ATTR_WAIT);
    
        NvSciSyncAttrListReconcile(unreconciledList, 2, &reconciledList, &newConflictList);
    
        NvSciSyncObjAlloc(reconciledList, &nvSciSyncObj);
    
        return nvSciSyncObj;
    }
    

An NvSciSync object (created as above) can be imported into CUDA using the NvSciSyncObj handle as shown below. Note that ownership of the NvSciSyncObj handle continues to lie with the application even after it is imported.
    
    
    cudaExternalSemaphore_t importNvSciSyncObject(void* nvSciSyncObj) {
        cudaExternalSemaphore_t extSem = NULL;
        cudaExternalSemaphoreHandleDesc desc = {};
    
        memset(&desc, 0, sizeof(desc));
    
        desc.type = cudaExternalSemaphoreHandleTypeNvSciSync;
        desc.handle.nvSciSyncObj = nvSciSyncObj;
    
        cudaImportExternalSemaphore(&extSem, &desc);
    
        // Deleting/Freeing the nvSciSyncObj beyond this point will lead to undefined behavior in CUDA
    
        return extSem;
    }
    

#####  6.2.16.5.5. Signaling/Waiting on Imported Synchronization Objects 

An imported `NvSciSyncObj` object can be signaled as outlined below. Signaling NvSciSync backed semaphore object initializes the _fence_ parameter passed as input. This fence parameter is waited upon by a wait operation that corresponds to the aforementioned signal. Additionally, the wait that waits on this signal must be issued after this signal has been issued. If the flags are set to `cudaExternalSemaphoreSignalSkipNvSciBufMemSync` then memory synchronization operations (over all the imported NvSciBuf in this process) that are executed as a part of the signal operation by default are skipped. When `NvsciBufGeneralAttrKey_GpuSwNeedCacheCoherency` is FALSE, this flag should be set.
    
    
    void signalExternalSemaphore(cudaExternalSemaphore_t extSem, cudaStream_t stream, void *fence) {
        cudaExternalSemaphoreSignalParams signalParams = {};
    
        memset(&signalParams, 0, sizeof(signalParams));
    
        signalParams.params.nvSciSync.fence = (void*)fence;
        signalParams.flags = 0; //OR cudaExternalSemaphoreSignalSkipNvSciBufMemSync
    
        cudaSignalExternalSemaphoresAsync(&extSem, &signalParams, 1, stream);
    
    }
    

An imported `NvSciSyncObj` object can be waited upon as outlined below. Waiting on NvSciSync backed semaphore object waits until the input _fence_ parameter is signaled by the corresponding signaler. Additionally, the signal must be issued before the wait can be issued. If the flags are set to `cudaExternalSemaphoreWaitSkipNvSciBufMemSync` then memory synchronization operations (over all the imported NvSciBuf in this process) that are executed as a part of the signal operation by default are skipped. When `NvsciBufGeneralAttrKey_GpuSwNeedCacheCoherency` is FALSE, this flag should be set.
    
    
    void waitExternalSemaphore(cudaExternalSemaphore_t extSem, cudaStream_t stream, void *fence) {
         cudaExternalSemaphoreWaitParams waitParams = {};
    
        memset(&waitParams, 0, sizeof(waitParams));
    
        waitParams.params.nvSciSync.fence = (void*)fence;
        waitParams.flags = 0; //OR cudaExternalSemaphoreWaitSkipNvSciBufMemSync
    
        cudaWaitExternalSemaphoresAsync(&extSem, &waitParams, 1, stream);
    }
    


##  6.3. Versioning and Compatibility 

There are two version numbers that developers should care about when developing a CUDA application: The compute capability that describes the general specifications and features of the compute device (see [Compute Capability](#compute-capability)) and the version of the CUDA driver API that describes the features supported by the driver API and runtime.

The version of the driver API is defined in the driver header file as `CUDA_VERSION`. It allows developers to check whether their application requires a newer device driver than the one currently installed. This is important, because the driver API is _backward compatible_ , meaning that applications, plug-ins, and libraries (including the CUDA runtime) compiled against a particular version of the driver API will continue to work on subsequent device driver releases as illustrated in [Figure 26](#versioning-and-compatibility-driver-api-is-backward-but-not-forward-compatible). The driver API is not _forward compatible_ , which means that applications, plug-ins, and libraries (including the CUDA runtime) compiled against a particular version of the driver API will not work on previous versions of the device driver.

It is important to note that there are limitations on the mixing and matching of versions that is supported:

  * Since only one version of the CUDA Driver can be installed at a time on a system, the installed driver must be of the same or higher version than the maximum Driver API version against which any application, plug-ins, or libraries that must run on that system were built.

  * All plug-ins and libraries used by an application must use the same version of the CUDA Runtime unless they statically link to the Runtime, in which case multiple versions of the runtime can coexist in the same process space. Note that if `nvcc` is used to link the application, the static version of the CUDA Runtime library will be used by default, and all CUDA Toolkit libraries are statically linked against the CUDA Runtime.

  * All plug-ins and libraries used by an application must use the same version of any libraries that use the runtime (such as cuFFT, cuBLAS, …) unless statically linking to those libraries.


![The Driver API Is Backward but Not Forward Compatible](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/compatibility-of-cuda-versions.png)

Figure 26 The Driver API Is Backward but Not Forward Compatible

For Tesla GPU products, CUDA 10 introduced a new forward-compatible upgrade path for the user-mode components of the CUDA Driver. This feature is described in [CUDA Compatibility](https://docs.nvidia.com/deploy/cuda-compatibility/index.html). The requirements on the CUDA Driver version described here apply to the version of the user-mode components.


##  6.4. Compute Modes 

On Tesla solutions running Windows Server 2008 and later or Linux, one can set any device in a system in one of the three following modes using NVIDIA’s System Management Interface (nvidia-smi), which is a tool distributed as part of the driver:

  * _Default_ compute mode: Multiple host threads can use the device (by calling `cudaSetDevice()` on this device, when using the runtime API, or by making current a context associated to the device, when using the driver API) at the same time.

  * _Exclusive-process_ compute mode: Only one CUDA context may be created on the device across all processes in the system. The context may be current to as many threads as desired within the process that created that context.

  * _Prohibited_ compute mode: No CUDA context can be created on the device.


This means, in particular, that a host thread using the runtime API without explicitly calling `cudaSetDevice()` might be associated with a device other than device 0 if device 0 turns out to be in prohibited mode or in exclusive-process mode and used by another process. `cudaSetValidDevices()` can be used to set a device from a prioritized list of devices.

Note also that, for devices featuring the Pascal architecture onwards (compute capability with major revision number 6 and higher), there exists support for Compute Preemption. This allows compute tasks to be preempted at instruction-level granularity, rather than thread block granularity as in prior Maxwell and Kepler GPU architecture, with the benefit that applications with long-running kernels can be prevented from either monopolizing the system or timing out. However, there will be context switch overheads associated with Compute Preemption, which is automatically enabled on those devices for which support exists. The individual attribute query function `cudaDeviceGetAttribute()` with the attribute `cudaDevAttrComputePreemptionSupported` can be used to determine if the device in use supports Compute Preemption. Users wishing to avoid context switch overheads associated with different processes can ensure that only one process is active on the GPU by selecting exclusive-process mode.

Applications may query the compute mode of a device by checking the attribute `cudaDevAttrComputeMode`.


##  6.5. Mode Switches 

GPUs that have a display output dedicate some DRAM memory to the so-called _primary surface_ , which is used to refresh the display device whose output is viewed by the user. When users initiate a _mode switch_ of the display by changing the resolution or bit depth of the display (using NVIDIA control panel or the Display control panel on Windows), the amount of memory needed for the primary surface changes. For example, if the user changes the display resolution from 1280x1024x32-bit to 1600x1200x32-bit, the system must dedicate 7.68 MB to the primary surface rather than 5.24 MB. (Full-screen graphics applications running with anti-aliasing enabled may require much more display memory for the primary surface.) On Windows, other events that may initiate display mode switches include launching a full-screen DirectX application, hitting Alt+Tab to task switch away from a full-screen DirectX application, or hitting Ctrl+Alt+Del to lock the computer.

If a mode switch increases the amount of memory needed for the primary surface, the system may have to cannibalize memory allocations dedicated to CUDA applications. Therefore, a mode switch results in any call to the CUDA runtime to fail and return an invalid context error.


##  6.6. Tesla Compute Cluster Mode for Windows 

Using NVIDIA’s System Management Interface (_nvidia-smi_), the Windows device driver can be put in TCC (Tesla Compute Cluster) mode for devices of the Tesla and Quadro Series.

TCC mode removes support for any graphics functionality.
