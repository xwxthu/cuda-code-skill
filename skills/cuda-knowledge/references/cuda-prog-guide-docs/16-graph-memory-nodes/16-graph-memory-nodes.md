# 16. Graph Memory Nodes


Warning

This document has been replaced by a new [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide). The information in this document should be considered legacy, and this document is no longer being updated as of CUDA 13.0. Please refer to the [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide) for up-to-date information on CUDA.


##  16.1. Introduction 

Graph memory nodes allow graphs to create and own memory allocations. Graph memory nodes have GPU ordered lifetime semantics, which dictate when memory is allowed to be accessed on the device. These GPU ordered lifetime semantics enable driver-managed memory reuse, and match those of the stream ordered allocation APIs `cudaMallocAsync` and `cudaFreeAsync`, which may be captured when creating a graph.

Graph allocations have fixed addresses over the life of a graph including repeated instantiations and launches. This allows the memory to be directly referenced by other operations within the graph without the need of a graph update, even when CUDA changes the backing physical memory. Within a graph, allocations whose graph ordered lifetimes do not overlap may use the same underlying physical memory.

CUDA may reuse the same physical memory for allocations across multiple graphs, aliasing virtual address mappings according to the GPU ordered lifetime semantics. For example when different graphs are launched into the same stream, CUDA may virtually alias the same physical memory to satisfy the needs of allocations which have single-graph lifetimes.


##  16.2. Support and Compatibility 

Graph memory nodes require an 11.4 capable CUDA driver and support for the stream ordered allocator on the GPU. The following snippet shows how to check for support on a given device.
    
    
    int driverVersion = 0;
    int deviceSupportsMemoryPools = 0;
    int deviceSupportsMemoryNodes = 0;
    cudaDriverGetVersion(&driverVersion);
    if (driverVersion >= 11020) { // avoid invalid value error in cudaDeviceGetAttribute
        cudaDeviceGetAttribute(&deviceSupportsMemoryPools, cudaDevAttrMemoryPoolsSupported, device);
    }
    deviceSupportsMemoryNodes = (driverVersion >= 11040) && (deviceSupportsMemoryPools != 0);
    

Doing the attribute query inside the driver version check avoids an invalid value return code on 11.0 and 11.1 drivers. Be aware that the compute sanitizer emits warnings when it detects CUDA returning error codes, and a version check before reading the attribute will avoid this. Graph memory nodes are only supported on driver versions 11.4 and newer.


##  16.3. API Fundamentals 

Graph memory nodes are graph nodes representing either memory allocation or free actions. As a shorthand, nodes that allocate memory are called allocation nodes. Likewise, nodes that free memory are called free nodes. Allocations created by allocation nodes are called graph allocations. CUDA assigns virtual addresses for the graph allocation at node creation time. While these virtual addresses are fixed for the lifetime of the allocation node, the allocation contents are not persistent past the freeing operation and may be overwritten by accesses referring to a different allocation.

Graph allocations are considered recreated every time a graph runs. A graph allocation’s lifetime, which differs from the node’s lifetime, begins when GPU execution reaches the allocating graph node and ends when one of the following occurs:

  * GPU execution reaches the freeing graph node

  * GPU execution reaches the freeing `cudaFreeAsync()` stream call

  * immediately upon the freeing call to `cudaFree()`


Note

Graph destruction does not automatically free any live graph-allocated memory, even though it ends the lifetime of the allocation node. The allocation must subsequently be freed in another graph, or using `cudaFreeAsync()``/cudaFree()`.

Just like other [Graph Structure](#graph-structure), graph memory nodes are ordered within a graph by dependency edges. A program must guarantee that operations accessing graph memory:

  * are ordered after the allocation node

  * are ordered before the operation freeing the memory


Graph allocation lifetimes begin and usually end according to GPU execution (as opposed to API invocation). GPU ordering is the order that work runs on the GPU as opposed to the order that the work is enqueued or described. Thus, graph allocations are considered ‘GPU ordered.’

###  16.3.1. Graph Node APIs 

Graph memory nodes may be explicitly created with the memory node creation APIs, `cudaGraphAddMemAllocNode` and `cudaGraphAddMemFreeNode`. The address allocated by `cudaGraphAddMemAllocNode` is returned to the user in the `dptr` field of the passed `CUDA_MEM_ALLOC_NODE_PARAMS` structure. All operations using graph allocations inside the allocating graph must be ordered after the allocating node. Similarly, any free nodes must be ordered after all uses of the allocation within the graph. `cudaGraphAddMemFreeNode` creates free nodes.

In the following figure, there is an example graph with an alloc and a free node. Kernel nodes **a** , **b** , and **c** are ordered after the allocation node and before the free node such that the kernels can access the allocation. Kernel node **e** is not ordered after the alloc node and therefore cannot safely access the memory. Kernel node **d** is not ordered before the free node, therefore it cannot safely access the memory.

![Kernel Nodes](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/kernel-nodes.png)

Figure 32 Kernel Nodes

The following code snippet establishes the graph in this figure:
    
    
    // Create the graph - it starts out empty
    cudaGraphCreate(&graph, 0);
    
    // parameters for a basic allocation
    cudaMemAllocNodeParams params = {};
    params.poolProps.allocType = cudaMemAllocationTypePinned;
    params.poolProps.location.type = cudaMemLocationTypeDevice;
    // specify device 0 as the resident device
    params.poolProps.location.id = 0;
    params.bytesize = size;
    
    cudaGraphAddMemAllocNode(&allocNode, graph, NULL, 0, &params);
    nodeParams->kernelParams[0] = params.dptr;
    cudaGraphAddKernelNode(&a, graph, &allocNode, 1, &nodeParams);
    cudaGraphAddKernelNode(&b, graph, &a, 1, &nodeParams);
    cudaGraphAddKernelNode(&c, graph, &a, 1, &nodeParams);
    cudaGraphNode_t dependencies[2];
    // kernel nodes b and c are using the graph allocation, so the freeing node must depend on them.  Since the dependency of node b on node a establishes an indirect dependency, the free node does not need to explicitly depend on node a.
    dependencies[0] = b;
    dependencies[1] = c;
    cudaGraphAddMemFreeNode(&freeNode, graph, dependencies, 2, params.dptr);
    // free node does not depend on kernel node d, so it must not access the freed graph allocation.
    cudaGraphAddKernelNode(&d, graph, &c, 1, &nodeParams);
    
    // node e does not depend on the allocation node, so it must not access the allocation.  This would be true even if the freeNode depended on kernel node e.
    cudaGraphAddKernelNode(&e, graph, NULL, 0, &nodeParams);
    

###  16.3.2. Stream Capture 

Graph memory nodes can be created by capturing the corresponding stream ordered allocation and free calls `cudaMallocAsync` and `cudaFreeAsync`. In this case, the virtual addresses returned by the captured allocation API can be used by other operations inside the graph. Since the stream ordered dependencies will be captured into the graph, the ordering requirements of the stream ordered allocation APIs guarantee that the graph memory nodes will be properly ordered with respect to the captured stream operations (for correctly written stream code).

Ignoring kernel nodes **d** and **e** , for clarity, the following code snippet shows how to use stream capture to create the graph from the previous figure:
    
    
    cudaMallocAsync(&dptr, size, stream1);
    kernel_A<<< ..., stream1 >>>(dptr, ...);
    
    // Fork into stream2
    cudaEventRecord(event1, stream1);
    cudaStreamWaitEvent(stream2, event1);
    
    kernel_B<<< ..., stream1 >>>(dptr, ...);
    // event dependencies translated into graph dependencies, so the kernel node created by the capture of kernel C will depend on the allocation node created by capturing the cudaMallocAsync call.
    kernel_C<<< ..., stream2 >>>(dptr, ...);
    
    // Join stream2 back to origin stream (stream1)
    cudaEventRecord(event2, stream2);
    cudaStreamWaitEvent(stream1, event2);
    
    // Free depends on all work accessing the memory.
    cudaFreeAsync(dptr, stream1);
    
    // End capture in the origin stream
    cudaStreamEndCapture(stream1, &graph);
    

###  16.3.3. Accessing and Freeing Graph Memory Outside of the Allocating Graph 

Graph allocations do not have to be freed by the allocating graph. When a graph does not free an allocation, that allocation persists beyond the execution of the graph and can be accessed by subsequent CUDA operations. These allocations may be accessed in another graph or directly using a stream operation as long as the accessing operation is ordered after the allocation through CUDA events and other stream ordering mechanisms. An allocation may subsequently be freed by regular calls to `cudaFree`, `cudaFreeAsync`, or by the launch of another graph with a corresponding free node, or a subsequent launch of the allocating graph (if it was instantiated with the [cudaGraphInstantiateFlagAutoFreeOnLaunch](#graph-memory-nodes-cudagraphinstantiateflagautofreeonlaunch) flag). It is illegal to access memory after it has been freed - the free operation must be ordered after all operations accessing the memory using graph dependencies, CUDA events, and other stream ordering mechanisms.

Note

Because graph allocations may share underlying physical memory with each other, the [Virtual Aliasing Support](#virtual-aliasing-support) rules relating to consistency and coherency must be considered. Simply put, the free operation must be ordered after the full device operation (for example, compute kernel / memcpy) completes. Specifically, out of band synchronization - for example a handshake through memory as part of a compute kernel that accesses the graph-allocated memory - is not sufficient for providing ordering guarantees between the memory writes to graph memory and the free operation of that graph memory.

The following code snippets demonstrate accessing graph allocations outside of the allocating graph with ordering properly established by: using a single stream, using events between streams, and using events baked into the allocating and freeing graph.

**Ordering established by using a single stream:**
    
    
    void *dptr;
    cudaGraphAddMemAllocNode(&allocNode, allocGraph, NULL, 0, &params);
    dptr = params.dptr;
    
    cudaGraphInstantiate(&allocGraphExec, allocGraph, NULL, NULL, 0);
    
    cudaGraphLaunch(allocGraphExec, stream);
    kernel<<< …, stream >>>(dptr, …);
    cudaFreeAsync(dptr, stream);
    

**Ordering established by recording and waiting on CUDA events:**
    
    
    void *dptr;
    
    // Contents of allocating graph
    cudaGraphAddMemAllocNode(&allocNode, allocGraph, NULL, 0, &params);
    dptr = params.dptr;
    
    // contents of consuming/freeing graph
    nodeParams->kernelParams[0] = params.dptr;
    cudaGraphAddKernelNode(&a, graph, NULL, 0, &nodeParams);
    cudaGraphAddMemFreeNode(&freeNode, freeGraph, &a, 1, dptr);
    
    cudaGraphInstantiate(&allocGraphExec, allocGraph, NULL, NULL, 0);
    cudaGraphInstantiate(&freeGraphExec, freeGraph, NULL, NULL, 0);
    
    cudaGraphLaunch(allocGraphExec, allocStream);
    
    // establish the dependency of stream2 on the allocation node
    // note: the dependency could also have been established with a stream synchronize operation
    cudaEventRecord(allocEvent, allocStream)
    cudaStreamWaitEvent(stream2, allocEvent);
    
    kernel<<< …, stream2 >>> (dptr, …);
    
    // establish the dependency between the stream 3 and the allocation use
    cudaStreamRecordEvent(streamUseDoneEvent, stream2);
    cudaStreamWaitEvent(stream3, streamUseDoneEvent);
    
    // it is now safe to launch the freeing graph, which may also access the memory
    cudaGraphLaunch(freeGraphExec, stream3);
    

**Ordering established by using graph external event nodes:**
    
    
    void *dptr;
    cudaEvent_t allocEvent; // event indicating when the allocation will be ready for use.
    cudaEvent_t streamUseDoneEvent; // event indicating when the stream operations are done with the allocation.
    
    // Contents of allocating graph with event record node
    cudaGraphAddMemAllocNode(&allocNode, allocGraph, NULL, 0, &params);
    dptr = params.dptr;
    // note: this event record node depends on the alloc node
    cudaGraphAddEventRecordNode(&recordNode, allocGraph, &allocNode, 1, allocEvent);
    cudaGraphInstantiate(&allocGraphExec, allocGraph, NULL, NULL, 0);
    
    // contents of consuming/freeing graph with event wait nodes
    cudaGraphAddEventWaitNode(&streamUseDoneEventNode, waitAndFreeGraph, NULL, 0, streamUseDoneEvent);
    cudaGraphAddEventWaitNode(&allocReadyEventNode, waitAndFreeGraph, NULL, 0, allocEvent);
    nodeParams->kernelParams[0] = params.dptr;
    
    // The allocReadyEventNode provides ordering with the alloc node for use in a consuming graph.
    cudaGraphAddKernelNode(&kernelNode, waitAndFreeGraph, &allocReadyEventNode, 1, &nodeParams);
    
    // The free node has to be ordered after both external and internal users.
    // Thus the node must depend on both the kernelNode and the
    // streamUseDoneEventNode.
    dependencies[0] = kernelNode;
    dependencies[1] = streamUseDoneEventNode;
    cudaGraphAddMemFreeNode(&freeNode, waitAndFreeGraph, &dependencies, 2, dptr);
    cudaGraphInstantiate(&waitAndFreeGraphExec, waitAndFreeGraph, NULL, NULL, 0);
    
    cudaGraphLaunch(allocGraphExec, allocStream);
    
    // establish the dependency of stream2 on the event node satisfies the ordering requirement
    cudaStreamWaitEvent(stream2, allocEvent);
    kernel<<< …, stream2 >>> (dptr, …);
    cudaStreamRecordEvent(streamUseDoneEvent, stream2);
    
    // the event wait node in the waitAndFreeGraphExec establishes the dependency on the “readyForFreeEvent” that is needed to prevent the kernel running in stream two from accessing the allocation after the free node in execution order.
    cudaGraphLaunch(waitAndFreeGraphExec, stream3);
    

###  16.3.4. cudaGraphInstantiateFlagAutoFreeOnLaunch 

Under normal circumstances, CUDA will prevent a graph from being relaunched if it has unfreed memory allocations because multiple allocations at the same address will leak memory. Instantiating a graph with the `cudaGraphInstantiateFlagAutoFreeOnLaunch` flag allows the graph to be relaunched while it still has unfreed allocations. In this case, the launch automatically inserts an asynchronous free of the unfreed allocations.

Auto free on launch is useful for single-producer multiple-consumer algorithms. At each iteration, a producer graph creates several allocations, and, depending on runtime conditions, a varying set of consumers accesses those allocations. This type of variable execution sequence means that consumers cannot free the allocations because a subsequent consumer may require access. Auto free on launch means that the launch loop does not need to track the producer’s allocations - instead, that information remains isolated to the producer’s creation and destruction logic. In general, auto free on launch simplifies an algorithm which would otherwise need to free all the allocations owned by a graph before each relaunch.

Note

The `cudaGraphInstantiateFlagAutoFreeOnLaunch` flag does not change the behavior of graph destruction. The application must explicitly free the unfreed memory in order to avoid memory leaks, even for graphs instantiated with the flag. The following code shows the use of `cudaGraphInstantiateFlagAutoFreeOnLaunch` to simplify a single-producer / multiple-consumer algorithm:
    
    
    // Create producer graph which allocates memory and populates it with data
    cudaStreamBeginCapture(cudaStreamPerThread, cudaStreamCaptureModeGlobal);
    cudaMallocAsync(&data1, blocks * threads, cudaStreamPerThread);
    cudaMallocAsync(&data2, blocks * threads, cudaStreamPerThread);
    produce<<<blocks, threads, 0, cudaStreamPerThread>>>(data1, data2);
    ...
    cudaStreamEndCapture(cudaStreamPerThread, &graph);
    cudaGraphInstantiateWithFlags(&producer,
                                  graph,
                                  cudaGraphInstantiateFlagAutoFreeOnLaunch);
    cudaGraphDestroy(graph);
    
    // Create first consumer graph by capturing an asynchronous library call
    cudaStreamBeginCapture(cudaStreamPerThread, cudaStreamCaptureModeGlobal);
    consumerFromLibrary(data1, cudaStreamPerThread);
    cudaStreamEndCapture(cudaStreamPerThread, &graph);
    cudaGraphInstantiateWithFlags(&consumer1, graph, 0); //regular instantiation
    cudaGraphDestroy(graph);
    
    // Create second consumer graph
    cudaStreamBeginCapture(cudaStreamPerThread, cudaStreamCaptureModeGlobal);
    consume2<<<blocks, threads, 0, cudaStreamPerThread>>>(data2);
    ...
    cudaStreamEndCapture(cudaStreamPerThread, &graph);
    cudaGraphInstantiateWithFlags(&consumer2, graph, 0);
    cudaGraphDestroy(graph);
    
    // Launch in a loop
    bool launchConsumer2 = false;
    do {
        cudaGraphLaunch(producer, myStream);
        cudaGraphLaunch(consumer1, myStream);
        if (launchConsumer2) {
            cudaGraphLaunch(consumer2, myStream);
        }
    } while (determineAction(&launchConsumer2));
    
    cudaFreeAsync(data1, myStream);
    cudaFreeAsync(data2, myStream);
    
    cudaGraphExecDestroy(producer);
    cudaGraphExecDestroy(consumer1);
    cudaGraphExecDestroy(consumer2);
    


##  16.4. Optimized Memory Reuse 

CUDA reuses memory in two ways:

  * Virtual and physical memory reuse within a graph is based on virtual address assignment, like in the stream ordered allocator.

  * Physical memory reuse between graphs is done with virtual aliasing: different graphs can map the same physical memory to their unique virtual addresses.


###  16.4.1. Address Reuse within a Graph 

CUDA may reuse memory within a graph by assigning the same virtual address ranges to different allocations whose lifetimes do not overlap. Since virtual addresses may be reused, pointers to different allocations with disjoint lifetimes are not guaranteed to be unique.

The following figure shows adding a new allocation node (2) that can reuse the address freed by a dependent node (1).

![Adding New Alloc Node 2](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/new-alloc-node.png)

Figure 33 Adding New Alloc Node 2

The following figure shows adding a new alloc node (4). The new alloc node is not dependent on the free node (2) so cannot reuse the address from the associated alloc node (2). If the alloc node (2) used the address freed by free node (1), the new alloc node 3 would need a new address.

![Adding New Alloc Node 3](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/adding-new-alloc-nodes.png)

Figure 34 Adding New Alloc Node 3

###  16.4.2. Physical Memory Management and Sharing 

CUDA is responsible for mapping physical memory to the virtual address before the allocating node is reached in GPU order. As an optimization for memory footprint and mapping overhead, multiple graphs may use the same physical memory for distinct allocations if they will not run simultaneously; however, physical pages cannot be reused if they are bound to more than one executing graph at the same time, or to a graph allocation which remains unfreed.

CUDA may update physical memory mappings at any time during graph instantiation, launch, or execution. CUDA may also introduce synchronization between future graph launches in order to prevent live graph allocations from referring to the same physical memory. As for any allocate-free-allocate pattern, if a program accesses a pointer outside of an allocation’s lifetime, the erroneous access may silently read or write live data owned by another allocation (even if the virtual address of the allocation is unique). Use of compute sanitizer tools can catch this error.

The following figure shows graphs sequentially launched in the same stream. In this example, each graph frees all the memory it allocates. Since the graphs in the same stream never run concurrently, CUDA can and should use the same physical memory to satisfy all the allocations.

![Sequentially Launched Graphs](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/sequentially-launched-graphs.png)

Figure 35 Sequentially Launched Graphs


##  16.5. Performance Considerations 

When multiple graphs are launched into the same stream, CUDA attempts to allocate the same physical memory to them because the execution of these graphs cannot overlap. Physical mappings for a graph are retained between launches as an optimization to avoid the cost of remapping. If, at a later time, one of the graphs is launched such that its execution may overlap with the others (for example if it is launched into a different stream) then CUDA must perform some remapping because concurrent graphs require distinct memory to avoid data corruption.

In general, remapping of graph memory in CUDA is likely caused by these operations:

  * Changing the stream into which a graph is launched

  * A trim operation on the graph memory pool, which explicitly frees unused memory (discussed in [Physical Memory Footprint](#graph-memory-nodes-physical-memory-footprint))

  * Relaunching a graph while an unfreed allocation from another graph is mapped to the same memory will cause a remap of memory before relaunch


Remapping must happen in execution order, but after any previous execution of that graph is complete (otherwise memory that is still in use could be unmapped). Due to this ordering dependency, as well as because mapping operations are OS calls, mapping operations can be relatively expensive. Applications can avoid this cost by launching graphs containing allocation memory nodes consistently into the same stream.

###  16.5.1. First Launch / cudaGraphUpload 

Physical memory cannot be allocated or mapped during graph instantiation because the stream in which the graph will execute is unknown. Mapping is done instead during graph launch. Calling `cudaGraphUpload` can separate out the cost of allocation from the launch by performing all mappings for that graph immediately and associating the graph with the upload stream. If the graph is then launched into the same stream, it will launch without any additional remapping.

Using different streams for graph upload and graph launch behaves similarly to switching streams, likely resulting in remap operations. In addition, unrelated memory pool management is permitted to pull memory from an idle stream, which could negate the impact of the uploads.


##  16.6. Physical Memory Footprint 

The pool-management behavior of asynchronous allocation means that destroying a graph which contains memory nodes (even if their allocations are free) will not immediately return physical memory to the OS for use by other processes. To explicitly release memory back to the OS, an application should use the `cudaDeviceGraphMemTrim` API.

`cudaDeviceGraphMemTrim` will unmap and release any physical memory reserved by graph memory nodes that is not actively in use. Allocations that have not been freed and graphs that are scheduled or running are considered to be actively using the physical memory and will not be impacted. Use of the trim API will make physical memory available to other allocation APIs and other applications or processes, but will cause CUDA to reallocate and remap memory when the trimmed graphs are next launched. Note that `cudaDeviceGraphMemTrim` operates on a different pool from `cudaMemPoolTrimTo()`. The graph memory pool is not exposed to the steam ordered memory allocator. CUDA allows applications to query their graph memory footprint through the `cudaDeviceGetGraphMemAttribute` API. Querying the attribute `cudaGraphMemAttrReservedMemCurrent` returns the amount of physical memory reserved by the driver for graph allocations in the current process. Querying `cudaGraphMemAttrUsedMemCurrent` returns the amount of physical memory currently mapped by at least one graph. Either of these attributes can be used to track when new physical memory is acquired by CUDA for the sake of an allocating graph. Both of these attributes are useful for examining how much memory is saved by the sharing mechanism.


##  16.7. Peer Access 

Graph allocations can be configured for access from multiple GPUs, in which case CUDA will map the allocations onto the peer GPUs as required. CUDA allows graph allocations requiring different mappings to reuse the same virtual address. When this occurs, the address range is mapped onto all GPUs required by the different allocations. This means an allocation may sometimes allow more peer access than was requested during its creation; however, relying on these extra mappings is still an error.

###  16.7.1. Peer Access with Graph Node APIs 

The `cudaGraphAddMemAllocNode` API accepts mapping requests in the `accessDescs` array field of the node parameters structures. The `poolProps.location` embedded structure specifies the resident device for the allocation. Access from the allocating GPU is assumed to be needed, thus the application does not need to specify an entry for the resident device in the `accessDescs` array.
    
    
    cudaMemAllocNodeParams params = {};
    params.poolProps.allocType = cudaMemAllocationTypePinned;
    params.poolProps.location.type = cudaMemLocationTypeDevice;
    // specify device 1 as the resident device
    params.poolProps.location.id = 1;
    params.bytesize = size;
    
    // allocate an allocation resident on device 1 accessible from device 1
    cudaGraphAddMemAllocNode(&allocNode, graph, NULL, 0, &params);
    
    accessDescs[2];
    // boilerplate for the access descs (only ReadWrite and Device access supported by the add node api)
    accessDescs[0].flags = cudaMemAccessFlagsProtReadWrite;
    accessDescs[0].location.type = cudaMemLocationTypeDevice;
    accessDescs[1].flags = cudaMemAccessFlagsProtReadWrite;
    accessDescs[1].location.type = cudaMemLocationTypeDevice;
    
    // access being requested for device 0 & 2.  Device 1 access requirement left implicit.
    accessDescs[0].location.id = 0;
    accessDescs[1].location.id = 2;
    
    // access request array has 2 entries.
    params.accessDescCount = 2;
    params.accessDescs = accessDescs;
    
    // allocate an allocation resident on device 1 accessible from devices 0, 1 and 2. (0 & 2 from the descriptors, 1 from it being the resident device).
    cudaGraphAddMemAllocNode(&allocNode, graph, NULL, 0, &params);
    

###  16.7.2. Peer Access with Stream Capture 

For stream capture, the allocation node records the peer accessibility of the allocating pool at the time of the capture. Altering the peer accessibility of the allocating pool after a `cudaMallocFromPoolAsync` call is captured does not affect the mappings that the graph will make for the allocation.
    
    
    // boilerplate for the access descs (only ReadWrite and Device access supported by the add node api)
    accessDesc.flags = cudaMemAccessFlagsProtReadWrite;
    accessDesc.location.type = cudaMemLocationTypeDevice;
    accessDesc.location.id = 1;
    
    // let memPool be resident and accessible on device 0
    
    cudaStreamBeginCapture(stream);
    cudaMallocAsync(&dptr1, size, memPool, stream);
    cudaStreamEndCapture(stream, &graph1);
    
    cudaMemPoolSetAccess(memPool, &accessDesc, 1);
    
    cudaStreamBeginCapture(stream);
    cudaMallocAsync(&dptr2, size, memPool, stream);
    cudaStreamEndCapture(stream, &graph2);
    
    //The graph node allocating dptr1 would only have the device 0 accessibility even though memPool now has device 1 accessibility.
    //The graph node allocating dptr2 will have device 0 and device 1 accessibility, since that was the pool accessibility at the time of the cudaMallocAsync call.
    


##  16.8. Memory Nodes in Child Graphs 

CUDA 12.9 introduces the ability to move child graph ownership to a parent graph. Child graphs which are moved to the parent are allowed to contain memory allocation and free nodes. This allows a child graph containing allocation or free nodes to be independently constructed prior to its addition in a parent graph.

The following restrictions apply to child graphs after they have been moved:

  * Cannot be independently instantiated or destroyed.

  * Cannot be added as a child graph of a separate parent graph.

  * Cannot be used as an argument to cuGraphExecUpdate.

  * Cannot have additional memory allocation or free nodes added.


    
    
    // Create the child graph
    cudaGraphCreate(&child, 0);
    
    // parameters for a basic allocation
    cudaMemAllocNodeParams params = {};
    params.poolProps.allocType = cudaMemAllocationTypePinned;
    params.poolProps.location.type = cudaMemLocationTypeDevice;
    // specify device 0 as the resident device
    params.poolProps.location.id = 0;
    params.bytesize = size;
    
    cudaGraphAddMemAllocNode(&allocNode, graph, NULL, 0, &params);
    // Additional nodes using the allocation could be added here
    cudaGraphAddMemFreeNode(&freeNode, graph, &allocNode, 1, params.dptr);
    
    // Create the parent graph
    cudaGraphCreate(&parent, 0);
    
    // Move the child graph to the parent graph
    cudaGraphNodeParams childNodeParams = { cudaGraphNodeTypeGraph };
    childNodeParams.graph.graph = child;
    childNodeParams.graph.ownership = cudaGraphChildGraphOwnershipMove;
    cudaGraphAddNode(&parentNode, parent, NULL, NULL, 0, &childNodeParams);
    
