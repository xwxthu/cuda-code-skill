# 11. Cooperative Groups


Warning

This document has been replaced by a new [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide). The information in this document should be considered legacy, and this document is no longer being updated as of CUDA 13.0. Please refer to the [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide) for up-to-date information on CUDA.


##  11.1. Introduction 

Cooperative Groups is an extension to the CUDA programming model, introduced in CUDA 9, for organizing groups of communicating threads. Cooperative Groups allows developers to express the granularity at which threads are communicating, helping them to express richer, more efficient parallel decompositions.

Historically, the CUDA programming model has provided a single, simple construct for synchronizing cooperating threads: a barrier across all threads of a thread block, as implemented with the `__syncthreads()` intrinsic function. However, programmers would like to define and synchronize groups of threads at other granularities to enable greater performance, design flexibility, and software reuse in the form of “collective” group-wide function interfaces. In an effort to express broader patterns of parallel interaction, many performance-oriented programmers have resorted to writing their own ad hoc and unsafe primitives for synchronizing threads within a single warp, or across sets of thread blocks running on a single GPU. Whilst the performance improvements achieved have often been valuable, this has resulted in an ever-growing collection of brittle code that is expensive to write, tune, and maintain over time and across GPU generations. Cooperative Groups addresses this by providing a safe and future-proof mechanism to enable performant code.


##  11.2. What’s New in Cooperative Groups 

###  11.2.1. CUDA 13.0 

  * `multi_grid_group` was removed.


###  11.2.2. CUDA 12.2 

  * `barrier_arrive` and `barrier_wait` member functions were added for [grid_group](#grid-group-cg) and [thread_block](#thread-block-group-cg). Description of the API is available [here](#collectives-cg-sync).


###  11.2.3. CUDA 12.1 

  * [invoke_one and invoke_one_broadcast](#invoke-one-and-invoke-one-broadcast) APIs were added.


###  11.2.4. CUDA 12.0 

  * The following experimental APIs are now moved to the main namespace:

    * asynchronous reduce and scan update added in CUDA 11.7

    * `thread_block_tile` larger than 32 added in CUDA 11.1

  * It is no longer required to provide memory using the `block_tile_memory` object in order to create these large tiles on Compute Capability 8.0 or higher.


##  11.3. Programming Model Concept 

The Cooperative Groups programming model describes synchronization patterns both within and across CUDA thread blocks. It provides both the means for applications to define their own groups of threads, and the interfaces to synchronize them. It also provides new launch APIs that enforce certain restrictions and therefore can guarantee the synchronization will work. These primitives enable new patterns of cooperative parallelism within CUDA, including producer-consumer parallelism, opportunistic parallelism, and global synchronization across the entire Grid.

The Cooperative Groups programming model consists of the following elements:

  * Data types for representing groups of cooperating threads;

  * Operations to obtain implicit groups defined by the CUDA launch API (e.g., thread blocks);

  * Collectives for partitioning existing groups into new groups;

  * Collective Algorithms for data movement and manipulation (e.g. memcpy_async, reduce, scan);

  * An operation to synchronize all threads within the group;

  * Operations to inspect the group properties;

  * Collectives that expose low-level, group-specific and often HW accelerated, operations.


The main concept in Cooperative Groups is that of objects naming the set of threads that are part of it. This expression of groups as first-class program objects improves software composition, since collective functions can receive an explicit object representing the group of participating threads. This object also makes programmer intent explicit, which eliminates unsound architectural assumptions that result in brittle code, undesirable restrictions upon compiler optimizations, and better compatibility with new GPU generations.

To write efficient code, its best to use specialized groups (going generic loses a lot of compile time optimizations), and pass these group objects by reference to functions that intend to use these threads in some cooperative fashion.

Cooperative Groups requires CUDA 9.0 or later. To use Cooperative Groups, include the header file:
    
    
    // Primary header is compatible with pre-C++11, collective algorithm headers require C++11
    #include <cooperative_groups.h>
    // Optionally include for memcpy_async() collective
    #include <cooperative_groups/memcpy_async.h>
    // Optionally include for reduce() collective
    #include <cooperative_groups/reduce.h>
    // Optionally include for inclusive_scan() and exclusive_scan() collectives
    #include <cooperative_groups/scan.h>
    

and use the Cooperative Groups namespace:
    
    
    using namespace cooperative_groups;
    // Alternatively use an alias to avoid polluting the namespace with collective algorithms
    namespace cg = cooperative_groups;
    

The code can be compiled in a normal way using nvcc, however if you wish to use memcpy_async, reduce or scan functionality and your host compiler’s default dialect is not C++11 or higher, then you must add `--std=c++11` to the command line.

###  11.3.1. Composition Example 

To illustrate the concept of groups, this example attempts to perform a block-wide sum reduction. Previously, there were hidden constraints on the implementation when writing this code:
    
    
    __device__ int sum(int *x, int n) {
        // ...
        __syncthreads();
        return total;
    }
    
    __global__ void parallel_kernel(float *x) {
        // ...
        // Entire thread block must call sum
        sum(x, n);
    }
    

All threads in the thread block must arrive at the `__syncthreads()` barrier, however, this constraint is hidden from the developer who might want to use `sum(…)`. With Cooperative Groups, a better way of writing this would be:
    
    
    __device__ int sum(const thread_block& g, int *x, int n) {
        // ...
        g.sync()
        return total;
    }
    
    __global__ void parallel_kernel(...) {
        // ...
        // Entire thread block must call sum
        thread_block tb = this_thread_block();
        sum(tb, x, n);
        // ...
    }
    


##  11.4. Group Types 

###  11.4.1. Implicit Groups 

Implicit groups represent the launch configuration of the kernel. Regardless of how your kernel is written, it always has a set number of threads, blocks and block dimensions, a single grid and grid dimensions. In addition, if the multi-device cooperative launch API is used, it can have multiple grids (single grid per device). These groups provide a starting point for decomposition into finer grained groups which are typically HW accelerated and are more specialized for the problem the developer is solving.

Although you can create an implicit group anywhere in the code, it is dangerous to do so. Creating a handle for an implicit group is a collective operation—all threads in the group must participate. If the group was created in a conditional branch that not all threads reach, this can lead to deadlocks or data corruption. For this reason, it is recommended that you create a handle for the implicit group upfront (as early as possible, before any branching has occurred) and use that handle throughout the kernel. Group handles must be initialized at declaration time (there is no default constructor) for the same reason and copy-constructing them is discouraged.

####  11.4.1.1. Thread Block Group 

Any CUDA programmer is already familiar with a certain group of threads: the thread block. The Cooperative Groups extension introduces a new datatype, `thread_block`, to explicitly represent this concept within the kernel.

`class thread_block;`

Constructed via:
    
    
    thread_block g = this_thread_block();
    

**Public Member Functions:**

`static void sync()`: Synchronize the threads named in the group, equivalent to `g.barrier_wait(g.barrier_arrive())`

`thread_block::arrival_token barrier_arrive()`: Arrive on the thread_block barrier, returns a token that needs to be passed into `barrier_wait()`. More details [here](#collectives-cg-sync)

`void barrier_wait(thread_block::arrival_token&& t)`: Wait on the `thread_block` barrier, takes arrival token returned from `barrier_arrive()` as an rvalue reference. More details [here](#collectives-cg-sync)

`static unsigned int thread_rank()`: Rank of the calling thread within [0, num_threads)

`static dim3 group_index()`: 3-Dimensional index of the block within the launched grid

`static dim3 thread_index()`: 3-Dimensional index of the thread within the launched block

`static dim3 dim_threads()`: Dimensions of the launched block in units of threads

`static unsigned int num_threads()`: Total number of threads in the group

Legacy member functions (aliases):

`static unsigned int size()`: Total number of threads in the group (alias of `num_threads()`)

`static dim3 group_dim()`: Dimensions of the launched block (alias of `dim_threads()`)

**Example:**
    
    
    /// Loading an integer from global into shared memory
    __global__ void kernel(int *globalInput) {
        __shared__ int x;
        thread_block g = this_thread_block();
        // Choose a leader in the thread block
        if (g.thread_rank() == 0) {
            // load from global into shared for all threads to work with
            x = (*globalInput);
        }
        // After loading data into shared memory, you want to synchronize
        // if all threads in your thread block need to see it
        g.sync(); // equivalent to __syncthreads();
    }
    

**Note:** that all threads in the group must participate in collective operations, or the behavior is undefined.

**Related:** The `thread_block` datatype is derived from the more generic `thread_group` datatype, which can be used to represent a wider class of groups.

####  11.4.1.2. Cluster Group 

This group object represents all the threads launched in a single cluster. Refer to [Thread Block Clusters](#thread-block-clusters). The APIs are available on all hardware with Compute Capability 9.0+. In such cases, when a non-cluster grid is launched, the APIs assume a 1x1x1 cluster.

`class cluster_group;`

Constructed via:
    
    
    cluster_group g = this_cluster();
    

**Public Member Functions:**

`static void sync()`: Synchronize the threads named in the group, equivalent to `g.barrier_wait(g.barrier_arrive())`

`static cluster_group::arrival_token barrier_arrive()`: Arrive on the cluster barrier, returns a token that needs to be passed into `barrier_wait()`. More details [here](#collectives-cg-sync)

`static void barrier_wait(cluster_group::arrival_token&& t)`: Wait on the cluster barrier, takes arrival token returned from `barrier_arrive()` as a rvalue reference. More details [here](#collectives-cg-sync)

`static unsigned int thread_rank()`: Rank of the calling thread within [0, num_threads)

`static unsigned int block_rank()`: Rank of the calling block within [0, num_blocks)

`static unsigned int num_threads()`: Total number of threads in the group

`static unsigned int num_blocks()`: Total number of blocks in the group

`static dim3 dim_threads()`: Dimensions of the launched cluster in units of threads

`static dim3 dim_blocks()`: Dimensions of the launched cluster in units of blocks

`static dim3 block_index()`: 3-Dimensional index of the calling block within the launched cluster

`static unsigned int query_shared_rank(const void *addr)`: Obtain the block rank to which a shared memory address belongs

`static T* map_shared_rank(T *addr, int rank)`: Obtain the address of a shared memory variable of another block in the cluster

Legacy member functions (aliases):

`static unsigned int size()`: Total number of threads in the group (alias of `num_threads()`)

####  11.4.1.3. Grid Group 

This group object represents all the threads launched in a single grid. APIs other than `sync()` are available at all times, but to be able to synchronize across the grid, you need to use the cooperative launch API.

`class grid_group;`

Constructed via:
    
    
    grid_group g = this_grid();
    

**Public Member Functions:**

`bool is_valid() const`: Returns whether the grid_group can synchronize

`void sync() const`: Synchronize the threads named in the group, equivalent to `g.barrier_wait(g.barrier_arrive())`

`grid_group::arrival_token barrier_arrive()`: Arrive on the grid barrier, returns a token that needs to be passed into `barrier_wait()`. More details [here](#collectives-cg-sync)

`void barrier_wait(grid_group::arrival_token&& t)`: Wait on the grid barrier, takes arrival token returned from `barrier_arrive()` as a rvalue reference. More details [here](#collectives-cg-sync)

`static unsigned long long thread_rank()`: Rank of the calling thread within [0, num_threads)

`static unsigned long long block_rank()`: Rank of the calling block within [0, num_blocks)

`static unsigned long long cluster_rank()`: Rank of the calling cluster within [0, num_clusters)

`static unsigned long long num_threads()`: Total number of threads in the group

`static unsigned long long num_blocks()`: Total number of blocks in the group

`static unsigned long long num_clusters()`: Total number of clusters in the group

`static dim3 dim_blocks()`: Dimensions of the launched grid in units of blocks

`static dim3 dim_clusters()`: Dimensions of the launched grid in units of clusters

`static dim3 block_index()`: 3-Dimensional index of the block within the launched grid

`static dim3 cluster_index()`: 3-Dimensional index of the cluster within the launched grid

Legacy member functions (aliases):

`static unsigned long long size()`: Total number of threads in the group (alias of `num_threads()`)

`static dim3 group_dim()`: Dimensions of the launched grid (alias of `dim_blocks()`)

###  11.4.2. Explicit Groups 

####  11.4.2.1. Thread Block Tile 

A templated version of a tiled group, where a template parameter is used to specify the size of the tile - with this known at compile time there is the potential for more optimal execution.
    
    
    template <unsigned int Size, typename ParentT = void>
    class thread_block_tile;
    

Constructed via:
    
    
    template <unsigned int Size, typename ParentT>
    _CG_QUALIFIER thread_block_tile<Size, ParentT> tiled_partition(const ParentT& g)
    

`Size` must be a power of 2 and less than or equal to 1024. Notes section describes extra steps needed to create tiles of size larger than 32 on hardware with Compute Capability 7.5 or lower.

`ParentT` is the parent-type from which this group was partitioned. It is automatically inferred, but a value of void will store this information in the group handle rather than in the type.

**Public Member Functions:**

`void sync() const`: Synchronize the threads named in the group

`unsigned long long num_threads() const`: Total number of threads in the group

`unsigned long long thread_rank() const`: Rank of the calling thread within [0, num_threads)

`unsigned long long meta_group_size() const`: Returns the number of groups created when the parent group was partitioned.

`unsigned long long meta_group_rank() const`: Linear rank of the group within the set of tiles partitioned from a parent group (bounded by meta_group_size)

`T shfl(T var, unsigned int src_rank) const`: Refer to [Warp Shuffle Functions](#warp-shuffle-functions), **Note: For sizes larger than 32 all threads in the group have to specify the same src_rank, otherwise the behavior is undefined.**

`T shfl_up(T var, int delta) const`: Refer to [Warp Shuffle Functions](#warp-shuffle-functions), available only for sizes lower or equal to 32.

`T shfl_down(T var, int delta) const`: Refer to [Warp Shuffle Functions](#warp-shuffle-functions), available only for sizes lower or equal to 32.

`T shfl_xor(T var, int delta) const`: Refer to [Warp Shuffle Functions](#warp-shuffle-functions), available only for sizes lower or equal to 32.

`int any(int predicate) const`: Refer to [Warp Vote Functions](index.html#warp-vote-functions)

`int all(int predicate) const`: Refer to [Warp Vote Functions](index.html#warp-vote-functions)

`unsigned int ballot(int predicate) const`: Refer to [Warp Vote Functions](index.html#warp-vote-functions), available only for sizes lower or equal to 32.

`unsigned int match_any(T val) const`: Refer to [Warp Match Functions](#warp-match-functions), available only for sizes lower or equal to 32.

`unsigned int match_all(T val, int &pred) const`: Refer to [Warp Match Functions](#warp-match-functions), available only for sizes lower or equal to 32.

Legacy member functions (aliases):

`unsigned long long size() const`: Total number of threads in the group (alias of `num_threads()`)

**Notes:**

  * `thread_block_tile` templated data structure is being used here, the size of the group is passed to the `tiled_partition` call as a template parameter rather than an argument.

  * `shfl, shfl_up, shfl_down, and shfl_xor` functions accept objects of any type when compiled with C++11 or later. This means it’s possible to shuffle non-integral types as long as they satisfy the below constraints:

    * Qualifies as trivially copyable i.e., `is_trivially_copyable<T>::value == true`

    * `sizeof(T) <= 32` for tile sizes lower or equal 32, `sizeof(T) <= 8` for larger tiles

  * On hardware with Compute Capability 7.5 or lower tiles of size larger than 32 need small amount of memory reserved for them. This can be done using `cooperative_groups::block_tile_memory` struct template that has to reside in either shared or global memory.
        
        template <unsigned int MaxBlockSize = 1024>
        struct block_tile_memory;
        

`MaxBlockSize` Specifies the maximal number of threads in the current thread block. This parameter can be used to minimize the shared memory usage of `block_tile_memory` in kernels launched only with smaller thread counts.

This `block_tile_memory` needs be then passed into `cooperative_groups::this_thread_block`, allowing the resulting `thread_block` to be partitioned into tiles of sizes larger than 32. Overload of `this_thread_block` accepting `block_tile_memory` argument is a collective operation and has to be called with all threads in the `thread_block`.

`block_tile_memory` can be used on hardware with Compute Capability 8.0 or higher in order to be able to write one source targeting multiple different Compute Capabilities. It should consume no memory when instantiated in shared memory in cases where its not required.


**Examples:**
    
    
    /// The following code will create two sets of tiled groups, of size 32 and 4 respectively:
    /// The latter has the provenance encoded in the type, while the first stores it in the handle
    thread_block block = this_thread_block();
    thread_block_tile<32> tile32 = tiled_partition<32>(block);
    thread_block_tile<4, thread_block> tile4 = tiled_partition<4>(block);
    
    
    
    /// The following code will create tiles of size 128 on all Compute Capabilities.
    /// block_tile_memory can be omitted on Compute Capability 8.0 or higher.
    __global__ void kernel(...) {
        // reserve shared memory for thread_block_tile usage,
        //   specify that block size will be at most 256 threads.
        __shared__ block_tile_memory<256> shared;
        thread_block thb = this_thread_block(shared);
    
        // Create tiles with 128 threads.
        auto tile = tiled_partition<128>(thb);
    
        // ...
    }
    

#####  11.4.2.1.1. Warp-Synchronous Code Pattern 

Developers might have had warp-synchronous codes that they previously made implicit assumptions about the warp size and would code around that number. Now this needs to be specified explicitly.
    
    
    __global__ void cooperative_kernel(...) {
        // obtain default "current thread block" group
        thread_block my_block = this_thread_block();
    
        // subdivide into 32-thread, tiled subgroups
        // Tiled subgroups evenly partition a parent group into
        // adjacent sets of threads - in this case each one warp in size
        auto my_tile = tiled_partition<32>(my_block);
    
        // This operation will be performed by only the
        // first 32-thread tile of each block
        if (my_tile.meta_group_rank() == 0) {
            // ...
            my_tile.sync();
        }
    }
    

#####  11.4.2.1.2. Single Thread Group 

Group representing the current thread can be obtained from `this_thread` function:
    
    
    thread_block_tile<1> this_thread();
    

The following `memcpy_async` API uses a `thread_group`, to copy an int element from source to destination:
    
    
    #include <cooperative_groups.h>
    #include <cooperative_groups/memcpy_async.h>
    
    cooperative_groups::memcpy_async(cooperative_groups::this_thread(), dest, src, sizeof(int));
    

More detailed examples of using `this_thread` to perform asynchronous copies can be found in the [Single-Stage Asynchronous Data Copies using cuda::pipeline](#with-memcpy-async-pipeline-pattern-single) and [Multi-Stage Asynchronous Data Copies using cuda::pipeline](#with-memcpy-async-pipeline-pattern-multi) sections.

####  11.4.2.2. Coalesced Groups 

In CUDA’s SIMT architecture, at the hardware level the multiprocessor executes threads in groups of 32 called warps. If there exists a data-dependent conditional branch in the application code such that threads within a warp diverge, then the warp serially executes each branch disabling threads not on that path. The threads that remain active on the path are referred to as coalesced. Cooperative Groups has functionality to discover, and create, a group containing all coalesced threads.

Constructing the group handle via `coalesced_threads()` is opportunistic. It returns the set of active threads at that point in time, and makes no guarantee about which threads are returned (as long as they are active) or that they will stay coalesced throughout execution (they will be brought back together for the execution of a collective but can diverge again afterwards).

`class coalesced_group;`

Constructed via:
    
    
    coalesced_group active = coalesced_threads();
    

**Public Member Functions:**

`void sync() const`: Synchronize the threads named in the group

`unsigned long long num_threads() const`: Total number of threads in the group

`unsigned long long thread_rank() const`: Rank of the calling thread within [0, num_threads)

`unsigned long long meta_group_size() const`: Returns the number of groups created when the parent group was partitioned. If this group was created by querying the set of active threads, for example `coalesced_threads()` the value of `meta_group_size()` will be 1.

`unsigned long long meta_group_rank() const`: Linear rank of the group within the set of tiles partitioned from a parent group (bounded by meta_group_size). If this group was created by querying the set of active threads, e.g. `coalesced_threads()` the value of `meta_group_rank()` will always be 0.

`T shfl(T var, unsigned int src_rank) const`: Refer to [Warp Shuffle Functions](#warp-shuffle-functions)

`T shfl_up(T var, int delta) const`: Refer to [Warp Shuffle Functions](#warp-shuffle-functions)

`T shfl_down(T var, int delta) const`: Refer to [Warp Shuffle Functions](#warp-shuffle-functions)

`int any(int predicate) const`: Refer to [Warp Vote Functions](index.html#warp-vote-functions)

`int all(int predicate) const`: Refer to [Warp Vote Functions](index.html#warp-vote-functions)

`unsigned int ballot(int predicate) const`: Refer to [Warp Vote Functions](index.html#warp-vote-functions)

`unsigned int match_any(T val) const`: Refer to [Warp Match Functions](#warp-match-functions)

`unsigned int match_all(T val, int &pred) const`: Refer to [Warp Match Functions](#warp-match-functions)

Legacy member functions (aliases):

`unsigned long long size() const`: Total number of threads in the group (alias of `num_threads()`)

**Notes:**

`shfl, shfl_up, and shfl_down` functions accept objects of any type when compiled with C++11 or later. This means it’s possible to shuffle non-integral types as long as they satisfy the below constraints:

  * Qualifies as trivially copyable i.e. `is_trivially_copyable<T>::value == true`

  * `sizeof(T) <= 32`


**Example:**
    
    
    /// Consider a situation whereby there is a branch in the
    /// code in which only the 2nd, 4th and 8th threads in each warp are
    /// active. The coalesced_threads() call, placed in that branch, will create (for each
    /// warp) a group, active, that has three threads (with
    /// ranks 0-2 inclusive).
    __global__ void kernel(int *globalInput) {
        // Lets say globalInput says that threads 2, 4, 8 should handle the data
        if (threadIdx.x == *globalInput) {
            coalesced_group active = coalesced_threads();
            // active contains 0-2 inclusive
            active.sync();
        }
    }
    

#####  11.4.2.2.1. Discovery Pattern 

Commonly developers need to work with the current active set of threads. No assumption is made about the threads that are present, and instead developers work with the threads that happen to be there. This is seen in the following “aggregating atomic increment across threads in a warp” example (written using the correct CUDA 9.0 set of intrinsics):
    
    
    {
        unsigned int writemask = __activemask();
        unsigned int total = __popc(writemask);
        unsigned int prefix = __popc(writemask & __lanemask_lt());
        // Find the lowest-numbered active lane
        int elected_lane = __ffs(writemask) - 1;
        int base_offset = 0;
        if (prefix == 0) {
            base_offset = atomicAdd(p, total);
        }
        base_offset = __shfl_sync(writemask, base_offset, elected_lane);
        int thread_offset = prefix + base_offset;
        return thread_offset;
    }
    

This can be re-written with Cooperative Groups as follows:
    
    
    {
        cg::coalesced_group g = cg::coalesced_threads();
        int prev;
        if (g.thread_rank() == 0) {
            prev = atomicAdd(p, g.num_threads());
        }
        prev = g.thread_rank() + g.shfl(prev, 0);
        return prev;
    }
    


##  11.5. Group Partitioning 

###  11.5.1. `tiled_partition`
    
    
    template <unsigned int Size, typename ParentT>
    thread_block_tile<Size, ParentT> tiled_partition(const ParentT& g);
    
    
    
    thread_group tiled_partition(const thread_group& parent, unsigned int tilesz);
    

The `tiled_partition` method is a collective operation that partitions the parent group into a one-dimensional, row-major, tiling of subgroups. A total of ((size(parent)/tilesz) subgroups will be created, therefore the parent group size must be evenly divisible by the `Size`. The allowed parent groups are `thread_block` or `thread_block_tile`.

The implementation may cause the calling thread to wait until all the members of the parent group have invoked the operation before resuming execution. Functionality is limited to native hardware sizes, 1/2/4/8/16/32 and the `cg::size(parent)` must be greater than the `Size` parameter. The templated version of `tiled_partition` supports 64/128/256/512 sizes as well, but some additional steps are required on Compute Capability 7.5 or lower, refer to [Thread Block Tile](#thread-block-tile-group-cg) for details.

**Codegen Requirements:** Compute Capability 5.0 minimum, C++11 for sizes larger than 32

**Example:**
    
    
    /// The following code will create a 32-thread tile
    thread_block block = this_thread_block();
    thread_block_tile<32> tile32 = tiled_partition<32>(block);
    

We can partition each of these groups into even smaller groups, each of size 4 threads:
    
    
    auto tile4 = tiled_partition<4>(tile32);
    // or using a general group
    // thread_group tile4 = tiled_partition(tile32, 4);
    

If, for instance, if we were to then include the following line of code:
    
    
    if (tile4.thread_rank()==0) printf("Hello from tile4 rank 0\n");
    

then the statement would be printed by every fourth thread in the block: the threads of rank 0 in each `tile4` group, which correspond to those threads with ranks 0,4,8,12,etc. in the `block` group.

###  11.5.2. `labeled_partition`
    
    
    template <typename Label>
    coalesced_group labeled_partition(const coalesced_group& g, Label label);
    
    
    
    template <unsigned int Size, typename Label>
    coalesced_group labeled_partition(const thread_block_tile<Size>& g, Label label);
    

The `labeled_partition` method is a collective operation that partitions the parent group into one-dimensional subgroups within which the threads are coalesced. The implementation will evaluate a condition label and assign threads that have the same value for label into the same group.

`Label` can be any integral type.

The implementation may cause the calling thread to wait until all the members of the parent group have invoked the operation before resuming execution.

**Note:** This functionality is still being evaluated and may slightly change in the future.

**Codegen Requirements:** Compute Capability 7.0 minimum, C++11

###  11.5.3. `binary_partition`
    
    
    coalesced_group binary_partition(const coalesced_group& g, bool pred);
    
    
    
    template <unsigned int Size>
    coalesced_group binary_partition(const thread_block_tile<Size>& g, bool pred);
    

The `binary_partition()` method is a collective operation that partitions the parent group into one-dimensional subgroups within which the threads are coalesced. The implementation will evaluate a predicate and assign threads that have the same value into the same group. This is a specialized form of `labeled_partition()`, where the label can only be 0 or 1.

The implementation may cause the calling thread to wait until all the members of the parent group have invoked the operation before resuming execution.

**Note:** This functionality is still being evaluated and may slightly change in the future.

**Codegen Requirements:** Compute Capability 7.0 minimum, C++11

**Example:**
    
    
    /// This example divides a 32-sized tile into a group with odd
    /// numbers and a group with even numbers
    _global__ void oddEven(int *inputArr) {
        auto block = cg::this_thread_block();
        auto tile32 = cg::tiled_partition<32>(block);
    
        // inputArr contains random integers
        int elem = inputArr[block.thread_rank()];
        // after this, tile32 is split into 2 groups,
        // a subtile where elem&1 is true and one where its false
        auto subtile = cg::binary_partition(tile32, (elem & 1));
    }
    


##  11.6. Group Collectives 

Cooperative Groups library provides a set of collective operations that can be performed by a group of threads. These operations require participation of all threads in the specified group in order to complete the operation. All threads in the group need to pass the same values for corresponding arguments to each collective call, unless different values are explicitly allowed in the argument description. Otherwise the behavior of the call is undefined.

###  11.6.1. Synchronization 

####  11.6.1.1. `barrier_arrive` and `barrier_wait`
    
    
    T::arrival_token T::barrier_arrive();
    void T::barrier_wait(T::arrival_token&&);
    

`barrier_arrive` and `barrier_wait` member functions provide a synchronization API similar to `cuda::barrier` [(read more)](#aw-barrier). Cooperative Groups automatically initializes the group barrier, but arrive and wait operations have an additional restriction resulting from collective nature of those operations: All threads in the group must arrive and wait at the barrier once per phase. When `barrier_arrive` is called with a group, result of calling any collective operation or another barrier arrival with that group is undefined until completion of the barrier phase is observed with `barrier_wait` call. Threads blocked on `barrier_wait` might be released from the synchronization before other threads call `barrier_wait`, but only after all threads in the group called `barrier_arrive`. Group type `T` can be any of the [implicit groups](#group-types-implicit-cg).This allows threads to do independent work after they arrive and before they wait for the synchronization to resolve, allowing to hide some of the synchronization latency. `barrier_arrive` returns an `arrival_token` object that must be passed into the corresponding `barrier_wait`. Token is consumed this way and can not be used for another `barrier_wait` call.

**Example of barrier_arrive and barrier_wait used to synchronize initalization of shared memory across the cluster:**
    
    
    #include <cooperative_groups.h>
    
    using namespace cooperative_groups;
    
    void __device__ init_shared_data(const thread_block& block, int *data);
    void __device__ local_processing(const thread_block& block);
    void __device__ process_shared_data(const thread_block& block, int *data);
    
    __global__ void cluster_kernel() {
        extern __shared__ int array[];
        auto cluster = this_cluster();
        auto block   = this_thread_block();
    
        // Use this thread block to initialize some shared state
        init_shared_data(block, &array[0]);
    
        auto token = cluster.barrier_arrive(); // Let other blocks know this block is running and data was initialized
    
        // Do some local processing to hide the synchronization latency
        local_processing(block);
    
        // Map data in shared memory from the next block in the cluster
        int *dsmem = cluster.map_shared_rank(&array[0], (cluster.block_rank() + 1) % cluster.num_blocks());
    
        // Make sure all other blocks in the cluster are running and initialized shared data before accessing dsmem
        cluster.barrier_wait(std::move(token));
    
        // Consume data in distributed shared memory
        process_shared_data(block, dsmem);
        cluster.sync();
    }
    

####  11.6.1.2. `sync`
    
    
    static void T::sync();
    
    template <typename T>
    void sync(T& group);
    

`sync` synchronizes the threads named in the group. Group type `T` can be any of the existing group types, as all of them support synchronization. Its available as a member function in every group type or as a free function taking a group as parameter. If the group is a `grid_group` the kernel must have been launched using the appropriate cooperative launch APIs. Equivalent to `T.barrier_wait(T.barrier_arrive())`.

###  11.6.2. Data Transfer 

####  11.6.2.1. `memcpy_async`

`memcpy_async` is a group-wide collective memcpy that utilizes hardware accelerated support for non-blocking memory transactions from global to shared memory. Given a set of threads named in the group, `memcpy_async` will move specified amount of bytes or elements of the input type through a single pipeline stage. Additionally for achieving best performance when using the `memcpy_async` API, an alignment of 16 bytes for both shared memory and global memory is required. It is important to note that while this is a memcpy in the general case, it is only asynchronous if the source is global memory and the destination is shared memory and both can be addressed with 16, 8, or 4 byte alignments. Asynchronously copied data should only be read following a call to wait or wait_prior which signals that the corresponding stage has completed moving data to shared memory.

Having to wait on all outstanding requests can lose some flexibility (but gain simplicity). In order to efficiently overlap data transfer and execution, its important to be able to kick off an **N+1**` memcpy_async` request while waiting on and operating on request **N**. To do so, use `memcpy_async` and wait on it using the collective stage-based `wait_prior` API. See [wait and wait_prior](#collectives-cg-wait) for more details.

Usage 1
    
    
    template <typename TyGroup, typename TyElem, typename TyShape>
    void memcpy_async(
      const TyGroup &group,
      TyElem *__restrict__ _dst,
      const TyElem *__restrict__ _src,
      const TyShape &shape
    );
    

Performs a copy of **``shape`` bytes**.

Usage 2
    
    
    template <typename TyGroup, typename TyElem, typename TyDstLayout, typename TySrcLayout>
    void memcpy_async(
      const TyGroup &group,
      TyElem *__restrict__ dst,
      const TyDstLayout &dstLayout,
      const TyElem *__restrict__ src,
      const TySrcLayout &srcLayout
    );
    

Performs a copy of **``min(dstLayout, srcLayout)`` elements**. If layouts are of type `cuda::aligned_size_t<N>`, both must specify the same alignment.

**Errata** The `memcpy_async` API introduced in CUDA 11.1 with both src and dst input layouts, expects the layout to be provided in elements rather than bytes. The element type is inferred from `TyElem` and has the size `sizeof(TyElem)`. If `cuda::aligned_size_t<N>` type is used as the layout, the number of elements specified times `sizeof(TyElem)` must be a multiple of N and it is recommended to use `std::byte` or `char` as the element type.

If specified shape or layout of the copy is of type `cuda::aligned_size_t<N>`, alignment will be guaranteed to be at least `min(16, N)`. In that case both `dst` and `src` pointers need to be aligned to N bytes and the number of bytes copied needs to be a multiple of N.

**Codegen Requirements:** Compute Capability 5.0 minimum, Compute Capability 8.0 for asynchronicity, C++11

`cooperative_groups/memcpy_async.h` header needs to be included.

**Example:**
    
    
    /// This example streams elementsPerThreadBlock worth of data from global memory
    /// into a limited sized shared memory (elementsInShared) block to operate on.
    #include <cooperative_groups.h>
    #include <cooperative_groups/memcpy_async.h>
    
    namespace cg = cooperative_groups;
    
    __global__ void kernel(int* global_data) {
        cg::thread_block tb = cg::this_thread_block();
        const size_t elementsPerThreadBlock = 16 * 1024;
        const size_t elementsInShared = 128;
        __shared__ int local_smem[elementsInShared];
    
        size_t copy_count;
        size_t index = 0;
        while (index < elementsPerThreadBlock) {
            cg::memcpy_async(tb, local_smem, elementsInShared, global_data + index, elementsPerThreadBlock - index);
            copy_count = min(elementsInShared, elementsPerThreadBlock - index);
            cg::wait(tb);
            // Work with local_smem
            index += copy_count;
        }
    }
    

####  11.6.2.2. `wait and wait_prior`
    
    
    template <typename TyGroup>
    void wait(TyGroup & group);
    
    template <unsigned int NumStages, typename TyGroup>
    void wait_prior(TyGroup & group);
    

`wait` and `wait_prior` collectives allow to wait for memcpy_async copies to complete. `wait` blocks calling threads until all previous copies are done. `wait_prior` allows that the latest NumStages are still not done and waits for all the previous requests. So with `N` total copies requested, it waits until the first `N-NumStages` are done and the last `NumStages` might still be in progress. Both `wait` and `wait_prior` will synchronize the named group.

**Codegen Requirements:** Compute Capability 5.0 minimum, Compute Capability 8.0 for asynchronicity, C++11

`cooperative_groups/memcpy_async.h` header needs to be included.

**Example:**
    
    
    /// This example streams elementsPerThreadBlock worth of data from global memory
    /// into a limited sized shared memory (elementsInShared) block to operate on in
    /// multiple (two) stages. As stage N is kicked off, we can wait on and operate on stage N-1.
    #include <cooperative_groups.h>
    #include <cooperative_groups/memcpy_async.h>
    
    namespace cg = cooperative_groups;
    
    __global__ void kernel(int* global_data) {
        cg::thread_block tb = cg::this_thread_block();
        const size_t elementsPerThreadBlock = 16 * 1024 + 64;
        const size_t elementsInShared = 128;
        __align__(16) __shared__ int local_smem[2][elementsInShared];
        int stage = 0;
        // First kick off an extra request
        size_t copy_count = elementsInShared;
        size_t index = copy_count;
        cg::memcpy_async(tb, local_smem[stage], elementsInShared, global_data, elementsPerThreadBlock - index);
        while (index < elementsPerThreadBlock) {
            // Now we kick off the next request...
            cg::memcpy_async(tb, local_smem[stage ^ 1], elementsInShared, global_data + index, elementsPerThreadBlock - index);
            // ... but we wait on the one before it
            cg::wait_prior<1>(tb);
    
            // Its now available and we can work with local_smem[stage] here
            // (...)
            //
    
            // Calculate the amount fo data that was actually copied, for the next iteration.
            copy_count = min(elementsInShared, elementsPerThreadBlock - index);
            index += copy_count;
    
            // A cg::sync(tb) might be needed here depending on whether
            // the work done with local_smem[stage] can release threads to race ahead or not
            // Wrap to the next stage
            stage ^= 1;
        }
        cg::wait(tb);
        // The last local_smem[stage] can be handled here
    }
    

###  11.6.3. Data Manipulation 

####  11.6.3.1. `reduce`
    
    
    template <typename TyGroup, typename TyArg, typename TyOp>
    auto reduce(const TyGroup& group, TyArg&& val, TyOp&& op) -> decltype(op(val, val));
    

`reduce` performs a reduction operation on the data provided by each thread named in the group passed in. This takes advantage of hardware acceleration (on compute 80 and higher devices) for the arithmetic add, min, or max operations and the logical AND, OR, or XOR, as well as providing a software fallback on older generation hardware. Only 4B types are accelerated by hardware.

`group`: Valid group types are `coalesced_group` and `thread_block_tile`.

`val`: Any type that satisfies the below requirements:

  * Qualifies as trivially copyable i.e. `is_trivially_copyable<TyArg>::value == true`

  * `sizeof(T) <= 32` for `coalesced_group` and tiles of size lower or equal 32, `sizeof(T) <= 8` for larger tiles

  * Has suitable arithmetic or comparative operators for the given function object.


**Note:** Different threads in the group can pass different values for this argument.

`op`: Valid function objects that will provide hardware acceleration with integral types are `plus(), less(), greater(), bit_and(), bit_xor(), bit_or()`. These must be constructed, hence the TyVal template argument is required, i.e. `plus<int>()`. Reduce also supports lambdas and other function objects that can be invoked using `operator()`

Asynchronous reduce
    
    
    template <typename TyGroup, typename TyArg, typename TyAtomic, typename TyOp>
    void reduce_update_async(const TyGroup& group, TyAtomic& atomic, TyArg&& val, TyOp&& op);
    
    template <typename TyGroup, typename TyArg, typename TyAtomic, typename TyOp>
    void reduce_store_async(const TyGroup& group, TyAtomic& atomic, TyArg&& val, TyOp&& op);
    
    template <typename TyGroup, typename TyArg, typename TyOp>
    void reduce_store_async(const TyGroup& group, TyArg* ptr, TyArg&& val, TyOp&& op);
    

`*_async` variants of the API are asynchronously calculating the result to either store to or update a specified destination by one of the participating threads, instead of returning it by each thread. To observe the effect of these asynchronous calls, calling group of threads or a larger group containing them need to be synchronized.

  * In case of the atomic store or update variant, `atomic` argument can be either of `cuda::atomic` or `cuda::atomic_ref` available in [CUDA C++ Standard Library](https://nvidia.github.io/libcudacxx/extended_api/synchronization_primitives.html). This variant of the API is available only on platforms and devices, where these types are supported by the CUDA C++ Standard Library. Result of the reduction is used to atomically update the atomic according to the specified `op`, eg. the result is atomically added to the atomic in case of `cg::plus()`. Type held by the `atomic` must match the type of `TyArg`. Scope of the atomic must include all the threads in the group and if multiple groups are using the same atomic concurrently, scope must include all threads in all groups using it. Atomic update is performed with relaxed memory ordering.

  * In case of the pointer store variant, result of the reduction will be weakly stored into the `dst` pointer.


**Codegen Requirements:** Compute Capability 5.0 minimum, Compute Capability 8.0 for HW acceleration, C++11.

`cooperative_groups/reduce.h` header needs to be included.

**Example of approximate standard deviation for integer vector:**
    
    
    #include <cooperative_groups.h>
    #include <cooperative_groups/reduce.h>
    namespace cg = cooperative_groups;
    
    /// Calculate approximate standard deviation of integers in vec
    __device__ int std_dev(const cg::thread_block_tile<32>& tile, int *vec, int length) {
        int thread_sum = 0;
    
        // calculate average first
        for (int i = tile.thread_rank(); i < length; i += tile.num_threads()) {
            thread_sum += vec[i];
        }
        // cg::plus<int> allows cg::reduce() to know it can use hardware acceleration for addition
        int avg = cg::reduce(tile, thread_sum, cg::plus<int>()) / length;
    
        int thread_diffs_sum = 0;
        for (int i = tile.thread_rank(); i < length; i += tile.num_threads()) {
            int diff = vec[i] - avg;
            thread_diffs_sum += diff * diff;
        }
    
        // temporarily use floats to calculate the square root
        float diff_sum = static_cast<float>(cg::reduce(tile, thread_diffs_sum, cg::plus<int>())) / length;
    
        return static_cast<int>(sqrtf(diff_sum));
    }
    

**Example of block wide reduction:**
    
    
    #include <cooperative_groups.h>
    #include <cooperative_groups/reduce.h>
    namespace cg=cooperative_groups;
    
    /// The following example accepts input in *A and outputs a result into *sum
    /// It spreads the data equally within the block
    __device__ void block_reduce(const int* A, int count, cuda::atomic<int, cuda::thread_scope_block>& total_sum) {
        auto block = cg::this_thread_block();
        auto tile = cg::tiled_partition<32>(block);
        int thread_sum = 0;
    
        // Stride loop over all values, each thread accumulates its part of the array.
        for (int i = block.thread_rank(); i < count; i += block.size()) {
            thread_sum += A[i];
        }
    
        // reduce thread sums across the tile, add the result to the atomic
        // cg::plus<int> allows cg::reduce() to know it can use hardware acceleration for addition
     cg::reduce_update_async(tile, total_sum, thread_sum, cg::plus<int>());
    
     // synchronize the block, to ensure all async reductions are ready
        block.sync();
    }
    

####  11.6.3.2. `Reduce` Operators 

Below are the prototypes of function objects for some of the basic operations that can be done with `reduce`
    
    
    namespace cooperative_groups {
      template <typename Ty>
      struct cg::plus;
    
      template <typename Ty>
      struct cg::less;
    
      template <typename Ty>
      struct cg::greater;
    
      template <typename Ty>
      struct cg::bit_and;
    
      template <typename Ty>
      struct cg::bit_xor;
    
      template <typename Ty>
      struct cg::bit_or;
    }
    

Reduce is limited to the information available to the implementation at compile time. Thus in order to make use of intrinsics introduced in CC 8.0, the `cg::` namespace exposes several functional objects that mirror the hardware. These objects appear similar to those presented in the C++ STL, with the exception of `less/greater`. The reason for any difference from the STL is that these function objects are designed to actually mirror the operation of the hardware intrinsics.

**Functional description:**

  * `cg::plus:` Accepts two values and returns the sum of both using operator+.

  * `cg::less:` Accepts two values and returns the lesser using operator<. This differs in that the **lower value is returned** rather than a Boolean.

  * `cg::greater:` Accepts two values and returns the greater using operator<. This differs in that the **greater value is returned** rather than a Boolean.

  * `cg::bit_and:` Accepts two values and returns the result of operator&.

  * `cg::bit_xor:` Accepts two values and returns the result of operator^.

  * `cg::bit_or:` Accepts two values and returns the result of operator|.


**Example:**
    
    
    {
        // cg::plus<int> is specialized within cg::reduce and calls __reduce_add_sync(...) on CC 8.0+
        cg::reduce(tile, (int)val, cg::plus<int>());
    
        // cg::plus<float> fails to match with an accelerator and instead performs a standard shuffle based reduction
        cg::reduce(tile, (float)val, cg::plus<float>());
    
        // While individual components of a vector are supported, reduce will not use hardware intrinsics for the following
        // It will also be necessary to define a corresponding operator for vector and any custom types that may be used
        int4 vec = {...};
        cg::reduce(tile, vec, cg::plus<int4>())
    
        // Finally lambdas and other function objects cannot be inspected for dispatch
        // and will instead perform shuffle based reductions using the provided function object.
        cg::reduce(tile, (int)val, [](int l, int r) -> int {return l + r;});
    }
    

####  11.6.3.3. `inclusive_scan` and `exclusive_scan`
    
    
    template <typename TyGroup, typename TyVal, typename TyFn>
    auto inclusive_scan(const TyGroup& group, TyVal&& val, TyFn&& op) -> decltype(op(val, val));
    
    template <typename TyGroup, typename TyVal>
    TyVal inclusive_scan(const TyGroup& group, TyVal&& val);
    
    template <typename TyGroup, typename TyVal, typename TyFn>
    auto exclusive_scan(const TyGroup& group, TyVal&& val, TyFn&& op) -> decltype(op(val, val));
    
    template <typename TyGroup, typename TyVal>
    TyVal exclusive_scan(const TyGroup& group, TyVal&& val);
    

`inclusive_scan` and `exclusive_scan` performs a scan operation on the data provided by each thread named in the group passed in. Result for each thread is a reduction of data from threads with lower `thread_rank` than that thread in case of `exclusive_scan`. `inclusive_scan` result also includes the calling thread data in the reduction.

`group`: Valid group types are `coalesced_group` and `thread_block_tile`.

`val`: Any type that satisfies the below requirements:

  * Qualifies as trivially copyable i.e. `is_trivially_copyable<TyArg>::value == true`

  * `sizeof(T) <= 32` for `coalesced_group` and tiles of size lower or equal 32, `sizeof(T) <= 8` for larger tiles

  * Has suitable arithmetic or comparative operators for the given function object.


**Note:** Different threads in the group can pass different values for this argument.

`op`: Function objects defined for convenience are `plus(), less(), greater(), bit_and(), bit_xor(), bit_or()` described in [Reduce Operators](#collectives-cg-reduce-operators). These must be constructed, hence the TyVal template argument is required, i.e. `plus<int>()`. `inclusive_scan` and `exclusive_scan` also supports lambdas and other function objects that can be invoked using `operator()`. Overloads without this argument use `cg::plus<TyVal>()`.

**Scan update**
    
    
    template <typename TyGroup, typename TyAtomic, typename TyVal, typename TyFn>
    auto inclusive_scan_update(const TyGroup& group, TyAtomic& atomic, TyVal&& val, TyFn&& op) -> decltype(op(val, val));
    
    template <typename TyGroup, typename TyAtomic, typename TyVal>
    TyVal inclusive_scan_update(const TyGroup& group, TyAtomic& atomic, TyVal&& val);
    
    template <typename TyGroup, typename TyAtomic, typename TyVal, typename TyFn>
    auto exclusive_scan_update(const TyGroup& group, TyAtomic& atomic, TyVal&& val, TyFn&& op) -> decltype(op(val, val));
    
    template <typename TyGroup, typename TyAtomic, typename TyVal>
    TyVal exclusive_scan_update(const TyGroup& group, TyAtomic& atomic, TyVal&& val);
    

`*_scan_update` collectives take an additional argument `atomic` that can be either of `cuda::atomic` or `cuda::atomic_ref` available in [CUDA C++ Standard Library](https://nvidia.github.io/libcudacxx/extended_api/synchronization_primitives.html). These variants of the API are available only on platforms and devices, where these types are supported by the CUDA C++ Standard Library. These variants will perform an update to the `atomic` according to `op` with value of the sum of input values of all threads in the group. Previous value of the `atomic` will be combined with the result of scan by each thread and returned. Type held by the `atomic` must match the type of `TyVal`. Scope of the atomic must include all the threads in the group and if multiple groups are using the same atomic concurrently, scope must include all threads in all groups using it. Atomic update is performed with relaxed memory ordering.

Following pseudocode illustrates how the update variant of scan works:
    
    
    /*
     inclusive_scan_update behaves as the following block,
     except both reduce and inclusive_scan is calculated simultaneously.
    auto total = reduce(group, val, op);
    TyVal old;
    if (group.thread_rank() == selected_thread) {
        atomically {
            old = atomic.load();
            atomic.store(op(old, total));
        }
    }
    old = group.shfl(old, selected_thread);
    return op(inclusive_scan(group, val, op), old);
    */
    

**Codegen Requirements:** Compute Capability 5.0 minimum, C++11.

`cooperative_groups/scan.h` header needs to be included.

**Example:**
    
    
    #include <stdio.h>
    #include <cooperative_groups.h>
    #include <cooperative_groups/scan.h>
    namespace cg = cooperative_groups;
    
    __global__ void kernel() {
        auto thread_block = cg::this_thread_block();
        auto tile = cg::tiled_partition<8>(thread_block);
        unsigned int val = cg::inclusive_scan(tile, tile.thread_rank());
        printf("%u: %u\n", tile.thread_rank(), val);
    }
    
    /*  prints for each group:
        0: 0
        1: 1
        2: 3
        3: 6
        4: 10
        5: 15
        6: 21
        7: 28
    */
    

**Example of stream compaction using exclusive_scan:**
    
    
    #include <cooperative_groups.h>
    #include <cooperative_groups/scan.h>
    namespace cg = cooperative_groups;
    
    // put data from input into output only if it passes test_fn predicate
    template<typename Group, typename Data, typename TyFn>
    __device__ int stream_compaction(Group &g, Data *input, int count, TyFn&& test_fn, Data *output) {
        int per_thread = count / g.num_threads();
        int thread_start = min(g.thread_rank() * per_thread, count);
        int my_count = min(per_thread, count - thread_start);
    
        // get all passing items from my part of the input
        //  into a contagious part of the array and count them.
        int i = thread_start;
        while (i < my_count + thread_start) {
            if (test_fn(input[i])) {
                i++;
            }
            else {
                my_count--;
                input[i] = input[my_count + thread_start];
            }
        }
    
        // scan over counts from each thread to calculate my starting
        //  index in the output
        int my_idx = cg::exclusive_scan(g, my_count);
    
        for (i = 0; i < my_count; ++i) {
            output[my_idx + i] = input[thread_start + i];
        }
        // return the total number of items in the output
        return g.shfl(my_idx + my_count, g.num_threads() - 1);
    }
    

**Example of dynamic buffer space allocation using exclusive_scan_update:**
    
    
    #include <cooperative_groups.h>
    #include <cooperative_groups/scan.h>
    namespace cg = cooperative_groups;
    
    // Buffer partitioning is static to make the example easier to follow,
    // but any arbitrary dynamic allocation scheme can be implemented by replacing this function.
    __device__ int calculate_buffer_space_needed(cg::thread_block_tile<32>& tile) {
        return tile.thread_rank() % 2 + 1;
    }
    
    __device__ int my_thread_data(int i) {
        return i;
    }
    
    __global__ void kernel() {
        __shared__ extern int buffer[];
        __shared__ cuda::atomic<int, cuda::thread_scope_block> buffer_used;
    
        auto block = cg::this_thread_block();
        auto tile = cg::tiled_partition<32>(block);
        buffer_used = 0;
        block.sync();
    
        // each thread calculates buffer size it needs
        int buf_needed = calculate_buffer_space_needed(tile);
    
        // scan over the needs of each thread, result for each thread is an offset
        // of that thread’s part of the buffer. buffer_used is atomically updated with
        // the sum of all thread's inputs, to correctly offset other tile’s allocations
        int buf_offset =
            cg::exclusive_scan_update(tile, buffer_used, buf_needed);
    
        // each thread fills its own part of the buffer with thread specific data
        for (int i = 0 ; i < buf_needed ; ++i) {
            buffer[buf_offset + i] = my_thread_data(i);
        }
    
        block.sync();
        // buffer_used now holds total amount of memory allocated
        // buffer is {0, 0, 1, 0, 0, 1 ...};
    }
    

###  11.6.4. Execution control 

####  11.6.4.1. `invoke_one` and `invoke_one_broadcast`
    
    
    template<typename Group, typename Fn, typename... Args>
    void invoke_one(const Group& group, Fn&& fn, Args&&... args);
    
    template<typename Group, typename Fn, typename... Args>
    auto invoke_one_broadcast(const Group& group, Fn&& fn, Args&&... args) -> decltype(fn(args...));
    

`invoke_one` selects a single arbitrary thread from the calling `group` and uses that thread to call the supplied invocable `fn` with the supplied arguments `args`. In case of `invoke_one_broadcast` the result of the call is also distributed to all threads in the group and returned from this collective.

Calling group can be synchronized with the selected thread before and/or after it calls the supplied invocable. It means that communication within the calling group is not allowed inside the supplied invocable body, otherwise forward progress is not guaranteed. Communication with threads outside of the calling group is allowed in the body of the supplied invocable. Thread selection mechanism is **not** guaranteed to be deterministic.

On devices with Compute Capability 9.0 or higher hardware acceleration might be used to select the thread when called with [explicit group types](#group-types-explicit-cg).

`group`: All group types are valid for `invoke_one`, `coalesced_group` and `thread_block_tile` are valid for `invoke_one_broadcast`.

`fn`: Function or object that can be invoked using `operator()`.

`args`: Parameter pack of types matching types of parameters of the supplied invocable `fn`.

In case of `invoke_one_broadcast` the return type of the supplied invocable `fn` must satisfy the below requirements:

  * Qualifies as trivially copyable i.e. `is_trivially_copyable<T>::value == true`

  * `sizeof(T) <= 32` for `coalesced_group` and tiles of size lower or equal 32, `sizeof(T) <= 8` for larger tiles


**Codegen Requirements:** Compute Capability 5.0 minimum, Compute Capability 9.0 for hardware acceleration, C++11.

**Aggregated atomic example from** [Discovery pattern section](#discovery-pattern-cg) **re-written to use invoke_one_broadcast:**
    
    
    #include <cooperative_groups.h>
    #include <cuda/atomic>
    namespace cg = cooperative_groups;
    
    template<cuda::thread_scope Scope>
    __device__ unsigned int atomicAddOneRelaxed(cuda::atomic<unsigned int, Scope>& atomic) {
        auto g = cg::coalesced_threads();
        auto prev = cg::invoke_one_broadcast(g, [&] () {
            return atomic.fetch_add(g.num_threads(), cuda::memory_order_relaxed);
        });
        return prev + g.thread_rank();
    }
    


##  11.7. Grid Synchronization 

Prior to the introduction of Cooperative Groups, the CUDA programming model only allowed synchronization between thread blocks at a kernel completion boundary. The kernel boundary carries with it an implicit invalidation of state, and with it, potential performance implications.

For example, in certain use cases, applications have a large number of small kernels, with each kernel representing a stage in a processing pipeline. The presence of these kernels is required by the current CUDA programming model to ensure that the thread blocks operating on one pipeline stage have produced data before the thread block operating on the next pipeline stage is ready to consume it. In such cases, the ability to provide global inter thread block synchronization would allow the application to be restructured to have persistent thread blocks, which are able to synchronize on the device when a given stage is complete.

To synchronize across the grid, from within a kernel, you would simply use the `grid.sync()` function:
    
    
    grid_group grid = this_grid();
    grid.sync();
    

And when launching the kernel it is necessary to use, instead of the `<<<...>>>` execution configuration syntax, the `cudaLaunchCooperativeKernel` CUDA runtime launch API or the `CUDA driver equivalent`.

**Example:**

To guarantee co-residency of the thread blocks on the GPU, the number of blocks launched needs to be carefully considered. For example, as many blocks as there are SMs can be launched as follows:
    
    
    int dev = 0;
    cudaDeviceProp deviceProp;
    cudaGetDeviceProperties(&deviceProp, dev);
    // initialize, then launch
    cudaLaunchCooperativeKernel((void*)my_kernel, deviceProp.multiProcessorCount, numThreads, args);
    

Alternatively, you can maximize the exposed parallelism by calculating how many blocks can fit simultaneously per-SM using the occupancy calculator as follows:
    
    
    /// This will launch a grid that can maximally fill the GPU, on the default stream with kernel arguments
    int numBlocksPerSm = 0;
     // Number of threads my_kernel will be launched with
    int numThreads = 128;
    cudaDeviceProp deviceProp;
    cudaGetDeviceProperties(&deviceProp, dev);
    cudaOccupancyMaxActiveBlocksPerMultiprocessor(&numBlocksPerSm, my_kernel, numThreads, 0);
    // launch
    void *kernelArgs[] = { /* add kernel args */ };
    dim3 dimBlock(numThreads, 1, 1);
    dim3 dimGrid(deviceProp.multiProcessorCount*numBlocksPerSm, 1, 1);
    cudaLaunchCooperativeKernel((void*)my_kernel, dimGrid, dimBlock, kernelArgs);
    

It is good practice to first ensure the device supports cooperative launches by querying the device attribute `cudaDevAttrCooperativeLaunch`:
    
    
    int dev = 0;
    int supportsCoopLaunch = 0;
    cudaDeviceGetAttribute(&supportsCoopLaunch, cudaDevAttrCooperativeLaunch, dev);
    

which will set `supportsCoopLaunch` to 1 if the property is supported on device 0. Only devices with compute capability of 6.0 and higher are supported. In addition, you need to be running on either of these:

  * The Linux platform without MPS

  * The Linux platform with MPS and on a device with compute capability 7.0 or higher

  * The latest Windows platform


