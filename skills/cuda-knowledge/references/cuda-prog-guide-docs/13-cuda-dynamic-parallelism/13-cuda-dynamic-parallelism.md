# 13. CUDA Dynamic Parallelism


Warning

This document has been replaced by a new [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide). The information in this document should be considered legacy, and this document is no longer being updated as of CUDA 13.0. Please refer to the [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide) for up-to-date information on CUDA.


##  13.1. Introduction 

###  13.1.1. Overview 

_Dynamic Parallelism_ is an extension to the CUDA programming model enabling a CUDA kernel to create and synchronize with new work directly on the GPU. The creation of parallelism dynamically at whichever point in a program that it is needed offers exciting capabilities.

The ability to create work directly from the GPU can reduce the need to transfer execution control and data between host and device, as launch configuration decisions can now be made at runtime by threads executing on the device. Additionally, data-dependent parallel work can be generated inline within a kernel at run-time, taking advantage of the GPU’s hardware schedulers and load balancers dynamically and adapting in response to data-driven decisions or workloads. Algorithms and programming patterns that had previously required modifications to eliminate recursion, irregular loop structure, or other constructs that do not fit a flat, single-level of parallelism may more transparently be expressed.

This document describes the extended capabilities of CUDA which enable Dynamic Parallelism, including the modifications and additions to the CUDA programming model necessary to take advantage of these, as well as guidelines and best practices for exploiting this added capacity.

Dynamic Parallelism is only supported by devices of compute capability 3.5 and higher.

###  13.1.2. Glossary 

Definitions for terms used in this guide.

Grid
    

A Grid is a collection of _Threads_. Threads in a Grid execute a _Kernel Function_ and are divided into _Thread Blocks_.

Thread Block
    

A Thread Block is a group of threads which execute on the same multiprocessor (_SM_). Threads within a Thread Block have access to shared memory and can be explicitly synchronized.

Kernel Function
    

A Kernel Function is an implicitly parallel subroutine that executes under the CUDA execution and memory model for every Thread in a Grid.

Host
    

The Host refers to the execution environment that initially invoked CUDA. Typically the thread running on a system’s CPU processor.

Parent
    

A _Parent Thread_ , Thread Block, or Grid is one that has launched new grid(s), the _Child_ Grid(s). The Parent is not considered completed until all of its launched Child Grids have also completed.

Child
    

A Child thread, block, or grid is one that has been launched by a Parent grid. A Child grid must complete before the Parent Thread, Thread Block, or Grid are considered complete.

Thread Block Scope
    

Objects with Thread Block Scope have the lifetime of a single Thread Block. They only have defined behavior when operated on by Threads in the Thread Block that created the object and are destroyed when the Thread Block that created them is complete.

Device Runtime
    

The Device Runtime refers to the runtime system and APIs available to enable Kernel Functions to use Dynamic Parallelism.


##  13.2. Execution Environment and Memory Model 

###  13.2.1. Execution Environment 

The CUDA execution model is based on primitives of threads, thread blocks, and grids, with kernel functions defining the program executed by individual threads within a thread block and grid. When a kernel function is invoked the grid’s properties are described by an execution configuration, which has a special syntax in CUDA. Support for dynamic parallelism in CUDA extends the ability to configure, launch, and implicitly synchronize upon new grids to threads that are running on the device.

####  13.2.1.1. Parent and Child Grids 

A device thread that configures and launches a new grid belongs to the parent grid, and the grid created by the invocation is a child grid.

The invocation and completion of child grids is properly nested, meaning that the parent grid is not considered complete until all child grids created by its threads have completed, and the runtime guarantees an implicit synchronization between the parent and child.

![Parent-Child Launch Nesting](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/parent-child-launch-nesting.png)

Figure 30 Parent-Child Launch Nesting

####  13.2.1.2. Scope of CUDA Primitives 

On both host and device, the CUDA runtime offers an API for launching kernels and for tracking dependencies between launches via streams and events. On the host system, the state of launches and the CUDA primitives referencing streams and events are shared by all threads within a process; however processes execute independently and may not share CUDA objects.

On the device, launched kernels and CUDA objects are visible to all threads in a grid. This means, for example, that a stream may be created by one thread and used by any other thread in the grid.

####  13.2.1.3. Synchronization 

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6 and removed for compute_90+ compilation. For compute capability < 9.0, compile-time opt-in by specifying `-DCUDA_FORCE_CDP1_IF_SUPPORTED` is required to continue using `cudaDeviceSynchronize()` in device code. Note that this is slated for full removal in a future CUDA release.

CUDA runtime operations from any thread, including kernel launches, are visible across all the threads in a grid. This means that an invoking thread in the parent grid may perform synchronization to control the launch order of grids launched by any thread in the grid on streams created by any thread in the grid. Execution of a grid is not considered complete until all launches by all threads in the grid have completed. If all threads in a grid exit before all child launches have completed, an implicit synchronization operation will automatically be triggered.

####  13.2.1.4. Streams and Events 

CUDA _Streams_ and _Events_ allow control over dependencies between grid launches: grids launched into the same stream execute in-order, and events may be used to create dependencies between streams. Streams and events created on the device serve this exact same purpose.

Streams and events created within a grid exist within grid scope, but have undefined behavior when used outside of the grid where they were created. As described above, all work launched by a grid is implicitly synchronized when the grid exits; work launched into streams is included in this, with all dependencies resolved appropriately. The behavior of operations on a stream that has been modified outside of grid scope is undefined.

Streams and events created on the host have undefined behavior when used within any kernel, just as streams and events created by a parent grid have undefined behavior if used within a child grid.

####  13.2.1.5. Ordering and Concurrency 

The ordering of kernel launches from the device runtime follows CUDA Stream ordering semantics. Within a grid, all kernel launches into the same stream (with the exception of the fire-and-forget stream discussed later) are executed in-order. With multiple threads in the same grid launching into the same stream, the ordering within the stream is dependent on the thread scheduling within the grid, which may be controlled with synchronization primitives such as `__syncthreads()`.

Note that while named streams are shared by all threads within a grid, the implicit _NULL_ stream is only shared by all threads within a thread block. If multiple threads in a thread block launch into the implicit stream, then these launches will be executed in-order. If multiple threads in different thread blocks launch into the implicit stream, then these launches may be executed concurrently. If concurrency is desired for launches by multiple threads within a thread block, explicit named streams should be used.

_Dynamic Parallelism_ enables concurrency to be expressed more easily within a program; however, the device runtime introduces no new concurrency guarantees within the CUDA execution model. There is no guarantee of concurrent execution between any number of different thread blocks on a device.

The lack of concurrency guarantee extends to a parent grid and their child grids. When a parent grid launches a child grid, the child may start to execute once stream dependencies are satisfied and hardware resources are available to host the child, but is not guaranteed to begin execution until the parent grid reaches an implicit synchronization point.

While concurrency will often easily be achieved, it may vary as a function of device configuration, application workload, and runtime scheduling. It is therefore unsafe to depend upon any concurrency between different thread blocks.

####  13.2.1.6. Device Management 

There is no multi-GPU support from the device runtime; the device runtime is only capable of operating on the device upon which it is currently executing. It is permitted, however, to query properties for any CUDA capable device in the system.

###  13.2.2. Memory Model 

Parent and child grids share the same global and constant memory storage, but have distinct local and shared memory.

####  13.2.2.1. Coherence and Consistency 

#####  13.2.2.1.1. Global Memory 

Parent and child grids have coherent access to global memory, with weak consistency guarantees between child and parent. There is only one point of time in the execution of a child grid when its view of memory is fully consistent with the parent thread: at the point when the child grid is invoked by the parent.

All global memory operations in the parent thread prior to the child grid’s invocation are visible to the child grid. With the removal of `cudaDeviceSynchronize()`, it is no longer possible to access the modifications made by the threads in the child grid from the parent grid. The only way to access the modifications made by the threads in the child grid before the parent grid exits is via a kernel launched into the `cudaStreamTailLaunch` stream.

In the following example, the child grid executing `child_launch` is only guaranteed to see the modifications to `data` made before the child grid was launched. Since thread 0 of the parent is performing the launch, the child will be consistent with the memory seen by thread 0 of the parent. Due to the first `__syncthreads()` call, the child will see `data[0]=0`, `data[1]=1`, …, `data[255]=255` (without the `__syncthreads()` call, only `data[0]=0` would be guaranteed to be seen by the child). The child grid is only guaranteed to return at an implicit synchronization. This means that the modifications made by the threads in the child grid are never guaranteed to become available to the parent grid. To access modifications made by `child_launch`, a `tail_launch` kernel is launched into the `cudaStreamTailLaunch` stream.
    
    
    __global__ void tail_launch(int *data) {
       data[threadIdx.x] = data[threadIdx.x]+1;
    }
    
    __global__ void child_launch(int *data) {
       data[threadIdx.x] = data[threadIdx.x]+1;
    }
    
    __global__ void parent_launch(int *data) {
       data[threadIdx.x] = threadIdx.x;
    
       __syncthreads();
    
       if (threadIdx.x == 0) {
           child_launch<<< 1, 256 >>>(data);
           tail_launch<<< 1, 256, 0, cudaStreamTailLaunch >>>(data);
       }
    }
    
    void host_launch(int *data) {
        parent_launch<<< 1, 256 >>>(data);
    }
    

#####  13.2.2.1.2. Zero Copy Memory 

Zero-copy system memory has identical coherence and consistency guarantees to global memory, and follows the semantics detailed above. A kernel may not allocate or free zero-copy memory, but may use pointers to zero-copy passed in from the host program.

#####  13.2.2.1.3. Constant Memory 

Constants may not be modified from the device. They may only be modified from the host, but the behavior of modifying a constant from the host while there is a concurrent grid that access that constant at any point during its lifetime is undefined.

#####  13.2.2.1.4. Shared and Local Memory 

Shared and Local memory is private to a thread block or thread, respectively, and is not visible or coherent between parent and child. Behavior is undefined when an object in one of these locations is referenced outside of the scope within which it belongs, and may cause an error.

The NVIDIA compiler will attempt to warn if it can detect that a pointer to local or shared memory is being passed as an argument to a kernel launch. At runtime, the programmer may use the `__isGlobal()` intrinsic to determine whether a pointer references global memory and so may safely be passed to a child launch.

Note that calls to `cudaMemcpy*Async()` or `cudaMemset*Async()` may invoke new child kernels on the device in order to preserve stream semantics. As such, passing shared or local memory pointers to these APIs is illegal and will return an error.

#####  13.2.2.1.5. Local Memory 

Local memory is private storage for an executing thread, and is not visible outside of that thread. It is illegal to pass a pointer to local memory as a launch argument when launching a child kernel. The result of dereferencing such a local memory pointer from a child will be undefined.

For example the following is illegal, with undefined behavior if `x_array` is accessed by `child_launch`:
    
    
    int x_array[10];       // Creates x_array in parent's local memory
    child_launch<<< 1, 1 >>>(x_array);
    

It is sometimes difficult for a programmer to be aware of when a variable is placed into local memory by the compiler. As a general rule, all storage passed to a child kernel should be allocated explicitly from the global-memory heap, either with `cudaMalloc()`, `new()` or by declaring `__device__` storage at global scope. For example:
    
    
    // Correct - "value" is global storage
    __device__ int value;
    __device__ void x() {
        value = 5;
        child<<< 1, 1 >>>(&value);
    }
    
    
    
    // Invalid - "value" is local storage
    __device__ void y() {
        int value = 5;
        child<<< 1, 1 >>>(&value);
    }
    

#####  13.2.2.1.6. Texture Memory 

Writes to the global memory region over which a texture is mapped are incoherent with respect to texture accesses. Coherence for texture memory is enforced at the invocation of a child grid and when a child grid completes. This means that writes to memory prior to a child kernel launch are reflected in texture memory accesses of the child. Similarly to Global Memory above, writes to memory by a child are never guaranteed to be reflected in the texture memory accesses by a parent. The only way to access the modifications made by the threads in the child grid before the parent grid exits is via a kernel launched into the `cudaStreamTailLaunch` stream. Concurrent accesses by parent and child may result in inconsistent data.


##  13.3. Programming Interface 

###  13.3.1. CUDA C++ Reference 

This section describes changes and additions to the CUDA C++ language extensions for supporting _Dynamic Parallelism_.

The language interface and API available to CUDA kernels using CUDA C++ for Dynamic Parallelism, referred to as the _Device Runtime_ , is substantially like that of the CUDA Runtime API available on the host. Where possible the syntax and semantics of the CUDA Runtime API have been retained in order to facilitate ease of code reuse for routines that may run in either the host or device environments.

As with all code in CUDA C++, the APIs and code outlined here is per-thread code. This enables each thread to make unique, dynamic decisions regarding what kernel or operation to execute next. There are no synchronization requirements between threads within a block to execute any of the provided device runtime APIs, which enables the device runtime API functions to be called in arbitrarily divergent kernel code without deadlock.

####  13.3.1.1. Device-Side Kernel Launch 

Kernels may be launched from the device using the standard CUDA <<< >>> syntax:
    
    
    kernel_name<<< Dg, Db, Ns, S >>>([kernel arguments]);
    

  * `Dg` is of type `dim3` and specifies the dimensions and size of the grid

  * `Db` is of type `dim3` and specifies the dimensions and size of each thread block

  * `Ns` is of type `size_t` and specifies the number of bytes of shared memory that is dynamically allocated per thread block for this call in addition to statically allocated memory. `Ns` is an optional argument that defaults to 0.

  * `S` is of type `cudaStream_t` and specifies the stream associated with this call. The stream must have been allocated in the same grid where the call is being made. `S` is an optional argument that defaults to the NULL stream.


#####  13.3.1.1.1. Launches are Asynchronous 

Identical to host-side launches, all device-side kernel launches are asynchronous with respect to the launching thread. That is to say, the `<<<>>>` launch command will return immediately and the launching thread will continue to execute until it hits an implicit launch-synchronization point (such as at a kernel launched into the `cudaStreamTailLaunch` stream).

The child grid launch is posted to the device and will execute independently of the parent thread. The child grid may begin execution at any time after launch, but is not guaranteed to begin execution until the launching thread reaches an implicit launch-synchronization point.

#####  13.3.1.1.2. Launch Environment Configuration 

All global device configuration settings (for example, shared memory and L1 cache size as returned from `cudaDeviceGetCacheConfig()`, and device limits returned from `cudaDeviceGetLimit()`) will be inherited from the parent. Likewise, device limits such as stack size will remain as-configured.

For host-launched kernels, per-kernel configurations set from the host will take precedence over the global setting. These configurations will be used when the kernel is launched from the device as well. It is not possible to reconfigure a kernel’s environment from the device.

####  13.3.1.2. Streams 

Both named and unnamed (NULL) streams are available from the device runtime. Named streams may be used by any thread within a grid, but stream handles may not be passed to other child/parent kernels. In other words, a stream should be treated as private to the grid in which it is created.

Similar to host-side launch, work launched into separate streams may run concurrently, but actual concurrency is not guaranteed. Programs that depend upon concurrency between child kernels are not supported by the CUDA programming model and will have undefined behavior.

The host-side NULL stream’s cross-stream barrier semantic is not supported on the device (see below for details). In order to retain semantic compatibility with the host runtime, all device streams must be created using the `cudaStreamCreateWithFlags()` API, passing the `cudaStreamNonBlocking` flag. The `cudaStreamCreate()` call is a host-runtime- only API and will fail to compile for the device.

As `cudaStreamSynchronize()` and `cudaStreamQuery()` are unsupported by the device runtime, a kernel launched into the `cudaStreamTailLaunch` stream should be used instead when the application needs to know that stream-launched child kernels have completed.

#####  13.3.1.2.1. The Implicit (NULL) Stream 

Within a host program, the unnamed (NULL) stream has additional barrier synchronization semantics with other streams (see [Default Stream](#default-stream) for details). The device runtime offers a single implicit, unnamed stream shared between all threads in a thread block, but as all named streams must be created with the `cudaStreamNonBlocking` flag, work launched into the NULL stream will not insert an implicit dependency on pending work in any other streams (including NULL streams of other thread blocks).

#####  13.3.1.2.2. The Fire-and-Forget Stream 

The fire-and-forget named stream (`cudaStreamFireAndForget`) allows the user to launch fire-and-forget work with less boilerplate and without stream tracking overhead. It is functionally identical to, but faster than, creating a new stream per launch, and launching into that stream.

Fire-and-forget launches are immediately scheduled for launch without any dependency on the completion of previously launched grids. No other grid launches can depend on the completion of a fire-and-forget launch, except through the implicit synchronization at the end of the parent grid. So a tail launch or the next grid in parent grid’s stream won’t launch before a parent grid’s fire-and-forget work has completed.
    
    
    // In this example, C2's launch will not wait for C1's completion
    __global__ void P( ... ) {
       C1<<< ... , cudaStreamFireAndForget >>>( ... );
       C2<<< ... , cudaStreamFireAndForget >>>( ... );
    }
    

The fire-and-forget stream cannot be used to record or wait on events. Attempting to do so results in `cudaErrorInvalidValue`. The fire-and-forget stream is not supported when compiled with `CUDA_FORCE_CDP1_IF_SUPPORTED` defined. Fire-and-forget stream usage requires compilation to be in 64-bit mode.

#####  13.3.1.2.3. The Tail Launch Stream 

The tail launch named stream (`cudaStreamTailLaunch`) allows a grid to schedule a new grid for launch after its completion. It should be possible to to use a tail launch to achieve the same functionality as a `cudaDeviceSynchronize()` in most cases.

Each grid has its own tail launch stream. All non-tail launch work launched by a grid is implicitly synchronized before the tail stream is kicked off. I.e. A parent grid’s tail launch does not launch until the parent grid and all work launched by the parent grid to ordinary streams or per-thread or fire-and-forget streams have completed. If two grids are launched to the same grid’s tail launch stream, the later grid does not launch until the earlier grid and all its descendent work has completed.
    
    
    // In this example, C2 will only launch after C1 completes.
    __global__ void P( ... ) {
       C1<<< ... , cudaStreamTailLaunch >>>( ... );
       C2<<< ... , cudaStreamTailLaunch >>>( ... );
    }
    

Grids launched into the tail launch stream will not launch until the completion of all work by the parent grid, including all other grids (and their descendants) launched by the parent in all non-tail launched streams, including work executed or launched after the tail launch.
    
    
    // In this example, C will only launch after all X, F and P complete.
    __global__ void P( ... ) {
       C<<< ... , cudaStreamTailLaunch >>>( ... );
       X<<< ... , cudaStreamPerThread >>>( ... );
       F<<< ... , cudaStreamFireAndForget >>>( ... )
    }
    

The next grid in the parent grid’s stream will not be launched before a parent grid’s tail launch work has completed. In other words, the tail launch stream behaves as if it were inserted between its parent grid and the next grid in its parent grid’s stream.
    
    
    // In this example, P2 will only launch after C completes.
    __global__ void P1( ... ) {
       C<<< ... , cudaStreamTailLaunch >>>( ... );
    }
    
    __global__ void P2( ... ) {
    }
    
    int main ( ... ) {
       ...
       P1<<< ... >>>( ... );
       P2<<< ... >>>( ... );
       ...
    }
    

Each grid only gets one tail launch stream. To tail launch concurrent grids, it can be done like the example below.
    
    
    // In this example,  C1 and C2 will launch concurrently after P's completion
    __global__ void T( ... ) {
       C1<<< ... , cudaStreamFireAndForget >>>( ... );
       C2<<< ... , cudaStreamFireAndForget >>>( ... );
    }
    
    __global__ void P( ... ) {
       ...
       T<<< ... , cudaStreamTailLaunch >>>( ... );
    }
    

The tail launch stream cannot be used to record or wait on events. Attempting to do so results in `cudaErrorInvalidValue`. The tail launch stream is not supported when compiled with `CUDA_FORCE_CDP1_IF_SUPPORTED` defined. Tail launch stream usage requires compilation to be in 64-bit mode.

####  13.3.1.3. Events 

Only the inter-stream synchronization capabilities of CUDA events are supported. This means that `cudaStreamWaitEvent()` is supported, but `cudaEventSynchronize()`, `cudaEventElapsedTime()`, and `cudaEventQuery()` are not. As `cudaEventElapsedTime()` is not supported, cudaEvents must be created via `cudaEventCreateWithFlags()`, passing the `cudaEventDisableTiming` flag.

As with named streams, event objects may be shared between all threads within the grid which created them but are local to that grid and may not be passed to other kernels. Event handles are not guaranteed to be unique between grids, so using an event handle within a grid that did not create it will result in undefined behavior.

####  13.3.1.4. Synchronization 

It is up to the program to perform sufficient inter-thread synchronization, for example via a CUDA Event, if the calling thread is intended to synchronize with child grids invoked from other threads.

As it is not possible to explicitly synchronize child work from a parent thread, there is no way to guarantee that changes occurring in child grids are visible to threads within the parent grid.

####  13.3.1.5. Device Management 

Only the device on which a kernel is running will be controllable from that kernel. This means that device APIs such as `cudaSetDevice()` are not supported by the device runtime. The active device as seen from the GPU (returned from `cudaGetDevice()`) will have the same device number as seen from the host system. The `cudaDeviceGetAttribute()` call may request information about another device as this API allows specification of a device ID as a parameter of the call. Note that the catch-all `cudaGetDeviceProperties()` API is not offered by the device runtime - properties must be queried individually.

####  13.3.1.6. Memory Declarations 

#####  13.3.1.6.1. Device and Constant Memory 

Memory declared at file scope with `__device__` or `__constant__` memory space specifiers behaves identically when using the device runtime. All kernels may read or write device variables, whether the kernel was initially launched by the host or device runtime. Equivalently, all kernels will have the same view of `__constant__`s as declared at the module scope.

#####  13.3.1.6.2. Textures and Surfaces 

CUDA supports dynamically created texture and surface objects[7](#fn14), where a texture object may be created on the host, passed to a kernel, used by that kernel, and then destroyed from the host. The device runtime does not allow creation or destruction of texture or surface objects from within device code, but texture and surface objects created from the host may be used and passed around freely on the device. Regardless of where they are created, dynamically created texture objects are always valid and may be passed to child kernels from a parent.

Note

The device runtime does not support legacy module-scope (i.e., Fermi-style) textures and surfaces within a kernel launched from the device. Module-scope (legacy) textures may be created from the host and used in device code as for any kernel, but may only be used by a top-level kernel (i.e., the one which is launched from the host).

#####  13.3.1.6.3. Shared Memory Variable Declarations 

In CUDA C++ shared memory can be declared either as a statically sized file-scope or function-scoped variable, or as an `extern` variable with the size determined at runtime by the kernel’s caller via a launch configuration argument. Both types of declarations are valid under the device runtime.
    
    
    __global__ void permute(int n, int *data) {
       extern __shared__ int smem[];
       if (n <= 1)
           return;
    
       smem[threadIdx.x] = data[threadIdx.x];
       __syncthreads();
    
       permute_data(smem, n);
       __syncthreads();
    
       // Write back to GMEM since we can't pass SMEM to children.
       data[threadIdx.x] = smem[threadIdx.x];
       __syncthreads();
    
       if (threadIdx.x == 0) {
           permute<<< 1, 256, n/2*sizeof(int) >>>(n/2, data);
           permute<<< 1, 256, n/2*sizeof(int) >>>(n/2, data+n/2);
       }
    }
    
    void host_launch(int *data) {
        permute<<< 1, 256, 256*sizeof(int) >>>(256, data);
    }
    

#####  13.3.1.6.4. Symbol Addresses 

Device-side symbols (i.e., those marked `__device__`) may be referenced from within a kernel simply via the `&` operator, as all global-scope device variables are in the kernel’s visible address space. This also applies to `__constant__` symbols, although in this case the pointer will reference read-only data.

Given that device-side symbols can be referenced directly, those CUDA runtime APIs which reference symbols (e.g., `cudaMemcpyToSymbol()` or `cudaGetSymbolAddress()`) are redundant and hence not supported by the device runtime. Note this implies that constant data cannot be altered from within a running kernel, even ahead of a child kernel launch, as references to `__constant__` space are read-only.

####  13.3.1.7. API Errors and Launch Failures 

As usual for the CUDA runtime, any function may return an error code. The last error code returned is recorded and may be retrieved via the `cudaGetLastError()` call. Errors are recorded per-thread, so that each thread can identify the most recent error that it has generated. The error code is of type `cudaError_t`.

Similar to a host-side launch, device-side launches may fail for many reasons (invalid arguments, etc). The user must call `cudaGetLastError()` to determine if a launch generated an error, however lack of an error after launch does not imply the child kernel completed successfully.

For device-side exceptions, e.g., access to an invalid address, an error in a child grid will be returned to the host.

#####  13.3.1.7.1. Launch Setup APIs 

Kernel launch is a system-level mechanism exposed through the device runtime library, and as such is available directly from PTX via the underlying `cudaGetParameterBuffer()` and `cudaLaunchDevice()` APIs. It is permitted for a CUDA application to call these APIs itself, with the same requirements as for PTX. In both cases, the user is then responsible for correctly populating all necessary data structures in the correct format according to specification. Backwards compatibility is guaranteed in these data structures.

As with host-side launch, the device-side operator `<<<>>>` maps to underlying kernel launch APIs. This is so that users targeting PTX will be able to enact a launch, and so that the compiler front-end can translate `<<<>>>` into these calls.

Table 13 New Device-only Launch Implementation Functions Runtime API Launch Functions | Description of Difference From Host Runtime Behaviour (behavior is identical if no description)  
---|---  
`cudaGetParameterBuffer` | Generated automatically from `<<<>>>`. Note different API to host equivalent.  
`cudaLaunchDevice` | Generated automatically from `<<<>>>`. Note different API to host equivalent.  
  
The APIs for these launch functions are different to those of the CUDA Runtime API, and are defined as follows:
    
    
    extern   device   cudaError_t cudaGetParameterBuffer(void **params);
    extern __device__ cudaError_t cudaLaunchDevice(void *kernel,
                                            void *params, dim3 gridDim,
                                            dim3 blockDim,
                                            unsigned int sharedMemSize = 0,
                                            cudaStream_t stream = 0);
    

####  13.3.1.8. API Reference 

The portions of the CUDA Runtime API supported in the device runtime are detailed here. Host and device runtime APIs have identical syntax; semantics are the same except where indicated. The following table provides an overview of the API relative to the version available from the host.

Table 14 Supported API Functions Runtime API Functions | Details  
---|---  
`cudaDeviceGetCacheConfig` |   
`cudaDeviceGetLimit` |   
`cudaGetLastError` | Last error is per-thread state, not per-block state  
`cudaPeekAtLastError` |   
`cudaGetErrorString` |   
`cudaGetDeviceCount` |   
`cudaDeviceGetAttribute` | Will return attributes for any device  
`cudaGetDevice` | Always returns current device ID as would be seen from host  
`cudaStreamCreateWithFlags` | Must pass `cudaStreamNonBlocking` flag  
`cudaStreamDestroy` |   
`cudaStreamWaitEvent` |   
`cudaEventCreateWithFlags` | Must pass `cudaEventDisableTiming` flag  
`cudaEventRecord` |   
`cudaEventDestroy` |   
`cudaFuncGetAttributes` |   
`cudaMemcpyAsync` |  Notes about all `memcpy/memset` functions:

  * Only async `memcpy/set` functions are supported
  * Only device-to-device `memcpy` is permitted
  * May not pass in local or shared memory pointers

  
`cudaMemcpy2DAsync`  
`cudaMemcpy3DAsync`  
`cudaMemsetAsync`  
`cudaMemset2DAsync` |   
`cudaMemset3DAsync` |   
`cudaRuntimeGetVersion` |   
`cudaMalloc` | May not call `cudaFree` on the device on a pointer created on the host, and vice-versa  
`cudaFree`  
`cudaOccupancyMaxActiveBlocksPerMultiprocessor` |   
`cudaOccupancyMaxPotentialBlockSize` |   
`cudaOccupancyMaxPotentialBlockSizeVariableSMem` |   
  
###  13.3.2. Device-side Launch from PTX 

This section is for the programming language and compiler implementers who target _Parallel Thread Execution_ (PTX) and plan to support _Dynamic Parallelism_ in their language. It provides the low-level details related to supporting kernel launches at the PTX level.

####  13.3.2.1. Kernel Launch APIs 

Device-side kernel launches can be implemented using the following two APIs accessible from PTX: `cudaLaunchDevice()` and `cudaGetParameterBuffer()`. `cudaLaunchDevice()` launches the specified kernel with the parameter buffer that is obtained by calling `cudaGetParameterBuffer()` and filled with the parameters to the launched kernel. The parameter buffer can be NULL, i.e., no need to invoke `cudaGetParameterBuffer()`, if the launched kernel does not take any parameters.

#####  13.3.2.1.1. cudaLaunchDevice 

At the PTX level, `cudaLaunchDevice()`needs to be declared in one of the two forms shown below before it is used.
    
    
    // PTX-level Declaration of cudaLaunchDevice() when .address_size is 64
    .extern .func(.param .b32 func_retval0) cudaLaunchDevice
    (
      .param .b64 func,
      .param .b64 parameterBuffer,
      .param .align 4 .b8 gridDimension[12],
      .param .align 4 .b8 blockDimension[12],
      .param .b32 sharedMemSize,
      .param .b64 stream
    )
    ;
    

The CUDA-level declaration below is mapped to one of the aforementioned PTX-level declarations and is found in the system header file `cuda_device_runtime_api.h`. The function is defined in the `cudadevrt` system library, which must be linked with a program in order to use device-side kernel launch functionality.
    
    
    // CUDA-level declaration of cudaLaunchDevice()
    extern "C" __device__
    cudaError_t cudaLaunchDevice(void *func, void *parameterBuffer,
                                 dim3 gridDimension, dim3 blockDimension,
                                 unsigned int sharedMemSize,
                                 cudaStream_t stream);
    

The first parameter is a pointer to the kernel to be is launched, and the second parameter is the parameter buffer that holds the actual parameters to the launched kernel. The layout of the parameter buffer is explained in [Parameter Buffer Layout](#parameter-buffer-layout), below. Other parameters specify the launch configuration, i.e., as grid dimension, block dimension, shared memory size, and the stream associated with the launch (please refer to [Execution Configuration](#execution-configuration) for the detailed description of launch configuration.

#####  13.3.2.1.2. cudaGetParameterBuffer 

`cudaGetParameterBuffer()` needs to be declared at the PTX level before it’s used. The PTX-level declaration must be in one of the two forms given below, depending on address size:
    
    
    // PTX-level Declaration of cudaGetParameterBuffer() when .address_size is 64
    .extern .func(.param .b64 func_retval0) cudaGetParameterBuffer
    (
      .param .b64 alignment,
      .param .b64 size
    )
    ;
    

The following CUDA-level declaration of `cudaGetParameterBuffer()` is mapped to the aforementioned PTX-level declaration:
    
    
    // CUDA-level Declaration of cudaGetParameterBuffer()
    extern "C" __device__
    void *cudaGetParameterBuffer(size_t alignment, size_t size);
    

The first parameter specifies the alignment requirement of the parameter buffer and the second parameter the size requirement in bytes. In the current implementation, the parameter buffer returned by `cudaGetParameterBuffer()` is always guaranteed to be 64- byte aligned, and the alignment requirement parameter is ignored. However, it is recommended to pass the correct alignment requirement value - which is the largest alignment of any parameter to be placed in the parameter buffer - to `cudaGetParameterBuffer()` to ensure portability in the future.

####  13.3.2.2. Parameter Buffer Layout 

Parameter reordering in the parameter buffer is prohibited, and each individual parameter placed in the parameter buffer is required to be aligned. That is, each parameter must be placed at the _n_ th byte in the parameter buffer, where _n_ is the smallest multiple of the parameter size that is greater than the offset of the last byte taken by the preceding parameter. The maximum size of the parameter buffer is 4KB.

For a more detailed description of PTX code generated by the CUDA compiler, please refer to the PTX-3.5 specification.

###  13.3.3. Toolkit Support for Dynamic Parallelism 

####  13.3.3.1. Including Device Runtime API in CUDA Code 

Similar to the host-side runtime API, prototypes for the CUDA device runtime API are included automatically during program compilation. There is no need to include`cuda_device_runtime_api.h` explicitly.

####  13.3.3.2. Compiling and Linking 

When compiling and linking CUDA programs using dynamic parallelism with `nvcc`, the program will automatically link against the static device runtime library `libcudadevrt`.

The device runtime is offered as a static library (`cudadevrt.lib` on Windows, `libcudadevrt.a` under Linux), against which a GPU application that uses the device runtime must be linked. Linking of device libraries can be accomplished through `nvcc` and/or `nvlink`. Two simple examples are shown below.

A device runtime program may be compiled and linked in a single step, if all required source files can be specified from the command line:
    
    
    $ nvcc -arch=sm_75 -rdc=true hello_world.cu -o hello -lcudadevrt
    

It is also possible to compile CUDA .cu source files first to object files, and then link these together in a two-stage process:
    
    
    $ nvcc -arch=sm_75 -dc hello_world.cu -o hello_world.o
    $ nvcc -arch=sm_75 -rdc=true hello_world.o -o hello -lcudadevrt
    

Please see the Using Separate Compilation section of The CUDA Driver Compiler NVCC guide for more details.


##  13.4. Programming Guidelines 

###  13.4.1. Basics 

The device runtime is a functional subset of the host runtime. API level device management, kernel launching, device memcpy, stream management, and event management are exposed from the device runtime.

Programming for the device runtime should be familiar to someone who already has experience with CUDA. Device runtime syntax and semantics are largely the same as that of the host API, with any exceptions detailed earlier in this document.

The following example shows a simple _Hello World_ program incorporating dynamic parallelism:
    
    
    #include <stdio.h>
    
    __global__ void childKernel()
    {
        printf("Hello ");
    }
    
    __global__ void tailKernel()
    {
        printf("World!\n");
    }
    
    __global__ void parentKernel()
    {
        // launch child
        childKernel<<<1,1>>>();
        if (cudaSuccess != cudaGetLastError()) {
            return;
        }
    
        // launch tail into cudaStreamTailLaunch stream
        // implicitly synchronizes: waits for child to complete
        tailKernel<<<1,1,0,cudaStreamTailLaunch>>>();
    
    }
    
    int main(int argc, char *argv[])
    {
        // launch parent
        parentKernel<<<1,1>>>();
        if (cudaSuccess != cudaGetLastError()) {
            return 1;
        }
    
        // wait for parent to complete
        if (cudaSuccess != cudaDeviceSynchronize()) {
            return 2;
        }
    
        return 0;
    }
    

This program may be built in a single step from the command line as follows:
    
    
    $ nvcc -arch=sm_75 -rdc=true hello_world.cu -o hello -lcudadevrt
    

###  13.4.2. Performance 

####  13.4.2.1. Dynamic-parallelism-enabled Kernel Overhead 

System software which is active when controlling dynamic launches may impose an overhead on any kernel which is running at the time, whether or not it invokes kernel launches of its own. This overhead arises from the device runtime’s execution tracking and management software and may result in decreased performance. This overhead is, in general, incurred for applications that link against the device runtime library.

###  13.4.3. Implementation Restrictions and Limitations 

_Dynamic Parallelism_ guarantees all semantics described in this document, however, certain hardware and software resources are implementation-dependent and limit the scale, performance and other properties of a program which uses the device runtime.

####  13.4.3.1. Runtime 

#####  13.4.3.1.1. Memory Footprint 

The device runtime system software reserves memory for various management purposes, in particular a reservation for tracking pending grid launches. Configuration controls are available to reduce the size of this reservation in exchange for certain launch limitations. See [Configuration Options](#configuration-options), below, for details.

#####  13.4.3.1.2. Pending Kernel Launches 

When a kernel is launched, all associated configuration and parameter data is tracked until the kernel completes. This data is stored within a system-managed launch pool.

The size of the fixed-size launch pool is configurable by calling `cudaDeviceSetLimit()` from the host and specifying `cudaLimitDevRuntimePendingLaunchCount`.

#####  13.4.3.1.3. Configuration Options 

Resource allocation for the device runtime system software is controlled via the `cudaDeviceSetLimit()` API from the host program. Limits must be set before any kernel is launched, and may not be changed while the GPU is actively running programs.

The following named limits may be set:

Limit | Behavior  
---|---  
`cudaLimitDevRuntimePendingLaunchCount` | Controls the amount of memory set aside for buffering kernel launches and events which have not yet begun to execute, due either to unresolved dependencies or lack of execution resources. When the buffer is full, an attempt to allocate a launch slot during a device side kernel launch will fail and return `cudaErrorLaunchOutOfResources`, while an attempt to allocate an event slot will fail and return `cudaErrorMemoryAllocation`. The default number of launch slots is 2048. Applications may increase the number of launch and/or event slots by setting `cudaLimitDevRuntimePendingLaunchCount`. The number of event slots allocated is twice the value of that limit.  
`cudaLimitStackSize` | Controls the stack size in bytes of each GPU thread. The CUDA driver automatically increases the per-thread stack size for each kernel launch as needed. This size isn’t reset back to the original value after each launch. To set the per-thread stack size to a different value, `cudaDeviceSetLimit()` can be called to set this limit. The stack will be immediately resized, and if necessary, the device will block until all preceding requested tasks are complete. `cudaDeviceGetLimit()` can be called to get the current per-thread stack size.  
  
#####  13.4.3.1.4. Memory Allocation and Lifetime 

`cudaMalloc()` and `cudaFree()` have distinct semantics between the host and device environments. When invoked from the host, `cudaMalloc()` allocates a new region from unused device memory. When invoked from the device runtime these functions map to device-side `malloc()` and `free()`. This implies that within the device environment the total allocatable memory is limited to the device `malloc()` heap size, which may be smaller than the available unused device memory. Also, it is an error to invoke `cudaFree()` from the host program on a pointer which was allocated by `cudaMalloc()` on the device or vice-versa.

| `cudaMalloc()` on Host | `cudaMalloc()` on Device  
---|---|---  
`cudaFree()` on Host | Supported | Not Supported  
`cudaFree()` on Device | Not Supported | Supported  
Allocation limit | Free device memory | `cudaLimitMallocHeapSize`  
  
#####  13.4.3.1.5. SM Id and Warp Id 

Note that in PTX `%smid` and `%warpid` are defined as volatile values. The device runtime may reschedule thread blocks onto different SMs in order to more efficiently manage resources. As such, it is unsafe to rely upon `%smid` or `%warpid` remaining unchanged across the lifetime of a thread or thread block.

#####  13.4.3.1.6. ECC Errors 

No notification of ECC errors is available to code within a CUDA kernel. ECC errors are reported at the host side once the entire launch tree has completed. Any ECC errors which arise during execution of a nested program will either generate an exception or continue execution (depending upon error and configuration).


##  13.5. CDP2 vs CDP1 

This section summarises the differences between, and the compatibility and interoperability of, the new (CDP2) and legacy (CDP1) CUDA Dynamic Parallelism interfaces. It also shows how to opt-out of the CDP2 interface on devices of compute capability less than 9.0.

###  13.5.1. Differences Between CDP1 and CDP2 

Explicit device-side synchronization is no longer possible with CDP2 or on devices of compute capability 9.0 or higher. Implicit synchronization (such as tail launches) must be used instead.

Attempting to query or set `cudaLimitDevRuntimeSyncDepth` (or `CU_LIMIT_DEV_RUNTIME_SYNC_DEPTH`) with CDP2 or on devices of compute capability 9.0 or higher results in `cudaErrorUnsupportedLimit`.

CDP2 no longer has a virtualized pool for pending launches that don’t fit in the fixed-sized pool. `cudaLimitDevRuntimePendingLaunchCount` must be set to be large enough to avoid running out of launch slots.

For CDP2, there is a limit to the total number of events existing at once (note that events are destroyed only after a launch completes), equal to twice the pending launch count. `cudaLimitDevRuntimePendingLaunchCount` must be set to be large enough to avoid running out of event slots.

Streams are tracked per grid with CDP2 or on devices of compute capability 9.0 or higher, not per thread block. This allows work to be launched into a stream created by another thread block. Attempting to do so with the CDP1 results in `cudaErrorInvalidValue`.

CDP2 introduces the tail launch (`cudaStreamTailLaunch`) and fire-and-forget (`cudaStreamFireAndForget`) named streams.

CDP2 is supported only under 64-bit compilation mode.

###  13.5.2. Compatibility and Interoperability 

CDP2 is the default. Functions can be compiled with `-DCUDA_FORCE_CDP1_IF_SUPPORTED` to opt-out of using CDP2 on devices of compute capability less than 9.0.

| Function compiler with CUDA 12.0 and newer (default) | Function compiled with pre-CUDA 12.0 or with CUDA 12.0 and newer with `-DCUDA_FORCE_CDP1_IF_SUPPORTED` specified  
---|---|---  
Compilation | Compile error if device code references `cudaDeviceSynchronize`. | Compile error if code references `cudaStreamTailLaunch` or `cudaStreamFireAndForget`. Compile error if device code references `cudaDeviceSynchronize` and code is compiled for sm_90 or newer.  
Compute capability < 9.0 | New interface is used. | Legacy interface is used.  
Compute capability 9.0 and higher | New interface is used. | New interface is used. If function references `cudaDeviceSynchronize` in device code, function load returns `cudaErrorSymbolNotFound` (this could happen if the code is compiled for devices of compute capability less than 9.0, but run on devices of compute capability 9.0 or higher using JIT).  
  
Functions using CDP1 and CDP2 may be loaded and run simultaneously in the same context. The CDP1 functions are able to use CDP1-specific features (e.g. `cudaDeviceSynchronize`) and CDP2 functions are able to use CDP2-specific features (e.g. tail launch and fire-and-forget launch).

A function using CDP1 cannot launch a function using CDP2, and vice versa. If a function that would use CDP1 contains in its call graph a function that would use CDP2, or vice versa, `cudaErrorCdpVersionMismatch` would result during function load.


##  13.6. Legacy CUDA Dynamic Parallelism (CDP1) 

See [CUDA Dynamic Parallelism](#cuda-dynamic-parallelism), above, for CDP2 version of document.

###  13.6.1. Execution Environment and Memory Model (CDP1) 

See [Execution Environment and Memory Model](#execution-environment-and-memory-model-cdp2), above, for CDP2 version of document.

####  13.6.1.1. Execution Environment (CDP1) 

See [Execution Environment](#execution-environment-cdp2), above, for CDP2 version of document.

The CUDA execution model is based on primitives of threads, thread blocks, and grids, with kernel functions defining the program executed by individual threads within a thread block and grid. When a kernel function is invoked the grid’s properties are described by an execution configuration, which has a special syntax in CUDA. Support for dynamic parallelism in CUDA extends the ability to configure, launch, and synchronize upon new grids to threads that are running on the device.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) block is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

#####  13.6.1.1.1. Parent and Child Grids (CDP1) 

See [Parent and Child Grids](#parent-and-child-grids-cdp2), above, for CDP2 version of document.

A device thread that configures and launches a new grid belongs to the parent grid, and the grid created by the invocation is a child grid.

The invocation and completion of child grids is properly nested, meaning that the parent grid is not considered complete until all child grids created by its threads have completed. Even if the invoking threads do not explicitly synchronize on the child grids launched, the runtime guarantees an implicit synchronization between the parent and child.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

[![The GPU Devotes More Transistors to Data Processing](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/parent-child-launch-nesting.png)](_images/parent-child-launch-nesting.png)

Figure 31 Parent-Child Launch Nesting

#####  13.6.1.1.2. Scope of CUDA Primitives (CDP1) 

See [Scope of CUDA Primitives](#scope-of-cuda-primitives-cdp2), above, for CDP2 version of document.

On both host and device, the CUDA runtime offers an API for launching kernels, for waiting for launched work to complete, and for tracking dependencies between launches via streams and events. On the host system, the state of launches and the CUDA primitives referencing streams and events are shared by all threads within a process; however processes execute independently and may not share CUDA objects.

A similar hierarchy exists on the device: launched kernels and CUDA objects are visible to all threads in a thread block, but are independent between thread blocks. This means for example that a stream may be created by one thread and used by any other thread in the same thread block, but may not be shared with threads in any other thread block.

#####  13.6.1.1.3. Synchronization (CDP1) 

See [Synchronization](#dynamic-parallelism-synchronization), above, for CDP2 version of document.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

CUDA runtime operations from any thread, including kernel launches, are visible across a thread block. This means that an invoking thread in the parent grid may perform synchronization on the grids launched by that thread, by other threads in the thread block, or on streams created within the same thread block. Execution of a thread block is not considered complete until all launches by all threads in the block have completed. If all threads in a block exit before all child launches have completed, a synchronization operation will automatically be triggered.

#####  13.6.1.1.4. Streams and Events (CDP1) 

See [Streams and Events](#streams-and-events-cdp2), above, for CDP2 version of document.

CUDA _Streams_ and _Events_ allow control over dependencies between grid launches: grids launched into the same stream execute in-order, and events may be used to create dependencies between streams. Streams and events created on the device serve this exact same purpose.

Streams and events created within a grid exist within thread block scope but have undefined behavior when used outside of the thread block where they were created. As described above, all work launched by a thread block is implicitly synchronized when the block exits; work launched into streams is included in this, with all dependencies resolved appropriately. The behavior of operations on a stream that has been modified outside of thread block scope is undefined.

Streams and events created on the host have undefined behavior when used within any kernel, just as streams and events created by a parent grid have undefined behavior if used within a child grid.

#####  13.6.1.1.5. Ordering and Concurrency (CDP1) 

See [Ordering and Concurrency](#ordering-and-concurrency-cdp2), above, for CDP2 version of document.

The ordering of kernel launches from the device runtime follows CUDA Stream ordering semantics. Within a thread block, all kernel launches into the same stream are executed in-order. With multiple threads in the same thread block launching into the same stream, the ordering within the stream is dependent on the thread scheduling within the block, which may be controlled with synchronization primitives such as `__syncthreads()`.

Note that because streams are shared by all threads within a thread block, the implicit _NULL_ stream is also shared. If multiple threads in a thread block launch into the implicit stream, then these launches will be executed in-order. If concurrency is desired, explicit named streams should be used.

_Dynamic Parallelism_ enables concurrency to be expressed more easily within a program; however, the device runtime introduces no new concurrency guarantees within the CUDA execution model. There is no guarantee of concurrent execution between any number of different thread blocks on a device.

The lack of concurrency guarantee extends to parent thread blocks and their child grids. When a parent thread block launches a child grid, the child is not guaranteed to begin execution until the parent thread block reaches an explicit synchronization point (such as `cudaDeviceSynchronize()`).

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

While concurrency will often easily be achieved, it may vary as a function of deviceconfiguration, application workload, and runtime scheduling. It is therefore unsafe to depend upon any concurrency between different thread blocks.

#####  13.6.1.1.6. Device Management (CDP1) 

See [Device Management](#device-management-programming), above, for CDP2 version of document.

There is no multi-GPU support from the device runtime; the device runtime is only capable of operating on the device upon which it is currently executing. It is permitted, however, to query properties for any CUDA capable device in the system.

####  13.6.1.2. Memory Model (CDP1) 

See [Memory Model](#memory-model), above, for CDP2 version of document.

Parent and child grids share the same global and constant memory storage, but have distinct local and shared memory.

#####  13.6.1.2.1. Coherence and Consistency (CDP1) 

See [Coherence and Consistency](#coherence-and-consistency-cdp2), above, for CDP2 version of document.

######  13.6.1.2.1.1. Global Memory (CDP1) 

See [Global Memory](#global-memory-cdp2), above, for CDP2 version of document.

Parent and child grids have coherent access to global memory, with weak consistency guarantees between child and parent. There are two points in the execution of a child grid when its view of memory is fully consistent with the parent thread: when the child grid is invoked by the parent, and when the child grid completes as signaled by a synchronization API invocation in the parent thread.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

All global memory operations in the parent thread prior to the child grid’s invocation are visible to the child grid. All memory operations of the child grid are visible to the parent after the parent has synchronized on the child grid’s completion.

In the following example, the child grid executing `child_launch` is only guaranteed to see the modifications to `data` made before the child grid was launched. Since thread 0 of the parent is performing the launch, the child will be consistent with the memory seen by thread 0 of the parent. Due to the first `__syncthreads()` call, the child will see `data[0]=0`, `data[1]=1`, …, `data[255]=255` (without the `__syncthreads()` call, only `data[0]` would be guaranteed to be seen by the child). When the child grid returns, thread 0 is guaranteed to see modifications made by the threads in its child grid. Those modifications become available to the other threads of the parent grid only after the second `__syncthreads()` call:
    
    
    __global__ void child_launch(int *data) {
       data[threadIdx.x] = data[threadIdx.x]+1;
    }
    
    __global__ void parent_launch(int *data) {
       data[threadIdx.x] = threadIdx.x;
    
       __syncthreads();
    
       if (threadIdx.x == 0) {
           child_launch<<< 1, 256 >>>(data);
           cudaDeviceSynchronize();
       }
    
       __syncthreads();
    }
    
    void host_launch(int *data) {
        parent_launch<<< 1, 256 >>>(data);
    }
    

######  13.6.1.2.1.2. Zero Copy Memory (CDP1) 

See [Zero Copy Memory](#zero-copy-memory), above, for CDP2 version of document.

Zero-copy system memory has identical coherence and consistency guarantees to global memory, and follows the semantics detailed above. A kernel may not allocate or free zero-copy memory, but may use pointers to zero-copy passed in from the host program.

######  13.6.1.2.1.3. Constant Memory (CDP1) 

See [Constant Memory](#constant-memory), above, for CDP2 version of document.

Constants are immutable and may not be modified from the device, even between parent and child launches. That is to say, the value of all `__constant__` variables must be set from the host prior to launch. Constant memory is inherited automatically by all child kernels from their respective parents.

Taking the address of a constant memory object from within a kernel thread has the same semantics as for all CUDA programs, and passing that pointer from parent to child or from a child to parent is naturally supported.

######  13.6.1.2.1.4. Shared and Local Memory (CDP1) 

See [Shared and Local Memory](#shared-and-local-memory-cdp2), above, for CDP2 version of document.

Shared and Local memory is private to a thread block or thread, respectively, and is not visible or coherent between parent and child. Behavior is undefined when an object in one of these locations is referenced outside of the scope within which it belongs, and may cause an error.

The NVIDIA compiler will attempt to warn if it can detect that a pointer to local or shared memory is being passed as an argument to a kernel launch. At runtime, the programmer may use the `__isGlobal()` intrinsic to determine whether a pointer references global memory and so may safely be passed to a child launch.

Note that calls to `cudaMemcpy*Async()` or `cudaMemset*Async()` may invoke new child kernels on the device in order to preserve stream semantics. As such, passing shared or local memory pointers to these APIs is illegal and will return an error.

######  13.6.1.2.1.5. Local Memory (CDP1) 

See [Local Memory](#local-memory-cdp2), above, for CDP2 version of document.

Local memory is private storage for an executing thread, and is not visible outside of that thread. It is illegal to pass a pointer to local memory as a launch argument when launching a child kernel. The result of dereferencing such a local memory pointer from a child will be undefined.

For example the following is illegal, with undefined behavior if `x_array` is accessed by `child_launch`:
    
    
    int x_array[10];       // Creates x_array in parent's local memory
    child_launch<<< 1, 1 >>>(x_array);
    

It is sometimes difficult for a programmer to be aware of when a variable is placed into local memory by the compiler. As a general rule, all storage passed to a child kernel should be allocated explicitly from the global-memory heap, either with `cudaMalloc()`, `new()` or by declaring `__device__` storage at global scope. For example:
    
    
    // Correct - "value" is global storage
    __device__ int value;
    __device__ void x() {
        value = 5;
        child<<< 1, 1 >>>(&value);
    }
    
    
    
    // Invalid - "value" is local storage
    __device__ void y() {
        int value = 5;
        child<<< 1, 1 >>>(&value);
    }
    

######  13.6.1.2.1.6. Texture Memory (CDP1) 

See [Texture Memory](#texture-memory-cdp), above, for CDP2 version of document.

Writes to the global memory region over which a texture is mapped are incoherent with respect to texture accesses. Coherence for texture memory is enforced at the invocation of a child grid and when a child grid completes. This means that writes to memory prior to a child kernel launch are reflected in texture memory accesses of the child. Similarly, writes to memory by a child will be reflected in the texture memory accesses by a parent, but only after the parent synchronizes on the child’s completion. Concurrent accesses by parent and child may result in inconsistent data.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

###  13.6.2. Programming Interface (CDP1) 

See [Programming Interface](#programming-interface-cdp), above, for CDP2 version of document.

####  13.6.2.1. CUDA C++ Reference (CDP1) 

See [CUDA C++ Reference](#cuda-c-reference), above, for CDP2 version of document.

This section describes changes and additions to the CUDA C++ language extensions for supporting _Dynamic Parallelism_.

The language interface and API available to CUDA kernels using CUDA C++ for Dynamic Parallelism, referred to as the _Device Runtime_ , is substantially like that of the CUDA Runtime API available on the host. Where possible the syntax and semantics of the CUDA Runtime API have been retained in order to facilitate ease of code reuse for routines that may run in either the host or device environments.

As with all code in CUDA C++, the APIs and code outlined here is per-thread code. This enables each thread to make unique, dynamic decisions regarding what kernel or operation to execute next. There are no synchronization requirements between threads within a block to execute any of the provided device runtime APIs, which enables the device runtime API functions to be called in arbitrarily divergent kernel code without deadlock.

#####  13.6.2.1.1. Device-Side Kernel Launch (CDP1) 

See [Kernel Launch APIs](#id237), above, for CDP2 version of document.

Kernels may be launched from the device using the standard CUDA <<< >>> syntax:
    
    
    kernel_name<<< Dg, Db, Ns, S >>>([kernel arguments]);
    

  * `Dg` is of type `dim3` and specifies the dimensions and size of the grid

  * `Db` is of type `dim3` and specifies the dimensions and size of each thread block

  * `Ns` is of type `size_t` and specifies the number of bytes of shared memory that is dynamically allocated per thread block for this call and addition to statically allocated memory. `Ns` is an optional argument that defaults to 0.

  * `S` is of type `cudaStream_t` and specifies the stream associated with this call. The stream must have been allocated in the same thread block where the call is being made. `S` is an optional argument that defaults to 0.


######  13.6.2.1.1.1. Launches are Asynchronous (CDP1) 

See [Launches are Asynchronous](#launches-are-asynchronous), above, for CDP2 version of document.

Identical to host-side launches, all device-side kernel launches are asynchronous with respect to the launching thread. That is to say, the `<<<>>>` launch command will return immediately and the launching thread will continue to execute until it hits an explicit launch-synchronization point such as `cudaDeviceSynchronize()`.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

The grid launch is posted to the device and will execute independently of the parent thread. The child grid may begin execution at any time after launch, but is not guaranteed to begin execution until the launching thread reaches an explicit launch-synchronization point.

######  13.6.2.1.1.2. Launch Environment Configuration (CDP1) 

See [Launch Environment Configuration](#launch-environment-configuration), above, for CDP2 version of document.

All global device configuration settings (for example, shared memory and L1 cache size as returned from `cudaDeviceGetCacheConfig()`, and device limits returned from `cudaDeviceGetLimit()`) will be inherited from the parent. Likewise, device limits such as stack size will remain as-configured.

For host-launched kernels, per-kernel configurations set from the host will take precedence over the global setting. These configurations will be used when the kernel is launched from the device as well. It is not possible to reconfigure a kernel’s environment from the device.

#####  13.6.2.1.2. Streams (CDP1) 

See [Streams](#streams-cdp), above, for CDP2 version of document.

Both named and unnamed (NULL) streams are available from the device runtime. Named streams may be used by any thread within a thread-block, but stream handles may not be passed to other blocks or child/parent kernels. In other words, a stream should be treated as private to the block in which it is created. Stream handles are not guaranteed to be unique between blocks, so using a stream handle within a block that did not allocate it will result in undefined behavior.

Similar to host-side launch, work launched into separate streams may run concurrently, but actual concurrency is not guaranteed. Programs that depend upon concurrency between child kernels are not supported by the CUDA programming model and will have undefined behavior.

The host-side NULL stream’s cross-stream barrier semantic is not supported on the device (see below for details). In order to retain semantic compatibility with the host runtime, all device streams must be created using the `cudaStreamCreateWithFlags()` API, passing the `cudaStreamNonBlocking` flag. The `cudaStreamCreate()` call is a host-runtime- only API and will fail to compile for the device.

As `cudaStreamSynchronize()` and `cudaStreamQuery()` are unsupported by the device runtime, `cudaDeviceSynchronize()` should be used instead when the application needs to know that stream-launched child kernels have completed.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

######  13.6.2.1.2.1. The Implicit (NULL) Stream (CDP1) 

See [The Implicit (NULL) Stream](#the-implicit-null-stream), above, for CDP2 version of document.

Within a host program, the unnamed (NULL) stream has additional barrier synchronization semantics with other streams (see [Default Stream](#default-stream) for details). The device runtime offers a single implicit, unnamed stream shared between all threads in a block, but as all named streams must be created with the `cudaStreamNonBlocking` flag, work launched into the NULL stream will not insert an implicit dependency on pending work in any other streams (including NULL streams of other thread blocks).

#####  13.6.2.1.3. Events (CDP1) 

See [Events](#events-cdp), above, for CDP2 version of document.

Only the inter-stream synchronization capabilities of CUDA events are supported. This means that `cudaStreamWaitEvent()` is supported, but `cudaEventSynchronize()`, `cudaEventElapsedTime()`, and `cudaEventQuery()` are not. As `cudaEventElapsedTime()` is not supported, cudaEvents must be created via `cudaEventCreateWithFlags()`, passing the `cudaEventDisableTiming` flag.

As for all device runtime objects, event objects may be shared between all threads within the thread-block which created them but are local to that block and may not be passed to other kernels, or between blocks within the same kernel. Event handles are not guaranteed to be unique between blocks, so using an event handle within a block that did not create it will result in undefined behavior.

#####  13.6.2.1.4. Synchronization (CDP1) 

See [Synchronization](#synchronization-programming-interface), above, for CDP2 version of document.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

The `cudaDeviceSynchronize()` function will synchronize on all work launched by any thread in the thread-block up to the point where `cudaDeviceSynchronize()` was called. Note that `cudaDeviceSynchronize()` may be called from within divergent code (see [Block Wide Synchronization (CDP1)](#block-wide-synchronization-cdp1)).

It is up to the program to perform sufficient additional inter-thread synchronization, for example via a call to `__syncthreads()`, if the calling thread is intended to synchronize with child grids invoked from other threads.

######  13.6.2.1.4.1. Block Wide Synchronization (CDP1) 

See [CUDA Dynamic Parallelism](#cuda-dynamic-parallelism), above, for CDP2 version of document.

The `cudaDeviceSynchronize()` function does not imply intra-block synchronization. In particular, without explicit synchronization via a `__syncthreads()` directive the calling thread can make no assumptions about what work has been launched by any thread other than itself. For example if multiple threads within a block are each launching work and synchronization is desired for all this work at once (perhaps because of event-based dependencies), it is up to the program to guarantee that this work is submitted by all threads before calling `cudaDeviceSynchronize()`.

Because the implementation is permitted to synchronize on launches from any thread in the block, it is quite possible that simultaneous calls to `cudaDeviceSynchronize()` by multiple threads will drain all work in the first call and then have no effect for the later calls.

#####  13.6.2.1.5. Device Management (CDP1) 

See [Device Management](#device-management-programming), above, for CDP2 version of document.

Only the device on which a kernel is running will be controllable from that kernel. This means that device APIs such as `cudaSetDevice()` are not supported by the device runtime. The active device as seen from the GPU (returned from `cudaGetDevice()`) will have the same device number as seen from the host system. The `cudaDeviceGetAttribute()` call may request information about another device as this API allows specification of a device ID as a parameter of the call. Note that the catch-all `cudaGetDeviceProperties()` API is not offered by the device runtime - properties must be queried individually.

#####  13.6.2.1.6. Memory Declarations (CDP1) 

See [Memory Declarations](#memory-declarations), above, for CDP2 version of document.

######  13.6.2.1.6.1. Device and Constant Memory (CDP1) 

See [Device and Constant Memory](#device-and-constant-memory), above, for CDP2 version of document.

Memory declared at file scope with `__device__` or `__constant__` memory space specifiers behaves identically when using the device runtime. All kernels may read or write device variables, whether the kernel was initially launched by the host or device runtime. Equivalently, all kernels will have the same view of `__constant__`s as declared at the module scope.

######  13.6.2.1.6.2. Textures and Surfaces (CDP1) 

See [Textures and Surfaces](#textures-and-surfaces), above, for CDP2 version of document.

CUDA supports dynamically created texture and surface objects[7](#fn14), where a texture object may be created on the host, passed to a kernel, used by that kernel, and then destroyed from the host. The device runtime does not allow creation or destruction of texture or surface objects from within device code, but texture and surface objects created from the host may be used and passed around freely on the device. Regardless of where they are created, dynamically created texture objects are always valid and may be passed to child kernels from a parent.

Note

The device runtime does not support legacy module-scope (i.e., Fermi-style) textures and surfaces within a kernel launched from the device. Module-scope (legacy) textures may be created from the host and used in device code as for any kernel, but may only be used by a top-level kernel (i.e., the one which is launched from the host).

######  13.6.2.1.6.3. Shared Memory Variable Declarations (CDP1) 

See [Shared Memory Variable Declarations](#shared-memory-variable-declarations), above, for CDP2 version of document.

In CUDA C++ shared memory can be declared either as a statically sized file-scope or function-scoped variable, or as an `extern` variable with the size determined at runtime by the kernel’s caller via a launch configuration argument. Both types of declarations are valid under the device runtime.
    
    
    __global__ void permute(int n, int *data) {
       extern __shared__ int smem[];
       if (n <= 1)
           return;
    
       smem[threadIdx.x] = data[threadIdx.x];
       __syncthreads();
    
       permute_data(smem, n);
       __syncthreads();
    
       // Write back to GMEM since we can't pass SMEM to children.
       data[threadIdx.x] = smem[threadIdx.x];
       __syncthreads();
    
       if (threadIdx.x == 0) {
           permute<<< 1, 256, n/2*sizeof(int) >>>(n/2, data);
           permute<<< 1, 256, n/2*sizeof(int) >>>(n/2, data+n/2);
       }
    }
    
    void host_launch(int *data) {
        permute<<< 1, 256, 256*sizeof(int) >>>(256, data);
    }
    

######  13.6.2.1.6.4. Symbol Addresses (CDP1) 

See [Symbol Addresses](#symbol-addresses), above, for CDP2 version of document.

Device-side symbols (i.e., those marked `__device__`) may be referenced from within a kernel simply via the `&` operator, as all global-scope device variables are in the kernel’s visible address space. This also applies to `__constant__` symbols, although in this case the pointer will reference read-only data.

Given that device-side symbols can be referenced directly, those CUDA runtime APIs which reference symbols (for example, `cudaMemcpyToSymbol()` or `cudaGetSymbolAddress()`) are redundant and hence not supported by the device runtime. Note this implies that constant data cannot be altered from within a running kernel, even ahead of a child kernel launch, as references to `__constant__` space are read-only.

#####  13.6.2.1.7. API Errors and Launch Failures (CDP1) 

See [API Errors and Launch Failures](#api-errors-and-launch-failures), above, for CDP2 version of document.

As usual for the CUDA runtime, any function may return an error code. The last error code returned is recorded and may be retrieved via the `cudaGetLastError()` call. Errors are recorded per-thread, so that each thread can identify the most recent error that it has generated. The error code is of type `cudaError_t`.

Similar to a host-side launch, device-side launches may fail for many reasons (invalid arguments, and so on). The user must call `cudaGetLastError()` to determine if a launch generated an error, however lack of an error after launch does not imply the child kernel completed successfully.

For device-side exceptions, for example, access to an invalid address, an error in a child grid will be returned to the host instead of being returned by the parent’s call to `cudaDeviceSynchronize()`.

######  13.6.2.1.7.1. Launch Setup APIs (CDP1) 

See [Launch Setup APIs](#launch-setup-apis), above, for CDP2 version of document.

Kernel launch is a system-level mechanism exposed through the device runtime library, and as such is available directly from PTX via the underlying `cudaGetParameterBuffer()` and `cudaLaunchDevice()` APIs. It is permitted for a CUDA application to call these APIs itself, with the same requirements as for PTX. In both cases, the user is then responsible for correctly populating all necessary data structures in the correct format according to specification. Backwards compatibility is guaranteed in these data structures.

As with host-side launch, the device-side operator `<<<>>>` maps to underlying kernel launch APIs. This is so that users targeting PTX will be able to enact a launch, and so that the compiler front-end can translate `<<<>>>` into these calls.

Table 15 New Device-only Launch Implementation Functions Runtime API Launch Functions | Description of Difference From Host Runtime Behaviour (behavior is identical if no description)  
---|---  
`cudaGetParameterBuffer` | Generated automatically from `<<<>>>`. Note different API to host equivalent.  
`cudaLaunchDevice` | Generated automatically from `<<<>>>`. Note different API to host equivalent.  
  
The APIs for these launch functions are different to those of the CUDA Runtime API, and are defined as follows:
    
    
    extern   device   cudaError_t cudaGetParameterBuffer(void **params);
    extern __device__ cudaError_t cudaLaunchDevice(void *kernel,
                                            void *params, dim3 gridDim,
                                            dim3 blockDim,
                                            unsigned int sharedMemSize = 0,
                                            cudaStream_t stream = 0);
    

#####  13.6.2.1.8. API Reference (CDP1) 

See [API Reference](#api-reference-cdp2), above, for CDP2 version of document.

The portions of the CUDA Runtime API supported in the device runtime are detailed here. Host and device runtime APIs have identical syntax; semantics are the same except where indicated. The table below provides an overview of the API relative to the version available from the host.

Table 16 Supported API Functions Runtime API Functions | Details  
---|---  
`cudaDeviceSynchronize` |  Synchronizes on work launched from thread’s own block only. Warning: Note that calling this API from device code is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.  
`cudaDeviceGetCacheConfig` |   
`cudaDeviceGetLimit` |   
`cudaGetLastError` | Last error is per-thread state, not per-block state  
`cudaPeekAtLastError` |   
`cudaGetErrorString` |   
`cudaGetDeviceCount` |   
`cudaDeviceGetAttribute` | Will return attributes for any device  
`cudaGetDevice` | Always returns current device ID as would be seen from host  
`cudaStreamCreateWithFlags` | Must pass `cudaStreamNonBlocking` flag  
`cudaStreamDestroy` |   
`cudaStreamWaitEvent` |   
`cudaEventCreateWithFlags` | Must pass `cudaEventDisableTiming` flag  
`cudaEventRecord` |   
`cudaEventDestroy` |   
`cudaFuncGetAttributes` |   
`cudaMemcpyAsync` |  Notes about all `memcpy/memset` functions:

  * Only async `memcpy/set` functions are supported
  * Only device-to-device `memcpy` is permitted
  * May not pass in local or shared memory pointers

  
`cudaMemcpy2DAsync`  
`cudaMemcpy3DAsync`  
`cudaMemsetAsync`  
`cudaMemset2DAsync` |   
`cudaMemset3DAsync` |   
`cudaRuntimeGetVersion` |   
`cudaMalloc` | May not call `cudaFree` on the device on a pointer created on the host, and vice-versa  
`cudaFree`  
`cudaOccupancyMaxActiveBlocksPerMultiprocessor` |   
`cudaOccupancyMaxPotentialBlockSize` |   
`cudaOccupancyMaxPotentialBlockSizeVariableSMem` |   
  
####  13.6.2.2. Device-side Launch from PTX (CDP1) 

See [Device-side Launch from PTX](#device-side-launch-from-ptx-cdp2), above, for CDP2 version of document.

This section is for the programming language and compiler implementers who target _Parallel Thread Execution_ (PTX) and plan to support _Dynamic Parallelism_ in their language. It provides the low-level details related to supporting kernel launches at the PTX level.

#####  13.6.2.2.1. Kernel Launch APIs (CDP1) 

See [Kernel Launch APIs](#id237), above, for CDP2 version of document.

Device-side kernel launches can be implemented using the following two APIs accessible from PTX: `cudaLaunchDevice()` and `cudaGetParameterBuffer()`. `cudaLaunchDevice()` launches the specified kernel with the parameter buffer that is obtained by calling `cudaGetParameterBuffer()` and filled with the parameters to the launched kernel. The parameter buffer can be NULL, i.e., no need to invoke `cudaGetParameterBuffer()`, if the launched kernel does not take any parameters.

######  13.6.2.2.1.1. cudaLaunchDevice (CDP1) 

See [cudaLaunchDevice](#cudalaunchdevice-cdp2), above, for CDP2 version of document.

At the PTX level, `cudaLaunchDevice()`needs to be declared in one of the two forms shown below before it is used.
    
    
    // PTX-level Declaration of cudaLaunchDevice() when .address_size is 64
    .extern .func(.param .b32 func_retval0) cudaLaunchDevice
    (
      .param .b64 func,
      .param .b64 parameterBuffer,
      .param .align 4 .b8 gridDimension[12],
      .param .align 4 .b8 blockDimension[12],
      .param .b32 sharedMemSize,
      .param .b64 stream
    )
    ;
    
    
    
    // PTX-level Declaration of cudaLaunchDevice() when .address_size is 32
    .extern .func(.param .b32 func_retval0) cudaLaunchDevice
    (
      .param .b32 func,
      .param .b32 parameterBuffer,
      .param .align 4 .b8 gridDimension[12],
      .param .align 4 .b8 blockDimension[12],
      .param .b32 sharedMemSize,
      .param .b32 stream
    )
    ;
    

The CUDA-level declaration below is mapped to one of the aforementioned PTX-level declarations and is found in the system header file `cuda_device_runtime_api.h`. The function is defined in the `cudadevrt` system library, which must be linked with a program in order to use device-side kernel launch functionality.
    
    
    // CUDA-level declaration of cudaLaunchDevice()
    extern "C" __device__
    cudaError_t cudaLaunchDevice(void *func, void *parameterBuffer,
                                 dim3 gridDimension, dim3 blockDimension,
                                 unsigned int sharedMemSize,
                                 cudaStream_t stream);
    

The first parameter is a pointer to the kernel to be is launched, and the second parameter is the parameter buffer that holds the actual parameters to the launched kernel. The layout of the parameter buffer is explained in [Parameter Buffer Layout (CDP1)](#parameter-buffer-layout-cdp1), below. Other parameters specify the launch configuration, i.e., as grid dimension, block dimension, shared memory size, and the stream associated with the launch (please refer to [Execution Configuration](#execution-configuration) for the detailed description of launch configuration.

######  13.6.2.2.1.2. cudaGetParameterBuffer (CDP1) 

See [cudaGetParameterBuffer](#cudagetparameterbuffer-cdp2), above, for CDP2 version of document.

`cudaGetParameterBuffer()` needs to be declared at the PTX level before it’s used. The PTX-level declaration must be in one of the two forms given below, depending on address size:
    
    
    // PTX-level Declaration of cudaGetParameterBuffer() when .address_size is 64
    // When .address_size is 64
    .extern .func(.param .b64 func_retval0) cudaGetParameterBuffer
    (
      .param .b64 alignment,
      .param .b64 size
    )
    ;
    
    
    
    // PTX-level Declaration of cudaGetParameterBuffer() when .address_size is 32
    .extern .func(.param .b32 func_retval0) cudaGetParameterBuffer
    (
      .param .b32 alignment,
      .param .b32 size
    )
    ;
    

The following CUDA-level declaration of `cudaGetParameterBuffer()` is mapped to the aforementioned PTX-level declaration:
    
    
    // CUDA-level Declaration of cudaGetParameterBuffer()
    extern "C" __device__
    void *cudaGetParameterBuffer(size_t alignment, size_t size);
    

The first parameter specifies the alignment requirement of the parameter buffer and the second parameter the size requirement in bytes. In the current implementation, the parameter buffer returned by `cudaGetParameterBuffer()` is always guaranteed to be 64- byte aligned, and the alignment requirement parameter is ignored. However, it is recommended to pass the correct alignment requirement value - which is the largest alignment of any parameter to be placed in the parameter buffer - to `cudaGetParameterBuffer()` to ensure portability in the future.

#####  13.6.2.2.2. Parameter Buffer Layout (CDP1) 

See [Parameter Buffer Layout](#parameter-buffer-layout), above, for CDP2 version of document.

Parameter reordering in the parameter buffer is prohibited, and each individual parameter placed in the parameter buffer is required to be aligned. That is, each parameter must be placed at the _n_ th byte in the parameter buffer, where _n_ is the smallest multiple of the parameter size that is greater than the offset of the last byte taken by the preceding parameter. The maximum size of the parameter buffer is 4KB.

For a more detailed description of PTX code generated by the CUDA compiler, please refer to the PTX-3.5 specification.

####  13.6.2.3. Toolkit Support for Dynamic Parallelism (CDP1) 

See [Toolkit Support for Dynamic Parallelism](#toolkit-support-for-dynamic-parallelism), above, for CDP2 version of document.

#####  13.6.2.3.1. Including Device Runtime API in CUDA Code (CDP1) 

See [Including Device Runtime API in CUDA Code](#including-device-runtime-api-in-cuda-code-cdp2), above, for CDP2 version of document.

Similar to the host-side runtime API, prototypes for the CUDA device runtime API are included automatically during program compilation. There is no need to include `cuda_device_runtime_api.h` explicitly.

#####  13.6.2.3.2. Compiling and Linking (CDP1) 

See [Compiling and Linking](#compiling-and-linking), above, for CDP2 version of document.

When compiling and linking CUDA programs using dynamic parallelism with `nvcc`, the program will automatically link against the static device runtime library `libcudadevrt`.

The device runtime is offered as a static library (`cudadevrt.lib` on Windows, `libcudadevrt.a` under Linux), against which a GPU application that uses the device runtime must be linked. Linking of device libraries can be accomplished through `nvcc` and/or `nvlink`. Two simple examples are shown below.

A device runtime program may be compiled and linked in a single step, if all required source files can be specified from the command line:
    
    
    $ nvcc -arch=sm_75 -rdc=true hello_world.cu -o hello -lcudadevrt
    

It is also possible to compile CUDA .cu source files first to object files, and then link these together in a two-stage process:
    
    
    $ nvcc -arch=sm_75 -dc hello_world.cu -o hello_world.o
    $ nvcc -arch=sm_75 -rdc=true hello_world.o -o hello -lcudadevrt
    

Please see the Using Separate Compilation section of The CUDA Driver Compiler NVCC guide for more details.

###  13.6.3. Programming Guidelines (CDP1) 

See [Programming Guidelines](#programming-guidelines), above, for CDP2 version of document.

####  13.6.3.1. Basics (CDP1) 

See [Basics](#basics), above, for CDP2 version of document.

The device runtime is a functional subset of the host runtime. API level device management, kernel launching, device memcpy, stream management, and event management are exposed from the device runtime.

Programming for the device runtime should be familiar to someone who already has experience with CUDA. Device runtime syntax and semantics are largely the same as that of the host API, with any exceptions detailed earlier in this document.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

The following example shows a simple _Hello World_ program incorporating dynamic parallelism:
    
    
    #include <stdio.h>
    
    __global__ void childKernel()
    {
        printf("Hello ");
    }
    
    __global__ void parentKernel()
    {
        // launch child
        childKernel<<<1,1>>>();
        if (cudaSuccess != cudaGetLastError()) {
            return;
        }
    
        // wait for child to complete
        if (cudaSuccess != cudaDeviceSynchronize()) {
            return;
        }
    
        printf("World!\n");
    }
    
    int main(int argc, char *argv[])
    {
        // launch parent
        parentKernel<<<1,1>>>();
        if (cudaSuccess != cudaGetLastError()) {
            return 1;
        }
    
        // wait for parent to complete
        if (cudaSuccess != cudaDeviceSynchronize()) {
            return 2;
        }
    
        return 0;
    }
    

This program may be built in a single step from the command line as follows:
    
    
    $ nvcc -arch=sm_75 -rdc=true hello_world.cu -o hello -lcudadevrt
    

####  13.6.3.2. Performance (CDP1) 

See [Performance](#performance), above, for CDP2 version of document.

#####  13.6.3.2.1. Synchronization (CDP1) 

See [CUDA Dynamic Parallelism](#cuda-dynamic-parallelism), above, for CDP2 version of document.

Warning

Explicit synchronization with child kernels from a parent block (such as using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

Synchronization by one thread may impact the performance of other threads in the same _Thread Block_ , even when those other threads do not call `cudaDeviceSynchronize()` themselves. This impact will depend upon the underlying implementation. In general the implicit synchronization of child kernels done when a thread block ends is more efficient compared to calling `cudaDeviceSynchronize()` explicitly. It is therefore recommended to only call `cudaDeviceSynchronize()` if it is needed to synchronize with a child kernel before a thread block ends.

#####  13.6.3.2.2. Dynamic-parallelism-enabled Kernel Overhead (CDP1) 

See [Dynamic-parallelism-enabled Kernel Overhead](#dynamic-parallelism-enabled-kernel-overhead), above, for CDP2 version of document.

System software which is active when controlling dynamic launches may impose an overhead on any kernel which is running at the time, whether or not it invokes kernel launches of its own. This overhead arises from the device runtime’s execution tracking and management software and may result in decreased performance for example, library calls when made from the device compared to from the host side. This overhead is, in general, incurred for applications that link against the device runtime library.

####  13.6.3.3. Implementation Restrictions and Limitations (CDP1) 

See [Implementation Restrictions and Limitations](#implementation-restrictions-and-limitations), above, for CDP2 version of document.

_Dynamic Parallelism_ guarantees all semantics described in this document, however, certain hardware and software resources are implementation-dependent and limit the scale, performance and other properties of a program which uses the device runtime.

#####  13.6.3.3.1. Runtime (CDP1) 

See [Runtime](#runtime), above, for CDP2 version of document.

######  13.6.3.3.1.1. Memory Footprint (CDP1) 

See [Memory Footprint](#memory-footprint), above, for CDP2 version of document.

The device runtime system software reserves memory for various management purposes, in particular one reservation which is used for saving parent-grid state during synchronization, and a second reservation for tracking pending grid launches. Configuration controls are available to reduce the size of these reservations in exchange for certain launch limitations. See [Configuration Options (CDP1)](#configuration-options-cdp1), below, for details.

The majority of reserved memory is allocated as backing-store for parent kernel state, for use when synchronizing on a child launch. Conservatively, this memory must support storing of state for the maximum number of live threads possible on the device. This means that each parent generation at which `cudaDeviceSynchronize()` is callable may require up to 860MB of device memory, depending on the device configuration, which will be unavailable for program use even if it is not all consumed.

######  13.6.3.3.1.2. Nesting and Synchronization Depth (CDP1) 

See [CUDA Dynamic Parallelism](#cuda-dynamic-parallelism), above, for CDP2 version of document.

Using the device runtime, one kernel may launch another kernel, and that kernel may launch another, and so on. Each subordinate launch is considered a new _nesting level_ , and the total number of levels is the _nesting depth_ of the program. The _synchronization depth_ is defined as the deepest level at which the program will explicitly synchronize on a child launch. Typically this is one less than the nesting depth of the program, but if the program does not need to call `cudaDeviceSynchronize()` at all levels then the synchronization depth might be substantially different to the nesting depth.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

The overall maximum nesting depth is limited to 24, but practically speaking the real limit will be the amount of memory required by the system for each new level (see [Memory Footprint (CDP1)](#memory-footprint-cdp1) above). Any launch which would result in a kernel at a deeper level than the maximum will fail. Note that this may also apply to `cudaMemcpyAsync()`, which might itself generate a kernel launch. See [Configuration Options (CDP1)](#configuration-options-cdp1) for details.

By default, sufficient storage is reserved for two levels of synchronization. This maximum synchronization depth (and hence reserved storage) may be controlled by calling `cudaDeviceSetLimit()` and specifying `cudaLimitDevRuntimeSyncDepth`. The number of levels to be supported must be configured before the top-level kernel is launched from the host, in order to guarantee successful execution of a nested program. Calling `cudaDeviceSynchronize()` at a depth greater than the specified maximum synchronization depth will return an error.

An optimization is permitted where the system detects that it need not reserve space for the parent’s state in cases where the parent kernel never calls `cudaDeviceSynchronize()`. In this case, because explicit parent/child synchronization never occurs, the memory footprint required for a program will be much less than the conservative maximum. Such a program could specify a shallower maximum synchronization depth to avoid over-allocation of backing store.

######  13.6.3.3.1.3. Pending Kernel Launches (CDP1) 

See [Pending Kernel Launches](#pending-kernel-launches), above, for CDP2 version of document.

When a kernel is launched, all associated configuration and parameter data is tracked until the kernel completes. This data is stored within a system-managed launch pool.

The launch pool is divided into a fixed-size pool and a virtualized pool with lower performance. The device runtime system software will try to track launch data in the fixed-size pool first. The virtualized pool will be used to track new launches when the fixed-size pool is full.

The size of the fixed-size launch pool is configurable by calling `cudaDeviceSetLimit()` from the host and specifying `cudaLimitDevRuntimePendingLaunchCount`.

######  13.6.3.3.1.4. Configuration Options (CDP1) 

See [Configuration Options](#configuration-options), above, for CDP2 version of document.

Resource allocation for the device runtime system software is controlled via the `cudaDeviceSetLimit()` API from the host program. Limits must be set before any kernel is launched, and may not be changed while the GPU is actively running programs.

Warning

Explicit synchronization with child kernels from a parent block (i.e. using `cudaDeviceSynchronize()` in device code) is deprecated in CUDA 11.6, removed for compute_90+ compilation, and is slated for full removal in a future CUDA release.

The following named limits may be set:

Limit | Behavior  
---|---  
`cudaLimitDevRuntimeSyncDepth` | Sets the maximum depth at which `cudaDeviceSynchronize()` may be called. Launches may be performed deeper than this, but explicit synchronization deeper than this limit will return the `cudaErrorLaunchMaxDepthExceeded`. The default maximum sync depth is 2.  
`cudaLimitDevRuntimePendingLaunchCount` | Controls the amount of memory set aside for buffering kernel launches which have not yet begun to execute, due either to unresolved dependencies or lack of execution resources. When the buffer is full, the device runtime system software will attempt to track new pending launches in a lower performance virtualized buffer. If the virtualized buffer is also full, i.e. when all available heap space is consumed, launches will not occur, and the thread’s last error will be set to `cudaErrorLaunchPendingCountExceeded`. The default pending launch count is 2048 launches.  
`cudaLimitStackSize` | Controls the stack size in bytes of each GPU thread. The CUDA driver automatically increases the per-thread stack size for each kernel launch as needed. This size isn’t reset back to the original value after each launch. To set the per-thread stack size to a different value, `cudaDeviceSetLimit()` can be called to set this limit. The stack will be immediately resized, and if necessary, the device will block until all preceding requested tasks are complete. `cudaDeviceGetLimit()` can be called to get the current per-thread stack size.  
  
######  13.6.3.3.1.5. Memory Allocation and Lifetime (CDP1) 

See [Memory Allocation and Lifetime](#memory-allocation-and-lifetime), above, for CDP2 version of document.

`cudaMalloc()` and `cudaFree()` have distinct semantics between the host and device environments. When invoked from the host, `cudaMalloc()` allocates a new region from unused device memory. When invoked from the device runtime these functions map to device-side `malloc()` and `free()`. This implies that within the device environment the total allocatable memory is limited to the device `malloc()` heap size, which may be smaller than the available unused device memory. Also, it is an error to invoke `cudaFree()` from the host program on a pointer which was allocated by `cudaMalloc()` on the device or vice-versa.

| `cudaMalloc()` on Host | `cudaMalloc()` on Device  
---|---|---  
`cudaFree()` on Host | Supported | Not Supported  
`cudaFree()` on Device | Not Supported | Supported  
Allocation limit | Free device memory | `cudaLimitMallocHeapSize`  
  
######  13.6.3.3.1.6. SM Id and Warp Id (CDP1) 

See [SM Id and Warp Id](#sm-id-and-warp-id), above, for CDP2 version of document.

Note that in PTX `%smid` and `%warpid` are defined as volatile values. The device runtime may reschedule thread blocks onto different SMs in order to more efficiently manage resources. As such, it is unsafe to rely upon `%smid` or `%warpid` remaining unchanged across the lifetime of a thread or thread block.

######  13.6.3.3.1.7. ECC Errors (CDP1) 

See [ECC Errors](#ecc-errors), above, for CDP2 version of document.

No notification of ECC errors is available to code within a CUDA kernel. ECC errors are reported at the host side once the entire launch tree has completed. Any ECC errors which arise during execution of a nested program will either generate an exception or continue execution (depending upon error and configuration).

7([1](#id232),[2](#id281),[3](#id326))
    

Dynamically created texture and surface objects are an addition to the CUDA memory model introduced with CUDA 5.0. Please see the _CUDA Programming Guide_ for details.
