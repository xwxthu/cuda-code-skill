# 10. C++ Language Extensions


Warning

This document has been replaced by a new [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide). The information in this document should be considered legacy, and this document is no longer being updated as of CUDA 13.0. Please refer to the [CUDA Programming Guide](http://docs.nvidia.com/cuda/cuda-programming-guide) for up-to-date information on CUDA.


##  10.1. Function Execution Space Specifiers 

Function execution space specifiers denote whether a function executes on the host or on the device and whether it is callable from the host or from the device.

###  10.1.1. __global__ 

The `__global__` execution space specifier declares a function as being a kernel. Such a function is:

  * Executed on the device,

  * Callable from the host,

  * Callable from the device for devices of compute capability 5.0 or higher (see [CUDA Dynamic Parallelism](#cuda-dynamic-parallelism) for more details).


A `__global__` function must have void return type, and cannot be a member of a class.

Any call to a `__global__` function must specify its execution configuration as described in [Execution Configuration](#execution-configuration).

A call to a `__global__` function is asynchronous, meaning it returns before the device has completed its execution.

###  10.1.2. __device__ 

The `__device__` execution space specifier declares a function that is:

  * Executed on the device,

  * Callable from the device only.


The `__global__` and `__device__` execution space specifiers cannot be used together.

###  10.1.3. __host__ 

The `__host__` execution space specifier declares a function that is:

  * Executed on the host,

  * Callable from the host only.


It is equivalent to declare a function with only the `__host__` execution space specifier or to declare it without any of the `__host__`, `__device__`, or `__global__` execution space specifier; in either case the function is compiled for the host only.

The `__global__` and `__host__` execution space specifiers cannot be used together.

The `__device__` and `__host__` execution space specifiers can be used together however, in which case the function is compiled for both the host and the device. The `__CUDA_ARCH__` macro introduced in [Application Compatibility](#application-compatibility) can be used to differentiate code paths between host and device:
    
    
    __host__ __device__ func()
    {
    #if __CUDA_ARCH__ >= 800
       // Device code path for compute capability 8.x
    #elif __CUDA_ARCH__ >= 700
       // Device code path for compute capability 7.x
    #elif __CUDA_ARCH__ >= 600
       // Device code path for compute capability 6.x
    #elif __CUDA_ARCH__ >= 500
       // Device code path for compute capability 5.x
    #elif !defined(__CUDA_ARCH__)
       // Host code path
    #endif
    }
    

###  10.1.4. Undefined behavior 

A ‘cross-execution space’ call has undefined behavior when:

  * `__CUDA_ARCH__` is defined, a call from within a `__global__`, `__device__` or `__host__ __device__` function to a `__host__` function.

  * `__CUDA_ARCH__` is undefined, a call from within a `__host__` function to a `__device__` function. [4](#fn11)


###  10.1.5. __noinline__ and __forceinline__ 

The compiler inlines any `__device__` function when deemed appropriate.

The `__noinline__` function qualifier can be used as a hint for the compiler not to inline the function if possible.

The `__forceinline__` function qualifier can be used to force the compiler to inline the function.

The `__noinline__` and `__forceinline__` function qualifiers cannot be used together, and neither function qualifier can be applied to an inline function.

###  10.1.6. __inline_hint__ 

The `__inline_hint__` qualifier enables more aggressive inlining in the compiler. Unlike `__forceinline__`, it does not imply that the function is inline. It can be used to improve inlining across modules when using LTO.

Neither the `__noinline__` nor the `__forceinline__` function qualifier can be used with the `__inline_hint__` function qualifier.


##  10.2. Variable Memory Space Specifiers 

Variable memory space specifiers denote the memory location on the device of a variable.

An automatic variable declared in device code without any of the `__device__`, `__shared__` and `__constant__` memory space specifiers described in this section generally resides in a register. However in some cases the compiler might choose to place it in local memory, which can have adverse performance consequences as detailed in [Device Memory Accesses](#device-memory-accesses).

###  10.2.1. __device__ 

The `__device__` memory space specifier declares a variable that resides on the device.

At most one of the other memory space specifiers defined in the next three sections may be used together with `__device__` to further denote which memory space the variable belongs to. If none of them is present, the variable:

  * Resides in global memory space,

  * Has the lifetime of the CUDA context in which it is created,

  * Has a distinct object per device,

  * Is accessible from all the threads within the grid and from the host through the runtime library `(cudaGetSymbolAddress()` / `cudaGetSymbolSize()` / `cudaMemcpyToSymbol()` / `cudaMemcpyFromSymbol()`).


###  10.2.2. __constant__ 

The `__constant__` memory space specifier, optionally used together with `__device__`, declares a variable that:

  * Resides in constant memory space,

  * Has the lifetime of the CUDA context in which it is created,

  * Has a distinct object per device,

  * Is accessible from all the threads within the grid and from the host through the runtime library (`cudaGetSymbolAddress()` / `cudaGetSymbolSize()` / `cudaMemcpyToSymbol()` / `cudaMemcpyFromSymbol()`).


The behavior of modifying a constant from the host while there is a concurrent grid that access that constant at any point of this grid’s lifetime is undefined.

###  10.2.3. __shared__ 

The `__shared__` memory space specifier, optionally used together with `__device__`, declares a variable that:

  * Resides in the shared memory space of a thread block,

  * Has the lifetime of the block,

  * Has a distinct object per block,

  * Is only accessible from all the threads within the block,

  * Does not have a constant address.


When declaring a variable in shared memory as an external array such as
    
    
    extern __shared__ float shared[];
    

the size of the array is determined at launch time (see [Execution Configuration](#execution-configuration)). All variables declared in this fashion, start at the same address in memory, so that the layout of the variables in the array must be explicitly managed through offsets. For example, if one wants the equivalent of
    
    
    short array0[128];
    float array1[64];
    int   array2[256];
    

in dynamically allocated shared memory, one could declare and initialize the arrays the following way:
    
    
    extern __shared__ float array[];
    __device__ void func()      // __device__ or __global__ function
    {
        short* array0 = (short*)array;
        float* array1 = (float*)&array0[128];
        int*   array2 =   (int*)&array1[64];
    }
    

Note that pointers need to be aligned to the type they point to, so the following code, for example, does not work since array1 is not aligned to 4 bytes.
    
    
    extern __shared__ float array[];
    __device__ void func()      // __device__ or __global__ function
    {
        short* array0 = (short*)array;
        float* array1 = (float*)&array0[127];
    }
    

Alignment requirements for the built-in vector types are listed in [Table 7](#vector-types-alignment-requirements-in-device-code).

###  10.2.4. __grid_constant__ 

The `__grid_constant__` annotation for compute architectures greater or equal to 7.0 annotates a `const`-qualified `__global__` function parameter of non-reference type that:

  * Has the lifetime of the grid,

  * Is private to the grid, i.e., the object is not accessible to host threads and threads from other grids, including sub-grids,

  * Has a distinct object per grid, i.e., all threads in the grid see the same address,

  * Is read-only, i.e., modifying a `__grid_constant__` object or any of its sub-objects is _undefined behavior_ , including `mutable` members.


Requirements:

  * Kernel parameters annotated with `__grid_constant__` must have `const`-qualified non-reference types.

  * All function declarations must match with respect to any `__grid_constant_` parameters.

  * A function template specialization must match the primary template declaration with respect to any `__grid_constant__` parameters.

  * A function template instantiation directive must match the primary template declaration with respect to any `__grid_constant__` parameters.


If the address of a `__global__` function parameter is taken, the compiler will ordinarily make a copy of the kernel parameter in thread local memory and use the address of the copy, to partially support C++ semantics, which allow each thread to modify its own local copy of function parameters. Annotating a `__global__` function parameter with `__grid_constant__` ensures that the compiler will not create a copy of the kernel parameter in thread local memory, but will instead use the generic address of the parameter itself. Avoiding the local copy may result in improved performance.
    
    
    __device__ void unknown_function(S const&);
    __global__ void kernel(const __grid_constant__ S s) {
       s.x += threadIdx.x;  // Undefined Behavior: tried to modify read-only memory
    
       // Compiler will _not_ create a per-thread thread local copy of "s":
       unknown_function(s);
    }
    

###  10.2.5. __managed__ 

The `__managed__` memory space specifier, optionally used together with `__device__`, declares a variable that:

  * Can be referenced from both device and host code, for example, its address can be taken or it can be read or written directly from a device or host function.

  * Has the lifetime of an application.


See [__managed__ Memory Space Specifier](#managed-specifier) for more details.

###  10.2.6. __restrict__ 

`nvcc` supports restricted pointers via the `__restrict__` keyword.

Restricted pointers were introduced in C99 to alleviate the aliasing problem that exists in C-type languages, and which inhibits all kind of optimization from code re-ordering to common sub-expression elimination.

Here is an example subject to the aliasing issue, where use of restricted pointer can help the compiler to reduce the number of instructions:
    
    
    void foo(const float* a,
             const float* b,
             float* c)
    {
        c[0] = a[0] * b[0];
        c[1] = a[0] * b[0];
        c[2] = a[0] * b[0] * a[1];
        c[3] = a[0] * a[1];
        c[4] = a[0] * b[0];
        c[5] = b[0];
        ...
    }
    

In C-type languages, the pointers `a`, `b`, and `c` may be aliased, so any write through `c` could modify elements of `a` or `b`. This means that to guarantee functional correctness, the compiler cannot load `a[0]` and `b[0]` into registers, multiply them, and store the result to both `c[0]` and `c[1]`, because the results would differ from the abstract execution model if, say, `a[0]` is really the same location as `c[0]`. So the compiler cannot take advantage of the common sub-expression. Likewise, the compiler cannot just reorder the computation of `c[4]` into the proximity of the computation of `c[0]` and `c[1]` because the preceding write to `c[3]` could change the inputs to the computation of `c[4]`.

By making `a`, `b`, and `c` restricted pointers, the programmer asserts to the compiler that the pointers are in fact not aliased, which in this case means writes through `c` would never overwrite elements of `a` or `b`. This changes the function prototype as follows:
    
    
    void foo(const float* __restrict__ a,
             const float* __restrict__ b,
             float* __restrict__ c);
    

Note that all pointer arguments need to be made restricted for the compiler optimizer to derive any benefit. With the `__restrict__` keywords added, the compiler can now reorder and do common sub-expression elimination at will, while retaining functionality identical with the abstract execution model:
    
    
    void foo(const float* __restrict__ a,
             const float* __restrict__ b,
             float* __restrict__ c)
    {
        float t0 = a[0];
        float t1 = b[0];
        float t2 = t0 * t1;
        float t3 = a[1];
        c[0] = t2;
        c[1] = t2;
        c[4] = t2;
        c[2] = t2 * t3;
        c[3] = t0 * t3;
        c[5] = t1;
        ...
    }
    

The effects here are a reduced number of memory accesses and reduced number of computations. This is balanced by an increase in register pressure due to “cached” loads and common sub-expressions.

Since register pressure is a critical issue in many CUDA codes, use of restricted pointers can have negative performance impact on CUDA code, due to reduced occupancy.


##  10.3. Built-in Vector Types 

###  10.3.1. char, short, int, long, longlong, float, double 

These are vector types derived from the basic integer and floating-point types. They are structures and the 1st, 2nd, 3rd, and 4th components are accessible through the fields `x`, `y`, `z`, and `w`, respectively. They all come with a constructor function of the form `make_<type name>`; for example,
    
    
    int2 make_int2(int x, int y);
    

which creates a vector of type `int2` with value`(x, y)`.

The alignment requirements of the vector types are detailed in [Table 7](#vector-types-alignment-requirements-in-device-code).

Table 7 Alignment Requirements Type | Alignment  
---|---  
char1, uchar1 | 1  
char2, uchar2 | 2  
char3, uchar3 | 1  
char4, uchar4 | 4  
short1, ushort1 | 2  
short2, ushort2 | 4  
short3, ushort3 | 2  
short4, ushort4 | 8  
int1, uint1 | 4  
int2, uint2 | 8  
int3, uint3 | 4  
int4, uint4 | 16  
long1, ulong1 | 4 if sizeof(long) is equal to sizeof(int) 8, otherwise  
long2, ulong2 | 8 if sizeof(long) is equal to sizeof(int), 16, otherwise  
long3, ulong3 | 4 if sizeof(long) is equal to sizeof(int), 8, otherwise  
long4 [3](#fn32a) | 16  
long4_16a  
long4_32a | 32  
ulong4 [3](#fn32a) | 16  
ulong4_16a  
ulong4_32a | 32  
longlong1, ulonglong1 | 8  
longlong2, ulonglong2 | 16  
longlong3, ulonglong3 | 8  
longlong4 [3](#fn32a) | 16  
longlong4_16a  
longlong4_32a | 32  
ulonglong4 [3](#fn32a) | 16  
ulonglong4_16a  
ulonglong4_32a | 32  
float1 | 4  
float2 | 8  
float3 | 4  
float4 | 16  
double1 | 8  
double2 | 16  
double3 | 8  
double4 [3](#fn32a) | 16  
double4_16a  
double4_32a | 32  
  
3([1](#id155),[2](#id156),[3](#id157),[4](#id158),[5](#id159))
    

This vector type was deprecated in CUDA Toolkit 13.0. Please use the `_16a` or `_32a` variant instead, depending on your alignment requirements.

###  10.3.2. dim3 

This type is an integer vector type based on `uint3` that is used to specify dimensions. When defining a variable of type `dim3`, any component left unspecified is initialized to 1.


##  10.4. Built-in Variables 

Built-in variables specify the grid and block dimensions and the block and thread indices. They are only valid within functions that are executed on the device.

###  10.4.1. gridDim 

This variable is of type `dim3` (see [dim3](#dim3)) and contains the dimensions of the grid.

###  10.4.2. blockIdx 

This variable is of type `uint3` (see [char, short, int, long, longlong, float, double](#vector-types)) and contains the block index within the grid.

###  10.4.3. blockDim 

This variable is of type `dim3` (see [dim3](#dim3)) and contains the dimensions of the block.

###  10.4.4. threadIdx 

This variable is of type `uint3` (see [char, short, int, long, longlong, float, double](#vector-types)) and contains the thread index within the block.

###  10.4.5. warpSize 

This variable is of type `int` and contains the warp size in threads (see [SIMT Architecture](#simt-architecture) for the definition of a warp).


##  10.5. Memory Fence Functions 

The CUDA programming model assumes a device with a weakly-ordered memory model, that is the order in which a CUDA thread writes data to shared memory, global memory, page-locked host memory, or the memory of a peer device is not necessarily the order in which the data is observed being written by another CUDA or host thread. It is undefined behavior for two threads to read from or write to the same memory location without synchronization.

In the following example, thread 1 executes `writeXY()`, while thread 2 executes `readXY()`.
    
    
    __device__ int X = 1, Y = 2;
    
    __device__ void writeXY()
    {
        X = 10;
        Y = 20;
    }
    
    __device__ void readXY()
    {
        int B = Y;
        int A = X;
    }
    

The two threads read and write from the same memory locations `X` and `Y` simultaneously. Any data-race is undefined behavior, and has no defined semantics. The resulting values for `A` and `B` can be anything.

Memory fence functions can be used to enforce a [sequentially-consistent](https://en.cppreference.com/w/cpp/atomic/memory_order) ordering on memory accesses. The memory fence functions differ in the [scope](https://nvidia.github.io/libcudacxx/extended_api/memory_model.html#thread-scopes) in which the orderings are enforced but they are independent of the accessed memory space (shared memory, global memory, page-locked host memory, and the memory of a peer device).
    
    
    void __threadfence_block();
    

is equivalent to [cuda::atomic_thread_fence(cuda::memory_order_seq_cst, cuda::thread_scope_block)](https://nvidia.github.io/libcudacxx/extended_api/synchronization_primitives/atomic/atomic_thread_fence.html) and ensures that:

  * All writes to all memory made by the calling thread before the call to `__threadfence_block()` are observed by all threads in the block of the calling thread as occurring before all writes to all memory made by the calling thread after the call to `__threadfence_block()`;

  * All reads from all memory made by the calling thread before the call to `__threadfence_block()` are ordered before all reads from all memory made by the calling thread after the call to `__threadfence_block()`.


    
    
    void __threadfence();
    

is equivalent to [cuda::atomic_thread_fence(cuda::memory_order_seq_cst, cuda::thread_scope_device)](https://nvidia.github.io/libcudacxx/extended_api/synchronization_primitives/atomic/atomic_thread_fence.html) and ensures that no writes to all memory made by the calling thread after the call to `__threadfence()` are observed by any thread in the device as occurring before any write to all memory made by the calling thread before the call to `__threadfence()`.
    
    
    void __threadfence_system();
    

is equivalent to [cuda::atomic_thread_fence(cuda::memory_order_seq_cst, cuda::thread_scope_system)](https://nvidia.github.io/libcudacxx/extended_api/synchronization_primitives/atomic/atomic_thread_fence.html) and ensures that all writes to all memory made by the calling thread before the call to `__threadfence_system()` are observed by all threads in the device, host threads, and all threads in peer devices as occurring before all writes to all memory made by the calling thread after the call to `__threadfence_system()`.

`__threadfence_system()` is only supported by devices of compute capability 2.x and higher.

In the previous code sample, we can insert fences in the codes as follows:
    
    
    __device__ int X = 1, Y = 2;
    
    __device__ void writeXY()
    {
        X = 10;
        __threadfence();
        Y = 20;
    }
    
    __device__ void readXY()
    {
        int B = Y;
        __threadfence();
        int A = X;
    }
    

For this code, the following outcomes can be observed:

  * `A` equal to 1 and `B` equal to 2,

  * `A` equal to 10 and `B` equal to 2,

  * `A` equal to 10 and `B` equal to 20.


The fourth outcome is not possible, because the first write must be visible before the second write. If thread 1 and 2 belong to the same block, it is enough to use `__threadfence_block()`. If thread 1 and 2 do not belong to the same block, `__threadfence()` must be used if they are CUDA threads from the same device and `__threadfence_system()` must be used if they are CUDA threads from two different devices.

A common use case is when threads consume some data produced by other threads as illustrated by the following code sample of a kernel that computes the sum of an array of N numbers in one call. Each block first sums a subset of the array and stores the result in global memory. When all blocks are done, the last block done reads each of these partial sums from global memory and sums them to obtain the final result. In order to determine which block is finished last, each block atomically increments a counter to signal that it is done with computing and storing its partial sum (see [Atomic Functions](#atomic-functions) about atomic functions). The last block is the one that receives the counter value equal to `gridDim.x-1`. If no fence is placed between storing the partial sum and incrementing the counter, the counter might increment before the partial sum is stored and therefore, might reach `gridDim.x-1` and let the last block start reading partial sums before they have been actually updated in memory.

Memory fence functions only affect the ordering of memory operations by a thread; they do not, by themselves, ensure that these memory operations are visible to other threads (like `__syncthreads()` does for threads within a block; see [Synchronization Functions](#synchronization-functions)). In the code sample below, the visibility of memory operations on the `result` variable is ensured by declaring it as volatile (see [Volatile Qualifier](#volatile-qualifier)).
    
    
    __device__ unsigned int count = 0;
    __shared__ bool isLastBlockDone;
    __global__ void sum(const float* array, unsigned int N,
                        volatile float* result)
    {
        // Each block sums a subset of the input array.
        float partialSum = calculatePartialSum(array, N);
    
        if (threadIdx.x == 0) {
    
            // Thread 0 of each block stores the partial sum
            // to global memory. The compiler will use
            // a store operation that bypasses the L1 cache
            // since the "result" variable is declared as
            // volatile. This ensures that the threads of
            // the last block will read the correct partial
            // sums computed by all other blocks.
            result[blockIdx.x] = partialSum;
    
            // Thread 0 makes sure that the incrementing
            // of the "count" variable is only performed after
            // the partial sum has been written to global memory.
            __threadfence();
    
            // Thread 0 signals that it is done.
            unsigned int value = atomicInc(&count, gridDim.x);
    
            // Thread 0 determines if its block is the last
            // block to be done.
            isLastBlockDone = (value == (gridDim.x - 1));
        }
    
        // Synchronize to make sure that each thread reads
        // the correct value of isLastBlockDone.
        __syncthreads();
    
        if (isLastBlockDone) {
    
            // The last block sums the partial sums
            // stored in result[0 .. gridDim.x-1]
            float totalSum = calculateTotalSum(result);
    
            if (threadIdx.x == 0) {
    
                // Thread 0 of last block stores the total sum
                // to global memory and resets the count
                // variable, so that the next kernel call
                // works properly.
                result[0] = totalSum;
                count = 0;
            }
        }
    }
    


##  10.6. Synchronization Functions 
    
    
    void __syncthreads();
    

waits until all threads in the thread block have reached this point and all global and shared memory accesses made by these threads prior to `__syncthreads()` are visible to all threads in the block.

`__syncthreads()` is used to coordinate communication between the threads of the same block. When some threads within a block access the same addresses in shared or global memory, there are potential read-after-write, write-after-read, or write-after-write hazards for some of these memory accesses. These data hazards can be avoided by synchronizing threads in-between these accesses.

`__syncthreads()` is allowed in conditional code but only if the conditional evaluates identically across the entire thread block, otherwise the code execution is likely to hang or produce unintended side effects.

Devices of compute capability 2.x and higher support three variations of `__syncthreads()` described below.
    
    
    int __syncthreads_count(int predicate);
    

is identical to `__syncthreads()` with the additional feature that it evaluates predicate for all threads of the block and returns the number of threads for which predicate evaluates to non-zero.
    
    
    int __syncthreads_and(int predicate);
    

is identical to `__syncthreads()` with the additional feature that it evaluates predicate for all threads of the block and returns non-zero if and only if predicate evaluates to non-zero for all of them.
    
    
    int __syncthreads_or(int predicate);
    

is identical to `__syncthreads()` with the additional feature that it evaluates predicate for all threads of the block and returns non-zero if and only if predicate evaluates to non-zero for any of them.
    
    
    void __syncwarp(unsigned mask=0xffffffff);
    

will cause the executing thread to wait until all warp lanes named in mask have executed a `__syncwarp()` (with the same mask) before resuming execution. Each calling thread must have its own bit set in the mask and all non-exited threads named in mask must execute a corresponding `__syncwarp()` with the same mask, or the result is undefined.

Executing `__syncwarp()` guarantees memory ordering among threads participating in the barrier. Thus, threads within a warp that wish to communicate via memory can store to memory, execute `__syncwarp()`, and then safely read values stored by other threads in the warp.

Note

For .target sm_6x or below, all threads in mask must execute the same `__syncwarp()` in convergence, and the union of all values in mask must be equal to the active mask. Otherwise, the behavior is undefined.


##  10.7. Mathematical Functions 

The reference manual lists all C/C++ standard library mathematical functions that are supported in device code and all intrinsic functions that are only supported in device code.

[Mathematical Functions](#mathematical-functions-appendix) provides accuracy information for some of these functions when relevant.


##  10.8. Texture Functions 

Texture objects are described in [Texture Object API](#texture-object-api).

Texture fetching is described in [Texture Fetching](#texture-fetching).

###  10.8.1. Texture Object API 

####  10.8.1.1. tex1Dfetch() 
    
    
    template<class T>
    T tex1Dfetch(cudaTextureObject_t texObj, int x);
    

fetches from the region of linear memory specified by the one-dimensional texture object `texObj` using integer texture coordinate `x`. `tex1Dfetch()` only works with non-normalized coordinates, so only the border and clamp addressing modes are supported. It does not perform any texture filtering. For integer types, it may optionally promote the integer to single-precision floating point.

####  10.8.1.2. tex1D() 
    
    
    template<class T>
    T tex1D(cudaTextureObject_t texObj, float x);
    

fetches from the CUDA array specified by the one-dimensional texture object `texObj` using texture coordinate `x`.

####  10.8.1.3. tex1DLod() 
    
    
    template<class T>
    T tex1DLod(cudaTextureObject_t texObj, float x, float level);
    

fetches from the CUDA array specified by the one-dimensional texture object `texObj` using texture coordinate `x` at the level-of-detail `level`.

####  10.8.1.4. tex1DGrad() 
    
    
    template<class T>
    T tex1DGrad(cudaTextureObject_t texObj, float x, float dx, float dy);
    

fetches from the CUDA array specified by the one-dimensional texture object `texObj` using texture coordinate `x`. The level-of-detail is derived from the X-gradient `dx` and Y-gradient `dy`.

####  10.8.1.5. tex2D() 
    
    
    template<class T>
    T tex2D(cudaTextureObject_t texObj, float x, float y);
    

fetches from the CUDA array or the region of linear memory specified by the two-dimensional texture object `texObj` using texture coordinate `(x,y)`.

####  10.8.1.6. tex2D() for sparse CUDA arrays 
    
    
                    template<class T>
    T tex2D(cudaTextureObject_t texObj, float x, float y, bool* isResident);
    

fetches from the CUDA array specified by the two-dimensional texture object `texObj` using texture coordinate `(x,y)`. Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.7. tex2Dgather() 
    
    
    template<class T>
    T tex2Dgather(cudaTextureObject_t texObj,
                  float x, float y, int comp = 0);
    

fetches from the CUDA array specified by the 2D texture object `texObj` using texture coordinates `x` and `y` and the `comp` parameter as described in [Texture Gather](#texture-gather).

####  10.8.1.8. tex2Dgather() for sparse CUDA arrays 
    
    
                    template<class T>
    T tex2Dgather(cudaTextureObject_t texObj,
                float x, float y, bool* isResident, int comp = 0);
    

fetches from the CUDA array specified by the 2D texture object `texObj` using texture coordinates `x` and `y` and the `comp` parameter as described in [Texture Gather](#texture-gather). Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.9. tex2DGrad() 
    
    
    template<class T>
    T tex2DGrad(cudaTextureObject_t texObj, float x, float y,
                float2 dx, float2 dy);
    

fetches from the CUDA array specified by the two-dimensional texture object `texObj` using texture coordinate `(x,y)`. The level-of-detail is derived from the `dx` and `dy` gradients.

####  10.8.1.10. tex2DGrad() for sparse CUDA arrays 
    
    
                    template<class T>
    T tex2DGrad(cudaTextureObject_t texObj, float x, float y,
            float2 dx, float2 dy, bool* isResident);
    

fetches from the CUDA array specified by the two-dimensional texture object `texObj` using texture coordinate `(x,y)`. The level-of-detail is derived from the `dx` and `dy` gradients. Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.11. tex2DLod() 
    
    
    template<class T>
    tex2DLod(cudaTextureObject_t texObj, float x, float y, float level);
    

fetches from the CUDA array or the region of linear memory specified by the two-dimensional texture object `texObj` using texture coordinate `(x,y)` at level-of-detail `level`.

####  10.8.1.12. tex2DLod() for sparse CUDA arrays 
    
    
            template<class T>
    tex2DLod(cudaTextureObject_t texObj, float x, float y, float level, bool* isResident);
    

fetches from the CUDA array specified by the two-dimensional texture object `texObj` using texture coordinate `(x,y)` at level-of-detail `level`. Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.13. tex3D() 
    
    
    template<class T>
    T tex3D(cudaTextureObject_t texObj, float x, float y, float z);
    

fetches from the CUDA array specified by the three-dimensional texture object `texObj` using texture coordinate `(x,y,z)`.

####  10.8.1.14. tex3D() for sparse CUDA arrays 
    
    
                    template<class T>
    T tex3D(cudaTextureObject_t texObj, float x, float y, float z, bool* isResident);
    

fetches from the CUDA array specified by the three-dimensional texture object `texObj` using texture coordinate `(x,y,z)`. Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.15. tex3DLod() 
    
    
    template<class T>
    T tex3DLod(cudaTextureObject_t texObj, float x, float y, float z, float level);
    

fetches from the CUDA array or the region of linear memory specified by the three-dimensional texture object `texObj` using texture coordinate `(x,y,z)` at level-of-detail `level`.

####  10.8.1.16. tex3DLod() for sparse CUDA arrays 
    
    
                    template<class T>
    T tex3DLod(cudaTextureObject_t texObj, float x, float y, float z, float level, bool* isResident);
    

fetches from the CUDA array or the region of linear memory specified by the three-dimensional texture object `texObj` using texture coordinate `(x,y,z)` at level-of-detail `level`. Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.17. tex3DGrad() 
    
    
    template<class T>
    T tex3DGrad(cudaTextureObject_t texObj, float x, float y, float z,
                float4 dx, float4 dy);
    

fetches from the CUDA array specified by the three-dimensional texture object `texObj` using texture coordinate `(x,y,z)` at a level-of-detail derived from the X and Y gradients `dx` and `dy`.

####  10.8.1.18. tex3DGrad() for sparse CUDA arrays 
    
    
                    template<class T>
    T tex3DGrad(cudaTextureObject_t texObj, float x, float y, float z,
            float4 dx, float4 dy, bool* isResident);
    

fetches from the CUDA array specified by the three-dimensional texture object `texObj` using texture coordinate `(x,y,z)` at a level-of-detail derived from the X and Y gradients `dx` and `dy`. Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.19. tex1DLayered() 
    
    
    template<class T>
    T tex1DLayered(cudaTextureObject_t texObj, float x, int layer);
    

fetches from the CUDA array specified by the one-dimensional texture object `texObj` using texture coordinate `x` and index `layer`, as described in [Layered Textures](#layered-textures).

####  10.8.1.20. tex1DLayeredLod() 
    
    
    template<class T>
    T tex1DLayeredLod(cudaTextureObject_t texObj, float x, int layer, float level);
    

fetches from the CUDA array specified by the one-dimensional [Layered Textures](#layered-textures) at layer `layer` using texture coordinate `x` and level-of-detail `level`.

####  10.8.1.21. tex1DLayeredGrad() 
    
    
    template<class T>
    T tex1DLayeredGrad(cudaTextureObject_t texObj, float x, int layer,
                       float dx, float dy);
    

fetches from the CUDA array specified by the one-dimensional [layered texture](#layered-textures) at layer `layer` using texture coordinate `x` and a level-of-detail derived from the `dx` and `dy` gradients.

####  10.8.1.22. tex2DLayered() 
    
    
    template<class T>
    T tex2DLayered(cudaTextureObject_t texObj,
                   float x, float y, int layer);
    

fetches from the CUDA array specified by the two-dimensional texture object `texObj` using texture coordinate `(x,y)` and index `layer`, as described in [Layered Textures](#layered-textures).

####  10.8.1.23. tex2DLayered() for Sparse CUDA Arrays 
    
    
                    template<class T>
    T tex2DLayered(cudaTextureObject_t texObj,
                float x, float y, int layer, bool* isResident);
    

fetches from the CUDA array specified by the two-dimensional texture object `texObj` using texture coordinate `(x,y)` and index `layer`, as described in [Layered Textures](#layered-textures). Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.24. tex2DLayeredLod() 
    
    
    template<class T>
    T tex2DLayeredLod(cudaTextureObject_t texObj, float x, float y, int layer,
                      float level);
    

fetches from the CUDA array specified by the two-dimensional [layered texture](#layered-textures) at layer `layer` using texture coordinate `(x,y)`.

####  10.8.1.25. tex2DLayeredLod() for sparse CUDA arrays 
    
    
                    template<class T>
    T tex2DLayeredLod(cudaTextureObject_t texObj, float x, float y, int layer,
                    float level, bool* isResident);
    

fetches from the CUDA array specified by the two-dimensional [layered texture](#layered-textures) at layer `layer` using texture coordinate `(x,y)`. Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.26. tex2DLayeredGrad() 
    
    
    template<class T>
    T tex2DLayeredGrad(cudaTextureObject_t texObj, float x, float y, int layer,
                       float2 dx, float2 dy);
    

fetches from the CUDA array specified by the two-dimensional [layered texture](#layered-textures) at layer `layer` using texture coordinate `(x,y)` and a level-of-detail derived from the `dx` and `dy` gradients.

####  10.8.1.27. tex2DLayeredGrad() for sparse CUDA arrays 
    
    
                    template<class T>
    T tex2DLayeredGrad(cudaTextureObject_t texObj, float x, float y, int layer,
                    float2 dx, float2 dy, bool* isResident);
    

fetches from the CUDA array specified by the two-dimensional [layered texture](#layered-textures) at layer `layer` using texture coordinate `(x,y)` and a level-of-detail derived from the `dx` and `dy` gradients. Also returns whether the texel is resident in memory via `isResident` pointer. If not, the values fetched will be zeros.

####  10.8.1.28. texCubemap() 
    
    
    template<class T>
    T texCubemap(cudaTextureObject_t texObj, float x, float y, float z);
    

fetches the CUDA array specified by the cubemap texture object `texObj` using texture coordinate `(x,y,z)`, as described in [Cubemap Textures](#cubemap-textures).

####  10.8.1.29. texCubemapGrad() 
    
    
    template<class T>
    T texCubemapGrad(cudaTextureObject_t texObj, float x, float, y, float z,
                    float4 dx, float4 dy);
    

fetches from the CUDA array specified by the cubemap texture object `texObj` using texture coordinate `(x,y,z)` as described in [Cubemap Textures](#cubemap-textures). The level-of-detail used is derived from the `dx` and `dy` gradients.

####  10.8.1.30. texCubemapLod() 
    
    
    template<class T>
    T texCubemapLod(cudaTextureObject_t texObj, float x, float, y, float z,
                    float level);
    

fetches from the CUDA array specified by the cubemap texture object `texObj` using texture coordinate `(x,y,z)` as described in [Cubemap Textures](#cubemap-textures). The level-of-detail used is given by `level`.

####  10.8.1.31. texCubemapLayered() 
    
    
    template<class T>
    T texCubemapLayered(cudaTextureObject_t texObj,
                        float x, float y, float z, int layer);
    

fetches from the CUDA array specified by the cubemap layered texture object `texObj` using texture coordinates `(x,y,z)`, and index `layer`, as described in [Cubemap Layered Textures](#cubemap-layered-textures).

####  10.8.1.32. texCubemapLayeredGrad() 
    
    
    template<class T>
    T texCubemapLayeredGrad(cudaTextureObject_t texObj, float x, float y, float z,
                           int layer, float4 dx, float4 dy);
    

fetches from the CUDA array specified by the cubemap layered texture object `texObj` using texture coordinate `(x,y,z)` and index `layer`, as described in [Cubemap Layered Textures](#cubemap-layered-textures), at level-of-detail derived from the `dx` and `dy` gradients.

####  10.8.1.33. texCubemapLayeredLod() 
    
    
    template<class T>
    T texCubemapLayeredLod(cudaTextureObject_t texObj, float x, float y, float z,
                           int layer, float level);
    

fetches from the CUDA array specified by the cubemap layered texture object `texObj` using texture coordinate `(x,y,z)` and index `layer`, as described in [Cubemap Layered Textures](#cubemap-layered-textures), at level-of-detail level `level`.


##  10.9. Surface Functions 

Surface functions are only supported by devices of compute capability 2.0 and higher.

Surface objects are described in described in [Surface Object API](#surface-object-api-appendix).

In the sections below, `boundaryMode` specifies the boundary mode, that is how out-of-range surface coordinates are handled; it is equal to either `cudaBoundaryModeClamp`, in which case out-of-range coordinates are clamped to the valid range, or `cudaBoundaryModeZero`, in which case out-of-range reads return zero and out-of-range writes are ignored, or `cudaBoundaryModeTrap`, in which case out-of-range accesses cause the kernel execution to fail.

###  10.9.1. Surface Object API 

####  10.9.1.1. surf1Dread() 
    
    
    template<class T>
    T surf1Dread(cudaSurfaceObject_t surfObj, int x,
                   boundaryMode = cudaBoundaryModeTrap);
    

reads the CUDA array specified by the one-dimensional surface object `surfObj` using byte coordinate x.

####  10.9.1.2. surf1Dwrite 
    
    
    template<class T>
    void surf1Dwrite(T data,
                      cudaSurfaceObject_t surfObj,
                      int x,
                      boundaryMode = cudaBoundaryModeTrap);
    

writes value data to the CUDA array specified by the one-dimensional surface object `surfObj` at byte coordinate x.

####  10.9.1.3. surf2Dread() 
    
    
    template<class T>
    T surf2Dread(cudaSurfaceObject_t surfObj,
                  int x, int y,
                  boundaryMode = cudaBoundaryModeTrap);
    template<class T>
    void surf2Dread(T* data,
                     cudaSurfaceObject_t surfObj,
                     int x, int y,
                     boundaryMode = cudaBoundaryModeTrap);
    

reads the CUDA array specified by the two-dimensional surface object `surfObj` using byte coordinates x and y.

####  10.9.1.4. surf2Dwrite() 
    
    
    template<class T>
    void surf2Dwrite(T data,
                      cudaSurfaceObject_t surfObj,
                      int x, int y,
                      boundaryMode = cudaBoundaryModeTrap);
    

writes value data to the CUDA array specified by the two-dimensional surface object `surfObj` at byte coordinate x and y.

####  10.9.1.5. surf3Dread() 
    
    
    template<class T>
    T surf3Dread(cudaSurfaceObject_t surfObj,
                  int x, int y, int z,
                  boundaryMode = cudaBoundaryModeTrap);
    template<class T>
    void surf3Dread(T* data,
                     cudaSurfaceObject_t surfObj,
                     int x, int y, int z,
                     boundaryMode = cudaBoundaryModeTrap);
    

reads the CUDA array specified by the three-dimensional surface object `surfObj` using byte coordinates x, y, and z.

####  10.9.1.6. surf3Dwrite() 
    
    
    template<class T>
    void surf3Dwrite(T data,
                      cudaSurfaceObject_t surfObj,
                      int x, int y, int z,
                      boundaryMode = cudaBoundaryModeTrap);
    

writes value data to the CUDA array specified by the three-dimensional object `surfObj` at byte coordinate x, y, and z.

####  10.9.1.7. surf1DLayeredread() 
    
    
    template<class T>
    T surf1DLayeredread(
                     cudaSurfaceObject_t surfObj,
                     int x, int layer,
                     boundaryMode = cudaBoundaryModeTrap);
    template<class T>
    void surf1DLayeredread(T data,
                     cudaSurfaceObject_t surfObj,
                     int x, int layer,
                     boundaryMode = cudaBoundaryModeTrap);
    

reads the CUDA array specified by the one-dimensional layered surface object `surfObj` using byte coordinate x and index `layer`.

####  10.9.1.8. surf1DLayeredwrite() 
    
    
    template<class Type>
    void surf1DLayeredwrite(T data,
                     cudaSurfaceObject_t surfObj,
                     int x, int layer,
                     boundaryMode = cudaBoundaryModeTrap);
    

writes value data to the CUDA array specified by the two-dimensional layered surface object `surfObj` at byte coordinate x and index `layer`.

####  10.9.1.9. surf2DLayeredread() 
    
    
    template<class T>
    T surf2DLayeredread(
                     cudaSurfaceObject_t surfObj,
                     int x, int y, int layer,
                     boundaryMode = cudaBoundaryModeTrap);
    template<class T>
    void surf2DLayeredread(T data,
                             cudaSurfaceObject_t surfObj,
                             int x, int y, int layer,
                             boundaryMode = cudaBoundaryModeTrap);
    

reads the CUDA array specified by the two-dimensional layered surface object `surfObj` using byte coordinate x and y, and index `layer`.

####  10.9.1.10. surf2DLayeredwrite() 
    
    
    template<class T>
    void surf2DLayeredwrite(T data,
                              cudaSurfaceObject_t surfObj,
                              int x, int y, int layer,
                              boundaryMode = cudaBoundaryModeTrap);
    

writes value data to the CUDA array specified by the one-dimensional layered surface object `surfObj` at byte coordinate x and y, and index `layer`.

####  10.9.1.11. surfCubemapread() 
    
    
    template<class T>
    T surfCubemapread(
                     cudaSurfaceObject_t surfObj,
                     int x, int y, int face,
                     boundaryMode = cudaBoundaryModeTrap);
    template<class T>
    void surfCubemapread(T data,
                     cudaSurfaceObject_t surfObj,
                     int x, int y, int face,
                     boundaryMode = cudaBoundaryModeTrap);
    

reads the CUDA array specified by the cubemap surface object `surfObj` using byte coordinate x and y, and face index face.

####  10.9.1.12. surfCubemapwrite() 
    
    
    template<class T>
    void surfCubemapwrite(T data,
                     cudaSurfaceObject_t surfObj,
                     int x, int y, int face,
                     boundaryMode = cudaBoundaryModeTrap);
    

writes value data to the CUDA array specified by the cubemap object `surfObj` at byte coordinate x and y, and face index face.

####  10.9.1.13. surfCubemapLayeredread() 
    
    
    template<class T>
    T surfCubemapLayeredread(
                 cudaSurfaceObject_t surfObj,
                 int x, int y, int layerFace,
                 boundaryMode = cudaBoundaryModeTrap);
    template<class T>
    void surfCubemapLayeredread(T data,
                 cudaSurfaceObject_t surfObj,
                 int x, int y, int layerFace,
                 boundaryMode = cudaBoundaryModeTrap);
    

reads the CUDA array specified by the cubemap layered surface object `surfObj` using byte coordinate x and y, and index `layerFace.`

####  10.9.1.14. surfCubemapLayeredwrite() 
    
    
    template<class T>
    void surfCubemapLayeredwrite(T data,
                 cudaSurfaceObject_t surfObj,
                 int x, int y, int layerFace,
                 boundaryMode = cudaBoundaryModeTrap);
    

writes value data to the CUDA array specified by the cubemap layered object `surfObj` at byte coordinate `x` and `y`, and index `layerFace`.


##  10.10. Read-Only Data Cache Load Function 

The read-only data cache load function is only supported by devices of compute capability 5.0 and higher.
    
    
    T __ldg(const T* address);
    

returns the data of type `T` located at address `address`, where `T` is `char`, `signed char`, `short`, `int`, `long`, `long long``unsigned char`, `unsigned short`, `unsigned int`, `unsigned long`, `unsigned long long`, `char2`, `char4`, `short2`, `short4`, `int2`, `int4`, `longlong2``uchar2`, `uchar4`, `ushort2`, `ushort4`, `uint2`, `uint4`, `ulonglong2``float`, `float2`, `float4`, `double`, or `double2`. With the `cuda_fp16.h` header included, `T` can be `__half` or `__half2`. Similarly, with the `cuda_bf16.h` header included, `T` can also be `__nv_bfloat16` or `__nv_bfloat162`. The operation is cached in the read-only data cache (see [Global Memory](#global-memory-5-x)).


##  10.11. Load Functions Using Cache Hints 

These load functions are only supported by devices of compute capability 5.0 and higher.
    
    
    T __ldcg(const T* address);
    T __ldca(const T* address);
    T __ldcs(const T* address);
    T __ldlu(const T* address);
    T __ldcv(const T* address);
    

returns the data of type `T` located at address `address`, where `T` is `char`, `signed char`, `short`, `int`, `long`, `long long``unsigned char`, `unsigned short`, `unsigned int`, `unsigned long`, `unsigned long long`, `char2`, `char4`, `short2`, `short4`, `int2`, `int4`, `longlong2``uchar2`, `uchar4`, `ushort2`, `ushort4`, `uint2`, `uint4`, `ulonglong2``float`, `float2`, `float4`, `double`, or `double2`. With the `cuda_fp16.h` header included, `T` can be `__half` or `__half2`. Similarly, with the `cuda_bf16.h` header included, `T` can also be `__nv_bfloat16` or `__nv_bfloat162`. The operation is using the corresponding cache operator (see [PTX ISA](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#cache-operators))


##  10.12. Store Functions Using Cache Hints 

These store functions are only supported by devices of compute capability 5.0 and higher.
    
    
    void __stwb(T* address, T value);
    void __stcg(T* address, T value);
    void __stcs(T* address, T value);
    void __stwt(T* address, T value);
    

stores the `value` argument of type `T` to the location at address `address`, where `T` is `char`, `signed char`, `short`, `int`, `long`, `long long``unsigned char`, `unsigned short`, `unsigned int`, `unsigned long`, `unsigned long long`, `char2`, `char4`, `short2`, `short4`, `int2`, `int4`, `longlong2``uchar2`, `uchar4`, `ushort2`, `ushort4`, `uint2`, `uint4`, `ulonglong2``float`, `float2`, `float4`, `double`, or `double2`. With the `cuda_fp16.h` header included, `T` can be `__half` or `__half2`. Similarly, with the `cuda_bf16.h` header included, `T` can also be `__nv_bfloat16` or `__nv_bfloat162`. The operation is using the corresponding cache operator (see [PTX ISA](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#cache-operators) )


##  10.13. Time Function 
    
    
    clock_t clock();
    long long int clock64();
    

when executed in device code, returns the value of a per-multiprocessor counter that is incremented every clock cycle. Sampling this counter at the beginning and at the end of a kernel, taking the difference of the two samples, and recording the result per thread provides a measure for each thread of the number of clock cycles taken by the device to completely execute the thread, but not of the number of clock cycles the device actually spent executing thread instructions. The former number is greater than the latter since threads are time sliced.


##  10.14. Atomic Functions 

An atomic function performs a read-modify-write atomic operation on one 32-bit, 64-bit, or 128-bit word residing in global or shared memory. In the case of `float2` or `float4`, the read-modify-write operation is performed on each element of the vector residing in global memory. For example, `atomicAdd()` reads a word at some address in global or shared memory, adds a number to it, and writes the result back to the same address. Atomic functions can only be used in device functions.

The atomic functions described in this section have ordering [cuda::memory_order_relaxed](https://en.cppreference.com/w/cpp/atomic/memory_order) and are only atomic at a particular [scope](https://nvidia.github.io/libcudacxx/extended_api/memory_model.html#thread-scopes):

  * Atomic APIs with `_system` suffix (example: `atomicAdd_system`) are atomic at scope `cuda::thread_scope_system` if they meet particular [conditions](https://nvidia.github.io/libcudacxx/extended_api/memory_model.html#atomicity).

  * Atomic APIs without a suffix (example: `atomicAdd`) are atomic at scope `cuda::thread_scope_device`.

  * Atomic APIs with `_block` suffix (example: `atomicAdd_block`) are atomic at scope `cuda::thread_scope_block`.


In the following example both the CPU and the GPU atomically update an integer value at address `addr`:
    
    
    __global__ void mykernel(int *addr) {
      atomicAdd_system(addr, 10);       // only available on devices with compute capability 6.x
    }
    
    void foo() {
      int *addr;
      cudaMallocManaged(&addr, 4);
      *addr = 0;
    
       mykernel<<<...>>>(addr);
       __sync_fetch_and_add(addr, 10);  // CPU atomic operation
    }
    

Note that any atomic operation can be implemented based on `atomicCAS()` (Compare And Swap). For example, `atomicAdd()` for double-precision floating-point numbers is not available on devices with compute capability lower than 6.0 but it can be implemented as follows:
    
    
    #if __CUDA_ARCH__ < 600
    __device__ double atomicAdd(double* address, double val)
    {
        unsigned long long int* address_as_ull =
                                  (unsigned long long int*)address;
        unsigned long long int old = *address_as_ull, assumed;
    
        do {
            assumed = old;
            old = atomicCAS(address_as_ull, assumed,
                            __double_as_longlong(val +
                                   __longlong_as_double(assumed)));
    
        // Note: uses integer comparison to avoid hang in case of NaN (since NaN != NaN)
        } while (assumed != old);
    
        return __longlong_as_double(old);
    }
    #endif
    

There are system-wide and block-wide variants of the following device-wide atomic APIs, with the following exceptions:

  * Devices with compute capability less than 6.0 only support device-wide atomic operations,

  * Tegra devices with compute capability less than 7.2 do not support system-wide atomic operations.


CUDA 12.8 and later support CUDA compiler builtin functions for atomic operations with memory order and thread scope. We follows the [GNU’s atomic built-in function signature](https://gcc.gnu.org/onlinedocs/gcc/_005f_005fatomic-Builtins.html) with an extra argument of thread scope. We use the following atomic operation memory orders and thread scopes:
    
    
    enum {
       __NV_ATOMIC_RELAXED,
       __NV_ATOMIC_CONSUME,
       __NV_ATOMIC_ACQUIRE,
       __NV_ATOMIC_RELEASE,
       __NV_ATOMIC_ACQ_REL,
       __NV_ATOMIC_SEQ_CST
    };
    
    enum {
       __NV_THREAD_SCOPE_THREAD,
       __NV_THREAD_SCOPE_BLOCK,
       __NV_THREAD_SCOPE_CLUSTER,
       __NV_THREAD_SCOPE_DEVICE,
       __NV_THREAD_SCOPE_SYSTEM
    };
    

Example:
    
    
    __device__ T __nv_atomic_load_n(T* ptr, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

T can be any integral type that is size of 1, 2, 4, 8 and 16 bytes.

These atomic functions cannot operate on local memory. For example:
    
    
    __device__ void foo() {
       int a = 1; // defined in local memory
       int b;
       __nv_atomic_load(&a, &b, __NV_ATOMIC_RELAXED, __NV_THREAD_SCOPE_SYSTEM);
    }
    

These functions must only be used within the block scope of a `__device__` function. For example:
    
    
    __device__ void foo() {
       __shared__ unsigned int u1 = 1;
       __shared__ unsigned int u2 = 2;
       __nv_atomic_load(&u1, &u2, __NV_ATOMIC_RELAXED, __NV_THREAD_SCOPE_SYSTEM);
    }
    

And these functions’ address cannot be taken. Here are three unsupported examples:
    
    
    // Not permitted to be used in a host function
    __host__ void bar() {
       __shared__ unsigned int u1 = 1;
       __shared__ unsigned int u2 = 2;
       __nv_atomic_load(&u1, &u2, __NV_ATOMIC_RELAXED, __NV_THREAD_SCOPE_SYSTEM);
    }
    
    // Not permitted to be used as a template default argument.
    // The function address cannot be taken.
    template<void *F = __nv_atomic_load_n>
    class X {
       void *f = F;
    };
    
    // Not permitted to be called in a constructor initialization list.
    class Y {
       int a;
    public:
       __device__ Y(int *b): a(__nv_atomic_load_n(b, __NV_ATOMIC_RELAXED)) {}
    };
    

The memory order corresponds to [C++ standard atomic operation’s memory order](https://en.cppreference.com/w/cpp/atomic/memory_order). And for thread scope, we follows cuda::thread_scope’s [definition](https://nvidia.github.io/cccl/libcudacxx/extended_api/memory_model.html#thread-scopes).

`__NV_ATOMIC_CONSUME` memory order is currently implemented using stronger `__NV_ATOMIC_ACQUIRE` memory order.

`__NV_THREAD_SCOPE_THREAD` thread scope is currently implemented using wider `__NV_THREAD_SCOPE_BLOCK` thread scope.

For the supported data types, please refer to the corresponding section of different atomic operations.

###  10.14.1. Arithmetic Functions 

####  10.14.1.1. atomicAdd() 
    
    
    int atomicAdd(int* address, int val);
    unsigned int atomicAdd(unsigned int* address,
                           unsigned int val);
    unsigned long long int atomicAdd(unsigned long long int* address,
                                     unsigned long long int val);
    float atomicAdd(float* address, float val);
    double atomicAdd(double* address, double val);
    __half2 atomicAdd(__half2 *address, __half2 val);
    __half atomicAdd(__half *address, __half val);
    __nv_bfloat162 atomicAdd(__nv_bfloat162 *address, __nv_bfloat162 val);
    __nv_bfloat16 atomicAdd(__nv_bfloat16 *address, __nv_bfloat16 val);
    float2 atomicAdd(float2* address, float2 val);
    float4 atomicAdd(float4* address, float4 val);
    

reads the 16-bit, 32-bit or 64-bit `old` located at the address `address` in global or shared memory, computes `(old + val)`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

The 32-bit floating-point version of `atomicAdd()` is only supported by devices of compute capability 2.x and higher.

The 64-bit floating-point version of `atomicAdd()` is only supported by devices of compute capability 6.x and higher.

The 32-bit `__half2` floating-point version of `atomicAdd()` is only supported by devices of compute capability 6.x and higher. The atomicity of the `__half2` or `__nv_bfloat162` add operation is guaranteed separately for each of the two `__half` or `__nv_bfloat16` elements; the entire `__half2` or `__nv_bfloat162` is not guaranteed to be atomic as a single 32-bit access.

The `float2` and `float4` floating-point vector versions of `atomicAdd()` are only supported by devices of compute capability 9.x and higher. The atomicity of the `float2` or `float4` add operation is guaranteed separately for each of the two or four `float` elements; the entire `float2` or `float4` is not guaranteed to be atomic as a single 64-bit or 128-bit access.

The 16-bit `__half` floating-point version of `atomicAdd()` is only supported by devices of compute capability 7.x and higher.

The 16-bit `__nv_bfloat16` floating-point version of `atomicAdd()` is only supported by devices of compute capability 8.x and higher.

The `float2` and `float4` floating-point vector versions of `atomicAdd()` are only supported by devices of compute capability 9.x and higher.

The `float2` and `float4` floating-point vector versions of `atomicAdd()` are only supported for global memory addresses.

####  10.14.1.2. atomicSub() 
    
    
    int atomicSub(int* address, int val);
    unsigned int atomicSub(unsigned int* address,
                           unsigned int val);
    

reads the 32-bit word `old` located at the address `address` in global or shared memory, computes `(old - val)`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

####  10.14.1.3. atomicExch() 
    
    
    int atomicExch(int* address, int val);
    unsigned int atomicExch(unsigned int* address,
                            unsigned int val);
    unsigned long long int atomicExch(unsigned long long int* address,
                                      unsigned long long int val);
    float atomicExch(float* address, float val);
    

reads the 32-bit or 64-bit word `old` located at the address `address` in global or shared memory and stores `val` back to memory at the same address. These two operations are performed in one atomic transaction. The function returns `old`.
    
    
    template<typename T> T atomicExch(T* address, T val);
    

reads the 128-bit word `old` located at the address `address` in global or shared memory and stores `val` back to memory at the same address. These two operations are performed in one atomic transaction. The function returns `old`. The type `T` must meet the following requirements:
    
    
    sizeof(T) == 16
    alignof(T) >= 16
    std::is_trivially_copyable<T>::value == true
    // for C++03 and older
    std::is_default_constructible<T>::value == true
    

So, `T` must be 128-bit and properly aligned, be trivially copyable, and on C++03 or older, it must also be default constructible.

The 128-bit `atomicExch()` is only supported by devices of compute capability 9.x and higher.

####  10.14.1.4. atomicMin() 
    
    
    int atomicMin(int* address, int val);
    unsigned int atomicMin(unsigned int* address,
                           unsigned int val);
    unsigned long long int atomicMin(unsigned long long int* address,
                                     unsigned long long int val);
    long long int atomicMin(long long int* address,
                                    long long int val);
    

reads the 32-bit or 64-bit word `old` located at the address `address` in global or shared memory, computes the minimum of `old` and `val`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

The 64-bit version of `atomicMin()` is only supported by devices of compute capability 5.0 and higher.

####  10.14.1.5. atomicMax() 
    
    
    int atomicMax(int* address, int val);
    unsigned int atomicMax(unsigned int* address,
                           unsigned int val);
    unsigned long long int atomicMax(unsigned long long int* address,
                                     unsigned long long int val);
    long long int atomicMax(long long int* address,
                                     long long int val);
    

reads the 32-bit or 64-bit word `old` located at the address `address` in global or shared memory, computes the maximum of `old` and `val`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

The 64-bit version of `atomicMax()` is only supported by devices of compute capability 5.0 and higher.

####  10.14.1.6. atomicInc() 
    
    
    unsigned int atomicInc(unsigned int* address,
                           unsigned int val);
    

reads the 32-bit word `old` located at the address `address` in global or shared memory, computes `((old >= val) ? 0 : (old+1))`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

####  10.14.1.7. atomicDec() 
    
    
    unsigned int atomicDec(unsigned int* address,
                           unsigned int val);
    

reads the 32-bit word `old` located at the address `address` in global or shared memory, computes `(((old == 0) || (old > val)) ? val : (old-1)` ), and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

####  10.14.1.8. atomicCAS() 
    
    
    int atomicCAS(int* address, int compare, int val);
    unsigned int atomicCAS(unsigned int* address,
                           unsigned int compare,
                           unsigned int val);
    unsigned long long int atomicCAS(unsigned long long int* address,
                                     unsigned long long int compare,
                                     unsigned long long int val);
    unsigned short int atomicCAS(unsigned short int *address,
                                 unsigned short int compare,
                                 unsigned short int val);
    

reads the 16-bit, 32-bit or 64-bit word `old` located at the address `address` in global or shared memory, computes `(old == compare ? val : old)`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old` (Compare And Swap).
    
    
    template<typename T> T atomicCAS(T* address, T compare, T val);
    

reads the 128-bit word `old` located at the address `address` in global or shared memory, computes `(old == compare ? val : old)`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old` (Compare And Swap). The type `T` must meet the following requirements:
    
    
    sizeof(T) == 16
    alignof(T) >= 16
    std::is_trivially_copyable<T>::value == true
    // for C++03 and older
    std::is_default_constructible<T>::value == true
    

So, `T` must be 128-bit and properly aligned, be trivially copyable, and on C++03 or older, it must also be default constructible.

The 128-bit `atomicCAS()` is only supported by devices of compute capability 9.x and higher.

####  10.14.1.9. __nv_atomic_exchange() 
    
    
    __device__ void __nv_atomic_exchange(T* ptr, T* val, T *ret, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function is introduced in CUDA 12.8. It reads the value where `ptr` points to and stores the value to where `ret` points to. And it reads the value where `val` points to and stores the value to where `ptr` points to.

This is a generic atomic exchange, which means that `T` can be any data type that is size of 4, 8 or 16 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

16-byte data type is supported on the architecture `sm_90` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.1.10. __nv_atomic_exchange_n() 
    
    
    __device__ T __nv_atomic_exchange_n(T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function is introduced in CUDA 12.8. It reads the value where `ptr` points to and use this value as the return value. And it stores `val` to where `ptr` points to.

This is a non-generic atomic exchange, which means that `T` can only be an integral type that is size of 4, 8 or 16 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

16-byte data type is supported on the architecture `sm_90` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.1.11. __nv_atomic_compare_exchange() 
    
    
    __device__ bool __nv_atomic_compare_exchange (T* ptr, T* expected, T* desired, bool weak, int success_order, int failure_order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function is introduced in CUDA 12.8. It reads the value where `ptr` points to and compare it with the value where `expected` points to. If they are equal, the return value is `true` and the value where `desired` points to is stored to where `ptr` points to. Otherwise, it returns `false` and the value where `ptr` points to is stored to where `expected` points to. The parameter `weak` is ignored and it picks the stronger memory order between `success_order` and `failure_order` to execute the compare-and-exchange operation.

This is a generic atomic compare-and-exchange, which means that `T` can be any data type that is size of 2, 4, 8 or 16 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

16-byte data type is supported on the architecture `sm_90` and higher.

2-byte data type is supported on the architecture `sm_70` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.1.12. __nv_atomic_compare_exchange_n() 
    
    
    __device__ bool __nv_atomic_compare_exchange_n (T* ptr, T* expected, T desired, bool weak, int success_order, int failure_order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function is introduced in CUDA 12.8. It reads the value where `ptr` points to and compare it with the value where `expected` points to. If they are equal, the return value is `true` and `desired` is stored to where `ptr` points to. Otherwise, it returns `false` and the value where `ptr` points to is stored to where `expected` points to. The parameter `weak` is ignored and it picks the stronger memory order between `success_order` and `failure_order` to execute the compare-and-exchange operation.

This is a non-generic atomic compare-and-exchange, which means that `T` can only be an integral type that is size of 2, 4, 8 or 16 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

16-byte data type is supported on the architecture `sm_90` and higher.

2-byte data type is supported on the architecture `sm_70` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.1.13. __nv_atomic_fetch_add() and __nv_atomic_add() 
    
    
    __device__ T __nv_atomic_fetch_add (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    __device__ void __nv_atomic_add (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

These two atomic functions are introduced in CUDA 12.8. It reads the value where `ptr` points to, adds with `val`, and stores the result back to where `ptr` points to. `__nv_atomic_fetch_add` returns the old value where `ptr` points to. `__nv_atomic_add` does not have return value.

`T` can only be `unsigned int`, `int`, `unsigned long long`, `float` or `double`.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.1.14. __nv_atomic_fetch_sub() and __nv_atomic_sub() 
    
    
    __device__ T __nv_atomic_fetch_sub (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    __device__ void __nv_atomic_sub (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

These two atomic functions are introduced in CUDA 12.8. It reads the value where `ptr` points to, subtracts with `val`, and stores the result back to where `ptr` points to. `__nv_atomic_fetch_sub` returns the old value where `ptr` points to. `__nv_atomic_sub` does not have return value.

`T` can only be `unsigned int`, `int`, `unsigned long long`, `float` or `double`.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.1.15. __nv_atomic_fetch_min() and __nv_atomic_min() 
    
    
    __device__ T __nv_atomic_fetch_min (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    __device__ void __nv_atomic_min (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

These two atomic functions are introduced in CUDA 12.8. It reads the value where `ptr` points to, compares with `val`, and stores the smaller value back to where `ptr` points to. `__nv_atomic_fetch_min` returns the old value where `ptr` points to. `__nv_atomic_min` does not have return value.

`T` can only be `unsigned int`, `int`, `unsigned long long` or `long long`.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.1.16. __nv_atomic_fetch_max() and __nv_atomic_max() 
    
    
    __device__ T __nv_atomic_fetch_max (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    __device__ void __nv_atomic_max (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

These two atomic functions are introduced in CUDA 12.8. It reads the value where `ptr` points to, compares with `val`, and stores the bigger value back to where `ptr` points to. `__nv_atomic_fetch_max` returns the old value where `ptr` points to. `__nv_atomic_max` does not have return value.

`T` can only be `unsigned int`, `int`, `unsigned long long` or `long long`.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

###  10.14.2. Bitwise Functions 

####  10.14.2.1. atomicAnd() 
    
    
    int atomicAnd(int* address, int val);
    unsigned int atomicAnd(unsigned int* address,
                           unsigned int val);
    unsigned long long int atomicAnd(unsigned long long int* address,
                                     unsigned long long int val);
    

reads the 32-bit or 64-bit word `old` located at the address `address` in global or shared memory, computes `(old & val`), and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

The 64-bit version of `atomicAnd()` is only supported by devices of compute capability 5.0 and higher.

####  10.14.2.2. atomicOr() 
    
    
    int atomicOr(int* address, int val);
    unsigned int atomicOr(unsigned int* address,
                          unsigned int val);
    unsigned long long int atomicOr(unsigned long long int* address,
                                    unsigned long long int val);
    

reads the 32-bit or 64-bit word `old` located at the address `address` in global or shared memory, computes `(old | val)`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

The 64-bit version of `atomicOr()` is only supported by devices of compute capability 5.0 and higher.

####  10.14.2.3. atomicXor() 
    
    
    int atomicXor(int* address, int val);
    unsigned int atomicXor(unsigned int* address,
                           unsigned int val);
    unsigned long long int atomicXor(unsigned long long int* address,
                                     unsigned long long int val);
    

reads the 32-bit or 64-bit word `old` located at the address `address` in global or shared memory, computes `(old ^ val)`, and stores the result back to memory at the same address. These three operations are performed in one atomic transaction. The function returns `old`.

The 64-bit version of `atomicXor()` is only supported by devices of compute capability 5.0 and higher.

####  10.14.2.4. __nv_atomic_fetch_or() and __nv_atomic_or() 
    
    
    __device__ T __nv_atomic_fetch_or (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    __device__ void __nv_atomic_or (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

These two atomic functions are introduced in CUDA 12.8. It reads the value where `ptr` points to, `or` with `val`, and stores the result back to where `ptr` points to. `__nv_atomic_fetch_or` returns the old value where `ptr` points to. `__nv_atomic_or` does not have return value.

`T` can only be an integral type that is size of 4 or 8 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.2.5. __nv_atomic_fetch_xor() and __nv_atomic_xor() 
    
    
    __device__ T __nv_atomic_fetch_xor (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    __device__ void __nv_atomic_xor (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

These two atomic functions are introduced in CUDA 12.8. It reads the value where `ptr` points to, `xor` with `val`, and stores the result back to where `ptr` points to. `__nv_atomic_fetch_xor` returns the old value where `ptr` points to. `__nv_atomic_xor` does not have return value.

`T` can only be an integral type that is size of 4 or 8 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

####  10.14.2.6. __nv_atomic_fetch_and() and __nv_atomic_and() 
    
    
    __device__ T __nv_atomic_fetch_and (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    __device__ void __nv_atomic_and (T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

These two atomic functions are introduced in CUDA 12.8. It reads the value where `ptr` points to, `and` with `val`, and stores the result back to where `ptr` points to. `__nv_atomic_fetch_and` returns the old value where `ptr` points to. `__nv_atomic_and` does not have return value.

`T` can only be an integral type that is size of 4 or 8 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.

###  10.14.3. Other atomic functions 

####  10.14.3.1. __nv_atomic_load() 
    
    
    __device__ void __nv_atomic_load(T* ptr, T* ret, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function is introduced in CUDA 12.8. It loads the value where `ptr` points to and writes the value to where `ret` points to.

This is a generic atomic load, which means that `T` can be any data type that is size of 1, 2, 4, 8 or 16 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

16-byte data type is supported on the architecture `sm_70` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables. `order` cannot be `__NV_ATOMIC_RELEASE` or `__NV_ATOMIC_ACQ_REL`.

####  10.14.3.2. __nv_atomic_load_n() 
    
    
    __device__ T __nv_atomic_load_n(T* ptr, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function is introduced in CUDA 12.8. It loads the value where `ptr` points to and returns this value.

This is a non-generic atomic load, which means that `T` can only be an integral type that is size of 1, 2, 4, 8 or 16 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

16-byte data type is supported on the architecture `sm_70` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables. `order` cannot be `__NV_ATOMIC_RELEASE` or `__NV_ATOMIC_ACQ_REL`.

####  10.14.3.3. __nv_atomic_store() 
    
    
    __device__ void __nv_atomic_store(T* ptr, T* val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function is introduced in CUDA 12.8. It reads the value where `val` points to and stores to where `ptr` points to.

This is a generic atomic load, which means that `T` can be any data type that is size of 1, 2, 4, 8 or 16 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

16-byte data type is supported on the architecture `sm_70` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables. `order` cannot be `__NV_ATOMIC_CONSUME`, `__NV_ATOMIC_ACQUIRE` or `__NV_ATOMIC_ACQ_REL`.

####  10.14.3.4. __nv_atomic_store_n() 
    
    
    __device__ void __nv_atomic_store_n(T* ptr, T val, int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function is introduced in CUDA 12.8. It stores `val` to where `ptr` points to.

This is a non-generic atomic load, which means that `T` can only be an integral type that is size of 1, 2, 4, 8 or 16 bytes.

The atomic operation with memory order and thread scope is supported on the architecture `sm_60` and higher.

16-byte data type is supported on the architecture `sm_70` and higher.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables. `order` cannot be `__NV_ATOMIC_CONSUME`, `__NV_ATOMIC_ACQUIRE` or `__NV_ATOMIC_ACQ_REL`.

####  10.14.3.5. __nv_atomic_thread_fence() 
    
    
    __device__ void __nv_atomic_thread_fence (int order, int scope = __NV_THREAD_SCOPE_SYSTEM);
    

This atomic function establishes an ordering between memory accesses requested by this thread based on the specified memory order. And the thread scope parameter specifies the set of threads that may observe the ordering effect of this operation.

The thread scope of `cluster` is supported on the architecture `sm_90` and higher.

The arguments `order` and `scope` need to be integer literals, i.e., the arguments cannot be variables.


##  10.15. Address Space Predicate Functions 

The functions described in this section have unspecified behavior if the argument is a null pointer.

###  10.15.1. __isGlobal() 
    
    
    __device__ unsigned int __isGlobal(const void *ptr);
    

Returns 1 if `ptr` contains the generic address of an object in global memory space, otherwise returns 0.

###  10.15.2. __isShared() 
    
    
    __device__ unsigned int __isShared(const void *ptr);
    

Returns 1 if `ptr` contains the generic address of an object in shared memory space, otherwise returns 0.

###  10.15.3. __isConstant() 
    
    
    __device__ unsigned int __isConstant(const void *ptr);
    

Returns 1 if `ptr` contains the generic address of an object in constant memory space, otherwise returns 0.

###  10.15.4. __isGridConstant() 
    
    
    __device__ unsigned int __isGridConstant(const void *ptr);
    

Returns 1 if `ptr` contains the generic address of a kernel parameter annotated with `__grid_constant__`, otherwise returns 0. Only supported for compute architectures greater than or equal to 7.x or later.

###  10.15.5. __isLocal() 
    
    
    __device__ unsigned int __isLocal(const void *ptr);
    

Returns 1 if `ptr` contains the generic address of an object in local memory space, otherwise returns 0.


##  10.16. Address Space Conversion Functions 

CUDA C++ pointers (`T*`) can access CUDA C++ objects independently of where these objects are stored. For example, an `int*` can access `int` objects independently of whether they are stored in global or shared memory.

The Address Space Conversion Functions below enable converting CUDA C++ pointers from and to other representations. This is required, among others, to interoperate with certain PTX instructions, or to exploit properties of these other representations for performance optimizations.

As an example of interoperating with certain PTX instructions, an `ld.shared.u32 r0, [addr];` PTX instruction expects `addr` to refer to the `shared` space. A CUDA C++ program with a CUDA C++ `uint32_t*` pointer to an object in `__shared__` memory, needs to convert this pointer to the `shared` space before passing it to such a PTX instruction by calling `__cvta_generic_to_shared` as follows:
    
    
    __shared__ uint32_t x;
    x = 42;
    void* p = &x;
    size_t sp = __cvta_generic_to_shared(p);
    uint32_t o;
    asm volatile("ld.shared.u32 %0, [%1];" : "=r"(o) : "l"(sp) : "memory");
    assert(o == 42);
    

A common program optimization that exploits properties of these other address representations is reducing data-structure size by leveraging that the address ranges of shared, local, and const spaces is smaller than 32-bit, which allows programs to store 32-bit addresses instead of 64-bit pointers. To obtain the 32-bit integer representation of these addresses, it suffices to truncate it by performing an unsigned 64-bit integer to unsigned 32-bit integer cast:
    
    
    __shared__ int x;
    void* p = &x;
    uint32_t smem32 = __cvta_generic_to_shared(p);
    

To obtain a generic address from such a 32-bit integer representation, it suffices to zero-extend the address back to an unsigned 64-bit integer before calling the corresponding address space conversion function:
    
    
    size_t smem64 = smem32;
    void* q = __cvta_shared_to_generic(smem64);
    assert(p == q);
    

A roundtrip from an input generic space pointer to its 32-bit integer representation and back to an output generic space pointer is guaranteed to return an output pointer that is equivalent to the input pointer of the roundtrip for the spaces listed above.

###  10.16.1. __cvta_generic_to_global() 
    
    
    __device__ size_t __cvta_generic_to_global(const void *ptr);
    

Returns the result of executing the _PTX_` cvta.to.global` instruction on the generic address denoted by `ptr`.

###  10.16.2. __cvta_generic_to_shared() 
    
    
    __device__ size_t __cvta_generic_to_shared(const void *ptr);
    

Returns the result of executing the _PTX_` cvta.to.shared` instruction on the generic address denoted by `ptr`.

###  10.16.3. __cvta_generic_to_constant() 
    
    
    __device__ size_t __cvta_generic_to_constant(const void *ptr);
    

Returns the result of executing the _PTX_` cvta.to.const` instruction on the generic address denoted by `ptr`.

###  10.16.4. __cvta_generic_to_local() 
    
    
    __device__ size_t __cvta_generic_to_local(const void *ptr);
    

Returns the result of executing the _PTX_` cvta.to.local` instruction on the generic address denoted by `ptr`.

###  10.16.5. __cvta_global_to_generic() 
    
    
    __device__ void * __cvta_global_to_generic(size_t rawbits);
    

Returns the generic pointer obtained by executing the _PTX_` cvta.global` instruction on the value provided by `rawbits`.

###  10.16.6. __cvta_shared_to_generic() 
    
    
    __device__ void * __cvta_shared_to_generic(size_t rawbits);
    

Returns the generic pointer obtained by executing the _PTX_` cvta.shared` instruction on the value provided by `rawbits`.

###  10.16.7. __cvta_constant_to_generic() 
    
    
    __device__ void * __cvta_constant_to_generic(size_t rawbits);
    

Returns the generic pointer obtained by executing the _PTX_` cvta.const` instruction on the value provided by `rawbits`.

###  10.16.8. __cvta_local_to_generic() 
    
    
    __device__ void * __cvta_local_to_generic(size_t rawbits);
    

Returns the generic pointer obtained by executing the _PTX_` cvta.local` instruction on the value provided by `rawbits`.


##  10.17. Alloca Function 

###  10.17.1. Synopsis 
    
    
    __host__ __device__ void * alloca(size_t size);
    

###  10.17.2. Description 

The `alloca()` function allocates `size` bytes of memory in the stack frame of the caller. The returned value is a pointer to allocated memory, the beginning of the memory is 16 bytes aligned when the function is invoked from device code. The allocated memory is automatically freed when the caller to `alloca()` is returned.

Note

On Windows platform, `<malloc.h>` must be included before using `alloca()`. Using `alloca()` may cause the stack to overflow, user needs to adjust stack size accordingly.

It is supported with compute capability 5.2 or higher.

###  10.17.3. Example 
    
    
    __device__ void foo(unsigned int num) {
        int4 *ptr = (int4 *)alloca(num * sizeof(int4));
        // use of ptr
        ...
    }
    


##  10.18. Compiler Optimization Hint Functions 

The functions described in this section can be used to provide additional information to the compiler optimizer.

###  10.18.1. __builtin_assume_aligned() 
    
    
    void * __builtin_assume_aligned (const void *exp, size_t align)
    

Allows the compiler to assume that the argument pointer is aligned to at least `align` bytes, and returns the argument pointer.

Example:
    
    
    void *res = __builtin_assume_aligned(ptr, 32); // compiler can assume 'res' is
                                                   // at least 32-byte aligned
    

Three parameter version:
    
    
    void * __builtin_assume_aligned (const void *exp, size_t align,
                                     <integral type> offset)
    

Allows the compiler to assume that `(char *)exp - offset` is aligned to at least `align` bytes, and returns the argument pointer.

Example:
    
    
    void *res = __builtin_assume_aligned(ptr, 32, 8); // compiler can assume
                                                      // '(char *)res - 8' is
                                                      // at least 32-byte aligned.
    

###  10.18.2. __builtin_assume() 
    
    
    void __builtin_assume(bool exp)
    

Allows the compiler to assume that the Boolean argument is true. If the argument is not true at run time, then the behavior is undefined. Note that if the argument has side effects, the behavior is unspecified.

Example:
    
    
     __device__ int get(int *ptr, int idx) {
       __builtin_assume(idx <= 2);
       return ptr[idx];
    }
    

###  10.18.3. __assume() 
    
    
    void __assume(bool exp)
    

Allows the compiler to assume that the Boolean argument is true. If the argument is not true at run time, then the behavior is undefined. Note that if the argument has side effects, the behavior is unspecified.

Example:
    
    
     __device__ int get(int *ptr, int idx) {
       __assume(idx <= 2);
       return ptr[idx];
    }
    

###  10.18.4. __builtin_expect() 
    
    
    long __builtin_expect (long exp, long c)
    

Indicates to the compiler that it is expected that `exp == c`, and returns the value of `exp`. Typically used to indicate branch prediction information to the compiler.

Example:
    
    
    // indicate to the compiler that likely "var == 0",
    // so the body of the if-block is unlikely to be
    // executed at run time.
    if (__builtin_expect (var, 0))
      doit ();
    

###  10.18.5. __builtin_unreachable() 
    
    
    void __builtin_unreachable(void)
    

Indicates to the compiler that control flow never reaches the point where this function is being called from. The program has undefined behavior if the control flow does actually reach this point at run time.

Example:
    
    
    // indicates to the compiler that the default case label is never reached.
    switch (in) {
    case 1: return 4;
    case 2: return 10;
    default: __builtin_unreachable();
    }
    

###  10.18.6. Restrictions 

`__assume()` is only supported when using `cl.exe` host compiler. The other functions are supported on all platforms, subject to the following restrictions:

  * If the host compiler supports the function, the function can be invoked from anywhere in translation unit.

  * Otherwise, the function must be invoked from within the body of a `__device__`/ `__global__`function, or only when the `__CUDA_ARCH__` macro is defined[5](#fn12).


##  10.19. Warp Vote Functions 
    
    
    int __all_sync(unsigned mask, int predicate);
    int __any_sync(unsigned mask, int predicate);
    unsigned __ballot_sync(unsigned mask, int predicate);
    unsigned __activemask();
    

Deprecation notice: `__any`, `__all`, and `__ballot` have been deprecated in CUDA 9.0 for all devices.

Removal notice: When targeting devices with compute capability 7.x or higher, `__any`, `__all`, and `__ballot` are no longer available and their sync variants should be used instead.

The warp vote functions allow the threads of a given [warp](#simt-architecture) to perform a reduction-and-broadcast operation. These functions take as input an integer `predicate` from each thread in the warp and compare those values with zero. The results of the comparisons are combined (reduced) across the [active](#simt-architecture-notes) threads of the warp in one of the following ways, broadcasting a single return value to each participating thread:

`__all_sync(unsigned mask, predicate)`:
    

Evaluate `predicate` for all non-exited threads in `mask` and return non-zero if and only if `predicate` evaluates to non-zero for all of them.

`__any_sync(unsigned mask, predicate)`:
    

Evaluate `predicate` for all non-exited threads in `mask` and return non-zero if and only if `predicate` evaluates to non-zero for any of them.

`__ballot_sync(unsigned mask, predicate)`:
    

Evaluate `predicate` for all non-exited threads in `mask` and return an integer whose Nth bit is set if and only if `predicate` evaluates to non-zero for the Nth thread of the warp and the Nth thread is active.

`__activemask()`:
    

Returns a 32-bit integer mask of all currently active threads in the calling warp. The Nth bit is set if the Nth lane in the warp is active when `__activemask()` is called. [Inactive](#simt-architecture-notes) threads are represented by 0 bits in the returned mask. Threads which have exited the program are always marked as inactive. Note that threads that are convergent at an `__activemask()` call are not guaranteed to be convergent at subsequent instructions unless those instructions are synchronizing warp-builtin functions.

For `__all_sync`, `__any_sync`, and `__ballot_sync`, a mask must be passed that specifies the threads participating in the call. A bit, representing the thread’s lane ID, must be set for each participating thread to ensure they are properly converged before the intrinsic is executed by the hardware. Each calling thread must have its own bit set in the mask and all non-exited threads named in mask must execute the same intrinsic with the same mask, or the result is undefined.

These intrinsics do not imply a memory barrier. They do not guarantee any memory ordering.


##  10.20. Warp Match Functions 

`__match_any_sync` and `__match_all_sync` perform a broadcast-and-compare operation of a variable between threads within a [warp](#simt-architecture).

Supported by devices of compute capability 7.x or higher.

###  10.20.1. Synopsis 
    
    
    unsigned int __match_any_sync(unsigned mask, T value);
    unsigned int __match_all_sync(unsigned mask, T value, int *pred);
    

`T` can be `int`, `unsigned int`, `long`, `unsigned long`, `long long`, `unsigned long long`, `float` or `double`.

###  10.20.2. Description 

The `__match_sync()` intrinsics permit a broadcast-and-compare of a value `value` across threads in a warp after synchronizing threads named in `mask`.

`__match_any_sync`
    

Returns mask of threads that have same value of `value` in `mask`

`__match_all_sync`
    

Returns `mask` if all threads in `mask` have the same value for `value`; otherwise 0 is returned. Predicate `pred` is set to true if all threads in `mask` have the same value of `value`; otherwise the predicate is set to false.

The new `*_sync` match intrinsics take in a mask indicating the threads participating in the call. A bit, representing the thread’s lane id, must be set for each participating thread to ensure they are properly converged before the intrinsic is executed by the hardware. Each calling thread must have its own bit set in the mask and all non-exited threads named in mask must execute the same intrinsic with the same mask, or the result is undefined.

These intrinsics do not imply a memory barrier. They do not guarantee any memory ordering.


##  10.21. Warp Reduce Functions 

The `__reduce_sync(unsigned mask, T value)` intrinsics perform a reduction operation on the data provided in `value` after synchronizing threads named in `mask`. T can be unsigned or signed for {add, min, max} and unsigned only for {and, or, xor} operations.

Supported by devices of compute capability 8.x or higher.

###  10.21.1. Synopsis 
    
    
    // add/min/max
    unsigned __reduce_add_sync(unsigned mask, unsigned value);
    unsigned __reduce_min_sync(unsigned mask, unsigned value);
    unsigned __reduce_max_sync(unsigned mask, unsigned value);
    int __reduce_add_sync(unsigned mask, int value);
    int __reduce_min_sync(unsigned mask, int value);
    int __reduce_max_sync(unsigned mask, int value);
    
    // and/or/xor
    unsigned __reduce_and_sync(unsigned mask, unsigned value);
    unsigned __reduce_or_sync(unsigned mask, unsigned value);
    unsigned __reduce_xor_sync(unsigned mask, unsigned value);
    

###  10.21.2. Description 

`__reduce_add_sync`, `__reduce_min_sync`, `__reduce_max_sync`
    

Returns the result of applying an arithmetic add, min, or max reduction operation on the values provided in `value` by each thread named in `mask`.

`__reduce_and_sync`, `__reduce_or_sync`, `__reduce_xor_sync`
    

Returns the result of applying a logical AND, OR, or XOR reduction operation on the values provided in `value` by each thread named in `mask`.

The `mask` indicates the threads participating in the call. A bit, representing the thread’s lane id, must be set for each participating thread to ensure they are properly converged before the intrinsic is executed by the hardware. Each calling thread must have its own bit set in the mask and all non-exited threads named in mask must execute the same intrinsic with the same mask, or the result is undefined.

These intrinsics do not imply a memory barrier. They do not guarantee any memory ordering.


##  10.22. Warp Shuffle Functions 

`__shfl_sync`, `__shfl_up_sync`, `__shfl_down_sync`, and `__shfl_xor_sync` exchange a variable between threads within a [warp](#simt-architecture).

Supported by devices of compute capability 5.0 or higher.

Deprecation Notice: `__shfl`, `__shfl_up`, `__shfl_down`, and `__shfl_xor` have been deprecated in CUDA 9.0 for all devices.

Removal Notice: When targeting devices with compute capability 7.x or higher, `__shfl`, `__shfl_up`, `__shfl_down`, and `__shfl_xor` are no longer available and their sync variants should be used instead.

###  10.22.1. Synopsis 
    
    
    T __shfl_sync(unsigned mask, T var, int srcLane, int width=warpSize);
    T __shfl_up_sync(unsigned mask, T var, unsigned int delta, int width=warpSize);
    T __shfl_down_sync(unsigned mask, T var, unsigned int delta, int width=warpSize);
    T __shfl_xor_sync(unsigned mask, T var, int laneMask, int width=warpSize);
    

`T` can be `int`, `unsigned int`, `long`, `unsigned long`, `long long`, `unsigned long long`, `float` or `double`. With the `cuda_fp16.h` header included, `T` can also be `__half` or `__half2`. Similarly, with the `cuda_bf16.h` header included, `T` can also be `__nv_bfloat16` or `__nv_bfloat162`.

###  10.22.2. Description 

The `__shfl_sync()` intrinsics permit exchanging of a variable between threads within a warp without use of shared memory. The exchange occurs simultaneously for all [active](#simt-architecture-notes) threads within the warp (and named in `mask`), moving 4 or 8 bytes of data per thread depending on the type.

Threads within a warp are referred to as _lanes_ , and may have an index between 0 and `warpSize-1` (inclusive). Four source-lane addressing modes are supported:

`__shfl_sync()`
    

Direct copy from indexed lane

`__shfl_up_sync()`
    

Copy from a lane with lower ID relative to caller

`__shfl_down_sync()`
    

Copy from a lane with higher ID relative to caller

`__shfl_xor_sync()`
    

Copy from a lane based on bitwise XOR of own lane ID

Threads may only read data from another thread which is actively participating in the `__shfl_sync()` command. If the target thread is [inactive](#simt-architecture-notes), the retrieved value is undefined.

All of the `__shfl_sync()` intrinsics take an optional `width` parameter which alters the behavior of the intrinsic. `width` must have a value which is a power of two in the range [1, warpSize] (i.e., 1, 2, 4, 8, 16 or 32). Results are undefined for other values.

`__shfl_sync()` returns the value of `var` held by the thread whose ID is given by `srcLane`. If width is less than `warpSize` then each subsection of the warp behaves as a separate entity with a starting logical lane ID of 0. If `srcLane` is outside the range `[0:width-1]`, the value returned corresponds to the value of var held by the `srcLane modulo width` (i.e. within the same subsection).

`__shfl_up_sync()` calculates a source lane ID by subtracting `delta` from the caller’s lane ID. The value of `var` held by the resulting lane ID is returned: in effect, `var` is shifted up the warp by `delta` lanes. If width is less than `warpSize` then each subsection of the warp behaves as a separate entity with a starting logical lane ID of 0. The source lane index will not wrap around the value of `width`, so effectively the lower `delta` lanes will be unchanged.

`__shfl_down_sync()` calculates a source lane ID by adding `delta` to the caller’s lane ID. The value of `var` held by the resulting lane ID is returned: this has the effect of shifting `var` down the warp by `delta` lanes. If width is less than `warpSize` then each subsection of the warp behaves as a separate entity with a starting logical lane ID of 0. As for `__shfl_up_sync()`, the ID number of the source lane will not wrap around the value of width and so the upper `delta` lanes will remain unchanged.

`__shfl_xor_sync()` calculates a source line ID by performing a bitwise XOR of the caller’s lane ID with `laneMask`: the value of `var` held by the resulting lane ID is returned. If `width` is less than `warpSize` then each group of `width` consecutive threads are able to access elements from earlier groups of threads, however if they attempt to access elements from later groups of threads their own value of `var` will be returned. This mode implements a butterfly addressing pattern such as is used in tree reduction and broadcast.

The new `*_sync` shfl intrinsics take in a mask indicating the threads participating in the call. A bit, representing the thread’s lane id, must be set for each participating thread to ensure they are properly converged before the intrinsic is executed by the hardware. Each calling thread must have its own bit set in the mask and all non-exited threads named in mask must execute the same intrinsic with the same mask, or the result is undefined.

Threads may only read data from another thread which is actively participating in the `__shfl_sync()` command. If the target thread is inactive, the retrieved value is undefined.

These intrinsics do not imply a memory barrier. They do not guarantee any memory ordering.

###  10.22.3. Examples 

####  10.22.3.1. Broadcast of a single value across a warp 
    
    
    #include <stdio.h>
    
    __global__ void bcast(int arg) {
        int laneId = threadIdx.x & 0x1f;
        int value;
        if (laneId == 0)        // Note unused variable for
            value = arg;        // all threads except lane 0
        value = __shfl_sync(0xffffffff, value, 0);   // Synchronize all threads in warp, and get "value" from lane 0
        if (value != arg)
            printf("Thread %d failed.\n", threadIdx.x);
    }
    
    int main() {
        bcast<<< 1, 32 >>>(1234);
        cudaDeviceSynchronize();
    
        return 0;
    }
    

####  10.22.3.2. Inclusive plus-scan across sub-partitions of 8 threads 
    
    
    #include <stdio.h>
    
    __global__ void scan4() {
        int laneId = threadIdx.x & 0x1f;
        // Seed sample starting value (inverse of lane ID)
        int value = 31 - laneId;
    
        // Loop to accumulate scan within my partition.
        // Scan requires log2(n) == 3 steps for 8 threads
        // It works by an accumulated sum up the warp
        // by 1, 2, 4, 8 etc. steps.
        for (int i=1; i<=4; i*=2) {
            // We do the __shfl_sync unconditionally so that we
            // can read even from threads which won't do a
            // sum, and then conditionally assign the result.
            int n = __shfl_up_sync(0xffffffff, value, i, 8);
            if ((laneId & 7) >= i)
                value += n;
        }
    
        printf("Thread %d final value = %d\n", threadIdx.x, value);
    }
    
    int main() {
        scan4<<< 1, 32 >>>();
        cudaDeviceSynchronize();
    
        return 0;
    }
    

####  10.22.3.3. Reduction across a warp 
    
    
    #include <stdio.h>
    
    __global__ void warpReduce() {
        int laneId = threadIdx.x & 0x1f;
        // Seed starting value as inverse lane ID
        int value = 31 - laneId;
    
        // Use XOR mode to perform butterfly reduction
        for (int i=16; i>=1; i/=2)
            value += __shfl_xor_sync(0xffffffff, value, i, 32);
    
        // "value" now contains the sum across all threads
        printf("Thread %d final value = %d\n", threadIdx.x, value);
    }
    
    int main() {
        warpReduce<<< 1, 32 >>>();
        cudaDeviceSynchronize();
    
        return 0;
    }
    


##  10.23. Nanosleep Function 

###  10.23.1. Synopsis 
    
    
    void __nanosleep(unsigned ns);
    

###  10.23.2. Description 

`__nanosleep(ns)` suspends the thread for a sleep duration of approximately `ns` nanoseconds. The maximum sleep duration is approximately 1 millisecond.

It is supported with compute capability 7.0 or higher.

###  10.23.3. Example 

The following code implements a mutex with exponential back-off.
    
    
    __device__ void mutex_lock(unsigned int *mutex) {
        unsigned int ns = 8;
        while (atomicCAS(mutex, 0, 1) == 1) {
            __nanosleep(ns);
            if (ns < 256) {
                ns *= 2;
            }
        }
    }
    
    __device__ void mutex_unlock(unsigned int *mutex) {
        atomicExch(mutex, 0);
    }
    


##  10.24. Warp Matrix Functions 

C++ warp matrix operations leverage Tensor Cores to accelerate matrix problems of the form `D=A*B+C`. These operations are supported on mixed-precision floating point data for devices of compute capability 7.0 or higher. This requires co-operation from all threads in a [warp](#simt-architecture). In addition, these operations are allowed in conditional code only if the condition evaluates identically across the entire [warp](#simt-architecture), otherwise the code execution is likely to hang.

###  10.24.1. Description 

All following functions and types are defined in the namespace `nvcuda::wmma`. Sub-byte operations are considered preview, i.e. the data structures and APIs for them are subject to change and may not be compatible with future releases. This extra functionality is defined in the `nvcuda::wmma::experimental` namespace.
    
    
    template<typename Use, int m, int n, int k, typename T, typename Layout=void> class fragment;
    
    void load_matrix_sync(fragment<...> &a, const T* mptr, unsigned ldm);
    void load_matrix_sync(fragment<...> &a, const T* mptr, unsigned ldm, layout_t layout);
    void store_matrix_sync(T* mptr, const fragment<...> &a, unsigned ldm, layout_t layout);
    void fill_fragment(fragment<...> &a, const T& v);
    void mma_sync(fragment<...> &d, const fragment<...> &a, const fragment<...> &b, const fragment<...> &c, bool satf=false);
    

`fragment`
    

An overloaded class containing a section of a matrix distributed across all threads in the warp. The mapping of matrix elements into `fragment` internal storage is unspecified and subject to change in future architectures.

Only certain combinations of template arguments are allowed. The first template parameter specifies how the fragment will participate in the matrix operation. Acceptable values for `Use` are:

  * `matrix_a` when the fragment is used as the first multiplicand, `A`,

  * `matrix_b` when the fragment is used as the second multiplicand, `B`, or

  * `accumulator` when the fragment is used as the source or destination accumulators (`C` or `D`, respectively).

The `m`, `n` and `k` sizes describe the shape of the warp-wide matrix tiles participating in the multiply-accumulate operation. The dimension of each tile depends on its role. For `matrix_a` the tile takes dimension `m x k`; for `matrix_b` the dimension is `k x n`, and `accumulator` tiles are `m x n`.

The data type, `T`, may be `double`, `float`, `__half`, `__nv_bfloat16`, `char`, or `unsigned char` for multiplicands and `double`, `float`, `int`, or `__half` for accumulators. As documented in [Element Types and Matrix Sizes](#wmma-type-sizes), limited combinations of accumulator and multiplicand types are supported. The Layout parameter must be specified for `matrix_a` and `matrix_b` fragments. `row_major` or `col_major` indicate that elements within a matrix row or column are contiguous in memory, respectively. The `Layout` parameter for an `accumulator` matrix should retain the default value of `void`. A row or column layout is specified only when the accumulator is loaded or stored as described below.


`load_matrix_sync`
    

Waits until all warp lanes have arrived at load_matrix_sync and then loads the matrix fragment a from memory. `mptr` must be a 256-bit aligned pointer pointing to the first element of the matrix in memory. `ldm` describes the stride in elements between consecutive rows (for row major layout) or columns (for column major layout) and must be a multiple of 8 for `__half` element type or multiple of 4 for `float` element type. (i.e., multiple of 16 bytes in both cases). If the fragment is an `accumulator`, the `layout` argument must be specified as either `mem_row_major` or `mem_col_major`. For `matrix_a` and `matrix_b` fragments, the layout is inferred from the fragment’s `layout` parameter. The values of `mptr`, `ldm`, `layout` and all template parameters for `a` must be the same for all threads in the warp. This function must be called by all threads in the warp, or the result is undefined.

`store_matrix_sync`
    

Waits until all warp lanes have arrived at store_matrix_sync and then stores the matrix fragment a to memory. `mptr` must be a 256-bit aligned pointer pointing to the first element of the matrix in memory. `ldm` describes the stride in elements between consecutive rows (for row major layout) or columns (for column major layout) and must be a multiple of 8 for `__half` element type or multiple of 4 for `float` element type. (i.e., multiple of 16 bytes in both cases). The layout of the output matrix must be specified as either `mem_row_major` or `mem_col_major`. The values of `mptr`, `ldm`, `layout` and all template parameters for a must be the same for all threads in the warp.

`fill_fragment`
    

Fill a matrix fragment with a constant value `v`. Because the mapping of matrix elements to each fragment is unspecified, this function is ordinarily called by all threads in the warp with a common value for `v`.

`mma_sync`
    

Waits until all warp lanes have arrived at mma_sync, and then performs the warp-synchronous matrix multiply-accumulate operation `D=A*B+C`. The in-place operation, `C=A*B+C`, is also supported. The value of `satf` and template parameters for each matrix fragment must be the same for all threads in the warp. Also, the template parameters `m`, `n` and `k` must match between fragments `A`, `B`, `C` and `D`. This function must be called by all threads in the warp, or the result is undefined.

If `satf` (saturate to finite value) mode is `true`, the following additional numerical properties apply for the destination accumulator:

  * If an element result is +Infinity, the corresponding accumulator will contain `+MAX_NORM`

  * If an element result is -Infinity, the corresponding accumulator will contain `-MAX_NORM`

  * If an element result is NaN, the corresponding accumulator will contain `+0`


Because the map of matrix elements into each thread’s `fragment` is unspecified, individual matrix elements must be accessed from memory (shared or global) after calling `store_matrix_sync`. In the special case where all threads in the warp will apply an element-wise operation uniformly to all fragment elements, direct element access can be implemented using the following `fragment` class members.
    
    
    enum fragment<Use, m, n, k, T, Layout>::num_elements;
    T fragment<Use, m, n, k, T, Layout>::x[num_elements];
    

As an example, the following code scales an `accumulator` matrix tile by half.
    
    
    wmma::fragment<wmma::accumulator, 16, 16, 16, float> frag;
    float alpha = 0.5f; // Same value for all threads in warp
    /*...*/
    for(int t=0; t<frag.num_elements; t++)
    frag.x[t] *= alpha;
    

###  10.24.2. Alternate Floating Point 

Tensor Cores support alternate types of floating point operations on devices with compute capability 8.0 and higher.

`__nv_bfloat16`
    

This data format is an alternate fp16 format that has the same range as f32 but reduced precision (7 bits). You can use this data format directly with the `__nv_bfloat16` type available in `cuda_bf16.h`. Matrix fragments with `__nv_bfloat16` data types are required to be composed with accumulators of `float` type. The shapes and operations supported are the same as with `__half`.

`tf32`
    

This data format is a special floating point format supported by Tensor Cores, with the same range as f32 and reduced precision (>=10 bits). The internal layout of this format is implementation defined. In order to use this floating point format with WMMA operations, the input matrices must be manually converted to tf32 precision.

To facilitate conversion, a new intrinsic `__float_to_tf32` is provided. While the input and output arguments to the intrinsic are of `float` type, the output will be `tf32` numerically. This new precision is intended to be used with Tensor Cores only, and if mixed with other `float`type operations, the precision and range of the result will be undefined.

Once an input matrix (`matrix_a` or `matrix_b`) is converted to tf32 precision, the combination of a `fragment` with `precision::tf32` precision, and a data type of `float` to `load_matrix_sync` will take advantage of this new capability. Both the accumulator fragments must have `float` data types. The only supported matrix size is 16x16x8 (m-n-k).

The elements of the fragment are represented as `float`, hence the mapping from `element_type<T>` to `storage_element_type<T>` is:
    
    
    precision::tf32 -> float
    

###  10.24.3. Double Precision 

Tensor Cores support double-precision floating point operations on devices with compute capability 8.0 and higher. To use this new functionality, a `fragment` with the `double` type must be used. The `mma_sync` operation will be performed with the .rn (rounds to nearest even) rounding modifier.

###  10.24.4. Sub-byte Operations 

Sub-byte WMMA operations provide a way to access the low-precision capabilities of Tensor Cores. They are considered a preview feature i.e. the data structures and APIs for them are subject to change and may not be compatible with future releases. This functionality is available via the `nvcuda::wmma::experimental` namespace:
    
    
    namespace experimental {
        namespace precision {
            struct u4; // 4-bit unsigned
            struct s4; // 4-bit signed
            struct b1; // 1-bit
       }
        enum bmmaBitOp {
            bmmaBitOpXOR = 1, // compute_75 minimum
            bmmaBitOpAND = 2  // compute_80 minimum
        };
        enum bmmaAccumulateOp { bmmaAccumulateOpPOPC = 1 };
    }
    

For 4 bit precision, the APIs available remain the same, but you must specify `experimental::precision::u4` or `experimental::precision::s4` as the fragment data type. Since the elements of the fragment are packed together, `num_storage_elements` will be smaller than `num_elements` for that fragment. The `num_elements` variable for a sub-byte fragment, hence returns the number of elements of sub-byte type `element_type<T>`. This is true for single bit precision as well, in which case, the mapping from `element_type<T>` to `storage_element_type<T>` is as follows:
    
    
    experimental::precision::u4 -> unsigned (8 elements in 1 storage element)
    experimental::precision::s4 -> int (8 elements in 1 storage element)
    experimental::precision::b1 -> unsigned (32 elements in 1 storage element)
    T -> T  //all other types
    

The allowed layouts for sub-byte fragments is always `row_major` for `matrix_a` and `col_major` for `matrix_b`.

For sub-byte operations the value of `ldm` in `load_matrix_sync` should be a multiple of 32 for element type `experimental::precision::u4` and `experimental::precision::s4` or a multiple of 128 for element type `experimental::precision::b1` (i.e., multiple of 16 bytes in both cases).

Note

Support for the following variants for MMA instructions is deprecated and will be removed in sm_90:

>   * `experimental::precision::u4`
> 
>   * `experimental::precision::s4`
> 
>   * `experimental::precision::b1` with `bmmaBitOp` set to `bmmaBitOpXOR`
> 
> 


`bmma_sync`
    

Waits until all warp lanes have executed bmma_sync, and then performs the warp-synchronous bit matrix multiply-accumulate operation `D = (A op B) + C`, where `op` consists of a logical operation `bmmaBitOp` followed by the accumulation defined by `bmmaAccumulateOp`. The available operations are:

`bmmaBitOpXOR`, a 128-bit XOR of a row in `matrix_a` with the 128-bit column of `matrix_b`

`bmmaBitOpAND`, a 128-bit AND of a row in `matrix_a` with the 128-bit column of `matrix_b`, available on devices with compute capability 8.0 and higher.

The accumulate op is always `bmmaAccumulateOpPOPC` which counts the number of set bits.

###  10.24.5. Restrictions 

The special format required by tensor cores may be different for each major and minor device architecture. This is further complicated by threads holding only a fragment (opaque architecture-specific ABI data structure) of the overall matrix, with the developer not allowed to make assumptions on how the individual parameters are mapped to the registers participating in the matrix multiply-accumulate.

Since fragments are architecture-specific, it is unsafe to pass them from function A to function B if the functions have been compiled for different link-compatible architectures and linked together into the same device executable. In this case, the size and layout of the fragment will be specific to one architecture and using WMMA APIs in the other will lead to incorrect results or potentially, corruption.

An example of two link-compatible architectures, where the layout of the fragment differs, is sm_70 and sm_75.
    
    
    fragA.cu: void foo() { wmma::fragment<...> mat_a; bar(&mat_a); }
    fragB.cu: void bar(wmma::fragment<...> *mat_a) { // operate on mat_a }
    
    
    
    // sm_70 fragment layout
    $> nvcc -dc -arch=compute_70 -code=sm_70 fragA.cu -o fragA.o
    // sm_75 fragment layout
    $> nvcc -dc -arch=compute_75 -code=sm_75 fragB.cu -o fragB.o
    // Linking the two together
    $> nvcc -dlink -arch=sm_75 fragA.o fragB.o -o frag.o
    

This undefined behavior might also be undetectable at compilation time and by tools at runtime, so extra care is needed to make sure the layout of the fragments is consistent. This linking hazard is most likely to appear when linking with a legacy library that is both built for a different link-compatible architecture and expecting to be passed a WMMA fragment.

Note that in the case of weak linkages (for example, a CUDA C++ inline function), the linker may choose any available function definition which may result in implicit passes between compilation units.

To avoid these sorts of problems, the matrix should always be stored out to memory for transit through external interfaces (e.g. `wmma::store_matrix_sync(dst, …);`) and then it can be safely passed to `bar()` as a pointer type [e.g. `float *dst`].

Note that since sm_70 can run on sm_75, the above example sm_75 code can be changed to sm_70 and correctly work on sm_75. However, it is recommended to have sm_75 native code in your application when linking with other sm_75 separately compiled binaries.

###  10.24.6. Element Types and Matrix Sizes 

Tensor Cores support a variety of element types and matrix sizes. The following table presents the various combinations of `matrix_a`, `matrix_b` and `accumulator` matrix supported:

Matrix A | Matrix B | Accumulator | Matrix Size (m-n-k)  
---|---|---|---  
__half | __half | float | 16x16x16  
__half | __half | float | 32x8x16  
__half | __half | float | 8x32x16  
__half | __half | __half | 16x16x16  
__half | __half | __half | 32x8x16  
__half | __half | __half | 8x32x16  
unsigned char | unsigned char | int | 16x16x16  
unsigned char | unsigned char | int | 32x8x16  
unsigned char | unsigned char | int | 8x32x16  
signed char | signed char | int | 16x16x16  
signed char | signed char | int | 32x8x16  
signed char | signed char | int | 8x32x16  
  
Alternate Floating Point support:

Matrix A | Matrix B | Accumulator | Matrix Size (m-n-k)  
---|---|---|---  
__nv_bfloat16 | __nv_bfloat16 | float | 16x16x16  
__nv_bfloat16 | __nv_bfloat16 | float | 32x8x16  
__nv_bfloat16 | __nv_bfloat16 | float | 8x32x16  
precision::tf32 | precision::tf32 | float | 16x16x8  
  
Double Precision Support:

Matrix A | Matrix B | Accumulator | Matrix Size (m-n-k)  
---|---|---|---  
double | double | double | 8x8x4  
  
Experimental support for sub-byte operations:

Matrix A | Matrix B | Accumulator | Matrix Size (m-n-k)  
---|---|---|---  
precision::u4 | precision::u4 | int | 8x8x32  
precision::s4 | precision::s4 | int | 8x8x32  
precision::b1 | precision::b1 | int | 8x8x128  
  
###  10.24.7. Example 

The following code implements a 16x16x16 matrix multiplication in a single warp.
    
    
    #include <mma.h>
    using namespace nvcuda;
    
    __global__ void wmma_ker(half *a, half *b, float *c) {
       // Declare the fragments
       wmma::fragment<wmma::matrix_a, 16, 16, 16, half, wmma::col_major> a_frag;
       wmma::fragment<wmma::matrix_b, 16, 16, 16, half, wmma::row_major> b_frag;
       wmma::fragment<wmma::accumulator, 16, 16, 16, float> c_frag;
    
       // Initialize the output to zero
       wmma::fill_fragment(c_frag, 0.0f);
    
       // Load the inputs
       wmma::load_matrix_sync(a_frag, a, 16);
       wmma::load_matrix_sync(b_frag, b, 16);
    
       // Perform the matrix multiplication
       wmma::mma_sync(c_frag, a_frag, b_frag, c_frag);
    
       // Store the output
       wmma::store_matrix_sync(c, c_frag, 16, wmma::mem_row_major);
    }
    


##  10.25. DPX 

DPX is a set of functions that enable finding min and max values, as well as fused addition and min/max, for up to three 16 and 32-bit signed or unsigned integer parameters, with optional ReLU (clamping to zero):

  * three parameters: `__vimax3_s32`, `__vimax3_s16x2`, `__vimax3_u32`, `__vimax3_u16x2`, `__vimin3_s32`, `__vimin3_s16x2`, `__vimin3_u32`, `__vimin3_u16x2`

  * two parameters, with ReLU: `__vimax_s32_relu`, `__vimax_s16x2_relu`, `__vimin_s32_relu`, `__vimin_s16x2_relu`

  * three parameters, with ReLU: `__vimax3_s32_relu`, `__vimax3_s16x2_relu`, `__vimin3_s32_relu`, `__vimin3_s16x2_relu`

  * two parameters, also returning which parameter was smaller/larger: `__vibmax_s32`, `__vibmax_u32`, `__vibmin_s32`, `__vibmin_u32`, `__vibmax_s16x2`, `__vibmax_u16x2`, `__vibmin_s16x2`, `__vibmin_u16x2`

  * three parameters, comparing (first + second) with the third: `__viaddmax_s32`, `__viaddmax_s16x2`, `__viaddmax_u32`, `__viaddmax_u16x2`, `__viaddmin_s32`, `__viaddmin_s16x2`, `__viaddmin_u32`, `__viaddmin_u16x2`

  * three parameters, with ReLU, comparing (first + second) with the third and a zero: `__viaddmax_s32_relu`, `__viaddmax_s16x2_relu`, `__viaddmin_s32_relu`, `__viaddmin_s16x2_relu`


These instructions are hardware-accelerated on devices with compute capability 9 and higher, and software emulation on older devices.

Full API can be found in [CUDA Math API documentation](https://docs.nvidia.com/cuda/cuda-math-api/cuda_math_api/group__CUDA__MATH__INTRINSIC__SIMD.html).

DPX is exceptionally useful when implementing dynamic programming algorithms, such as Smith-Waterman or Needleman–Wunsch in genomics and Floyd-Warshall in route optimization.

###  10.25.1. Examples 

Max value of three signed 32-bit integers, with ReLU
    
    
    const int a = -15;
    const int b = 8;
    const int c = 5;
    int max_value_0 = __vimax3_s32_relu(a, b, c); // max(-15, 8, 5, 0) = 8
    const int d = -2;
    const int e = -4;
    int max_value_1 = __vimax3_s32_relu(a, d, e); // max(-15, -2, -4, 0) = 0
    

Min value of the sum of two 32-bit signed integers, another 32-bit signed integer and a zero (ReLU)
    
    
    const int a = -5;
    const int b = 6;
    const int c = -2;
    int max_value_0 = __viaddmax_s32_relu(a, b, c); // max(-5 + 6, -2, 0) = max(1, -2, 0) = 1
    const int d = 4;
    int max_value_1 = __viaddmax_s32_relu(a, d, c); // max(-5 + 4, -2, 0) = max(-1, -2, 0) = 0
    

Min value of two unsigned 32-bit integers and determining which value is smaller
    
    
    const unsigned int a = 9;
    const unsigned int b = 6;
    bool smaller_value;
    unsigned int min_value = __vibmin_u32(a, b, &smaller_value); // min_value is 6, smaller_value is true
    

Max values of three pairs of unsigned 16-bit integers
    
    
    const unsigned a = 0x00050002;
    const unsigned b = 0x00070004;
    const unsigned c = 0x00020006;
    unsigned int max_value = __vimax3_u16x2(a, b, c); // max(5, 7, 2) and max(2, 4, 6), so max_value is 0x00070006
    


##  10.26. Asynchronous Barrier 

The NVIDIA C++ standard library introduces a GPU implementation of [std::barrier](https://nvidia.github.io/libcudacxx/extended_api/synchronization_primitives/barrier.html). Along with the implementation of `std::barrier` the library provides extensions that allow users to specify the scope of barrier objects. The barrier API scopes are documented under [Thread Scopes](https://nvidia.github.io/libcudacxx/extended_api/memory_model.html#thread-scopes). Devices of compute capability 8.0 or higher provide hardware acceleration for barrier operations and integration of these barriers with the [memcpy_async](#asynchronous-data-copies) feature. On devices with compute capability below 8.0 but starting 7.0, these barriers are available without hardware acceleration.

`nvcuda::experimental::awbarrier` is deprecated in favor of `cuda::barrier`.

###  10.26.1. Simple Synchronization Pattern 

Without the arrive/wait barrier, synchronization is achieved using `__syncthreads()` (to synchronize all threads in a block) or `group.sync()` when using [Cooperative Groups](#cooperative-groups).
    
    
    #include <cooperative_groups.h>
    
    __global__ void simple_sync(int iteration_count) {
        auto block = cooperative_groups::this_thread_block();
    
        for (int i = 0; i < iteration_count; ++i) {
            /* code before arrive */
            block.sync(); /* wait for all threads to arrive here */
            /* code after wait */
        }
    }
    

Threads are blocked at the synchronization point (`block.sync()`) until all threads have reached the synchronization point. In addition, memory updates that happened before the synchronization point are guaranteed to be visible to all threads in the block after the synchronization point, i.e., equivalent to `atomic_thread_fence(memory_order_seq_cst, thread_scope_block)` as well as the `sync`.

This pattern has three stages:

  * Code **before** sync performs memory updates that will be read **after** the sync.

  * Synchronization point

  * Code **after** sync point with visibility of memory updates that happened **before** sync point.


###  10.26.2. Temporal Splitting and Five Stages of Synchronization 

The temporally-split synchronization pattern with the `std::barrier` is as follows.
    
    
    #include <cuda/barrier>
    #include <cooperative_groups.h>
    
    __device__ void compute(float* data, int curr_iteration);
    
    __global__ void split_arrive_wait(int iteration_count, float *data) {
        using barrier = cuda::barrier<cuda::thread_scope_block>;
        __shared__  barrier bar;
        auto block = cooperative_groups::this_thread_block();
    
        if (block.thread_rank() == 0) {
            init(&bar, block.size()); // Initialize the barrier with expected arrival count
        }
        block.sync();
    
        for (int curr_iter = 0; curr_iter < iteration_count; ++curr_iter) {
            /* code before arrive */
           barrier::arrival_token token = bar.arrive(); /* this thread arrives. Arrival does not block a thread */
           compute(data, curr_iter);
           bar.wait(std::move(token)); /* wait for all threads participating in the barrier to complete bar.arrive()*/
            /* code after wait */
        }
    }
    

In this pattern, the synchronization point (`block.sync()`) is split into an arrive point (`bar.arrive()`) and a wait point (`bar.wait(std::move(token))`). A thread begins participating in a `cuda::barrier` with its first call to `bar.arrive()`. When a thread calls `bar.wait(std::move(token))` it will be blocked until participating threads have completed `bar.arrive()` the expected number of times as specified by the expected arrival count argument passed to `init()`. Memory updates that happen before participating threads’ call to `bar.arrive()` are guaranteed to be visible to participating threads after their call to `bar.wait(std::move(token))`. Note that the call to `bar.arrive()` does not block a thread, it can proceed with other work that does not depend upon memory updates that happen before other participating threads’ call to `bar.arrive()`.

The _arrive and then wait_ pattern has five stages which may be iteratively repeated:

  * Code **before** arrive performs memory updates that will be read **after** the wait.

  * Arrive point with implicit memory fence (i.e., equivalent to `atomic_thread_fence(memory_order_seq_cst, thread_scope_block)`).

  * Code **between** arrive and wait.

  * Wait point.

  * Code **after** the wait, with visibility of updates that were performed **before** the arrive.


###  10.26.3. Bootstrap Initialization, Expected Arrival Count, and Participation 

Initialization must happen before any thread begins participating in a `cuda::barrier`.
    
    
    #include <cuda/barrier>
    #include <cooperative_groups.h>
    
    __global__ void init_barrier() {
        __shared__ cuda::barrier<cuda::thread_scope_block> bar;
        auto block = cooperative_groups::this_thread_block();
    
        if (block.thread_rank() == 0) {
            init(&bar, block.size()); // Single thread initializes the total expected arrival count.
        }
        block.sync();
    }
    

Before any thread can participate in `cuda::barrier`, the barrier must be initialized using `init()` with an **expected arrival count** , `block.size()` in this example. Initialization must happen before any thread calls `bar.arrive()`. This poses a bootstrapping challenge in that threads must synchronize before participating in the `cuda::barrier`, but threads are creating a `cuda::barrier` in order to synchronize. In this example, threads that will participate are part of a cooperative group and use `block.sync()` to bootstrap initialization. In this example a whole thread block is participating in initialization, hence `__syncthreads()` could also be used.

The second parameter of `init()` is the **expected arrival count** , i.e., the number of times `bar.arrive()` will be called by participating threads before a participating thread is unblocked from its call to `bar.wait(std::move(token))`. In the prior example the `cuda::barrier` is initialized with the number of threads in the thread block i.e., `cooperative_groups::this_thread_block().size()`, and all threads within the thread block participate in the barrier.

A `cuda::barrier` is flexible in specifying how threads participate (split arrive/wait) and which threads participate. In contrast `this_thread_block.sync()` from cooperative groups or `__syncthreads()` is applicable to whole-thread-block and `__syncwarp(mask)` is a specified subset of a warp. If the intention of the user is to synchronize a full thread block or a full warp we recommend using `__syncthreads()` and `__syncwarp(mask)` respectively for performance reasons.

###  10.26.4. A Barrier’s Phase: Arrival, Countdown, Completion, and Reset 

A `cuda::barrier` counts down from the expected arrival count to zero as participating threads call `bar.arrive()`. When the countdown reaches zero, a `cuda::barrier` is complete for the current phase. When the last call to `bar.arrive()` causes the countdown to reach zero, the countdown is automatically and atomically reset. The reset assigns the countdown to the expected arrival count, and moves the `cuda::barrier` to the next phase.

A `token` object of class `cuda::barrier::arrival_token`, as returned from `token=bar.arrive()`, is associated with the current phase of the barrier. A call to `bar.wait(std::move(token))` blocks the calling thread while the `cuda::barrier` is in the current phase, i.e., while the phase associated with the token matches the phase of the `cuda::barrier`. If the phase is advanced (because the countdown reaches zero) before the call to `bar.wait(std::move(token))` then the thread does not block; if the phase is advanced while the thread is blocked in `bar.wait(std::move(token))`, the thread is unblocked.

**It is essential to know when a reset could or could not occur, especially in non-trivial arrive/wait synchronization patterns.**

  * A thread’s calls to `token=bar.arrive()` and `bar.wait(std::move(token))` must be sequenced such that `token=bar.arrive()` occurs during the `cuda::barrier`’s current phase, and `bar.wait(std::move(token))` occurs during the same or next phase.

  * A thread’s call to `bar.arrive()` must occur when the barrier’s counter is non-zero. After barrier initialization, if a thread’s call to `bar.arrive()` causes the countdown to reach zero then a call to `bar.wait(std::move(token))` must happen before the barrier can be reused for a subsequent call to `bar.arrive()`.

  * `bar.wait()` must only be called using a `token` object of the current phase or the immediately preceding phase. For any other values of the `token` object, the behavior is undefined.


For simple arrive/wait synchronization patterns, compliance with these usage rules is straightforward.

###  10.26.5. Spatial Partitioning (also known as Warp Specialization) 

A thread block can be spatially partitioned such that warps are specialized to perform independent computations. Spatial partitioning is used in a producer or consumer pattern, where one subset of threads produces data that is concurrently consumed by the other (disjoint) subset of threads.

A producer/consumer spatial partitioning pattern requires two one sided synchronizations to manage a data buffer between the producer and consumer.

Producer | Consumer  
---|---  
wait for buffer to be ready to be filled | signal buffer is ready to be filled  
produce data and fill the buffer |   
signal buffer is filled | wait for buffer to be filled  
| consume data in filled buffer  
  
Producer threads wait for consumer threads to signal that the buffer is ready to be filled; however, consumer threads do not wait for this signal. Consumer threads wait for producer threads to signal that the buffer is filled; however, producer threads do not wait for this signal. For full producer/consumer concurrency this pattern has (at least) double buffering where each buffer requires two `cuda::barrier`s.
    
    
    #include <cuda/barrier>
    #include <cooperative_groups.h>
    
    using barrier = cuda::barrier<cuda::thread_scope_block>;
    
    __device__ void producer(barrier ready[], barrier filled[], float* buffer, float* in, int N, int buffer_len)
    {
        for (int i = 0; i < (N/buffer_len); ++i) {
            ready[i%2].arrive_and_wait(); /* wait for buffer_(i%2) to be ready to be filled */
            /* produce, i.e., fill in, buffer_(i%2)  */
            barrier::arrival_token token = filled[i%2].arrive(); /* buffer_(i%2) is filled */
        }
    }
    
    __device__ void consumer(barrier ready[], barrier filled[], float* buffer, float* out, int N, int buffer_len)
    {
        barrier::arrival_token token1 = ready[0].arrive(); /* buffer_0 is ready for initial fill */
        barrier::arrival_token token2 = ready[1].arrive(); /* buffer_1 is ready for initial fill */
        for (int i = 0; i < (N/buffer_len); ++i) {
            filled[i%2].arrive_and_wait(); /* wait for buffer_(i%2) to be filled */
            /* consume buffer_(i%2) */
            barrier::arrival_token token = ready[i%2].arrive(); /* buffer_(i%2) is ready to be re-filled */
        }
    }
    
    //N is the total number of float elements in arrays in and out
    __global__ void producer_consumer_pattern(int N, int buffer_len, float* in, float* out) {
    
        // Shared memory buffer declared below is of size 2 * buffer_len
        // so that we can alternatively work between two buffers.
        // buffer_0 = buffer and buffer_1 = buffer + buffer_len
        __shared__ extern float buffer[];
    
        // bar[0] and bar[1] track if buffers buffer_0 and buffer_1 are ready to be filled,
        // while bar[2] and bar[3] track if buffers buffer_0 and buffer_1 are filled-in respectively
        __shared__ barrier bar[4];
    
    
        auto block = cooperative_groups::this_thread_block();
        if (block.thread_rank() < 4)
            init(bar + block.thread_rank(), block.size());
        block.sync();
    
        if (block.thread_rank() < warpSize)
            producer(bar, bar+2, buffer, in, N, buffer_len);
        else
            consumer(bar, bar+2, buffer, out, N, buffer_len);
    }
    

In this example the first warp is specialized as the producer and the remaining warps are specialized as the consumer. All producer and consumer threads participate (call `bar.arrive()` or `bar.arrive_and_wait()`) in each of the four `cuda::barrier`s so the expected arrival counts are equal to `block.size()`.

A producer thread waits for the consumer threads to signal that the shared memory buffer can be filled. In order to wait for a `cuda::barrier` a producer thread must first arrive on that `ready[i%2].arrive()` to get a token and then `ready[i%2].wait(token)` with that token. For simplicity `ready[i%2].arrive_and_wait()` combines these operations.
    
    
    bar.arrive_and_wait();
    /* is equivalent to */
    bar.wait(bar.arrive());
    

Producer threads compute and fill the ready buffer, they then signal that the buffer is filled by arriving on the filled barrier, `filled[i%2].arrive()`. A producer thread does not wait at this point, instead it waits until the next iteration’s buffer (double buffering) is ready to be filled.

A consumer thread begins by signaling that both buffers are ready to be filled. A consumer thread does not wait at this point, instead it waits for this iteration’s buffer to be filled, `filled[i%2].arrive_and_wait()`. After the consumer threads consume the buffer they signal that the buffer is ready to be filled again, `ready[i%2].arrive()`, and then wait for the next iteration’s buffer to be filled.

###  10.26.6. Early Exit (Dropping out of Participation) 

When a thread that is participating in a sequence of synchronizations must exit early from that sequence, that thread must explicitly drop out of participation before exiting. The remaining participating threads can proceed normally with subsequent `cuda::barrier` arrive and wait operations.
    
    
    #include <cuda/barrier>
    #include <cooperative_groups.h>
    
    __device__ bool condition_check();
    
    __global__ void early_exit_kernel(int N) {
        using barrier = cuda::barrier<cuda::thread_scope_block>;
        __shared__ barrier bar;
        auto block = cooperative_groups::this_thread_block();
    
        if (block.thread_rank() == 0)
            init(&bar , block.size());
        block.sync();
    
        for (int i = 0; i < N; ++i) {
            if (condition_check()) {
              bar.arrive_and_drop();
              return;
            }
            /* other threads can proceed normally */
            barrier::arrival_token token = bar.arrive();
            /* code between arrive and wait */
            bar.wait(std::move(token)); /* wait for all threads to arrive */
            /* code after wait */
        }
    }
    

This operation arrives on the `cuda::barrier` to fulfill the participating thread’s obligation to arrive in the **current** phase, and then decrements the expected arrival count for the **next** phase so that this thread is no longer expected to arrive on the barrier.

###  10.26.7. Completion Function 

The `CompletionFunction` of `cuda::barrier<Scope, CompletionFunction>` is executed once per phase, after the last thread _arrives_ and before any thread is unblocked from the `wait`. Memory operations performed by the threads that arrived at the `barrier` during the phase are visible to the thread executing the `CompletionFunction`, and all memory operations performed within the `CompletionFunction` are visible to all threads waiting at the `barrier` once they are unblocked from the `wait`.
    
    
    #include <cuda/barrier>
    #include <cooperative_groups.h>
    #include <functional>
    namespace cg = cooperative_groups;
    
    __device__ int divergent_compute(int*, int);
    __device__ int independent_computation(int*, int);
    
    __global__ void psum(int* data, int n, int* acc) {
      auto block = cg::this_thread_block();
    
      constexpr int BlockSize = 128;
      __shared__ int smem[BlockSize];
      assert(BlockSize == block.size());
      assert(n % 128 == 0);
    
      auto completion_fn = [&] {
        int sum = 0;
        for (int i = 0; i < 128; ++i) sum += smem[i];
        *acc += sum;
      };
    
      // Barrier storage
      // Note: the barrier is not default-constructible because
      //       completion_fn is not default-constructible due
      //       to the capture.
      using completion_fn_t = decltype(completion_fn);
      using barrier_t = cuda::barrier<cuda::thread_scope_block,
                                      completion_fn_t>;
      __shared__ std::aligned_storage<sizeof(barrier_t),
                                      alignof(barrier_t)> bar_storage;
    
      // Initialize barrier:
      barrier_t* bar = (barrier_t*)&bar_storage;
      if (block.thread_rank() == 0) {
        assert(*acc == 0);
        assert(blockDim.x == blockDim.y == blockDim.y == 1);
        new (bar) barrier_t{block.size(), completion_fn};
        // equivalent to: init(bar, block.size(), completion_fn);
      }
      block.sync();
    
      // Main loop
      for (int i = 0; i < n; i += block.size()) {
        smem[block.thread_rank()] = data[i] + *acc;
        auto t = bar->arrive();
        // We can do independent computation here
        bar->wait(std::move(t));
        // shared-memory is safe to re-use in the next iteration
        // since all threads are done with it, including the one
        // that did the reduction
      }
    }
    

###  10.26.8. Memory Barrier Primitives Interface 

Memory barrier primitives are C-like interfaces to `cuda::barrier` functionality. These primitives are available through including the `<cuda_awbarrier_primitives.h>` header.

####  10.26.8.1. Data Types 
    
    
    typedef /* implementation defined */ __mbarrier_t;
    typedef /* implementation defined */ __mbarrier_token_t;
    

####  10.26.8.2. Memory Barrier Primitives API 
    
    
    uint32_t __mbarrier_maximum_count();
    void __mbarrier_init(__mbarrier_t* bar, uint32_t expected_count);
    

  * `bar` must be a pointer to `__shared__` memory.

  * `expected_count <= __mbarrier_maximum_count()`

  * Initialize `*bar` expected arrival count for the current and next phase to `expected_count`.


    
    
    void __mbarrier_inval(__mbarrier_t* bar);
    

  * `bar` must be a pointer to the mbarrier object residing in shared memory.

  * Invalidation of `*bar` is required before the corresponding shared memory can be repurposed.


    
    
    __mbarrier_token_t __mbarrier_arrive(__mbarrier_t* bar);
    

  * Initialization of `*bar` must happen before this call.

  * Pending count must not be zero.

  * Atomically decrement the pending count for the current phase of the barrier.

  * Return an arrival token associated with the barrier state immediately prior to the decrement.


    
    
    __mbarrier_token_t __mbarrier_arrive_and_drop(__mbarrier_t* bar);
    

  * Initialization of `*bar` must happen before this call.

  * Pending count must not be zero.

  * Atomically decrement the pending count for the current phase and expected count for the next phase of the barrier.

  * Return an arrival token associated with the barrier state immediately prior to the decrement.


    
    
    bool __mbarrier_test_wait(__mbarrier_t* bar, __mbarrier_token_t token);
    

  * `token` must be associated with the immediately preceding phase or current phase of `*this`.

  * Returns `true` if `token` is associated with the immediately preceding phase of `*bar`, otherwise returns `false`.


    
    
    //Note: This API has been deprecated in CUDA 11.1
    uint32_t __mbarrier_pending_count(__mbarrier_token_t token);
    


##  10.27. Asynchronous Data Copies 

CUDA 11 introduces Asynchronous Data operations with `memcpy_async` API to allow device code to explicitly manage the asynchronous copying of data. The `memcpy_async` feature enables CUDA kernels to overlap computation with data movement.

###  10.27.1. `memcpy_async` API 

The `memcpy_async` APIs are provided in the `cuda/barrier`, `cuda/pipeline`, and `cooperative_groups/memcpy_async.h` header files.

The `cuda::memcpy_async` APIs work with `cuda::barrier` and `cuda::pipeline` synchronization primitives, while the `cooperative_groups::memcpy_async` synchronizes using `cooperative_groups::wait`.

These APIs have very similar semantics: copy objects from `src` to `dst` as-if performed by another thread which, on completion of the copy, can be synchronized through `cuda::pipeline`, `cuda::barrier`, or `cooperative_groups::wait`.

The complete API documentation of the `cuda::memcpy_async` overloads for `cuda::barrier` and `cuda::pipeline` is provided in the [libcudacxx API](https://nvidia.github.io/libcudacxx) documentation along with some examples.

The API documentation of [cooperative_groups::memcpy_async](#collectives-cg-memcpy-async) is provided in the [Cooperative Groups](#cooperative-groups) section.

The `memcpy_async` APIs that use [cuda::barrier](#aw-barrier) and `cuda::pipeline` require compute capability 7.0 or higher. On devices with compute capability 8.0 or higher, `memcpy_async` operations from global to shared memory can benefit from hardware acceleration.

###  10.27.2. Copy and Compute Pattern - Staging Data Through Shared Memory 

CUDA applications often employ a _copy and compute_ pattern that:

  * fetches data from global memory,

  * stores data to shared memory, and

  * performs computations on shared memory data, and potentially writes results back to global memory.


The following sections illustrate how this pattern can be expressed without and with the `memcpy_async` feature:

  * [Without memcpy_async](#without-memcpy-async) introduces an example that does not overlap computation with data movement and uses an intermediate register to copy data.

  * [With memcpy_async](#with-memcpy-async) improves the previous example by introducing the [memcpy_async](#collectives-cg-memcpy-async) and the `cuda::memcpy_async` APIs to directly copy data from global to shared memory without using intermediate registers.

  * [Asynchronous Data Copies using cuda::barrier](#memcpy-async-barrier) shows memcpy with cooperative groups and barrier.

  * [Single-Stage Asynchronous Data Copies using cuda::pipeline](#with-memcpy-async-pipeline-pattern-single) shows memcpy with single stage pipeline.

  * [Multi-Stage Asynchronous Data Copies using cuda::pipeline](#with-memcpy-async-pipeline-pattern-multi) shows memcpy with multi stage pipeline.


###  10.27.3. Without `memcpy_async`

Without `memcpy_async`, the _copy_ phase of the _copy and compute_ pattern is expressed as `shared[local_idx] = global[global_idx]`. This global to shared memory copy is expanded to a read from global memory into a register, followed by a write to shared memory from the register.

When this pattern occurs within an iterative algorithm, each thread block needs to synchronize after the `shared[local_idx] = global[global_idx]` assignment, to ensure all writes to shared memory have completed before the compute phase can begin. The thread block also needs to synchronize again after the compute phase, to prevent overwriting shared memory before all threads have completed their computations. This pattern is illustrated in the following code snippet.
    
    
    #include <cooperative_groups.h>
    __device__ void compute(int* global_out, int const* shared_in) {
        // Computes using all values of current batch from shared memory.
        // Stores this thread's result back to global memory.
    }
    
    __global__ void without_memcpy_async(int* global_out, int const* global_in, size_t size, size_t batch_sz) {
      auto grid = cooperative_groups::this_grid();
      auto block = cooperative_groups::this_thread_block();
      assert(size == batch_sz * grid.size()); // Exposition: input size fits batch_sz * grid_size
    
      extern __shared__ int shared[]; // block.size() * sizeof(int) bytes
    
      size_t local_idx = block.thread_rank();
    
      for (size_t batch = 0; batch < batch_sz; ++batch) {
        // Compute the index of the current batch for this block in global memory:
        size_t block_batch_idx = block.group_index().x * block.size() + grid.size() * batch;
        size_t global_idx = block_batch_idx + threadIdx.x;
        shared[local_idx] = global_in[global_idx];
    
        block.sync(); // Wait for all copies to complete
    
        compute(global_out + block_batch_idx, shared); // Compute and write result to global memory
    
        block.sync(); // Wait for compute using shared memory to finish
      }
    }
    

###  10.27.4. With `memcpy_async`

With `memcpy_async`, the assignment of shared memory from global memory
    
    
    shared[local_idx] = global_in[global_idx];
    

is replaced with an asynchronous copy operation from [cooperative groups](#cooperative-groups)
    
    
    cooperative_groups::memcpy_async(group, shared, global_in + batch_idx, sizeof(int) * block.size());
    

The [cooperative_groups::memcpy_async](#collectives-cg-memcpy-async) API copies `sizeof(int) * block.size()` bytes from global memory starting at `global_in + batch_idx` to the `shared` data. This operation happens as-if performed by another thread, which synchronizes with the current thread’s call to [cooperative_groups::wait](#collectives-cg-wait) after the copy has completed. Until the copy operation completes, modifying the global data or reading or writing the shared data introduces a data race.

On devices with compute capability 8.0 or higher, `memcpy_async` transfers from global to shared memory can benefit from hardware acceleration, which avoids transferring the data through an intermediate register.
    
    
    #include <cooperative_groups.h>
    #include <cooperative_groups/memcpy_async.h>
    
    __device__ void compute(int* global_out, int const* shared_in);
    
    __global__ void with_memcpy_async(int* global_out, int const* global_in, size_t size, size_t batch_sz) {
      auto grid = cooperative_groups::this_grid();
      auto block = cooperative_groups::this_thread_block();
      assert(size == batch_sz * grid.size()); // Exposition: input size fits batch_sz * grid_size
    
      extern __shared__ int shared[]; // block.size() * sizeof(int) bytes
    
      for (size_t batch = 0; batch < batch_sz; ++batch) {
        size_t block_batch_idx = block.group_index().x * block.size() + grid.size() * batch;
        // Whole thread-group cooperatively copies whole batch to shared memory:
        cooperative_groups::memcpy_async(block, shared, global_in + block_batch_idx, sizeof(int) * block.size());
    
        cooperative_groups::wait(block); // Joins all threads, waits for all copies to complete
    
        compute(global_out + block_batch_idx, shared);
    
        block.sync();
      }
    }}
    

###  10.27.5. Asynchronous Data Copies using `cuda::barrier`

The `cuda::memcpy_async` overload for [cuda::barrier](#aw-barrier) enables synchronizing asynchronous data transfers using a `barrier`. This overloads executes the copy operation as-if performed by another thread bound to the barrier by: incrementing the expected count of the current phase on creation, and decrementing it on completion of the copy operation, such that the phase of the `barrier` will only advance when all threads participating in the barrier have arrived, and all `memcpy_async` bound to the current phase of the barrier have completed. The following example uses a block-wide `barrier`, where all block threads participate, and swaps the wait operation with a barrier `arrive_and_wait`, while providing the same functionality as the previous example:
    
    
    #include <cooperative_groups.h>
    #include <cuda/barrier>
    __device__ void compute(int* global_out, int const* shared_in);
    
    __global__ void with_barrier(int* global_out, int const* global_in, size_t size, size_t batch_sz) {
      auto grid = cooperative_groups::this_grid();
      auto block = cooperative_groups::this_thread_block();
      assert(size == batch_sz * grid.size()); // Assume input size fits batch_sz * grid_size
    
      extern __shared__ int shared[]; // block.size() * sizeof(int) bytes
    
      // Create a synchronization object (C++20 barrier)
      __shared__ cuda::barrier<cuda::thread_scope::thread_scope_block> barrier;
      if (block.thread_rank() == 0) {
        init(&barrier, block.size()); // Friend function initializes barrier
      }
      block.sync();
    
      for (size_t batch = 0; batch < batch_sz; ++batch) {
        size_t block_batch_idx = block.group_index().x * block.size() + grid.size() * batch;
        cuda::memcpy_async(block, shared, global_in + block_batch_idx, sizeof(int) * block.size(), barrier);
    
        barrier.arrive_and_wait(); // Waits for all copies to complete
    
        compute(global_out + block_batch_idx, shared);
    
        block.sync();
      }
    }
    

###  10.27.6. Performance Guidance for `memcpy_async`

For compute capability 8.x, the pipeline mechanism is shared among CUDA threads in the same CUDA warp. This sharing causes batches of `memcpy_async` to be entangled within a warp, which can impact performance under certain circumstances.

This section highlights the warp-entanglement effect on _commit_ , _wait_ , and _arrive_ operations. Please refer to [Pipeline Interface](#pipeline-interface) and the [Pipeline Primitives Interface](#pipeline-primitives-interface) for an overview of the individual operations.

####  10.27.6.1. Alignment 

On devices with compute capability 8.0, the [cp.async family of instructions](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async) allows copying data from global to shared memory asynchronously. These instructions support copying 4, 8, and 16 bytes at a time. If the size provided to `memcpy_async` is a multiple of 4, 8, or 16, and both pointers passed to `memcpy_async` are aligned to a 4, 8, or 16 alignment boundary, then `memcpy_async` can be implemented using exclusively asynchronous memory operations.

Additionally for achieving best performance when using `memcpy_async` API, an alignment of 128 Bytes for both shared memory and global memory is required.

For pointers to values of types with an alignment requirement of 1 or 2, it is often not possible to prove that the pointers are always aligned to a higher alignment boundary. Determining whether the `cp.async` instructions can or cannot be used must be delayed until run-time. Performing such a runtime alignment check increases code-size and adds runtime overhead.

The [cuda::aligned_size_t<size_t Align>(size_t size)](https://nvidia.github.io/libcudacxx)[Shape](https://nvidia.github.io/libcudacxx) can be used to supply a proof that both pointers passed to `memcpy_async` are aligned to an `Align` alignment boundary and that `size` is a multiple of `Align`, by passing it as an argument where the `memcpy_async` APIs expect a `Shape`:
    
    
    cuda::memcpy_async(group, dst, src, cuda::aligned_size_t<16>(N * block.size()), pipeline);
    

If the proof is incorrect, the behavior is undefined.

####  10.27.6.2. Trivially copyable 

On devices with compute capability 8.0, the [cp.async family of instructions](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async) allows copying data from global to shared memory asynchronously. If the pointer types passed to `memcpy_async` do not point to [TriviallyCopyable](https://en.cppreference.com/w/cpp/named_req/TriviallyCopyable) types, the copy constructor of each output element needs to be invoked, and these instructions cannot be used to accelerate `memcpy_async`.

####  10.27.6.3. Warp Entanglement - Commit 

The sequence of `memcpy_async` batches is shared across the warp. The commit operation is coalesced such that the sequence is incremented once for all converged threads that invoke the commit operation. If the warp is fully converged, the sequence is incremented by one; if the warp is fully diverged, the sequence is incremented by 32.

  * Let _PB_ be the warp-shared pipeline’s _actual_ sequence of batches.

`PB = {BP0, BP1, BP2, …, BPL}`

  * Let _TB_ be a thread’s _perceived_ sequence of batches, as if the sequence were only incremented by this thread’s invocation of the commit operation.

`TB = {BT0, BT1, BT2, …, BTL}`

The `pipeline::producer_commit()` return value is from the thread’s _perceived_ batch sequence.

  * An index in a thread’s perceived sequence always aligns to an equal or larger index in the actual warp-shared sequence. The sequences are equal only when all commit operations are invoked from converged threads.

`BTn ≡ BPm` where `n <= m`


For example, when a warp is fully diverged:

  * The warp-shared pipeline’s actual sequence would be: `PB = {0, 1, 2, 3, ..., 31}` (`PL=31`).

  * The perceived sequence for each thread of this warp would be:

    * Thread 0: `TB = {0}` (`TL=0`)

    * Thread 1: `TB = {0}` (`TL=0`)

    * `…`

    * Thread 31: `TB = {0}` (`TL=0`)


####  10.27.6.4. Warp Entanglement - Wait 

A CUDA thread invokes either `pipeline_consumer_wait_prior<N>()` or `pipeline::consumer_wait()` to wait for batches in the _perceived_ sequence `TB` to complete. Note that `pipeline::consumer_wait()` is equivalent to `pipeline_consumer_wait_prior<N>()`, where `N = PL`.

The `pipeline_consumer_wait_prior<N>()` function waits for batches in the _actual_ sequence at least up to and including `PL-N`. Since `TL <= PL`, waiting for batch up to and including `PL-N` includes waiting for batch `TL-N`. Thus, when `TL < PL`, the thread will unintentionally wait for additional, more recent batches.

In the extreme fully-diverged warp example above, each thread could wait for all 32 batches.

####  10.27.6.5. Warp Entanglement - Arrive-On 

Warp-divergence affects the number of times an `arrive_on(bar)` operation updates the barrier. If the invoking warp is fully converged, then the barrier is updated once. If the invoking warp is fully diverged, then 32 individual updates are applied to the barrier.

####  10.27.6.6. Keep Commit and Arrive-On Operations Converged 

It is recommended that commit and arrive-on invocations are by converged threads:

  * to not over-wait, by keeping threads’ perceived sequence of batches aligned with the actual sequence, and

  * to minimize updates to the barrier object.


When code preceding these operations diverges threads, then the warp should be re-converged, via `__syncwarp` before invoking commit or arrive-on operations.


##  10.28. Asynchronous Data Copies using `cuda::pipeline`

CUDA provides the `cuda::pipeline` synchronization object to manage and overlap asynchronous data movement with computation.

The API documentation for `cuda::pipeline` is provided in the [libcudacxx API](https://nvidia.github.io/libcudacxx). A pipeline object is a double-ended N stage queue with a _head_ and a _tail_ , and is used to process work in a first-in first-out (FIFO) order. The pipeline object has following member functions to manage the stages of the pipeline.

Pipeline Class Member Function | Description  
---|---  
`producer_acquire` | Acquires an available stage in the pipeline internal queue.  
`producer_commit` | Commits the asynchronous operations issued after the `producer_acquire` call on the currently acquired stage of the pipeline.  
`consumer_wait` | Wait for completion of all asynchronous operations on the oldest stage of the pipeline.  
`consumer_release` | Release the oldest stage of the pipeline to the pipeline object for reuse. The released stage can be then acquired by the producer.  
  
###  10.28.1. Single-Stage Asynchronous Data Copies using `cuda::pipeline`

In previous examples we showed how to use [cooperative_groups](#collectives-cg-wait) and [cuda::barrier](#aw-barrier) to do asynchronous data transfers. In this section, we will use the `cuda::pipeline` API with a single stage to schedule asynchronous copies. And later we will expand this example to show multi staged overlapped compute and copy.
    
    
    #include <cooperative_groups/memcpy_async.h>
    #include <cuda/pipeline>
    
    __device__ void compute(int* global_out, int const* shared_in);
    __global__ void with_single_stage(int* global_out, int const* global_in, size_t size, size_t batch_sz) {
        auto grid = cooperative_groups::this_grid();
        auto block = cooperative_groups::this_thread_block();
        assert(size == batch_sz * grid.size()); // Assume input size fits batch_sz * grid_size
    
        constexpr size_t stages_count = 1; // Pipeline with one stage
        // One batch must fit in shared memory:
        extern __shared__ int shared[];  // block.size() * sizeof(int) bytes
    
        // Allocate shared storage for a single stage cuda::pipeline:
        __shared__ cuda::pipeline_shared_state<
            cuda::thread_scope::thread_scope_block,
            stages_count
        > shared_state;
        auto pipeline = cuda::make_pipeline(block, &shared_state);
    
        // Each thread processes `batch_sz` elements.
        // Compute offset of the batch `batch` of this thread block in global memory:
        auto block_batch = [&](size_t batch) -> int {
          return block.group_index().x * block.size() + grid.size() * batch;
        };
    
        for (size_t batch = 0; batch < batch_sz; ++batch) {
            size_t global_idx = block_batch(batch);
    
            // Collectively acquire the pipeline head stage from all producer threads:
            pipeline.producer_acquire();
    
            // Submit async copies to the pipeline's head stage to be
            // computed in the next loop iteration
            cuda::memcpy_async(block, shared, global_in + global_idx, sizeof(int) * block.size(), pipeline);
            // Collectively commit (advance) the pipeline's head stage
            pipeline.producer_commit();
    
            // Collectively wait for the operations committed to the
            // previous `compute` stage to complete:
            pipeline.consumer_wait();
    
            // Computation overlapped with the memcpy_async of the "copy" stage:
            compute(global_out + global_idx, shared);
    
            // Collectively release the stage resources
            pipeline.consumer_release();
        }
    }
    

###  10.28.2. Multi-Stage Asynchronous Data Copies using `cuda::pipeline`

In the previous examples with [cooperative_groups::wait](#collectives-cg-wait) and [cuda::barrier](#aw-barrier), the kernel threads immediately wait for the data transfer to shared memory to complete. This avoids data transfers from global memory into registers, but does not _hide_ the latency of the `memcpy_async` operation by overlapping computation.

For that we use the CUDA [pipeline](#pipeline-interface) feature in the following example. It provides a mechanism for managing a sequence of `memcpy_async` batches, enabling CUDA kernels to overlap memory transfers with computation. The following example implements a two-stage pipeline that overlaps data-transfer with computation. It:

  * Initializes the pipeline shared state (more below)

  * Kickstarts the pipeline by scheduling a `memcpy_async` for the first batch.

  * Loops over all the batches: it schedules `memcpy_async` for the next batch, blocks all threads on the completion of the `memcpy_async` for the previous batch, and then overlaps the computation on the previous batch with the asynchronous copy of the memory for the next batch.

  * Finally, it drains the pipeline by performing the computation on the last batch.


Note that, for interoperability with `cuda::pipeline`, `cuda::memcpy_async` from the `cuda/pipeline` header is used here.
    
    
    #include <cooperative_groups/memcpy_async.h>
    #include <cuda/pipeline>
    
    __device__ void compute(int* global_out, int const* shared_in);
    __global__ void with_staging(int* global_out, int const* global_in, size_t size, size_t batch_sz) {
        auto grid = cooperative_groups::this_grid();
        auto block = cooperative_groups::this_thread_block();
        assert(size == batch_sz * grid.size()); // Assume input size fits batch_sz * grid_size
    
        constexpr size_t stages_count = 2; // Pipeline with two stages
        // Two batches must fit in shared memory:
        extern __shared__ int shared[];  // stages_count * block.size() * sizeof(int) bytes
        size_t shared_offset[stages_count] = { 0, block.size() }; // Offsets to each batch
    
        // Allocate shared storage for a two-stage cuda::pipeline:
        __shared__ cuda::pipeline_shared_state<
            cuda::thread_scope::thread_scope_block,
            stages_count
        > shared_state;
        auto pipeline = cuda::make_pipeline(block, &shared_state);
    
        // Each thread processes `batch_sz` elements.
        // Compute offset of the batch `batch` of this thread block in global memory:
        auto block_batch = [&](size_t batch) -> int {
          return block.group_index().x * block.size() + grid.size() * batch;
        };
    
        // Initialize first pipeline stage by submitting a `memcpy_async` to fetch a whole batch for the block:
        if (batch_sz == 0) return;
        pipeline.producer_acquire();
        cuda::memcpy_async(block, shared + shared_offset[0], global_in + block_batch(0), sizeof(int) * block.size(), pipeline);
        pipeline.producer_commit();
    
        // Pipelined copy/compute:
        for (size_t batch = 1; batch < batch_sz; ++batch) {
            // Stage indices for the compute and copy stages:
            size_t compute_stage_idx = (batch - 1) % 2;
            size_t copy_stage_idx = batch % 2;
    
            size_t global_idx = block_batch(batch);
    
            // Collectively acquire the pipeline head stage from all producer threads:
            pipeline.producer_acquire();
    
            // Submit async copies to the pipeline's head stage to be
            // computed in the next loop iteration
            cuda::memcpy_async(block, shared + shared_offset[copy_stage_idx], global_in + global_idx, sizeof(int) * block.size(), pipeline);
            // Collectively commit (advance) the pipeline's head stage
            pipeline.producer_commit();
    
            // Collectively wait for the operations committed to the
            // previous `compute` stage to complete:
            pipeline.consumer_wait();
    
            // Computation overlapped with the memcpy_async of the "copy" stage:
            compute(global_out + global_idx, shared + shared_offset[compute_stage_idx]);
    
            // Collectively release the stage resources
            pipeline.consumer_release();
        }
    
        // Compute the data fetch by the last iteration
        pipeline.consumer_wait();
        compute(global_out + block_batch(batch_sz-1), shared + shared_offset[(batch_sz - 1) % 2]);
        pipeline.consumer_release();
    }
    

A [pipeline object](#pipeline-interface) is a double-ended queue with a _head_ and a _tail_ , and is used to process work in a first-in first-out (FIFO) order. Producer threads commit work to the pipeline’s head, while consumer threads pull work from the pipeline’s tail. In the example above, all threads are both producer and consumer threads. The threads first _commit_` memcpy_async` operations to fetch the _next_ batch while they _wait_ on the _previous_ batch of `memcpy_async` operations to complete.

  * Committing work to a pipeline stage involves:

    * Collectively _acquiring_ the pipeline _head_ from a set of producer threads using `pipeline.producer_acquire()`.

    * Submitting `memcpy_async` operations to the pipeline head.

    * Collectively _committing_ (advancing) the pipeline head using `pipeline.producer_commit()`.

  * Using a previously commited stage involves:

    * Collectively waiting for the stage to complete, e.g., using `pipeline.consumer_wait()` to wait on the tail (oldest) stage.

    * Collectively _releasing_ the stage using `pipeline.consumer_release()`.


`cuda::pipeline_shared_state<scope, count>` encapsulates the finite resources that allow a pipeline to process up to `count` concurrent stages. If all resources are in use, `pipeline.producer_acquire()` blocks producer threads until the resources of the next pipeline stage are released by consumer threads.

This example can be written in a more concise manner by merging the prolog and epilog of the loop with the loop itself as follows:
    
    
    template <size_t stages_count = 2 /* Pipeline with stages_count stages */>
    __global__ void with_staging_unified(int* global_out, int const* global_in, size_t size, size_t batch_sz) {
        auto grid = cooperative_groups::this_grid();
        auto block = cooperative_groups::this_thread_block();
        assert(size == batch_sz * grid.size()); // Assume input size fits batch_sz * grid_size
    
        extern __shared__ int shared[]; // stages_count * block.size() * sizeof(int) bytes
        size_t shared_offset[stages_count];
        for (int s = 0; s < stages_count; ++s) shared_offset[s] = s * block.size();
    
        __shared__ cuda::pipeline_shared_state<
            cuda::thread_scope::thread_scope_block,
            stages_count
        > shared_state;
        auto pipeline = cuda::make_pipeline(block, &shared_state);
    
        auto block_batch = [&](size_t batch) -> int {
            return block.group_index().x * block.size() + grid.size() * batch;
        };
    
        // compute_batch: next batch to process
        // fetch_batch:  next batch to fetch from global memory
        for (size_t compute_batch = 0, fetch_batch = 0; compute_batch < batch_sz; ++compute_batch) {
            // The outer loop iterates over the computation of the batches
            for (; fetch_batch < batch_sz && fetch_batch < (compute_batch + stages_count); ++fetch_batch) {
                // This inner loop iterates over the memory transfers, making sure that the pipeline is always full
                pipeline.producer_acquire();
                size_t shared_idx = fetch_batch % stages_count;
                size_t batch_idx = fetch_batch;
                size_t block_batch_idx = block_batch(batch_idx);
                cuda::memcpy_async(block, shared + shared_offset[shared_idx], global_in + block_batch_idx, sizeof(int) * block.size(), pipeline);
                pipeline.producer_commit();
            }
            pipeline.consumer_wait();
            int shared_idx = compute_batch % stages_count;
            int batch_idx = compute_batch;
            compute(global_out + block_batch(batch_idx), shared + shared_offset[shared_idx]);
            pipeline.consumer_release();
        }
    }
    

The `pipeline<thread_scope_block>` primitive used above is very flexible, and supports two features that our examples above are not using: any arbitrary subset of threads in the block can participate in the `pipeline`, and from the threads that participate, any subsets can be producers, consumers, or both. In the following example, threads with an “even” thread rank are producers, while other threads are consumers:
    
    
    __device__ void compute(int* global_out, int shared_in);
    
    template <size_t stages_count = 2>
    __global__ void with_specialized_staging_unified(int* global_out, int const* global_in, size_t size, size_t batch_sz) {
        auto grid = cooperative_groups::this_grid();
        auto block = cooperative_groups::this_thread_block();
    
        // In this example, threads with "even" thread rank are producers, while threads with "odd" thread rank are consumers:
        const cuda::pipeline_role thread_role
          = block.thread_rank() % 2 == 0? cuda::pipeline_role::producer : cuda::pipeline_role::consumer;
    
        // Each thread block only has half of its threads as producers:
        auto producer_threads = block.size() / 2;
    
        // Map adjacent even and odd threads to the same id:
        const int thread_idx = block.thread_rank() / 2;
    
        auto elements_per_batch = size / batch_sz;
        auto elements_per_batch_per_block = elements_per_batch / grid.group_dim().x;
    
        extern __shared__ int shared[]; // stages_count * elements_per_batch_per_block * sizeof(int) bytes
        size_t shared_offset[stages_count];
        for (int s = 0; s < stages_count; ++s) shared_offset[s] = s * elements_per_batch_per_block;
    
        __shared__ cuda::pipeline_shared_state<
            cuda::thread_scope::thread_scope_block,
            stages_count
        > shared_state;
        cuda::pipeline pipeline = cuda::make_pipeline(block, &shared_state, thread_role);
    
        // Each thread block processes `batch_sz` batches.
        // Compute offset of the batch `batch` of this thread block in global memory:
        auto block_batch = [&](size_t batch) -> int {
          return elements_per_batch * batch + elements_per_batch_per_block * blockIdx.x;
        };
    
        for (size_t compute_batch = 0, fetch_batch = 0; compute_batch < batch_sz; ++compute_batch) {
            // The outer loop iterates over the computation of the batches
            for (; fetch_batch < batch_sz && fetch_batch < (compute_batch + stages_count); ++fetch_batch) {
                // This inner loop iterates over the memory transfers, making sure that the pipeline is always full
                if (thread_role == cuda::pipeline_role::producer) {
                    // Only the producer threads schedule asynchronous memcpys:
                    pipeline.producer_acquire();
                    size_t shared_idx = fetch_batch % stages_count;
                    size_t batch_idx = fetch_batch;
                    size_t global_batch_idx = block_batch(batch_idx) + thread_idx;
                    size_t shared_batch_idx = shared_offset[shared_idx] + thread_idx;
                    cuda::memcpy_async(shared + shared_batch_idx, global_in + global_batch_idx, sizeof(int), pipeline);
                    pipeline.producer_commit();
                }
            }
            if (thread_role == cuda::pipeline_role::consumer) {
                // Only the consumer threads compute:
                pipeline.consumer_wait();
                size_t shared_idx = compute_batch % stages_count;
                size_t global_batch_idx = block_batch(compute_batch) + thread_idx;
                size_t shared_batch_idx = shared_offset[shared_idx] + thread_idx;
                compute(global_out + global_batch_idx, *(shared + shared_batch_idx));
                pipeline.consumer_release();
            }
        }
    }
    

There are some optimizations that `pipeline` performs, for example, when all threads are both producers and consumers, but in general, the cost of supporting all these features cannot be fully eliminated. For example, `pipeline` stores and uses a set of barriers in shared memory for synchronization, which is not really necessary if all threads in the block participate in the pipeline.

For the particular case in which all threads in the block participate in the `pipeline`, we can do better than `pipeline<thread_scope_block>` by using a `pipeline<thread_scope_thread>` combined with `__syncthreads()`:
    
    
    template<size_t stages_count>
    __global__ void with_staging_scope_thread(int* global_out, int const* global_in, size_t size, size_t batch_sz) {
        auto grid = cooperative_groups::this_grid();
        auto block = cooperative_groups::this_thread_block();
        auto thread = cooperative_groups::this_thread();
        assert(size == batch_sz * grid.size()); // Assume input size fits batch_sz * grid_size
    
        extern __shared__ int shared[]; // stages_count * block.size() * sizeof(int) bytes
        size_t shared_offset[stages_count];
        for (int s = 0; s < stages_count; ++s) shared_offset[s] = s * block.size();
    
        // No pipeline::shared_state needed
        cuda::pipeline<cuda::thread_scope_thread> pipeline = cuda::make_pipeline();
    
        auto block_batch = [&](size_t batch) -> int {
            return block.group_index().x * block.size() + grid.size() * batch;
        };
    
        for (size_t compute_batch = 0, fetch_batch = 0; compute_batch < batch_sz; ++compute_batch) {
            for (; fetch_batch < batch_sz && fetch_batch < (compute_batch + stages_count); ++fetch_batch) {
                pipeline.producer_acquire();
                size_t shared_idx = fetch_batch % stages_count;
                size_t batch_idx = fetch_batch;
                // Each thread fetches its own data:
                size_t thread_batch_idx = block_batch(batch_idx) + threadIdx.x;
                // The copy is performed by a single `thread` and the size of the batch is now that of a single element:
                cuda::memcpy_async(thread, shared + shared_offset[shared_idx] + threadIdx.x, global_in + thread_batch_idx, sizeof(int), pipeline);
                pipeline.producer_commit();
            }
            pipeline.consumer_wait();
            block.sync(); // __syncthreads: All memcpy_async of all threads in the block for this stage have completed here
            int shared_idx = compute_batch % stages_count;
            int batch_idx = compute_batch;
            compute(global_out + block_batch(batch_idx), shared + shared_offset[shared_idx]);
            pipeline.consumer_release();
        }
    }
    

If the `compute` operation only reads shared memory written to by other threads in the same warp as the current thread, `__syncwarp()` suffices.

###  10.28.3. Pipeline Interface 

The complete API documentation for `cuda::memcpy_async` is provided in the [libcudacxx API](https://nvidia.github.io/libcudacxx) documentation along with some examples.

The `pipeline` interface requires

  * at least CUDA 11.0,

  * at least ISO C++ 2011 compatibility, e.g., to be compiled with `-std=c++11`, and

  * `#include <cuda/pipeline>`.


For a C-like interface, when compiling without ISO C++ 2011 compatibility, see [Pipeline Primitives Interface](#pipeline-primitives-interface).

###  10.28.4. Pipeline Primitives Interface 

Pipeline primitives are a C-like interface for `memcpy_async` functionality. The pipeline primitives interface is available by including the `<cuda_pipeline.h>` header. When compiling without ISO C++ 2011 compatibility, include the `<cuda_pipeline_primitives.h>` header.

####  10.28.4.1. `memcpy_async` Primitive 
    
    
    void __pipeline_memcpy_async(void* __restrict__ dst_shared,
                                 const void* __restrict__ src_global,
                                 size_t size_and_align,
                                 size_t zfill=0);
    

  * Request that the following operation be submitted for asynchronous evaluation:
        
        size_t i = 0;
        for (; i < size_and_align - zfill; ++i) ((char*)dst_shared)[i] = ((char*)src_global)[i]; /* copy */
        for (; i < size_and_align; ++i) ((char*)dst_shared)[i] = 0; /* zero-fill */
        

  * Requirements:

    * `dst_shared` must be a pointer to the shared memory destination for the `memcpy_async`.

    * `src_global` must be a pointer to the global memory source for the `memcpy_async`.

    * `size_and_align` must be 4, 8, or 16.

    * `zfill <= size_and_align`.

    * `size_and_align` must be the alignment of `dst_shared` and `src_global`.

  * It is a race condition for any thread to modify the source memory or observe the destination memory prior to waiting for the `memcpy_async` operation to complete. Between submitting a `memcpy_async` operation and waiting for its completion, any of the following actions introduces a race condition:

    * Loading from `dst_shared`.

    * Storing to `dst_shared` or `src_global`.

    * Applying an atomic update to `dst_shared` or `src_global`.


####  10.28.4.2. Commit Primitive 
    
    
    void __pipeline_commit();
    

  * Commit submitted `memcpy_async` to the pipeline as the current batch.


####  10.28.4.3. Wait Primitive 
    
    
    void __pipeline_wait_prior(size_t N);
    

  * Let `{0, 1, 2, ..., L}` be the sequence of indices associated with invocations of `__pipeline_commit()` by a given thread.

  * Wait for completion of batches _at least_ up to and including `L-N`.


####  10.28.4.4. Arrive On Barrier Primitive 
    
    
    void __pipeline_arrive_on(__mbarrier_t* bar);
    

  * `bar` points to a barrier in shared memory.

  * Increments the barrier arrival count by one, when all memcpy_async operations sequenced before this call have completed, the arrival count is decremented by one and hence the net effect on the arrival count is zero. It is user’s responsibility to make sure that the increment on the arrival count does not exceed `__mbarrier_maximum_count()`.


##  10.29. Asynchronous Data Copies using the Tensor Memory Accelerator (TMA) 

Many applications require movement of large amounts of data from and to global memory. Often, the data is laid out in global memory as a multi-dimensional array with non-sequential data acess patterns. To reduce global memory usage, sub-tiles of such arrays are copied to shared memory before use in computations. The loading and storing involves address-calculations that can be error-prone and repetitive. To offload these computations, Compute Capability 9.0 introduces the Tensor Memory Accelerator (TMA). The primary goal of TMA is to provide an efficient data transfer mechanism from global memory to shared memory for multi-dimensional arrays.

**Naming**. Tensor memory accelerator (TMA) is a broad term used to refer to the features described in this section. For the purpose of forward-compatibility and to reduce discrepancies with the PTX ISA, the text in this section refers to TMA operations as either bulk-asynchronous copies or bulk tensor asynchronous copies, depending on the specific type of copy used. The term “bulk” is used to contrast these operations with the asynchronous memory operations described in the previous sections.

**Dimensions**. TMA supports copying both one-dimensional and multi-dimensional arrays (up to 5-dimensional). The programming model for **bulk-asynchronous copies** of one-dimensional contiguous arrays is different from the programming model for **bulk tensor asynchronous copies** of multi-dimensional arrays. To perform a bulk tensor asynchronous copy of a multi-dimensional array, the hardware requires a [tensor map](https://docs.nvidia.com/cuda/cuda-driver-api/structCUtensorMap.html#structCUtensorMap). This object describes the layout of the multi-dimensional array in global and shared memory. A tensor map is typically created on the host using the [cuTensorMapEncode API](https://docs.nvidia.com/cuda/cuda-driver-api/group__CUDA__TENSOR__MEMORY.html#group__CUDA__TENSOR__MEMORY) and then transferred from host to device as a `const` kernel parameter annotated with `__grid_constant__`. The tensor map is transferred from host to device as a `const` kernel parameter annotated with `__grid_constant__`, and can be used on the device to copy a tile of data between shared and global memory. In contrast, performing a bulk-asynchronous copy of a contiguous one-dimensional array does not require a tensor map: it can be performed on-device with a pointer and size parameter.

**Source and destination**. The source and destination addresses of bulk-asynchronous copy operations can be in shared or global memory. The operations can read data from global to shared memory, write data from shared to global memory, and also copy from shared memory to [Distributed Shared Memory](#distributed-shared-memory) of another block in the same cluster. In addition, when in a cluster, a bulk-asynchronous operation can be specified as being multicast. In this case, data can be transferred from global memory to the shared memory of multiple blocks within the cluster. The multicast feature is optimized for target architecture `sm_90a` and may have [significantly reduced performance](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor) on other targets. Hence, it is advised to be used with [compute architecture](https://docs.nvidia.com/cuda/cuda-compiler-driver-nvcc/index.html#gpu-feature-list) `sm_90a`.

**Asynchronous**. Data transfers using TMA are [asynchronous](#asynchronous-simt-programming-model). This allows the initiating thread to continue computing while the hardware asynchronously copies the data. **Whether the data transfer occurs asynchronously in practice is up to the hardware implementation and may change in the future**. There are several [completion mechanisms](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-asynchronous-copy-completion-mechanisms) that bulk-asynchronous operations can use to signal that they have completed. When the operation reads from global to shared memory, any thread in the block can wait for the data to be readable in shared memory by waiting on a [Shared Memory Barrier](#aw-barrier). When the bulk-asynchronous operation writes data from shared memory to global or distributed shared memory, only the initiating thread can wait for the operation to have completed. This is accomplished using a _bulk async-group_ based completion mechanism. A table describing the completion mechanisms can be found below and in the [PTX ISA](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk).

Table 8 Asynchronous copies with possible source and destinations memory spaces and completion mechanisms. An empty cell indicates that a source-destination pair is not supported. Direction | Completion mechanism  
---|---  
Destination | Source | Asynchronous copy | Bulk-asynchronous copy (TMA)  
Global | Global |  |   
Global | Shared::cta |  | Bulk async-group  
Shared::cta | Global | Async-group, mbarrier | Mbarrier  
Shared::cluster | Global |  | Mbarrier (multicast)  
Shared::cta | Shared::cluster |  | Mbarrier  
Shared::cta | Shared::cta |  |   
  
###  10.29.1. Using TMA to transfer one-dimensional arrays 

This section demonstrates how to write a simple kernel that read-modify-writes a one-dimensional array using TMA. This shows how to how to load and store data using bulk-asynchronous copies, as well as how to synchronize threads of execution with those copies.

The code of the kernel is included below. Some functionality requires inline PTX assembly that is currently made available through [libcu++](https://nvidia.github.io/cccl/libcudacxx/ptx.html). The availability of these wrappers can be checked with the following code:
    
    
    #if defined(__CUDA_MINIMUM_ARCH__) && __CUDA_MINIMUM_ARCH__ < 900
    static_assert(false, "Device code is being compiled with older architectures that are incompatible with TMA.");
    #endif // __CUDA_MINIMUM_ARCH__
    

The kernel goes through the following stages:

  1. Initialize shared memory barrier.

  2. Initiate bulk-asynchronous copy of a block of memory from global to shared memory.

  3. Arrive and wait on the shared memory barrier.

  4. Increment the shared memory buffer values.

  5. Wait for shared memory writes to be visible to the subsequent bulk-asynchronous copy, i.e., order the shared memory writes in the [async proxy](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#async-proxy) before the next step.

  6. Initiate bulk-asynchronous copy of the buffer in shared memory to global memory.

  7. Wait at end of kernel for bulk-asynchronous copy to have finished reading shared memory.


    
    
    #include <cuda/barrier>
    #include <cuda/ptx>
    using barrier = cuda::barrier<cuda::thread_scope_block>;
    namespace ptx = cuda::ptx;
    
    static constexpr size_t buf_len = 1024;
    __global__ void add_one_kernel(int* data, size_t offset)
    {
      // Shared memory buffer. The destination shared memory buffer of
      // a bulk operations should be 16 byte aligned.
      __shared__ alignas(16) int smem_data[buf_len];
    
      // 1. a) Initialize shared memory barrier with the number of threads participating in the barrier.
      //    b) Make initialized barrier visible in async proxy.
      #pragma nv_diag_suppress static_var_with_dynamic_init
      __shared__ barrier bar;
      if (threadIdx.x == 0) { 
        init(&bar, blockDim.x);                      // a)
        ptx::fence_proxy_async(ptx::space_shared);   // b)
      }
      __syncthreads();
    
      // 2. Initiate TMA transfer to copy global to shared memory.
      if (threadIdx.x == 0) {
        // 3a. cuda::memcpy_async arrives on the barrier and communicates
        //     how many bytes are expected to come in (the transaction count)
        cuda::memcpy_async(
            smem_data, 
            data + offset, 
            cuda::aligned_size_t<16>(sizeof(smem_data)),
            bar
        );
      }
      // 3b. All threads arrive on the barrier
      barrier::arrival_token token = bar.arrive();
      
      // 3c. Wait for the data to have arrived.
      bar.wait(std::move(token));
    
      // 4. Compute saxpy and write back to shared memory
      for (int i = threadIdx.x; i < buf_len; i += blockDim.x) {
        smem_data[i] += 1;
      }
    
      // 5. Wait for shared memory writes to be visible to TMA engine.
      ptx::fence_proxy_async(ptx::space_shared);   // b)
      __syncthreads();
      // After syncthreads, writes by all threads are visible to TMA engine.
    
      // 6. Initiate TMA transfer to copy shared memory to global memory
      if (threadIdx.x == 0) {
        ptx::cp_async_bulk(
            ptx::space_global,
            ptx::space_shared,
            data + offset, smem_data, sizeof(smem_data));
        // 7. Wait for TMA transfer to have finished reading shared memory.
        // Create a "bulk async-group" out of the previous bulk copy operation.
        ptx::cp_async_bulk_commit_group();
        // Wait for the group to have completed reading from shared memory.
        ptx::cp_async_bulk_wait_group_read(ptx::n32_t<0>());
      }
    }
    

**Barrier initialization**. The barrier is initialized with the number of threads participating in the block. As a result, the barrier will flip only if all threads have arrived on this barrier. Shared memory barriers are described in more detail in [Asynchronous Data Copies using cuda::barrier](#memcpy-async-barrier). To make the initialized barrier visible to subsequent bulk-asynchronous copies, the `fence.proxy.async.shared::cta` instruction is used. This instruction ensures that subsequent bulk-asynchronous copy operations operate on the initialized barrier.

**TMA read**. The bulk-asynchronous copy instruction directs the hardware to copy a large chunk of data into shared memory, and to update the [transaction count](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parallel-synchronization-and-communication-instructions-mbarrier-tracking-async-operations) of the shared memory barrier after completing the read. In general, issuing as few bulk copies with as big a size as possible results in the best performance. Because the copy can be performed asynchronously by the hardware, it is not necessary to split the copy into smaller chunks.

The thread that initiates the bulk-asynchronous copy operation arrives at the barrier using `mbarrier.expect_tx`. This is automatically performed by `cuda::memcpy_async`. This tells the barrier that the thread has arrived and also how many bytes (tx / transactions) are expected to arrive. Only a single thread has to update the expected transaction count. If multiple threads update the transaction count, the expected transaction will be the sum of the updates. The barrier will only flip once all threads have arrived **and** all bytes have arrived. Once the barrier has flipped, the bytes are safe to read from shared memory, both by the threads as well as by subsequent bulk-asynchronous copies. More information about barrier transaction accounting can be found in the [PTX ISA](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parallel-synchronization-and-communication-instructions-mbarrier-tracking-async-operations).

**Barrier wait**. Waiting for the barrier to flip is done using `mbarrier.try_wait`. It can either return true, indicating that the wait is over, or return false, which may mean that the wait timed out. The while loop waits for completion, and retries on time-out.

**SMEM write and sync**. The increment of the buffer values reads and writes to shared memory. To make the writes visible to subsequent bulk-asynchronous copies, the `fence.proxy.async.shared::cta` instruction is used. This orders the writes to shared memory before subsequent reads from bulk-asynchronous copy operations, which read through the async proxy. So each thread first orders the writes to objects in shared memory in the async proxy via the `fence.proxy.async.shared::cta`, and these operations by all threads are ordered before the async operation performed in thread 0 using `__syncthreads()`.

**TMA write and sync**. The write from shared to global memory is again initiated by a single thread. The completion of the write is not tracked by a shared memory barrier. Instead, a thread-local mechanism is used. Multiple writes can be batched into a so-called _bulk async-group_. Afterwards, the thread can wait for all operations in this group to have completed reading from shared memory (as in the code above) or to have completed writing to global memory, making the writes visible to the initiating thread. For more information, refer to the PTX ISA documentation of [cp.async.bulk.wait_group](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-wait-group). Note that the bulk-asynchronous and non-bulk asynchronous copy instructions have different async-groups: there exist both `cp.async.wait_group` and `cp.async.bulk.wait_group` instructions.

The bulk-asynchronous instructions have specific alignment requirements on their source and destination addresses. More information can be found in the table below.

Table 9 Alignment requirements for one-dimensional bulk-asynchronous operations in Compute Capability 9.0. Address / Size | Alignment  
---|---  
Global memory address | Must be 16 byte aligned.  
Shared memory address | Must be 16 byte aligned.  
Shared memory barrier address | Must be 8 byte aligned (this is guaranteed by `cuda::barrier`).  
Size of transfer | Must be a multiple of 16 bytes.  
  
###  10.29.2. Using TMA to transfer multi-dimensional arrays 

The primary difference between the one-dimensional and multi-dimensional case is that a tensor map must be created on the host and passed to the CUDA kernel. This section describes how to create a tensor map using the CUDA driver API, how to pass it to device, and how to use it on device.

**Driver API**. A tensor map is created using the [cuTensorMapEncodeTiled](https://docs.nvidia.com/cuda/cuda-driver-api/group__CUDA__TENSOR__MEMORY.html) driver API. This API can be accessed by linking to the driver directly (`-lcuda`) or by using the [cudaGetDriverEntryPointByVersion](https://docs.nvidia.com/cuda/cuda-runtime-api/group__CUDART__DRIVER__ENTRY__POINT.html) API. Below, we show how to get a pointer to the `cuTensorMapEncodeTiled` API. For more information, refer to [Driver Entry Point Access](#driver-entry-point-access).
    
    
    #include <cudaTypedefs.h> // PFN_cuTensorMapEncodeTiled, CUtensorMap
    
    PFN_cuTensorMapEncodeTiled_v12000 get_cuTensorMapEncodeTiled() {
      // Get pointer to cuTensorMapEncodeTiled
      cudaDriverEntryPointQueryResult driver_status;
      void* cuTensorMapEncodeTiled_ptr = nullptr;
      CUDA_CHECK(cudaGetDriverEntryPointByVersion("cuTensorMapEncodeTiled", &cuTensorMapEncodeTiled_ptr, 12000, cudaEnableDefault, &driver_status));
      assert(driver_status == cudaDriverEntryPointSuccess);
    
      return reinterpret_cast<PFN_cuTensorMapEncodeTiled_v12000>(cuTensorMapEncodeTiled_ptr);
    }
    

**Creation**. Creating a tensor map requires many parameters. Among them are the base pointer to an array in global memory, the size of the array (in number of elements), the stride from one row to the next (in bytes), the size of the shared memory buffer (in number of elements). The code below creates a tensor map to describe a two-dimensional row-major array of size `GMEM_HEIGHT x GMEM_WIDTH`. Note the order of the parameters: the fastest moving dimension comes first.
    
    
      CUtensorMap tensor_map{};
      // rank is the number of dimensions of the array.
      constexpr uint32_t rank = 2;
      uint64_t size[rank] = {GMEM_WIDTH, GMEM_HEIGHT};
      // The stride is the number of bytes to traverse from the first element of one row to the next.
      // It must be a multiple of 16.
      uint64_t stride[rank - 1] = {GMEM_WIDTH * sizeof(int)};
      // The box_size is the size of the shared memory buffer that is used as the
      // destination of a TMA transfer.
      uint32_t box_size[rank] = {SMEM_WIDTH, SMEM_HEIGHT};
      // The distance between elements in units of sizeof(element). A stride of 2
      // can be used to load only the real component of a complex-valued tensor, for instance.
      uint32_t elem_stride[rank] = {1, 1};
    
      // Get a function pointer to the cuTensorMapEncodeTiled driver API.
      auto cuTensorMapEncodeTiled = get_cuTensorMapEncodeTiled();
    
      // Create the tensor descriptor.
      CUresult res = cuTensorMapEncodeTiled(
        &tensor_map,                // CUtensorMap *tensorMap,
        CUtensorMapDataType::CU_TENSOR_MAP_DATA_TYPE_INT32,
        rank,                       // cuuint32_t tensorRank,
        tensor_ptr,                 // void *globalAddress,
        size,                       // const cuuint64_t *globalDim,
        stride,                     // const cuuint64_t *globalStrides,
        box_size,                   // const cuuint32_t *boxDim,
        elem_stride,                // const cuuint32_t *elementStrides,
        // Interleave patterns can be used to accelerate loading of values that
        // are less than 4 bytes long.
        CUtensorMapInterleave::CU_TENSOR_MAP_INTERLEAVE_NONE,
        // Swizzling can be used to avoid shared memory bank conflicts.
        CUtensorMapSwizzle::CU_TENSOR_MAP_SWIZZLE_NONE,
        // L2 Promotion can be used to widen the effect of a cache-policy to a wider
        // set of L2 cache lines.
        CUtensorMapL2promotion::CU_TENSOR_MAP_L2_PROMOTION_NONE,
        // Any element that is outside of bounds will be set to zero by the TMA transfer.
        CUtensorMapFloatOOBfill::CU_TENSOR_MAP_FLOAT_OOB_FILL_NONE
      );
    

**Host-to-device transfer**. There are three ways to make a tensor map accessible to device code. The recommended approach is to pass the tensor map as a const `__grid_constant__` parameter to a kernel. The other possibilities are copying the tensor map into device `__constant__` memory using `cudaMemcpyToSymbol` or accessing it via global memory. When passing the tensor map as a parameter, some versions of the GCC C++ compiler issue the warning “the ABI for passing parameters with 64-byte alignment has changed in GCC 4.6”. This warning can be ignored.
    
    
    #include <cuda.h>
    
    __global__ void kernel(const __grid_constant__ CUtensorMap tensor_map)
    {
       // Use tensor_map here.
    }
    int main() {
      CUtensorMap map;
      // [ ..Initialize map.. ]
      kernel<<<1, 1>>>(map);
    }
    

As an alternative to the `__grid_constant__` kernel parameter, a global [constant](#constant) variable can be used. An example is included below.
    
    
    #include <cuda.h>
    
    __constant__ CUtensorMap global_tensor_map;
    __global__ void kernel()
    {
      // Use global_tensor_map here.
    }
    int main() {
      CUtensorMap local_tensor_map;
      // [ ..Initialize map.. ]
      cudaMemcpyToSymbol(global_tensor_map, &local_tensor_map, sizeof(CUtensorMap));
      kernel<<<1, 1>>>();
    }
    

Finally, it is possible to copy the tensor map to global memory. Using a pointer to a tensor map in global device memory requires a fence in each thread block before any thread in the block uses the updated tensor map. Further uses of the tensor map by that thread block do not need to be fenced unless the tensor map is modified again. Note that this mechanism may be slower than the two mechanisms described above.
    
    
    #include <cuda.h>
    #include <cuda/ptx>
    namespace ptx = cuda::ptx;
    
    __device__ CUtensorMap global_tensor_map;
    __global__ void kernel(CUtensorMap *tensor_map)
    {
      // Fence acquire tensor map:
      ptx::n32_t<128> size_bytes;
      // Since the tensor map was modified from the host using cudaMemcpy,
      // the scope should be .sys.
      ptx::fence_proxy_tensormap_generic(
         ptx::sem_acquire, ptx::scope_sys, tensor_map, size_bytes
     );
     // Safe to use tensor_map after fence inside this thread..
    }
    int main() {
      CUtensorMap local_tensor_map;
      // [ ..Initialize map.. ]
      cudaMemcpy(&global_tensor_map, &local_tensor_map, sizeof(CUtensorMap), cudaMemcpyHostToDevice);
      kernel<<<1, 1>>>(global_tensor_map);
    }
    

**Use**. The kernel below loads a 2D tile of size `SMEM_HEIGHT x SMEM_WIDTH` from a larger 2D array. The top-left corner of the tile is indicated by the indices `x` and `y`. The tile is loaded into shared memory, modified, and written back to global memory.
    
    
    #include <cuda.h>         // CUtensormap
    #include <cuda/barrier>
    using barrier = cuda::barrier<cuda::thread_scope_block>;
    namespace cde = cuda::device::experimental;
    
    __global__ void kernel(const __grid_constant__ CUtensorMap tensor_map, int x, int y) {
      // The destination shared memory buffer of a bulk tensor operation should be
      // 128 byte aligned.
      __shared__ alignas(128) int smem_buffer[SMEM_HEIGHT][SMEM_WIDTH];
    
      // Initialize shared memory barrier with the number of threads participating in the barrier.
      #pragma nv_diag_suppress static_var_with_dynamic_init
      __shared__ barrier bar;
    
      if (threadIdx.x == 0) {
        // Initialize barrier. All `blockDim.x` threads in block participate.
        init(&bar, blockDim.x);
        // Make initialized barrier visible in async proxy.
        cde::fence_proxy_async_shared_cta();
      }
      // Syncthreads so initialized barrier is visible to all threads.
      __syncthreads();
    
      barrier::arrival_token token;
      if (threadIdx.x == 0) {
        // Initiate bulk tensor copy.
        cde::cp_async_bulk_tensor_2d_global_to_shared(&smem_buffer, &tensor_map, x, y, bar);
        // Arrive on the barrier and tell how many bytes are expected to come in.
        token = cuda::device::barrier_arrive_tx(bar, 1, sizeof(smem_buffer));
      } else {
        // Other threads just arrive.
        token = bar.arrive();
      }
      // Wait for the data to have arrived.
      bar.wait(std::move(token));
    
      // Symbolically modify a value in shared memory.
      smem_buffer[0][threadIdx.x] += threadIdx.x;
    
      // Wait for shared memory writes to be visible to TMA engine.
      cde::fence_proxy_async_shared_cta();
      __syncthreads();
      // After syncthreads, writes by all threads are visible to TMA engine.
    
      // Initiate TMA transfer to copy shared memory to global memory
      if (threadIdx.x == 0) {
        cde::cp_async_bulk_tensor_2d_shared_to_global(&tensor_map, x, y, &smem_buffer);
        // Wait for TMA transfer to have finished reading shared memory.
        // Create a "bulk async-group" out of the previous bulk copy operation.
        cde::cp_async_bulk_commit_group();
        // Wait for the group to have completed reading from shared memory.
        cde::cp_async_bulk_wait_group_read<0>();
      }
    
      // Destroy barrier. This invalidates the memory region of the barrier. If
      // further computations were to take place in the kernel, this allows the
      // memory location of the shared memory barrier to be reused.
      if (threadIdx.x == 0) {
        (&bar)->~barrier();
      }
    }
    

**Negative indices and out of bounds**. When part of the tile that is being _read_ from global to shared memory is out of bounds, the shared memory that corresponds to the out of bounds area is zero-filled. The top-left corner indices of the tile may also be negative. When _writing_ from shared to global memory, parts of the tile may be out of bounds, but the top left corner cannot have any negative indices.

**Size and stride**. The size of a tensor is the number of elements along one dimension. All sizes must be greater than one. The stride is the number of bytes between elements of the same dimension. For instance, a 4 x 4 matrix of integers has sizes 4 and 4. Since it has 4 bytes per element, the strides are 4 and 16 bytes. Due to alignment requirements, a 4 x 3 row-major matrix of integers must have strides of 4 and 16 bytes as well. Each row is padded with 4 extra bytes to ensure that the start of the next row is aligned to 16 bytes. For more information regarding alignment, refer to [Table 10](#table-alignment-multi-dim-tma).

Table 10 Alignment requirements for multi-dimensional bulk tensor asynchronous copy operations in Compute Capability 9.0. Address / Size | Alignment  
---|---  
Global memory address | Must be 16 byte aligned.  
Global memory sizes | Must be greater than or equal to one. Does not have to be a multiple of 16 bytes.  
Global memory strides | Must be multiples of 16 bytes.  
Shared memory address | Must be 128 byte aligned.  
Shared memory barrier address | Must be 8 byte aligned (this is guaranteed by `cuda::barrier`).  
Size of transfer | Must be a multiple of 16 bytes.  
  
####  10.29.2.1. Multi-dimensional TMA PTX wrappers 

Below, the PTX instructions are ordered by their use in the example code above.

The [cp.async.bulk.tensor](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor) instructions initiate a bulk tensor asynchronous copy between global and shared memory. The wrappers below read from global to shared memory and write from shared to global memory.
    
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_1d_global_to_shared(
        void *dest, const CUtensorMap *tensor_map , int c0, cuda::barrier<cuda::thread_scope_block> &bar
    );
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_2d_global_to_shared(
        void *dest, const CUtensorMap *tensor_map , int c0, int c1, cuda::barrier<cuda::thread_scope_block> &bar
    );
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_3d_global_to_shared(
        void *dest, const CUtensorMap *tensor_map, int c0, int c1, int c2, cuda::barrier<cuda::thread_scope_block> &bar
    );
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_4d_global_to_shared(
        void *dest, const CUtensorMap *tensor_map , int c0, int c1, int c2, int c3, cuda::barrier<cuda::thread_scope_block> &bar
    );
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_5d_global_to_shared(
        void *dest, const CUtensorMap *tensor_map , int c0, int c1, int c2, int c3, int c4, cuda::barrier<cuda::thread_scope_block> &bar
    );
    
    
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_1d_shared_to_global(
        const CUtensorMap *tensor_map, int c0, const void *src
    );
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_2d_shared_to_global(
        const CUtensorMap *tensor_map, int c0, int c1, const void *src
    );
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_3d_shared_to_global(
        const CUtensorMap *tensor_map, int c0, int c1, int c2, const void *src
    );
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_4d_shared_to_global(
        const CUtensorMap *tensor_map, int c0, int c1, int c2, int c3, const void *src
    );
    
    // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-cp-async-bulk-tensor
    inline __device__
    void cuda::device::experimental::cp_async_bulk_tensor_5d_shared_to_global(
        const CUtensorMap *tensor_map, int c0, int c1, int c2, int c3, int c4, const void *src
    );
    

###  10.29.3. TMA Swizzle 

By default, the TMA engine loads data to shared memory in the same order as it is laid out in global memory. However, this layout may not be optimal for certain shared memory access patterns, as it could cause shared memory bank conflicts. To improve performance and reduce bank conflicts, we can change the shared memory layout by applying a ‘swizzle pattern’.

Shared memory has 32 banks that are organized such that successive 32-bit words map to successive banks. Each bank has a bandwidth of 32 bits per clock cycle. When loading and storing shared memory, bank conflicts arise if the same bank is used multiple times within a transaction, resulting in reduced bandwidth. See [Shared Memory](#shared-memory-5-x), bank conflicts.

To ensure that data is laid out in shared memory in such a way that user code can avoid shared memory bank conflicts, the TMA engine can be instructed to ‘swizzle’ the data before storing it in shared memory and ‘unswizzle’ it when copying the data back from shared memory to global memory. The tensor map encodes the ‘swizzle mode’ indicating which swizzle pattern is used.

####  10.29.3.1. Example ‘Matrix Transpose’ 

An example is the transpose of a matrix where data is mapped from row to column first access. The data is stored row major in global memory, but we want to also access it column wise in shared memory, which leads to bank conflicts. However, by using the 128 bytes ‘swizzle’ mode and new shared memory indices, they are eliminated.

In the example, we load an 8x8 matrix of type `int4`, stored as row major in global memory to shared memory. Then, each set of eight threads loads a row from the shared memory buffer and stores it to a column in a separate transpose shared memory buffer. This results in an eight-way bank conflict when storing. Finally, the transpose buffer is written back to global memory.

To avoid bank conflicts, the `CU_TENSOR_MAP_SWIZZLE_128B` layout can be used. This layout matches the 128 bytes row length and changes the shared memory layout in a way that both the column wise and row wise access don’t require the same banks per transaction.

The two tables, [Figure 27](#figure-swizzle-example1) and [Figure 28](#figure-swizzle-example2), below show the normal and the swizzled shared memory layout of the 8x8 matrix of type `int4` and its transpose matrix. The colors indicate which of the eight groups of four banks the matrix element is mapped to, and the margin row and margin column list the global memory row and column indices. The entries show the shared memory indices of the 16-byte matrix elements.

[![The shared memory data layout without swizzle](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/example1.png)](_images/example1.png)

Figure 27 In the shared memory data layout without swizzle, the shared memory indices are equivalent to the global memory indices. Per load instruction, one row is read and stored in a column of the transpose buffer. Since all matrix elements of the column in the transpose fall in the same bank, the store must be serialized, resulting in eight store transactions, giving an eight-way bank conflict per stored column.

[![The shared memory data layout with CU_TENSOR_MAP_SWIZZLE_128B swizzle.](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/example2.png)](_images/example2.png)

Figure 28 The shared memory data layout with `CU_TENSOR_MAP_SWIZZLE_128B` swizzle. One row is stored in a column, each matrix element is from a different bank for both the rows and columns, and so without any bank conflicts.
    
    
    __global__ void kernel_tma(const __grid_constant__ CUtensorMap tensor_map) {
       // The destination shared memory buffer of a bulk tensor operation
       // with the 128-byte swizzle mode, it should be 1024 bytes aligned.
       __shared__ alignas(1024) int4 smem_buffer[8][8];
       __shared__ alignas(1024) int4 smem_buffer_tr[8][8];
    
       // Initialize shared memory barrier
       #pragma nv_diag_suppress static_var_with_dynamic_init
       __shared__ barrier bar;
    
       if (threadIdx.x == 0) {
         init(&bar, blockDim.x);
         cde::fence_proxy_async_shared_cta();
       }
    
       __syncthreads();
    
       barrier::arrival_token token;
       if (threadIdx.x == 0) {
         // Initiate bulk tensor copy from global to shared memory,
         // in the same way as without swizzle.
         cde::cp_async_bulk_tensor_2d_global_to_shared(&smem_buffer, &tensor_map, 0, 0, bar);
         token = cuda::device::barrier_arrive_tx(bar, 1, sizeof(smem_buffer));
       } else {
         token = bar.arrive();
       }
    
       bar.wait(std::move(token));
    
       /* Matrix transpose
        *  When using the normal shared memory layout, there are eight
        *  8-way shared memory bank conflict when storing to the transpose.
        *  When enabling the 128-byte swizzle pattern and using the according access pattern,
        *  they are eliminated both for load and store. */
       for(int sidx_j =threadIdx.x; sidx_j < 8; sidx_j+= blockDim.x){
          for(int sidx_i = 0; sidx_i < 8; ++sidx_i){
             const int swiz_j_idx = (sidx_i % 8) ^ sidx_j;
             const int swiz_i_idx_tr = (sidx_j % 8) ^ sidx_i;
             smem_buffer_tr[sidx_j][swiz_i_idx_tr] = smem_buffer[sidx_i][swiz_j_idx];
          }
       }
    
       // Wait for shared memory writes to be visible to TMA engine.
       cde::fence_proxy_async_shared_cta();
       __syncthreads();
    
       /* Initiate TMA transfer to copy the transposed shared memory buffer back to global memory,
        * it will 'unswizzle' the data. */
       if (threadIdx.x == 0) {
          cde::cp_async_bulk_tensor_2d_shared_to_global(&tensor_map, 0, 0, &smem_buffer_tr);
          cde::cp_async_bulk_commit_group();
          cde::cp_async_bulk_wait_group_read<0>();
       }
    
       // Destroy barrier
       if (threadIdx.x == 0) {
         (&bar)->~barrier();
       }
    }
    
    // --------------------------------- main ----------------------------------------
    
    int main(){
    
    ...
       void* tensor_ptr = d_data;
    
       CUtensorMap tensor_map{};
       // rank is the number of dimensions of the array.
       constexpr uint32_t rank = 2;
       // global memory size
       uint64_t size[rank] = {4*8, 8};
       // global memory stride, must be a multiple of 16.
       uint64_t stride[rank - 1] = {8 * sizeof(int4)};
       // The inner shared memory box dimension in bytes, equal to the swizzle span.
       uint32_t box_size[rank] = {4*8, 8};
    
       uint32_t elem_stride[rank] = {1, 1};
    
       // Create the tensor descriptor.
       CUresult res = cuTensorMapEncodeTiled(
           &tensor_map,                // CUtensorMap *tensorMap,
           CUtensorMapDataType::CU_TENSOR_MAP_DATA_TYPE_INT32,
           rank,                       // cuuint32_t tensorRank,
           tensor_ptr,                 // void *globalAddress,
           size,                       // const cuuint64_t *globalDim,
           stride,                     // const cuuint64_t *globalStrides,
           box_size,                   // const cuuint32_t *boxDim,
           elem_stride,                // const cuuint32_t *elementStrides,
           CUtensorMapInterleave::CU_TENSOR_MAP_INTERLEAVE_NONE,
           // Using a swizzle pattern of 128 bytes.
           CUtensorMapSwizzle::CU_TENSOR_MAP_SWIZZLE_128B,
           CUtensorMapL2promotion::CU_TENSOR_MAP_L2_PROMOTION_NONE,
           CUtensorMapFloatOOBfill::CU_TENSOR_MAP_FLOAT_OOB_FILL_NONE
       );
    
       kernel_tma<<<1, 8>>>(tensor_map);
     ...
    }
    

**Remark.** This example is supposed to show the use of swizzle and ‘as-is’ is not performant nor does it scale beyond the given dimensions.

**Explanation.** During data transfer, the TMA engine shuffles the data according to the swizzle pattern, as described in the following tables. These swizzle patterns define the mapping of the 16-byte chunks along the swizzle width to subgroups of four banks. It is of type `CUtensorMapSwizzle` and has four options: none, 32 bytes, 64 bytes and 128 bytes. Note that the shared memory box’s inner dimension must be less or equal to the span of the swizzle pattern.

####  10.29.3.2. The Swizzle Modes 

As previously mentioned, there are four swizzle modes. The following tables show the different swizzle patterns, including the relation of the new shared memory indices. The tables define the mapping of the 16-byte chunks along the 128 bytes to eight subgroups of four banks.

[![An Overview of TMA Swizzle Patterns](https://docs.nvidia.com/cuda/cuda-c-programming-guide/_images/swizzle-pattern.png)](_images/swizzle-pattern.png)

Figure 29 An Overview of TMA Swizzle Patterns

**Considerations.** When applying a TMA swizzle pattern, it is crucial to adhere to specific memory requirements:

  * **Global memory alignment:** Global memory must be aligned to 128 bytes.

  * **Shared memory alignment:** For simplicity shared memory should be aligned according to the number of bytes after which the swizzle pattern repeats. When the shared memory buffer is not aligned by the number of bytes by which the swizzle pattern repeats itself, there is an offset between the swizzle pattern and the shared memory. See [comment](#swizzle-pattern-pointer-offset-computation), below.

  * **Inner dimension:** The inner dimension of the shared memory block must meet the size requirements specified in [Table 12](#table-swizzle-pattern-properties-and-requirements). If these requirements are not met, the instruction is considered invalid. Additionally, if the swizzle width exceeds the inner dimension, ensure that the shared memory is allocated to accommodate the full swizzle width.

  * **Granularity:** The granularity of swizzle mapping is fixed at 16 bytes. This means that data is organized and accessed in chunks of 16 bytes, which must be considered when planning memory layout and access patterns.


**Swizzle Pattern Pointer Offset Computation**. Here, we describe how to determine the offset between the swizzle pattern and the shared memory, when the shared memory buffer is not aligned by the number of bytes by which the swizzle pattern repeats itself. When using TMA, the shared memory is required to be aligned to 128 bytes. To find how many times the shared memory buffer relative to the swizzle pattern is shifted by that, apply the corresponding offset formula.

Table 11 Swizzle Pattern Pointer Offset Formula and Index Relation Swizzle Mode | Offset Formula | Index Relation  
---|---|---  
CU_TENSOR_MAP_SWIZZLE_128B | `(reinterpret_cast <uintptr_t>(smem_ptr)/128)%8` | `smem[y][x] <-> smem[y][((y+offset)%8)^x]`  
CU_TENSOR_MAP_SWIZZLE_64B | `(reinterpret_cast <uintptr_t>(smem_ptr)/128)%4` | `smem[y][x] <-> smem[y][((y+offset)%4)^x]`  
CU_TENSOR_MAP_SWIZZLE_32B | `(reinterpret_cast <uintptr_t>(smem_ptr)/128)%2` | `smem[y][x] <-> smem[y][((y+offset)%2)^x]`  
  
In [Figure 29](#figure-swizzle-overview), this offset represents the initial row offset, thus, in the swizzle index calculation, it is added to the row index `y`. The following snippet shows how to access the swizzled shared memory in the `CU_TENSOR_MAP_SWIZZLE_128B` mode.
    
    
    data_t* smem_ptr = &smem[0][0];
    int offset = (reinterpret_cast<uintptr_t>(smem_ptr)/128)%8;
    smem[y][((y+offset)%8)^x] = ...
    

**Summary.** The following [Table 12](#table-swizzle-pattern-properties-and-requirements) summarizes the requirements and properties of the different swizzle patterns for Compute Capability 9.

Table 12 Requirements and properties of the different swizzle patterns for Compute Capability 9 Pattern | Swizzle width | Shared box’s inner dimension | Repeats after | Shared memory alignment | Global memory alignment  
---|---|---|---|---|---  
CU_TENSOR_MAP_SWIZZLE_128B | 128 bytes | <=128 bytes | 1024 bytes | 128 bytes | 128 bytes  
CU_TENSOR_MAP_SWIZZLE_64B | 64 bytes | <=64 bytes | 512 bytes | 128 bytes | 128 bytes  
CU_TENSOR_MAP_SWIZZLE_32B | 32 bytes | <=32 bytes | 256 bytes | 128 bytes | 128 bytes  
CU_TENSOR_MAP_SWIZZLE_NONE (default) |  |  |  | 128 bytes | 16 bytes


##  10.30. Encoding a Tensor Map on Device   
  
Previous sections have described how to create a tensor map on the host using the CUDA driver API.

This section explains how to encode a tiled-type tensor map on device. This is useful in situations where the typical way of transferring the tensor map (using `const __grid_constant__` kernel parameters) is undesirable, for instance, when processing a batch of tensors of various sizes in a single kernel launch.

The recommended pattern is as follows:

  1. Create a tensor map “template”, `template_tensor_map`, using the Driver API on the host.

  2. In a device kernel, copy the `template_tensor_map`, modify the copy, store in global memory, and appropriately fence.

  3. Use the tensor map in a kernel with appropriate fencing.


The high-level code structure is as follows:
    
    
    // Initialize device context:
    CUDA_CHECK(cudaDeviceSynchronize());
    
    // Create a tensor map template using the cuTensorMapEncodeTiled driver function
    CUtensorMap template_tensor_map = make_tensormap_template();
    
    // Allocate tensor map and tensor in global memory
    CUtensorMap* global_tensor_map;
    CUDA_CHECK(cudaMalloc(&global_tensor_map, sizeof(CUtensorMap)));
    char* global_buf;
    CUDA_CHECK(cudaMalloc(&global_buf, 8 * 256));
    
    // Fill global buffer with data.
    fill_global_buf<<<1, 1>>>(global_buf);
    
    // Define the parameters of the tensor map that will be created on device.
    tensormap_params p{};
    p.global_address    = global_buf;
    p.rank              = 2;
    p.box_dim[0]        = 128; // The box in shared memory has half the width of the full buffer
    p.box_dim[1]        = 4;   // The box in shared memory has half the height of the full buffer
    p.global_dim[0]     = 256; //
    p.global_dim[1]     = 8;   //
    p.global_stride[0]  = 256; //
    p.element_stride[0] = 1;   //
    p.element_stride[1] = 1;   //
    
    // Encode global_tensor_map on device:
    encode_tensor_map<<<1, 32>>>(template_tensor_map, p, global_tensor_map);
    
    // Use it from another kernel:
    consume_tensor_map<<<1, 1>>>(global_tensor_map);
    
    // Check for errors:
    CUDA_CHECK(cudaDeviceSynchronize());
    

The following sections describe the high-level steps. Throughout the examples, the following `tensormap_params` struct contains the new values of the fields to be updated. It is included here to reference when reading the examples.
    
    
    struct tensormap_params {
      void* global_address;
      int rank;
      uint32_t box_dim[5];
      uint64_t global_dim[5];
      size_t global_stride[4];
      uint32_t element_stride[5];
    };
    

###  10.30.1. Device-side Encoding and Modification of a Tensor Map 

The recommended process of encoding a tensor map in global memory proceeds as follows.

  1. Pass an existing tensor map, the `template_tensor_map`, to the kernel. In contrast to kernels that use the tensor map in a `cp.async.bulk.tensor` instruction, this may be done in any way: a pointer to global memory, kernel parameter, a `__const___` variable, and so on.

  2. Copy-initialize a tensor map in shared memory with the template_tensor_map value.

  3. Modify the tensor map in shared memory using the [cuda::ptx::tensormap_replace](https://nvidia.github.io/cccl/libcudacxx/ptx/instructions/tensormap.replace.html) functions. These functions wrap the [tensormap.replace](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-tensormap-replace) PTX instruction, which can be used to modify any field of a tiled-type tensor map, including the base address, size, stride, and so on.

  4. Using the [cuda::ptx::tensormap_copy_fenceproxy](https://nvidia.github.io/cccl/libcudacxx/ptx/instructions/tensormap.cp_fenceproxy.html#tensormap-cp-fenceproxy) function, copy the modified tensor map from shared memory to global memory and perform any necessary fencing.


The following code contains a kernel that follows these steps. For completeness, it modifies all the fields of the tensor map. Typically, a kernel will modify just a few fields.

In this kernel, `template_tensor_map` is passed as a kernel parameter. This is the preferred way of moving `template_tensor_map` from the host to the device. If the kernel is intended to update an existing tensor map in device memory, it can take a pointer to the existing tensor map to modify.

Note

The format of the tensor map may change over time. Therefore, the [cuda::ptx::tensormap_replace](https://nvidia.github.io/cccl/libcudacxx/ptx/instructions/tensormap.replace.html) functions and corresponding [tensormap.replace.tile](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#data-movement-and-conversion-instructions-tensormap-replace) PTX instructions are marked as specific to sm_90a. To use them, compile using `nvcc -arch sm_90a ....`.

Tip

On sm_90a, a zero-initialized buffer in shared memory may also be used as the initial tensor map value. This enables encoding a tensor map purely on device, without using the driver API to encode the `template_tensor_map value`.

Note

On-device modification is only supported for tiled-type tensor maps; other tensor map types cannot be modified on device. For more information on the tensor map types, refer to the [Driver API reference](https://docs.nvidia.com/cuda/cuda-driver-api/group__CUDA__TENSOR__MEMORY.html#group__CUDA__TENSOR__MEMORY).
    
    
    #include <cuda/ptx>
    
    namespace ptx = cuda::ptx;
    
    // launch with 1 warp.
    __launch_bounds__(32)
    __global__ void encode_tensor_map(const __grid_constant__ CUtensorMap template_tensor_map, tensormap_params p, CUtensorMap* out) {
       __shared__ alignas(128) CUtensorMap smem_tmap;
       if (threadIdx.x == 0) {
          // Copy template to shared memory:
          smem_tmap = template_tensor_map;
    
          const auto space_shared = ptx::space_shared;
          ptx::tensormap_replace_global_address(space_shared, &smem_tmap, p.global_address);
          // For field .rank, the operand new_val must be ones less than the desired
          // tensor rank as this field uses zero-based numbering.
          ptx::tensormap_replace_rank(space_shared, &smem_tmap, p.rank - 1);
    
          // Set box dimensions:
          if (0 < p.rank) { ptx::tensormap_replace_box_dim(space_shared, &smem_tmap, ptx::n32_t<0>{}, p.box_dim[0]); }
          if (1 < p.rank) { ptx::tensormap_replace_box_dim(space_shared, &smem_tmap, ptx::n32_t<1>{}, p.box_dim[1]); }
          if (2 < p.rank) { ptx::tensormap_replace_box_dim(space_shared, &smem_tmap, ptx::n32_t<2>{}, p.box_dim[2]); }
          if (3 < p.rank) { ptx::tensormap_replace_box_dim(space_shared, &smem_tmap, ptx::n32_t<3>{}, p.box_dim[3]); }
          if (4 < p.rank) { ptx::tensormap_replace_box_dim(space_shared, &smem_tmap, ptx::n32_t<4>{}, p.box_dim[4]); }
          // Set global dimensions:
          if (0 < p.rank) { ptx::tensormap_replace_global_dim(space_shared, &smem_tmap, ptx::n32_t<0>{}, (uint32_t) p.global_dim[0]); }
          if (1 < p.rank) { ptx::tensormap_replace_global_dim(space_shared, &smem_tmap, ptx::n32_t<1>{}, (uint32_t) p.global_dim[1]); }
          if (2 < p.rank) { ptx::tensormap_replace_global_dim(space_shared, &smem_tmap, ptx::n32_t<2>{}, (uint32_t) p.global_dim[2]); }
          if (3 < p.rank) { ptx::tensormap_replace_global_dim(space_shared, &smem_tmap, ptx::n32_t<3>{}, (uint32_t) p.global_dim[3]); }
          if (4 < p.rank) { ptx::tensormap_replace_global_dim(space_shared, &smem_tmap, ptx::n32_t<4>{}, (uint32_t) p.global_dim[4]); }
          // Set global stride:
          if (1 < p.rank) { ptx::tensormap_replace_global_stride(space_shared, &smem_tmap, ptx::n32_t<0>{}, p.global_stride[0]); }
          if (2 < p.rank) { ptx::tensormap_replace_global_stride(space_shared, &smem_tmap, ptx::n32_t<1>{}, p.global_stride[1]); }
          if (3 < p.rank) { ptx::tensormap_replace_global_stride(space_shared, &smem_tmap, ptx::n32_t<2>{}, p.global_stride[2]); }
          if (4 < p.rank) { ptx::tensormap_replace_global_stride(space_shared, &smem_tmap, ptx::n32_t<3>{}, p.global_stride[3]); }
          // Set element stride:
          if (0 < p.rank) { ptx::tensormap_replace_element_size(space_shared, &smem_tmap, ptx::n32_t<0>{}, p.element_stride[0]); }
          if (1 < p.rank) { ptx::tensormap_replace_element_size(space_shared, &smem_tmap, ptx::n32_t<1>{}, p.element_stride[1]); }
          if (2 < p.rank) { ptx::tensormap_replace_element_size(space_shared, &smem_tmap, ptx::n32_t<2>{}, p.element_stride[2]); }
          if (3 < p.rank) { ptx::tensormap_replace_element_size(space_shared, &smem_tmap, ptx::n32_t<3>{}, p.element_stride[3]); }
          if (4 < p.rank) { ptx::tensormap_replace_element_size(space_shared, &smem_tmap, ptx::n32_t<4>{}, p.element_stride[4]); }
    
          // These constants are documented in this table:
          // https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#tensormap-new-val-validity
          auto u8_elem_type = ptx::n32_t<0>{};
          ptx::tensormap_replace_elemtype(space_shared, &smem_tmap, u8_elem_type);
          auto no_interleave = ptx::n32_t<0>{};
          ptx::tensormap_replace_interleave_layout(space_shared, &smem_tmap, no_interleave);
          auto no_swizzle = ptx::n32_t<0>{};
          ptx::tensormap_replace_swizzle_mode(space_shared, &smem_tmap, no_swizzle);
          auto zero_fill = ptx::n32_t<0>{};
          ptx::tensormap_replace_fill_mode(space_shared, &smem_tmap, zero_fill);
       }
       // Synchronize the modifications with other threads in warp
       __syncwarp();
       // Copy the tensor map to global memory collectively with threads in the warp.
       // In addition: make the updated tensor map visible to other threads on device that
       // for use with cp.async.bulk.
       ptx::n32_t<128> bytes_128;
       ptx::tensormap_cp_fenceproxy(ptx::sem_release, ptx::scope_gpu, out, &smem_tmap, bytes_128);
    }
    

###  10.30.2. Usage of a Modified Tensor Map 

In contrast to using a tensor map that is passed as a `const __grid_constant__` kernel parameter, using a tensor map in global memory requires explicitly establishing a release-acquire pattern in the tensor map proxy between the threads that modify the tensor map and the threads that use it.

The release part of the pattern was shown in the previous section. It is accomplished using the [cuda::ptx::tensormap.cp_fenceproxy](https://nvidia.github.io/cccl/libcudacxx/ptx/instructions/tensormap.cp_fenceproxy.html) function.

The acquire part is accomplished using the [cuda::ptx::fence_proxy_tensormap_generic](https://nvidia.github.io/cccl/libcudacxx/ptx/instructions/fence.html) function that wraps the [fence.proxy.tensormap::generic.acquire](https://docs.nvidia.com/cuda/parallel-thread-execution/index.html#parallel-synchronization-and-communication-instructions-membar-fence) instruction. If the two threads participating in the release-acquire pattern are on the same device, the `.gpu` scope suffices. If the threads are on different devices, the `.sys` scope must be used. Once a tensor map has been acquired by one thread, it can be used by other threads in the block after sufficient synchronization, for example, using `__syncthreads()`. The thread that uses the tensor map and the thread that performs the fence must be in the same block. That is, if the threads are in, for example, two different thread blocks of the same cluster, the same grid, or a different kernel, synchronization APIs such as `cooperative_groups::cluster` or `grid_group::sync()` or stream-order synchronization do not suffice to establish ordering for tensor map updates, that is, threads in these other thread blocks still need to acquire the tensor map proxy at the right scope before using the updated tensor map. If there are no intermediate modifications, the fence does not have to be repeated before each `cp.async.bulk.tensor` instruction.

The `fence` and subsequent use of the tensor map is shown in the following example.
    
    
    // Consumer of tensor map in global memory:
    __global__ void consume_tensor_map(CUtensorMap* tensor_map) {
      // Fence acquire tensor map:
      ptx::n32_t<128> size_bytes;
      ptx::fence_proxy_tensormap_generic(ptx::sem_acquire, ptx::scope_sys, tensor_map, size_bytes);
      // Safe to use tensor_map after fence..
    
      __shared__ uint64_t bar;
      __shared__ alignas(128) char smem_buf[4][128];
    
      if (threadIdx.x == 0) {
        // Initialize barrier
        ptx::mbarrier_init(&bar, 1);
        // Make barrier init visible in async proxy, i.e., to TMA engine
        ptx::fence_proxy_async(ptx::space_shared);
        // Issue TMA request
        ptx::cp_async_bulk_tensor(ptx::space_cluster, ptx::space_global, smem_buf, tensor_map, {0, 0}, &bar);
    
        // Arrive on barrier. Expect 4 * 128 bytes.
        ptx::mbarrier_arrive_expect_tx(ptx::sem_release, ptx::scope_cta, ptx::space_shared, &bar, sizeof(smem_buf));
      }
      const int parity = 0;
      // Wait for load to have completed
      while (!ptx::mbarrier_try_wait_parity(&bar, parity)) {}
    
      // print items:
      printf("Got:\n\n");
      for (int j = 0; j < 4; ++j) {
        for (int i = 0; i < 128; ++i) {
          printf("%3d ", smem_buf[j][i]);
          if (i % 32 == 31) { printf("\n"); };
        }
        printf("\n");
      }
    }
    

###  10.30.3. Creating a Template Tensor Map Value Using the Driver API 

The following code creates a minimal tiled-type tensor map that can be subsequently modified on device.
    
    
    CUtensorMap make_tensormap_template() {
      CUtensorMap template_tensor_map{};
      auto cuTensorMapEncodeTiled = get_cuTensorMapEncodeTiled();
    
      uint32_t dims_32         = 16;
      uint64_t dims_strides_64 = 16;
      uint32_t elem_strides    = 1;
    
      // Create the tensor descriptor.
      CUresult res = cuTensorMapEncodeTiled(
        &template_tensor_map, // CUtensorMap *tensorMap,
        CUtensorMapDataType::CU_TENSOR_MAP_DATA_TYPE_UINT8,
        1,                // cuuint32_t tensorRank,
        nullptr,          // void *globalAddress,
        &dims_strides_64, // const cuuint64_t *globalDim,
        &dims_strides_64, // const cuuint64_t *globalStrides,
        &dims_32,         // const cuuint32_t *boxDim,
        &elem_strides,    // const cuuint32_t *elementStrides,
        CUtensorMapInterleave::CU_TENSOR_MAP_INTERLEAVE_NONE,
        CUtensorMapSwizzle::CU_TENSOR_MAP_SWIZZLE_NONE,
        CUtensorMapL2promotion::CU_TENSOR_MAP_L2_PROMOTION_NONE,
        CUtensorMapFloatOOBfill::CU_TENSOR_MAP_FLOAT_OOB_FILL_NONE);
    
      CU_CHECK(res);
      return template_tensor_map;
    }
    


##  10.31. Profiler Counter Function 

Each multiprocessor has a set of sixteen hardware counters that an application can increment with a single instruction by calling the `__prof_trigger()` function.
    
    
    void __prof_trigger(int counter);
    

increments by one per warp the per-multiprocessor hardware counter of index `counter`. Counters 8 to 15 are reserved and should not be used by applications.

The value of counters 0, 1, …, 7 can be obtained via `nvprof` by `nvprof --events prof_trigger_0x` where `x` is 0, 1, …, 7. All counters are reset before each kernel launch (note that when collecting counters, kernel launches are synchronous as mentioned in [Concurrent Execution between Host and Device](#concurrent-execution-host-device)).


##  10.32. Assertion 

Assertion is only supported by devices of compute capability 2.x and higher.
    
    
    void assert(int expression);
    

stops the kernel execution if `expression` is equal to zero. If the program is run within a debugger, this triggers a breakpoint and the debugger can be used to inspect the current state of the device. Otherwise, each thread for which `expression` is equal to zero prints a message to _stderr_ after synchronization with the host via `cudaDeviceSynchronize()`, `cudaStreamSynchronize()`, or `cudaEventSynchronize()`. The format of this message is as follows:
    
    
    <filename>:<line number>:<function>:
    block: [blockId.x,blockId.x,blockIdx.z],
    thread: [threadIdx.x,threadIdx.y,threadIdx.z]
    Assertion `<expression>` failed.
    

Any subsequent host-side synchronization calls made for the same device will return `cudaErrorAssert`. No more commands can be sent to this device until `cudaDeviceReset()` is called to reinitialize the device.

If `expression` is different from zero, the kernel execution is unaffected.

For example, the following program from source file _test.cu_
    
    
    #include <assert.h>
    
    __global__ void testAssert(void)
    {
        int is_one = 1;
        int should_be_one = 0;
    
        // This will have no effect
        assert(is_one);
    
        // This will halt kernel execution
        assert(should_be_one);
    }
    
    int main(int argc, char* argv[])
    {
        testAssert<<<1,1>>>();
        cudaDeviceSynchronize();
    
        return 0;
    }
    

will output:
    
    
    test.cu:19: void testAssert(): block: [0,0,0], thread: [0,0,0] Assertion `should_be_one` failed.
    

Assertions are for debugging purposes. They can affect performance and it is therefore recommended to disable them in production code. They can be disabled at compile time by defining the `NDEBUG` preprocessor macro before including `assert.h`. Note that `expression` should not be an expression with side effects (something like`(++i > 0)`, for example), otherwise disabling the assertion will affect the functionality of the code.


##  10.33. Trap function 

A trap operation can be initiated by calling the `__trap()` function from any device thread.
    
    
    void __trap();
    

The execution of the kernel is aborted and an interrupt is raised in the host program.


##  10.34. Breakpoint Function 

Execution of a kernel function can be suspended by calling the `__brkpt()` function from any device thread.
    
    
    void __brkpt();
    


##  10.35. Formatted Output 

Formatted output is only supported by devices of compute capability 2.x and higher.
    
    
    int printf(const char *format[, arg, ...]);
    

prints formatted output from a kernel to a host-side output stream.

The in-kernel `printf()` function behaves in a similar way to the standard C-library `printf()` function, and the user is referred to the host system’s manual pages for a complete description of `printf()` behavior. In essence, the string passed in as `format` is output to a stream on the host, with substitutions made from the argument list wherever a format specifier is encountered. Supported format specifiers are listed below.

The `printf()` command is executed as any other device-side function: per-thread, and in the context of the calling thread. From a multi-threaded kernel, this means that a straightforward call to `printf()` will be executed by every thread, using that thread’s data as specified. Multiple versions of the output string will then appear at the host stream, once for each thread which encountered the `printf()`.

It is up to the programmer to limit the output to a single thread if only a single output string is desired (see [Examples](#examples-per-thread) for an illustrative example).

Unlike the C-standard `printf()`, which returns the number of characters printed, CUDA’s `printf()` returns the number of arguments parsed. If no arguments follow the format string, 0 is returned. If the format string is NULL, -1 is returned. If an internal error occurs, -2 is returned.

###  10.35.1. Format Specifiers 

As for standard `printf()`, format specifiers take the form: `%[flags][width][.precision][size]type`

The following fields are supported (see widely-available documentation for a complete description of all behaviors):

  * Flags: `'#' ' ' '0' '+' '-'`

  * Width: `'*' '0-9'`

  * Precision: `'0-9'`

  * Size: `'h' 'l' 'll'`

  * Type: `"%cdiouxXpeEfgGaAs"`


Note that CUDA’s `printf()`will accept any combination of flag, width, precision, size and type, whether or not overall they form a valid format specifier. In other words, “`%hd`” will be accepted and printf will expect a double-precision variable in the corresponding location in the argument list.

###  10.35.2. Limitations 

Final formatting of the `printf()`output takes place on the host system. This means that the format string must be understood by the host-system’s compiler and C library. Every effort has been made to ensure that the format specifiers supported by CUDA’s printf function form a universal subset from the most common host compilers, but exact behavior will be host-OS-dependent.

As described in [Format Specifiers](#format-specifiers), `printf()` will accept _all_ combinations of valid flags and types. This is because it cannot determine what will and will not be valid on the host system where the final output is formatted. The effect of this is that output may be undefined if the program emits a format string which contains invalid combinations.

The `printf()` command can accept at most 32 arguments in addition to the format string. Additional arguments beyond this will be ignored, and the format specifier output as-is.

Owing to the differing size of the `long` type on 64-bit Windows platforms (four bytes on 64-bit Windows platforms, eight bytes on other 64-bit platforms), a kernel which is compiled on a non-Windows 64-bit machine but then run on a win64 machine will see corrupted output for all format strings which include “`%ld`”. It is recommended that the compilation platform matches the execution platform to ensure safety.

The output buffer for `printf()` is set to a fixed size before kernel launch (see [Associated Host-Side API](#associated-host-side-api)). It is circular and if more output is produced during kernel execution than can fit in the buffer, older output is overwritten. It is flushed only when one of these actions is performed:

  * Kernel launch via `<<<>>>` or `cuLaunchKernel()` (at the start of the launch, and if the CUDA_LAUNCH_BLOCKING environment variable is set to 1, at the end of the launch as well),

  * Synchronization via `cudaDeviceSynchronize()`, `cuCtxSynchronize()`, `cudaStreamSynchronize()`, `cuStreamSynchronize()`, `cudaEventSynchronize()`, or `cuEventSynchronize()`,

  * Memory copies via any blocking version of `cudaMemcpy*()` or `cuMemcpy*()`,

  * Module loading/unloading via `cuModuleLoad()` or `cuModuleUnload()`,

  * Context destruction via `cudaDeviceReset()` or `cuCtxDestroy()`.

  * Prior to executing a stream callback added by `cudaLaunchHostFunc` or `cuLaunchHostFunc`.


Note that the buffer is not flushed automatically when the program exits. The user must call `cudaDeviceReset()` or `cuCtxDestroy()` explicitly, as shown in the examples below.

Internally `printf()` uses a shared data structure and so it is possible that calling `printf()` might change the order of execution of threads. In particular, a thread which calls `printf()` might take a longer execution path than one which does not call `printf()`, and that path length is dependent upon the parameters of the `printf()`. Note, however, that CUDA makes no guarantees of thread execution order except at explicit `__syncthreads()` barriers, so it is impossible to tell whether execution order has been modified by `printf()` or by other scheduling behavior in the hardware.

###  10.35.3. Associated Host-Side API 

The following API functions get and set the size of the buffer used to transfer the `printf()` arguments and internal metadata to the host (default is 1 megabyte):

  * `cudaDeviceGetLimit(size_t* size,cudaLimitPrintfFifoSize)`

  * `cudaDeviceSetLimit(cudaLimitPrintfFifoSize, size_t size)`


###  10.35.4. Examples 

The following code sample:
    
    
    #include <stdio.h>
    
    __global__ void helloCUDA(float f)
    {
        printf("Hello thread %d, f=%f\n", threadIdx.x, f);
    }
    
    int main()
    {
        helloCUDA<<<1, 5>>>(1.2345f);
        cudaDeviceSynchronize();
        return 0;
    }
    

will output:
    
    
    Hello thread 2, f=1.2345
    Hello thread 1, f=1.2345
    Hello thread 4, f=1.2345
    Hello thread 0, f=1.2345
    Hello thread 3, f=1.2345
    

Notice how each thread encounters the `printf()` command, so there are as many lines of output as there were threads launched in the grid. As expected, global values (i.e., `float f`) are common between all threads, and local values (i.e., `threadIdx.x`) are distinct per-thread.

The following code sample:
    
    
    #include <stdio.h>
    
    __global__ void helloCUDA(float f)
    {
        if (threadIdx.x == 0)
            printf("Hello thread %d, f=%f\n", threadIdx.x, f) ;
    }
    
    int main()
    {
        helloCUDA<<<1, 5>>>(1.2345f);
        cudaDeviceSynchronize();
        return 0;
    }
    

will output:
    
    
    Hello thread 0, f=1.2345
    

Self-evidently, the `if()` statement limits which threads will call `printf`, so that only a single line of output is seen.


##  10.36. Dynamic Global Memory Allocation and Operations 

Dynamic global memory allocation and operations are only supported by devices of compute capability 2.x and higher.
    
    
    __host__ __device__ void* malloc(size_t size);
    __device__ void *__nv_aligned_device_malloc(size_t size, size_t align);
    __host__ __device__  void free(void* ptr);
    

allocate and free memory dynamically from a fixed-size heap in global memory.
    
    
    __host__ __device__ void* memcpy(void* dest, const void* src, size_t size);
    

copy `size` bytes from the memory location pointed by `src` to the memory location pointed by `dest`.
    
    
    __host__ __device__ void* memset(void* ptr, int value, size_t size);
    

set `size` bytes of memory block pointed by `ptr` to `value` (interpreted as an unsigned char).

The CUDA in-kernel `malloc()`function allocates at least `size` bytes from the device heap and returns a pointer to the allocated memory or NULL if insufficient memory exists to fulfill the request. The returned pointer is guaranteed to be aligned to a 16-byte boundary.

The CUDA in-kernel `__nv_aligned_device_malloc()` function allocates at least `size` bytes from the device heap and returns a pointer to the allocated memory or NULL if insufficient memory exists to fulfill the requested size or alignment. The address of the allocated memory will be a multiple of `align`. `align` must be a non-zero power of 2.

The CUDA in-kernel `free()` function deallocates the memory pointed to by `ptr`, which must have been returned by a previous call to `malloc()` or `__nv_aligned_device_malloc()`. If `ptr` is NULL, the call to `free()` is ignored. Repeated calls to `free()` with the same `ptr` has undefined behavior.

The memory allocated by a given CUDA thread via `malloc()` or `__nv_aligned_device_malloc()` remains allocated for the lifetime of the CUDA context, or until it is explicitly released by a call to `free()`. It can be used by any other CUDA threads even from subsequent kernel launches. Any CUDA thread may free memory allocated by another thread, but care should be taken to ensure that the same pointer is not freed more than once.

###  10.36.1. Heap Memory Allocation 

The device memory heap has a fixed size that must be specified before any program using `malloc()`, `__nv_aligned_device_malloc()` or `free()` is loaded into the context. A default heap of eight megabytes is allocated if any program uses `malloc()` or `__nv_aligned_device_malloc()` without explicitly specifying the heap size.

The following API functions get and set the heap size:

  * `cudaDeviceGetLimit(size_t* size, cudaLimitMallocHeapSize)`

  * `cudaDeviceSetLimit(cudaLimitMallocHeapSize, size_t size)`


The heap size granted will be at least `size` bytes. `cuCtxGetLimit()`and `cudaDeviceGetLimit()` return the currently requested heap size.

The actual memory allocation for the heap occurs when a module is loaded into the context, either explicitly via the CUDA driver API (see [Module](#module)), or implicitly via the CUDA runtime API (see [CUDA Runtime](#cuda-c-runtime)). If the memory allocation fails, the module load will generate a `CUDA_ERROR_SHARED_OBJECT_INIT_FAILED` error.

Heap size cannot be changed once a module load has occurred and it does not resize dynamically according to need.

Memory reserved for the device heap is in addition to memory allocated through host-side CUDA API calls such as `cudaMalloc()`.

###  10.36.2. Interoperability with Host Memory API 

Memory allocated via device `malloc()` or `__nv_aligned_device_malloc()` cannot be freed using the runtime (i.e., by calling any of the free memory functions from [Device Memory](#device-memory)).

Similarly, memory allocated via the runtime (i.e., by calling any of the memory allocation functions from [Device Memory](#device-memory)) cannot be freed via `free()`.

In addition, memory allocated by a call to `malloc()` or `__nv_aligned_device_malloc()` in device code cannot be used in any runtime or driver API calls (i.e. cudaMemcpy, cudaMemset, etc).

###  10.36.3. Examples 

####  10.36.3.1. Per Thread Allocation 

The following code sample:
    
    
    #include <stdlib.h>
    #include <stdio.h>
    
    __global__ void mallocTest()
    {
        size_t size = 123;
        char* ptr = (char*)malloc(size);
        memset(ptr, 0, size);
        printf("Thread %d got pointer: %p\n", threadIdx.x, ptr);
        free(ptr);
    }
    
    int main()
    {
        // Set a heap size of 128 megabytes. Note that this must
        // be done before any kernel is launched.
        cudaDeviceSetLimit(cudaLimitMallocHeapSize, 128*1024*1024);
        mallocTest<<<1, 5>>>();
        cudaDeviceSynchronize();
        return 0;
    }
    

will output:
    
    
    Thread 0 got pointer: 00057020
    Thread 1 got pointer: 0005708c
    Thread 2 got pointer: 000570f8
    Thread 3 got pointer: 00057164
    Thread 4 got pointer: 000571d0
    

Notice how each thread encounters the `malloc()` and `memset()` commands and so receives and initializes its own allocation. (Exact pointer values will vary: these are illustrative.)

####  10.36.3.2. Per Thread Block Allocation 
    
    
    #include <stdlib.h>
    
    __global__ void mallocTest()
    {
        __shared__ int* data;
    
        // The first thread in the block does the allocation and then
        // shares the pointer with all other threads through shared memory,
        // so that access can easily be coalesced.
        // 64 bytes per thread are allocated.
        if (threadIdx.x == 0) {
            size_t size = blockDim.x * 64;
            data = (int*)malloc(size);
        }
        __syncthreads();
    
        // Check for failure
        if (data == NULL)
            return;
    
        // Threads index into the memory, ensuring coalescence
        int* ptr = data;
        for (int i = 0; i < 64; ++i)
            ptr[i * blockDim.x + threadIdx.x] = threadIdx.x;
    
        // Ensure all threads complete before freeing
        __syncthreads();
    
        // Only one thread may free the memory!
        if (threadIdx.x == 0)
            free(data);
    }
    
    int main()
    {
        cudaDeviceSetLimit(cudaLimitMallocHeapSize, 128*1024*1024);
        mallocTest<<<10, 128>>>();
        cudaDeviceSynchronize();
        return 0;
    }
    

####  10.36.3.3. Allocation Persisting Between Kernel Launches 
    
    
    #include <stdlib.h>
    #include <stdio.h>
    
    #define NUM_BLOCKS 20
    
    __device__ int* dataptr[NUM_BLOCKS]; // Per-block pointer
    
    __global__ void allocmem()
    {
        // Only the first thread in the block does the allocation
        // since we want only one allocation per block.
        if (threadIdx.x == 0)
            dataptr[blockIdx.x] = (int*)malloc(blockDim.x * 4);
        __syncthreads();
    
        // Check for failure
        if (dataptr[blockIdx.x] == NULL)
            return;
    
        // Zero the data with all threads in parallel
        dataptr[blockIdx.x][threadIdx.x] = 0;
    }
    
    // Simple example: store thread ID into each element
    __global__ void usemem()
    {
        int* ptr = dataptr[blockIdx.x];
        if (ptr != NULL)
            ptr[threadIdx.x] += threadIdx.x;
    }
    
    // Print the content of the buffer before freeing it
    __global__ void freemem()
    {
        int* ptr = dataptr[blockIdx.x];
        if (ptr != NULL)
            printf("Block %d, Thread %d: final value = %d\n",
                          blockIdx.x, threadIdx.x, ptr[threadIdx.x]);
    
        // Only free from one thread!
        if (threadIdx.x == 0)
            free(ptr);
    }
    
    int main()
    {
        cudaDeviceSetLimit(cudaLimitMallocHeapSize, 128*1024*1024);
    
        // Allocate memory
        allocmem<<< NUM_BLOCKS, 10 >>>();
    
        // Use memory
        usemem<<< NUM_BLOCKS, 10 >>>();
        usemem<<< NUM_BLOCKS, 10 >>>();
        usemem<<< NUM_BLOCKS, 10 >>>();
    
        // Free memory
        freemem<<< NUM_BLOCKS, 10 >>>();
    
        cudaDeviceSynchronize();
    
        return 0;
    }
    


##  10.37. Execution Configuration 

Any call to a `__global__` function must specify the _execution configuration_ for that call. The execution configuration defines the dimension of the grid and blocks that will be used to execute the function on the device, as well as the associated stream (see [CUDA Runtime](#cuda-c-runtime) for a description of streams).

The execution configuration is specified by inserting an expression of the form `<<< Dg, Db, Ns, S >>>` between the function name and the parenthesized argument list, where:

  * `Dg` is of type `dim3` (see [dim3](#dim3)) and specifies the dimension and size of the grid, such that `Dg.x * Dg.y * Dg.z` equals the number of blocks being launched;

  * `Db` is of type `dim3` (see [dim3](#dim3)) and specifies the dimension and size of each block, such that `Db.x * Db.y * Db.z` equals the number of threads per block;

  * `Ns` is of type `size_t` and specifies the number of bytes in shared memory that is dynamically allocated per block for this call in addition to the statically allocated memory; this dynamically allocated memory is used by any of the variables declared as an external array as mentioned in [__shared__](#shared); `Ns` is an optional argument which defaults to 0;

  * `S` is of type `cudaStream_t` and specifies the associated stream; `S` is an optional argument which defaults to 0.


As an example, a function declared as
    
    
    __global__ void Func(float* parameter);
    

must be called like this:
    
    
    Func<<< Dg, Db, Ns >>>(parameter);
    

The arguments to the execution configuration are evaluated before the actual function arguments.

The function call will fail if `Dg` or `Db` are greater than the maximum sizes allowed for the device as specified in [Compute Capabilities](#compute-capabilities), or if `Ns` is greater than the maximum amount of shared memory available on the device, minus the amount of shared memory required for static allocation.

Compute capability 9.0 and above allows users to specify compile time thread block cluster dimensions, so that the kernel can use the cluster hierarchy in CUDA. Compile time cluster dimension can be specified using `__cluster_dims__([x, [y, [z]]])`. The example below shows compile time cluster size of 2 in X dimension and 1 in Y and Z dimension.
    
    
    __global__ void __cluster_dims__(2, 1, 1) Func(float* parameter);
    

The default form of `__cluster_dims__()` specifies that a kernel is to be launched as a cluster grid. By not specifying a cluster dimension, the user is free to specify the dimension at launch time. Not specifying a dimension at launch time will result in a launch time error.

Thread block cluster dimensions can also be specified at runtime and kernel with the cluster can be launched using `cudaLaunchKernelEx` API. The API takes a configuration argument of type `cudaLaunchConfig_t`, kernel function pointer and kernel arguments. Runtime kernel configuration is shown in the example below.
    
    
    __global__ void Func(float* parameter);
    
    
    // Kernel invocation with runtime cluster size
    {
        cudaLaunchConfig_t config = {0};
        // The grid dimension is not affected by cluster launch, and is still enumerated
        // using number of blocks.
        // The grid dimension should be a multiple of cluster size.
        config.gridDim = Dg;
        config.blockDim = Db;
        config.dynamicSmemBytes = Ns;
    
        cudaLaunchAttribute attribute[1];
        attribute[0].id = cudaLaunchAttributeClusterDimension;
        attribute[0].val.clusterDim.x = 2; // Cluster size in X-dimension
        attribute[0].val.clusterDim.y = 1;
        attribute[0].val.clusterDim.z = 1;
        config.attrs = attribute;
        config.numAttrs = 1;
    
        float* parameter;
        cudaLaunchKernelEx(&config, Func, parameter);
    }
    


##  10.38. Launch Bounds 

As discussed in detail in [Multiprocessor Level](#multiprocessor-level), the fewer registers a kernel uses, the more threads and thread blocks are likely to reside on a multiprocessor, which can improve performance.

Therefore, the compiler uses heuristics to minimize register usage while keeping register spilling (see [Device Memory Accesses](#device-memory-accesses)) and instruction count to a minimum. An application can optionally aid these heuristics by providing additional information to the compiler in the form of launch bounds that are specified using the `__launch_bounds__()` qualifier in the definition of a `__global__` function:
    
    
    __global__ void
    __launch_bounds__(maxThreadsPerBlock, minBlocksPerMultiprocessor, maxBlocksPerCluster)
    MyKernel(...)
    {
        ...
    }
    

  * `maxThreadsPerBlock` specifies the maximum number of threads per block with which the application will ever launch `MyKernel()`; it compiles to the `.maxntid` _PTX_ directive.

  * `minBlocksPerMultiprocessor` is optional and specifies the desired minimum number of resident blocks per multiprocessor; it compiles to the `.minnctapersm` _PTX_ directive.

  * `maxBlocksPerCluster` is optional and specifies the desired maximum number thread blocks per cluster with which the application will ever launch `MyKernel()`; it compiles to the `.maxclusterrank` _PTX_ directive.


If launch bounds are specified, the compiler first derives from them the upper limit _L_ on the number of registers the kernel should use to ensure that `minBlocksPerMultiprocessor` blocks (or a single block if `minBlocksPerMultiprocessor` is not specified) of `maxThreadsPerBlock` threads can reside on the multiprocessor (see [Hardware Multithreading](#hardware-multithreading) for the relationship between the number of registers used by a kernel and the number of registers allocated per block). The compiler then optimizes register usage in the following way:

  * If the initial register usage is higher than _L_ , the compiler reduces it further until it becomes less or equal to _L_ , usually at the expense of more local memory usage and/or higher number of instructions;

  * If the initial register usage is lower than _L_

    * If `maxThreadsPerBlock` is specified and `minBlocksPerMultiprocessor` is not, the compiler uses `maxThreadsPerBlock` to determine the register usage thresholds for the transitions between `n` and `n+1` resident blocks (i.e., when using one less register makes room for an additional resident block as in the example of [Multiprocessor Level](#multiprocessor-level)) and then applies similar heuristics as when no launch bounds are specified;

    * If both `minBlocksPerMultiprocessor` and `maxThreadsPerBlock` are specified, the compiler may increase register usage as high as _L_ to reduce the number of instructions and better hide single thread instruction latency.


A kernel will fail to launch if it is executed with more threads per block than its launch bound `maxThreadsPerBlock`.

A kernel will fail to launch if it is executed with more thread blocks per cluster than its launch bound `maxBlocksPerCluster`.

Per thread resources required by a CUDA kernel might limit the maximum block size in an unwanted way. In order to maintain forward compatibility to future hardware and toolkits and to ensure that at least one thread block can run on an SM, developers should include the single argument `__launch_bounds__(maxThreadsPerBlock)` which specifies the largest block size that the kernel will be launched with. Failure to do so could lead to “too many resources requested for launch” errors. Providing the two argument version of `__launch_bounds__(maxThreadsPerBlock,minBlocksPerMultiprocessor)` can improve performance in some cases. The right value for `minBlocksPerMultiprocessor` should be determined using a detailed per kernel analysis.

Optimal launch bounds for a given kernel will usually differ across major architecture revisions. The sample code below shows how this is typically handled in device code using the `__CUDA_ARCH__` macro introduced in [Application Compatibility](#application-compatibility).
    
    
    #define THREADS_PER_BLOCK          256
    #if __CUDA_ARCH__ >= 200
        #define MY_KERNEL_MAX_THREADS  (2 * THREADS_PER_BLOCK)
        #define MY_KERNEL_MIN_BLOCKS   3
    #else
        #define MY_KERNEL_MAX_THREADS  THREADS_PER_BLOCK
        #define MY_KERNEL_MIN_BLOCKS   2
    #endif
    
    // Device code
    __global__ void
    __launch_bounds__(MY_KERNEL_MAX_THREADS, MY_KERNEL_MIN_BLOCKS)
    MyKernel(...)
    {
        ...
    }
    

In the common case where `MyKernel` is invoked with the maximum number of threads per block (specified as the first parameter of `__launch_bounds__()`), it is tempting to use `MY_KERNEL_MAX_THREADS` as the number of threads per block in the execution configuration:
    
    
    // Host code
    MyKernel<<<blocksPerGrid, MY_KERNEL_MAX_THREADS>>>(...);
    

This will not work however since `__CUDA_ARCH__` is undefined in host code as mentioned in [Application Compatibility](#application-compatibility), so `MyKernel` will launch with 256 threads per block even when `__CUDA_ARCH__` is greater or equal to 200. Instead the number of threads per block should be determined:

  * Either at compile time using a macro that does not depend on `__CUDA_ARCH__`, for example
        
        // Host code
        MyKernel<<<blocksPerGrid, THREADS_PER_BLOCK>>>(...);
        

  * Or at runtime based on the compute capability
        
        // Host code
        cudaGetDeviceProperties(&deviceProp, device);
        int threadsPerBlock =
                  (deviceProp.major >= 2 ?
                            2 * THREADS_PER_BLOCK : THREADS_PER_BLOCK);
        MyKernel<<<blocksPerGrid, threadsPerBlock>>>(...);
        


Register usage is reported by the `--ptxas-options=-v` compiler option. The number of resident blocks can be derived from the occupancy reported by the CUDA profiler (see [Device Memory Accesses](#device-memory-accesses) for a definition of occupancy).


##  10.39. Maximum Number of Registers per Thread 

To provide a mechanism for low-level performance tuning, CUDA C++ provides the `__maxnreg__()` function qualifier to pass performance tuning information to the backend optimizing compiler. The `__maxnreg__()` qualifier specifies the maximum number of registers to be allocated to a single thread in a thread block. In the definition of a `__global__` function:
    
    
    __global__ void
    __maxnreg__(maxNumberRegistersPerThread)
    MyKernel(...)
    {
        ...
    }
    

  * `maxNumberRegistersPerThread` specifies the maximum number of registers to be allocated to a single thread in a thread block of the kernel `MyKernel()`; it compiles to the `.maxnreg` _PTX_ directive.


The `__launch_bounds__()` and `__maxnreg__()` qualifiers cannot be applied to the same kernel.

Register usage can also be controlled for all `__global__` functions in a file using the `maxrregcount` compiler option. The value of `maxrregcount` is ignored for functions with the `__maxnreg__` qualifier.


##  10.40. #pragma unroll 

By default, the compiler unrolls small loops with a known trip count. The `#pragma unroll` directive however can be used to control unrolling of any given loop. It must be placed immediately before the loop and only applies to that loop. It is optionally followed by an integral constant expression (ICE)[6](#fn13). If the ICE is absent, the loop will be completely unrolled if its trip count is constant. If the ICE evaluates to 1, the compiler will not unroll the loop. The pragma will be ignored if the ICE evaluates to a non-positive integer or to an integer greater than the maximum value representable by the `int` data type.

Examples:
    
    
    struct S1_t { static const int value = 4; };
    template <int X, typename T2>
    __device__ void foo(int *p1, int *p2) {
    
    // no argument specified, loop will be completely unrolled
    #pragma unroll
    for (int i = 0; i < 12; ++i)
      p1[i] += p2[i]*2;
    
    // unroll value = 8
    #pragma unroll (X+1)
    for (int i = 0; i < 12; ++i)
      p1[i] += p2[i]*4;
    
    // unroll value = 1, loop unrolling disabled
    #pragma unroll 1
    for (int i = 0; i < 12; ++i)
      p1[i] += p2[i]*8;
    
    // unroll value = 4
    #pragma unroll (T2::value)
    for (int i = 0; i < 12; ++i)
      p1[i] += p2[i]*16;
    }
    
    __global__ void bar(int *p1, int *p2) {
    foo<7, S1_t>(p1, p2);
    }
    


##  10.41. SIMD Video Instructions 

PTX ISA version 3.0 includes SIMD (Single Instruction, Multiple Data) video instructions which operate on pairs of 16-bit values and quads of 8-bit values. These are available on devices of compute capability 3.0.

The SIMD video instructions are:

  * vadd2, vadd4

  * vsub2, vsub4

  * vavrg2, vavrg4

  * vabsdiff2, vabsdiff4

  * vmin2, vmin4

  * vmax2, vmax4

  * vset2, vset4


PTX instructions, such as the SIMD video instructions, can be included in CUDA programs by way of the assembler, `asm()`, statement.

The basic syntax of an `asm()` statement is:
    
    
    asm("template-string" : "constraint"(output) : "constraint"(input)"));
    

An example of using the `vabsdiff4` PTX instruction is:
    
    
    asm("vabsdiff4.u32.u32.u32.add" " %0, %1, %2, %3;": "=r" (result):"r" (A), "r" (B), "r" (C));
    

This uses the `vabsdiff4` instruction to compute an integer quad byte SIMD sum of absolute differences. The absolute difference value is computed for each byte of the unsigned integers A and B in SIMD fashion. The optional accumulate operation (`.add`) is specified to sum these differences.

Refer to the document “Using Inline PTX Assembly in CUDA” for details on using the assembly statement in your code. Refer to the PTX ISA documentation (“Parallel Thread Execution ISA Version 3.0” for example) for details on the PTX instructions for the version of PTX that you are using.


##  10.42. Diagnostic Pragmas 

The following pragmas may be used to control the error severity used when a given diagnostic message is issued.
    
    
    #pragma nv_diag_suppress
    #pragma nv_diag_warning
    #pragma nv_diag_error
    #pragma nv_diag_default
    #pragma nv_diag_once
    

Uses of these pragmas have the following form:
    
    
    #pragma nv_diag_xxx error_number, error_number ...
    

The diagnostic affected is specified using an error number showed in a warning message. Any diagnostic may be overridden to be an error, but only warnings may have their severity suppressed or be restored to a warning after being promoted to an error. The `nv_diag_default` pragma is used to return the severity of a diagnostic to the one that was in effect before any pragmas were issued (i.e., the normal severity of the message as modified by any command-line options). The following example suppresses the `"declared but never referenced"` warning on the declaration of `foo`:
    
    
    #pragma nv_diag_suppress 177
    void foo()
    {
      int i=0;
    }
    #pragma nv_diag_default 177
    void bar()
    {
      int i=0;
    }
    

The following pragmas may be used to save and restore the current diagnostic pragma state:
    
    
    #pragma nv_diagnostic push
    #pragma nv_diagnostic pop
    

Examples:
    
    
    #pragma nv_diagnostic push
    #pragma nv_diag_suppress 177
    void foo()
    {
      int i=0;
    }
    #pragma nv_diagnostic pop
    void bar()
    {
      int i=0;
    }
    

Note that the pragmas only affect the nvcc CUDA frontend compiler; they have no effect on the host compiler.

Removal Notice: The support of diagnostic pragmas without `nv_` prefix are removed from CUDA 12.0, if the pragmas are inside the device code, warning `unrecognized #pragma in device code` will be emitted, otherwise they will be passed to the host compiler. If they are intended for CUDA code, use the pragmas with `nv_` prefix instead.

[4](#id144)
    

When the enclosing __host__ function is a template, nvcc may currently fail to issue a diagnostic message in some cases; this behavior may change in the future.

[5](#id170)
    

The intent is to prevent the host compiler from encountering the call to the function if the host compiler does not support it.

6([1](#id205),[2](#id206),[3](#id322))
    

See the C++ Standard for definition of integral constant expression.


##  10.43. Custom ABI Pragmas 

The `#pragma nv_abi` directive enables applications compiled in separate compilation mode to achieve performance similar to that of whole program compilation.

The syntax for using this pragma is as follows, where ICE refers to any integral constant expression (ICE): [6](#fn13).
    
    
    #pragma nv_abi preserve_n_data(ICE) preserve_n_control(ICE)
    

Note, the arguments that follow `#pragma nv_abi` are optional and can be provided in any order; however, at least one argument is required.

The `preserve_n` arguments set a limit on the number of registers preserved during a function call:

  * `preserve_n_data(ICE)` limits the number of data registers, and

  * `preserve_n_control(ICE)` limits the number of control registers.


`#pragma nv_abi` can be placed immediately before a device function declaration or definition. Alternatively, it can be placed directly before an indirect function call within a C++ expression statement inside a device function. Note, indirect function calls to free functions are supported, but indirect calls through function argument references or class member functions are not.

When the pragma is applied to a device function declaration or definition, it modifies the custom ABI properties for any calls to that function. When placed at an indirect function call site, the pragma affects the ABI properties for that indirect function call. The key point is that unlike direct function calls, where you can place the pragma before a function declaration or definition, `#pragma nv_abi` only affects indirect function calls when the pragma is placed before a call site.

As shown in the following example, we have two device functions, `foo()` and `bar()`. In this example the pragma is placed before the call site of the function pointer fptr to modify the ABI properties of the indirect function call. Notice that placing the pragma before the direct call does not affect the ABI properties of the call. To alter the ABI properties of a direct function call, the pragma must be placed before the function declaration or definition.
    
    
    __device__ int foo()
    {
      int value{0};
      ...
      return value;
    }
    
    __device__ int bar()
    {
      int value{0};
      ...
      return value;
    }
    
    __device__ void baz()
    {
      int result{0};
      int (*fptr)() = foo;  // function pointer
    
      #pragma nv_abi preserve_n_data(16) preserve_n_control(8)
      result = fptr();      // The pragma affects the indirect call to foo() via fptr
    
      #pragma nv_abi preserve_n_data(16) preserve_n_control(8)
      result = (*fptr)();   // Alternate syntax for the indirect call to foo()
    
      #pragma nv_abi preserve_n_data(16) preserve_n_control(8)
      result += bar();      // The pragma does NOT affect the direct call to bar()
    }
    

As shown in the following example, to modify direct function calls, you must apply the pragma to the function declaration or definition.
    
    
    #pragma nv_abi preserve_n_data(16)
    __device__ void foo();
    

Note that a program is ill-formed if the pragma arguments for a function declaration and its corresponding definition do not match.


##  10.44. CUDA C++ Memory Model 

The CUDA C++ Memory Model extends the ISO C++ Memory Model as documented in the [CUDA C++ Memory Model documentation](https://nvidia.github.io/cccl/libcudacxx/extended_api/memory_model.html).


##  10.45. CUDA C++ Execution Model 

The CUDA C++ Exeuction Model extends the ISO C++ execution model as documented in the [CUDA C++ Execution Model documentation](https://nvidia.github.io/cccl/libcudacxx/extended_api/execution_model.html).
